int			ti_i
int			ti_flag_activation
int			ti_ok
int			ti_flag
int			ti_client_count
int			ti_flag_activationby_capa1

ti_flag_activation = faux
ti_flag_activationby_capa1 = faux
ti_flag = 1

if ( mode_i_onlyoneclient )
	ti_client_count = 1
else
	ti_client_count = nombre_de_client


// ----( Destruction éventuelle : pete les liaisons )----
if(AI_TriggerIsValid(trigger_destruction))
{
	if (call_trigger(trigger_destruction))
	{
		for (ti_i = 0; ti_i < ti_client_count; ti_i++)
		{
			o_CL[ti_i] = LNK_ClientGet(Ci_LNK_INTERACTION, mid_CL_LIAISON_ID[ti_i], faux, "Interaction_exec_add_data", nofunc, nofunc)
		}
		
		OBJ_Destroy()
	}
}

// ----( activation )----
if ( OBJ_CapaTest( OBJ_Capa_1 ) )
{
	ti_flag_activationby_capa1 = vrai
	ti_flag_activation = 1
}
else if (AI_TriggerIsValid(trigger_activation))
{
	if (call_trigger(trigger_activation))
	{
		if (i_auto_activation == 0 || i_auto_activation == 1) 
		{
			ti_flag_activation = vrai
			i_auto_activation = 1
		}
	}
	else
		i_auto_activation = 0
}

// Manuel ?
if (manual && OBJ_CapaTest(OBJ_Capa_15))
{
	i_capa_15 = vrai
	ti_flag_activation = vrai
	ti_flag = 3
	OBJ_CapaSet(0, OBJ_Capa_15)	
}

// ----( toutes les liaisons sont bonnes ? )-----
ti_ok = vrai
for (ti_i = 0; ti_i < ti_client_count; ti_i++)
{
	if(!o_CL[ti_i]) ti_ok = faux
}

// Si tous ont répondu une fois, on ne demandera plus
if(ti_ok && i_auto_activation == 1) i_auto_activation = 2

// Si on a été valide une fois, on est valide tout le temps
if(i_activate_once && activate_always) ti_flag_activation = vrai

/////////////////////////////////////////////////////////////
// Affichage type d'interaction
OBJ_Me().des_int1 = 0
if(!ti_ok && manual)
{
	switch(type_interaction)
	{
		case Ci_LNK_INTERACTION_USE :
			OBJ_Me().des_int1 = Ci_DISPLAY_USE
			break	
	}
}

// JACK ---------------------------------------------------------------------------
if (AI_TriggerIsValid(trigger_jack) && call_trigger(trigger_jack))
{
	// Jack fait l'action a la place des marins !
	OBJ_CapaSet(Obj_Capa_Switch, none)
}
else
{
	// Jack ne fait rien donc c'est les liens qui decident
	OBJ_CapaSet(none, Obj_Capa_Switch)
}
// JACK ---------------------------------------------------------------------------

if(!test_capa_0 || OBJ_CapaTest(OBJ_Capa_0))
{
	if(ti_flag_activation)
	{
		if ( !ti_flag_activationby_capa1 )
			i_activate_once = vrai
			
		if ( mode_i_onlyoneclient )	
		{
			for (ti_i = 0; ti_i < nombre_de_client; ti_i++)
			{
				i_init_i = ti_i
				o_CL[0] = LNK_ClientGet_Interaction( mid_CL_LIAISON_ID[0], ti_flag, "Interaction_exec_add_data", nofunc, nofunc)
				if (	o_CL[0] && LNK_InteractionCStatusGet(mid_CL_LIAISON_ID[0]) == Ci_LNKINT_CSTATUS_Interact)
				{
					OBJ_CapaSet(Obj_Capa_Switch,none)
					break
				}
			}
		}
		else
		{
			for (ti_i = 0; ti_i < nombre_de_client; ti_i++)
			{
				i_init_i = ti_i
				o_CL[ti_i] = LNK_ClientGet_Interaction( mid_CL_LIAISON_ID[ti_i], ti_flag, "Interaction_exec_add_data", nofunc, nofunc)
				if (	o_CL[ti_i] && LNK_InteractionCStatusGet(mid_CL_LIAISON_ID[ti_i]) == Ci_LNKINT_CSTATUS_Interact)
					OBJ_CapaSet(Obj_Capa_Switch,none)
			}
		}
	}
	else
	{
		if ( mode_i_onlyoneclient )	
		{
			if(o_CL[0] && !MSG_GlobalIsValid(mid_CL_LIAISON_ID[0]))  o_CL[0] = nobody
			if (o_CL[0])
				o_CL[0] = LNK_ClientGet(Ci_LNK_INTERACTION, mid_CL_LIAISON_ID[0], faux, "Interaction_exec_add_data", nofunc, nofunc)
		}
		else
		{
			for (ti_i = 0; ti_i < ti_client_count; ti_i++)
			{
				if(o_CL[ti_i] && !MSG_GlobalIsValid(mid_CL_LIAISON_ID[ti_i]))  o_CL[ti_i] = nobody
				if (o_CL[ti_i])
				{
					if (	LNK_InteractionCStatusGet(mid_CL_LIAISON_ID[ti_i]) == Ci_LNKINT_CSTATUS_Interact)
						OBJ_CapaSet(Obj_Capa_Switch,none)
				}
			}	
		}
	}
}
else
{
	for (ti_i = 0; ti_i < ti_client_count; ti_i++)
	{
		o_CL[ti_i] = LNK_ClientGet(Ci_LNK_INTERACTION, mid_CL_LIAISON_ID[ti_i], faux, "Interaction_exec_add_data", nofunc, nofunc)
	}
}

i_capa_15 = faux

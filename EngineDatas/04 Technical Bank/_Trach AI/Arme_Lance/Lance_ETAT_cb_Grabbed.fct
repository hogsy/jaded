int			ti_what

vector	tv_sight
vector	tv_banking
vector	tv_pos

object	to_bidoche


// INIT ==================================
if( mi_State != Ci_bambou_state_grabbed )
{
	mi_State = Ci_bambou_state_grabbed
	macro_del_callback_after_cam("Lance_ETAT_cb_Plugged")
	f_grabbed_duration = 0.0
	
	// raz flags
	i_snd_impact = faux
	mi_fall = faux
	mo_LockObj = nobody
	
	if( o_joueur && o_grabbed_serveur == o_joueur )
	{
		@get_global o_last_lance_jack = OBJ_Me()
		@get_global i_cpt_lance_jack++
		i_numero_lance_jack = @get_global i_cpt_lance_jack
	}
}
else
{
	f_grabbed_duration += TIME_GetDt()
}


// CHECK MSG =====================
AI_Execute("Lance_exec_Check_Msg")
if( i_msg_launch_received )
{
	i_msg_launch_received = faux
	OBJ_FlagInvisibleSet(faux)
	macro_del_callback_after_cam("Lance_ETAT_cb_Grabbed")
	AI_TrackChange(2, "Lance_Launch_init")
	return
}

if( o_rack_de_lances )
	i_hide = vrai		// ne pas apparaitre trop tôt

// VISIBLE ?
if( f_grabbed_duration > 0.15 )
{
	// kill link !
	if( MSG_GlobalIsValid(mid_LNK_grabbed) )
		LNK_ServeurGet(Ci_LNK_GRAB_OBJECT, mid_LNK_grabbed, faux, nofunc, nofunc)

	if( o_rack_de_lances )
	{
		@get_Arme_Spawner_path o_rack_de_lances i_dup_weapon_generation = vrai
		@get_Arme_Spawner_path o_rack_de_lances o_ramasse_actor = o_grabbed_serveur
		o_rack_de_lances = nobody
	}
	
	DYN_Off()	// conserve gravité si récup en tombant
	
//	if( @o_grabbed_serveur ACT_CustomBitTest(ACT_CBit_ArmeGenerique) )
		o_grabbed_bone = @o_grabbed_serveur ANI_CanalObjectGet(Anim_Canal_ArmeLance)
//	else
//		o_grabbed_bone = @o_grabbed_serveur ANI_CanalObjectGet(Anim_Canal_ArmePrincipale)
		
	// visible ?
	if( o_joueur && o_grabbed_serveur == o_joueur )
	{
		OBJ_DrawAtEnd()		// ne pas afficher la main par dessus l'arc !!!!
		to_bidoche = @o_joueur H_Joueur_Gets_Bidoche()
		if( to_bidoche )
			@to_bidoche OBJ_DrawAtEnd()		// la bidoche doit être affichée par-dessus la lance
		
		if( @get_Humain_path o_joueur o_grabbed_object_en_main != OBJ_Me() )
			i_hide = vrai
		if( @get_global i_Player_is_Kong )
			i_hide = vrai
	}
	if( @o_grabbed_serveur ACT_CustomBitTest(ACT_CBit_ArmeCachee) )
		i_hide = vrai		// force hide weapon
	
	tv_pos = @o_grabbed_bone OBJ_PosGet() //+ @o_grabbed_bone OBJ_BankingGet())
	OBJ_PosSet(tv_pos)
	//tv_sight = @o_grabbed_bone OBJ_BankingGet()
	//tv_sight = MATH_VecRotate(tv_sight, Cv_VerticalVector, -0.5)
	//tv_banking = - @o_grabbed_bone OBJ_HorizonGet()
	tv_sight = @o_grabbed_bone OBJ_SightGet()
	tv_banking = @o_grabbed_bone OBJ_HorizonGet()
	OBJ_SightGeneralSet(tv_sight, tv_banking)
}

OBJ_FlagInvisibleSet(i_hide)

v_pos_de_lancement = OBJ_PosGet()

#ifndef _FINAL_
if( ti_dbg )
	Str_DisplayTextOnce("cb Grabbed", VIEW_3dWorldTo2d(0, OBJ_PosGet()))
#endif

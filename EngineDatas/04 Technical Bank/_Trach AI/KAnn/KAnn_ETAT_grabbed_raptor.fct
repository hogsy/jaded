#include "KAnn_defines.var"

int			ti_i
int			ti_numero
int			ti_index
int			ti_context

float		tf_liferatio

vector	tv_new_sight
vector	tv_dest_pos
vector	tv_me_to_dest

messageid		tmid_grab_lnk


// SORTIE ETAT ===================================================================
if (i_sort_etat)
{
	if (i_etat_courant != ETAT_projete)
	{
		DYN_FrictionVectorSet(cvector(1.0, 1.0, 0.0))
		COL_ColMapActivationSet(all, none)	
	}
	COL_UnCollidableDel(o_grabbed_raptor_actor)
	AI_CBDel(o_grabbed_raptor_actor, CallBack_After_Blend, "KAnn_CALLBACK_set_pos")	
		
	o_grabbed_raptor_actor = nobody
	
	i_sort_etat = faux
	return
}

	
// INITIALISATION ETAT ==============================================================
if (i_etat_courant != ETAT_grabbed_raptor)
{
	i_dernier_etat = i_etat_courant
	i_etat_courant = ETAT_grabbed_raptor
	
	//----( pour fred : capa 0 à 1 indique pendant une trame que le perso s'est fait taper ou grabber )----
	OBJ_CapaSet( OBJ_Capa_paffe_grabbe, 0 )
	
	if (fct_last_etat)
	{
		i_sort_etat = vrai
		AI_Execute(fct_last_etat)
	}

	fct_last_etat = AI_TrackCurGet()
	
//	KAnn_Texte(Gene_C_help)
	
	// Envoie un message a SUN
	EVENT_AddEventFilmation( C_EVENT_Filmation_filter_HumainGnaked, OBJ_Me(), ao_SRV[Ci_LNK_GRAB_RAPTOR], Cf_EVENT_Duree_1Trame)

	// AUTORISATIONS DES GRABS
	for (ti_i = 0; ti_i < Ci_LNK_TYPE_NB; ti_i++)
		ai_SRV_ENABLE[ti_i] = faux
	ai_SRV_ENABLE[Ci_LNK_GRAB_RAPTOR]	= vrai

	o_grabbed_raptor_actor = ao_SRV[Ci_LNK_GRAB_RAPTOR]
	COL_UnCollidableAdd(o_grabbed_raptor_actor)	

	AI_CBAdd(o_grabbed_raptor_actor, CallBack_After_Blend, "KAnn_CALLBACK_set_pos")	
		

	DYN_SpeedSetVector( Cv_NullVector)
	DYN_TractionSet( Cv_NullVector)
	DYN_FrictionVectorSet(cvector(0.5, 0.5, 0.0))

	COL_ColMapActivationSet(none, all)

	f_time_start_etat = 0.0
	
	KAnn_Set_Safe_Wp(nobody)

}
else
{
	f_time_start_etat += TIME_GetDt()
}


// ANALYSE  =====================================================================
AI_Execute("KAnn_exec_CHECK_paf")
o_paf_sender = nobody			// pafs ignorés


// FIN DU GRAB ?
AI_Execute("KAnn_exec_serveur_get")
if( ao_SRV[Ci_LNK_KKGRAB_OBJECT] )
{
	ao_SRV[Ci_LNK_GRAB_RAPTOR] = LNK_ServeurGet(Ci_LNK_GRAB_RAPTOR, amid_SRV_LIAISON_ID[Ci_LNK_GRAB_RAPTOR], faux, nofunc, nofunc)
	macro_change_etat("KAnn_ETAT_grabbed_KK")
}


// INTERRUPTION BRUTALE DE LA LIAISON
if( ! ao_SRV[Ci_LNK_GRAB_RAPTOR] )
	macro_change_etat("KAnn_ETAT_follow_network")


// COMPORTEMENT =================================================================

i_au_sol_flag = vrai


tmid_grab_lnk = amid_SRV_LIAISON_ID[Ci_LNK_GRAB_RAPTOR]
switch(LNK_GrabStatusGet(tmid_grab_lnk))
{
	case 0 :
		// Le Raptor me bouffe le bras
//		if( ! f_time_start_etat ) 
//			KAnn_Texte(Gene_C_caught)
		break
		
	case 1 :
		ACT_ActionSet(Ann_mort)
		tv_new_sight = MATH_VecBlendRotate(OBJ_SightGet(), -@o_grabbed_raptor_actor OBJ_SightGet(), 4.0 * TIME_GetDt())
		OBJ_BankingGeneralSet(tv_new_sight, Cv_VerticalVector)
		break

	case 2 :	
		// Le Raptor me secoue : KAnn ne prend pas de dégats, et c'est le raptor KK qui passe le statut de la liaison à 3
		break
	
	case 3:
		// J'attends que le raptor me lance
		break

	case 4 :
		// Le raptor me lance
		macro_change_etat("KAnn_ETAT_projete")
		break
}

// CAM IMPORTANCE
@get_global o_prio_camera = OBJ_Me()


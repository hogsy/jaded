#include "Kann_defines.var"

int ti_i, ti_link_param
vector tv_sight, tv_new_sight
float tf_dist
object to_head
vector tv_head

// SORTIE ETAT ===================================================================
if (i_sort_etat)
{
	o_safe_wp = nobody
	DYN_FrictionVectorSet(cvector(1.0, 1.0, 0.0))
	
	i_sort_etat = faux
	return
}

// INITIALISATION ETAT ==============================================================
if (i_etat_courant != ETAT_attack_bat)
{
	i_dernier_etat = i_etat_courant
	i_etat_courant = ETAT_attack_bat

	if (fct_last_etat)
	{
		i_sort_etat = vrai
		AI_Execute(fct_last_etat)
	}

	fct_last_etat = AI_TrackCurGet()
	
	// AUTORISATIONS DES GRABS
	for (ti_i = 0; ti_i < Ci_LNK_TYPE_NB; ti_i++)
		ai_SRV_ENABLE[ti_i] = faux
		
	ai_SRV_ENABLE[Ci_LNK_GRAB_TRANSPORTE]	= vrai
	ai_SRV_ENABLE[Ci_LNK_INTERACTION] = vrai
	
	f_time_start_etat = 0.0
	
	DYN_FrictionVectorSet(cvector(5.0, 5.0, 5.0))
	
	// Choix du WP -----------------------------------------------------------------------------------------------------------------------
	o_safe_wp = KAnn_Choix_du_WP_Safe()		// Choix d'un wp si ANN n'en a pas déjà choisi un

	o_fight_actor = ao_SRV[Ci_LNK_INTERACTION]

	LNK_InteractionParamSet(amid_SRV_LIAISON_ID[Ci_LNK_INTERACTION], 0)

}
else
{
	f_time_start_etat += TIME_GetDt()
}

AI_Execute("KAnn_exec_serveur_get")

if(ao_SRV[Ci_LNK_GRAB_TRANSPORTE])
{
	macro_change_etat("KAnn_ETAT_grabbed_bat")
}
	
// INTERRUPTION BRUTALE DE LA LIAISON
if (! ao_SRV[Ci_LNK_INTERACTION])
{
	ACT_ActionSet(Ann_terrorisee_attente)
	return 
}

ti_link_param = LNK_InteractionParamGet(amid_SRV_LIAISON_ID[Ci_LNK_INTERACTION])


if (	ti_link_param == 0 ) // goto waypoint
{
	if (o_safe_wp)
	{
		tf_dist = MATH_VecNorm(@o_safe_wp OBJ_PosGet() - OBJ_PosGet())
		if (tf_dist > 1.0)	
		{
			tv_sight = @o_safe_wp OBJ_PosGet() - OBJ_PosGet()
			MATH_VecNormalize(tv_sight)
			tv_sight.z = 0.0
			OBJ_SightSet(tv_sight)		
		}
		else
		{
//			ACT_ActionSet(Action_Attack_torch_wait)	
			LNK_InteractionParamSet(amid_SRV_LIAISON_ID[Ci_LNK_INTERACTION], 1)
		}
	}
	else
	{
		LNK_InteractionParamSet(amid_SRV_LIAISON_ID[Ci_LNK_INTERACTION], 1)		
	}
}
else if (ti_link_param == 1) // face the beast
{
	tv_sight = OBJ_SightGet()
	
	tv_new_sight =  @o_fight_actor  OBJ_PosGet() - OBJ_PosGet()
	tv_new_sight = MATH_VecNormalize(tv_new_sight)
	
	tv_new_sight= MATH_VecBlend(tv_sight ,tv_new_sight,TIME_GetDt() * 6)
	tv_new_sight.z = 0.0
	
	OBJ_SightSet(tv_new_sight)		

	if (MATH_VecNullToler(tv_new_sight - tv_sight, 0.005)	)
	{
		LNK_InteractionParamSet(amid_SRV_LIAISON_ID[Ci_LNK_INTERACTION], 10)
	}

	f_head_dist = 100000

}
else if (ti_link_param == 11) // i m being attacked
{
	to_head = @o_fight_actor ANI_CanalObjectGet(Anim_Canal_Tete)
	
	f_prev_head_dist = f_head_dist 
	f_head_dist = MATH_VecNorm(@to_head OBJ_PosGet() - OBJ_PosGet())

	if (f_head_dist > f_prev_head_dist)
	{	
//		ACT_ActionSet(Action_Attack_torch_1)
		LNK_InteractionParamSet(amid_SRV_LIAISON_ID[Ci_LNK_INTERACTION], 12)
	}
}
else if (ti_link_param == 12) // break time
{
	f_head_dist = 100000
	
//	if (ACT_ActionFinished())
//		ACT_ActionSet(Action_Attack_torch_wait)	
}

	








#include "KAnn_Defines.var"

int 			ti_interaction_type

//================================================================================================
// INTERACTION AVEC LES SMALL BATS
//================================================================================================

#ifndef _FINAL_
if( i_DBG_display_interaction )
	Str_DisplayTextOnce("Interaction Attack Small Bats", Cv_Interaction_type_display_pos2d)
#endif

o_fight_actor = ao_SRV[Ci_LNK_INTERACTION]
if( MSG_GlobalIsValid(amid_SRV_LIAISON_ID[Ci_LNK_INTERACTION]) )
{
	ti_interaction_type = LNK_InteractionParamGet(amid_SRV_LIAISON_ID[Ci_LNK_INTERACTION])
	switch( ti_interaction_type )
	{
		case C_INTER_FIGHT_WAIT_FOR_ANN :
			if( i_has_weapon )
			{
				// javelot en main
				if( ! o_fight_actor )
				{
					ACT_ActionSet(Ann_att_enjoue_bambou)
					ti_interaction_type = C_INTER_FIGHT_ANN_READY		// se prépare à subir une attaque
				}
				else
				{
		 			ACT_ActionSet(Ann_att_enjoue_bambou)
		 			if( i_interaction_modulo == 0 )		// les autres que raptors restent à 0
						ti_interaction_type = C_INTER_FIGHT_ANN_READY		// se prépare à subir une attaque
					else
					{
						ti_interaction_type = C_INTER_FIGHT_ANN_FAKING_ATTACK		// prépare une attaque/
					}
		 			i_interaction_modulo = MATH_Modulo(i_interaction_modulo + 1, 2)
				}
			}
			else
			{
				// pas d'arme
				ACT_ActionSet(Ann_Cache_Protect)
				ti_interaction_type = C_INTER_FIGHT_ANN_READY		// se prépare à subir une attaque
			}
			LNK_InteractionParamSet(amid_SRV_LIAISON_ID[Ci_LNK_INTERACTION], ti_interaction_type )
			break
		
		case C_INTER_FIGHT_ENNEMY_ATTACK :
		case C_INTER_FIGHT_ENEMY_FAKE_ATTACK :
			if( ACT_ActionGet() != Ann_Cache_Protect && 
			    ACT_ActionGet() != Ann_stress_att_protege)
			{
				if( MATH_RandFloat(-1.0,1.0) <= 0.0 )
					ACT_ActionSet(Ann_stress_att_protege)
				else
					ACT_ActionSet(Ann_Cache_Protect)
			}
			else if ( (ACT_ActionGet() == Ann_Cache_Protect && ANI_CurrentFrameGet(0) > 80) ||
			 		   (ACT_ActionGet() == Ann_stress_att_protege && ANI_CurrentFrameGet(0) > 50) )
			{
				ACT_ActionSet(Ann_att_enjoue_bambou)
				ti_interaction_type = C_INTER_FIGHT_WAIT_FOR_ANN
				LNK_InteractionParamSet(amid_SRV_LIAISON_ID[Ci_LNK_INTERACTION], ti_interaction_type )
			}
			break
			
		case C_INTER_FIGHT_ENEMY_READY :
			ti_interaction_type = C_INTER_FIGHT_ANN_FAKING_ATTACK
			LNK_InteractionParamSet(amid_SRV_LIAISON_ID[Ci_LNK_INTERACTION], ti_interaction_type )
			break
	
		case C_INTER_FIGHT_ANN_FAKING_ATTACK :
			if( ACT_ActionGet() != Ann_frappe_bambou &&
			    ACT_ActionGet() != Ann_attack_bambou_bas && 
			    ACT_ActionGet() != Ann_attack_bambou_haut)
			{
				i_interaction_fight_action_attack_modulo = MATH_Modulo(i_interaction_fight_action_attack_modulo + 1, 3)
//				switch (i_interaction_fight_action_attack_modulo)
//				{
//					case 0:
						ACT_ActionSet(Ann_frappe_bambou)
//						break
//					case 1:
//						ACT_ActionSet(Ann_attack_bambou_bas)
//						break
//					case 2:
//						ACT_ActionSet(Ann_attack_bambou_haut)					
//					break										
//				}
			}
			if (ACT_ActionFinished())
			{
				// ANN a forcément un bambou
				ACT_ActionSet(Ann_att_enjoue_bambou)
				LNK_InteractionParamSet(amid_SRV_LIAISON_ID[Ci_LNK_INTERACTION], C_INTER_FIGHT_WAIT_FOR_ANN)	
				f_attack_reaction_delay = -1 
			}
			else if( ACT_ActionGet() == Ann_frappe_bambou && ANI_CurrentFrameGet(0) > 40 )
			{
				@o_target KBC_Interact_Ann_Take_Fake_Paf()
				KAnn_Test_Capa_Force_Paf(o_target)
			}
			else if( ACT_ActionGet() == Ann_attack_bambou_bas && ANI_CurrentFrameGet(0) > 30 )
			{
				@o_target KBC_Interact_Ann_Take_Fake_Paf()
				KAnn_Test_Capa_Force_Paf(o_target)
			}
			else if( ACT_ActionGet() == Ann_attack_bambou_haut && ANI_CurrentFrameGet(0) > 30 )
			{
				@o_target KBC_Interact_Ann_Take_Fake_Paf()
				KAnn_Test_Capa_Force_Paf(o_target)
			}
			break
	}
}
else
{
 	if( i_has_weapon )
		ACT_ActionSet(Ann_att_enjoue_bambou)
	else
		ACT_ActionSet(Ann_stress_attente)
}

if( ACT_ActionGet() != Ann_ramasse_debout )
{
	if (o_target && ! i_freezeSight)
	{
		v_sight = @o_target OBJ_PosGet() - OBJ_PosGet()
		if ( ! MATH_VecNullEpsilon(v_sight))
			v_sight = MATH_VecNormalize(v_sight)
		
		v_sight = MATH_VecBlend(OBJ_SightGet(),v_sight, TIME_GetDt() * 5)
		OBJ_BankingGeneralSet( v_sight ,Cv_VerticalVector ) 
		i_freezeSight = vrai
	}
}

// TIMER MORT ANN ==========================
if( ao_SRV[Ci_LNK_INTERACTION] )
{
	KAnn_Death_Timer_Countdown()
}


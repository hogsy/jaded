#include "Projectile_defs.var"


object			to_obj, to_sender
int					ti_rank, ti_paf, ti_balle
vector			tv_paf_dir
messageid		EVT_ID
message		EVT_msg_filter

if( on_ground || mi_NoDyn )
{
	DYN_Off()
	COL_ColSetActivationSet(none, all)
}
else
{
	DYN_On()
	COL_ColSetActivationSet(all, none)
	DYN_FlagsSet(DYN_C_BasicForces + DYN_C_VectorFriction, none)
	DYN_GravitySet( cvector(0,0,-20) )
	if(COL_CollideType(COL_C_Ground)) on_ground = vrai
}

OBJ_InfoPhotoParamSet( PRIO_PICK_LEVIER, 0, 3, 3, 0.0, 0.0, 0.0, 1)		// Bouée d'aide à la visée
mv_LaunchSpeed = cvector( 10, 10, 10 )
mv_LaunchGravity = Cv_NormalGravity 
mv_LaunchFriction = cvector( 1, 1, 1 )
mo_Sender = nobody

to_obj = AI_MainActorGet(C_ID_Joueur)
if(OBJ_SqrDist(to_obj) <= 3*3 && mi_Type == ProjType_Levier) OBJ_Me().des_int1 = Ci_DISPLAY_TAKE
else OBJ_Me().des_int1 = 0

if (OBJ_CapaTest(OBJ_Capa_15))
{
	OBJ_CapaSet(0, OBJ_Capa_15)
	if
	(
		@"univ" i_weapon_ID[C_ID_Joueur] != Ci_weapon_ID_levier && 
		@"univ" i_weapon_ID_Save[C_ID_Joueur] != Ci_weapon_ID_levier && 
		@"univ" i_weapon_ID_second[C_ID_Joueur] != Ci_weapon_ID_levier
	)
	{		
		if (@"univ" o_ramasse_objet_who == nobody )
		{
		
			switch(mi_Type)
			{
//			case ProjType_Dynamite :	
//						@"univ" i_ramasse_objet = Ci_weapon_ID_grenade 
//						@"univ" i_ramasse_objet_param = i_dynam_allumee
//						@"univ" f_ramasse_objet_param = mf_dyna_time
//						break
//			case ProjType_Crane :	
//						@"univ" i_ramasse_objet = Ci_weapon_ID_crane 
//						@"univ" i_ramasse_objet_param = mi_crane_allume
//						break
			case ProjType_Levier :	
						@"univ" o_ramasse_objet_who = AI_MainActorGet(C_ID_Joueur)
						@"univ" i_ramasse_objet_param = 0
						@"univ" f_ramasse_objet_param = 0
						@"univ" i_ramasse_objet = Ci_weapon_ID_levier 
						break
			}
			
			AI_TrackCurChangeNow("Projectile_waitdestroy")
		}
	}
}

if  ( (mi_Type == ProjType_Dynamite) || (mi_Type == ProjType_Crane ) )
{
	if (GRID_LIB_CaseEnFeu( OBJ_PosGet() ) )
	{
		i_dynam_allumee = 1
		mf_dyna_time = -1
	}
}

if ( (mi_Type == ProjType_Crane) && mi_crane_takepaf )
{
	MSG_SetNull( EVT_msg_filter)
	EVT_msg_filter.msg_gao1 = OBJ_Me()
	ti_rank = -1
	ti_paf = faux
	ti_balle = faux
	EVT_ID = MSG_GlobalSearchIntGao(C_EVENT_TYPE_Paf, &ti_rank, EVT_msg_filter)
	while(MSG_GlobalIsValid(EVT_ID))
	{
		ti_paf = vrai
		tv_paf_dir = EVENT_PafDirGet( EVT_ID )
		if ( EVENT_PafTypeGet( EVT_ID ) & C_PAF_KK_Weapon )
			ti_balle = vrai
		to_sender = EVENT_PereGet( EVT_ID )
		// --------( paf suivant ? )--------
		EVT_ID = MSG_GlobalSearchIntGao(C_EVENT_TYPE_Paf, &ti_rank, EVT_msg_filter)
	}

	if ( ti_paf )
	{
		if ( ti_balle )
		{
			OBJ_FlagInvisibleSet( 1 )
			mi_crane_choc = 1
			mv_ImpactPos = OBJ_PosGet()
			mv_ImpactNormal  = Cv_VerticalVector
			v_last_speed = Cv_NullVector
		}
		else
		{
			mo_Sender = to_sender
			mv_LaunchSpeed = tv_paf_dir
			mv_LaunchGravity = Cv_NormalGravity
			mv_LaunchFriction = cvector( 1.0,1.0,1.0 )
			AI_TrackCurChange( "Projectile_initlaunch" )
		}
	}
}
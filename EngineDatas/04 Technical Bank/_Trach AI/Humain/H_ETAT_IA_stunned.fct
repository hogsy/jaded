#include "H_defines.var"
#include "H_action.var"


int				ti_i, ti_flag
int				ti_context
int				ti_assezdeplace

float			tf_liferatio
float			tf_heal

vector		tv_temp, tv_temp1

object		to_obj

messageid	msg_id, tmid_info

// SORTIE ETAT ===================================================================
if (i_sort_etat)
{
	Humains_BlaBlaFreq[TEXTE_PrevienDanger] = TIME_Get()
	o_healer = LNK_ClientGet(Ci_LNK_INTERACTION, mid_healer, faux, nofunc, nofunc, nofunc)
	o_carry = LNK_ClientGet(Ci_LNK_INTERACTION, mid_carry, faux, nofunc, nofunc, nofunc)
	i_sort_etat = faux
	COL_ColMapActivationSet(all, none)
	DYN_GravitySet(H_GRAVITY)
	COL_ColSetActivationSet(C_bit_zdm_pied, 0)
	ResetPath()
	return
}

// INITIALISATION ETAT ==============================================================
if (i_etat_courant != ETAT_IA_stunned)
{
	if(i_etat_courant != ETAT_IA_stunnedwalk && i_etat_courant != ETAT_IA_carried)
		STATS_IncInjuries( joueur, i_id_humain)

	i_dernier_etat = i_etat_courant
	i_etat_courant = ETAT_IA_stunned
	f_time_etourdit = 0
	if(fct_main_etat == nofunc) fct_main_etat = "H_ETAT_IA_suivi"

	if (fct_last_etat)
	{
		i_sort_etat = vrai
		AI_Execute(fct_last_etat)
	}

	fct_last_etat = AI_TrackCurGet()

	for (ti_i = 0; ti_i < Ci_LNK_TYPE_NB; ti_i++)
		ai_SRV_ENABLE[ti_i] = faux

	ai_SRV_ENABLE[Ci_LNK_CARRY] = vrai
	ai_SRV_ENABLE[Ci_LNK_GRAB_RAPTOR]	= vrai
	ai_SRV_ENABLE[Ci_LNK_PSSS]					= vrai

	DYN_GravitySet(Cv_NullVector)
	DYN_FrictionVectorSet(cvector(1.0, 1.0, 0.0))
	COL_ColMapActivationSet(none, all)
	COL_ColSetActivationSet(0, C_bit_zdm_pied)
	o_healer = LNK_ClientGet(Ci_LNK_INTERACTION, mid_healer, faux, nofunc, nofunc, nofunc)
		
	i_etat_state = 0
	f_time_start_etat = 0.0
}
else
{
	f_time_start_etat += TIME_GetDt()
}

	
// ANALYSE =======================================================================

// Pssss	
OBJ_Me().des_int1 = Ci_DISPLAY_PSSS
if(ao_SRV[Ci_LNK_PSSS])
{
	ForceRegardSurToujours(AI_MainActorGet(C_ID_Jack), 1.0)
	ao_SRV[Ci_LNK_PSSS] = LNK_ServeurGet(Ci_LNK_PSSS, amid_SRV_LIAISON_ID[Ci_LNK_PSSS], faux, nofunc, nofunc)
	if(i_id_humain == C_ID_Ann)
	{
		if(i_in_fight)
			i_request_txt = Gene_C_occupied_urg
		else
			i_request_txt = Gene_C_occupied_med
	}
}

AI_Execute("H_exec_read_world")
AI_Execute("H_exec_serveur_get")
AI_Execute("H_exec_change_etat")

o_paf_sender = nobody
AI_Execute("H_exec_ch_Stimulus_Paf")
if (o_paf_sender) 
{
	if(ACT_ActionGet() == Action_Humain_Blesse_Assis) i_flag_crouch = vrai
	macro_change_etat("H_ETAT_IA_paf")
}

// =======================================================================================

// Force un attente stress au tout debut
//if(!i_flag_hasbeen_carried && !i_flag_stunned_crouch)
//{
//	if(f_time_start_etat < 1.0)
//	{
//		ACT_ActionSet1(Action_Humain_Blesse_Attente)
//		return
//	}	
//	
//	if(ACT_ActionIsTransition()) return
//}

// ANN n'est pas la
//if (i_id_humain != C_ID_Ann)
//{
//	to_obj = @"univ" ao_AllHumains[ C_ID_Ann ]
//	if(!to_obj || @to_obj OBJ_FlagsControlGet() & OBJ_C_ControlFlag_ForceInactive)
//	{
//		if(f_time_start_etat > 2)
//		{
//			@"univ" LIFE_HumainEtat[i_id_humain] = Life_ETAT_Mort
//			macro_change_etat("H_ETAT_IA_mort")
//		}
//	}		
//}

// Dans l'eau haute
//if(i_flag_nage || f_hauteur_eau >= Hauteur_Eau_Haute)
//{
//	@"univ" LIFE_HumainEtat[i_id_humain] = Life_ETAT_Mort
//	macro_change_etat("H_ETAT_IA_mort")
//}

// AUTO SOIN
if (i_id_humain == C_ID_Ann)
{
	if(!o_carry && f_time_start_etat > 5) 
	{
		@"univ" LIFE_HumainEtat[i_id_humain] = Life_ETAT_Normal
		@"univ" LIFE_HumainPeau[i_id_humain] = PEAU_SOIGNE
	}
}

// DEMANDE DE SOIN
else if(i_etat_state != 2 && !o_carry)
{
	if (o_healer && !MSG_GlobalIsValid(mid_healer)) o_healer = nobody	
	if (!o_healer && (i_flag_interaction_ok || (i_id_humain != C_ID_Ann)))
	{
		i_interaction_type = Ci_LNK_INTERACTION_HEAL
		o_healer = LNK_ClientGet_Interaction( mid_healer, 1, "H_INTERACTION_add_data", nofunc, nofunc)
	}
}

// =======================================================================================

if(i_etat_state != 2)	
{	
	// PORTEZ MOI
	if (ao_SRV[Ci_LNK_CARRY]) macro_change_etat("H_ETAT_IA_carried")	
	if(!o_healer && (ACT_ActionGet() == Action_Humain_Blesse_Assis || i_noanim_stunned))
	{
		i_interaction_type = Ci_LNK_INTERACTION_CARRY
		o_carry = LNK_ClientGet_Interaction( mid_carry, 1, "H_INTERACTION_add_data", nofunc, nofunc)
	}
	else if(o_carry)
	{
		o_carry = LNK_ClientGet(Ci_LNK_INTERACTION, mid_carry, faux, nofunc, nofunc, nofunc)
	}
	
	if (o_carry && !MSG_GlobalIsValid(mid_carry)) o_carry = nobody
}

if (i_etat_state == 0)
{
	if(o_carry || o_healer) f_time_prestunned = 0
	
	// Auto conval
	if(i_id_humain != C_ID_Ann && !o_healer && !o_carry && (f_time_start_etat + f_time_prestunned) > 8)
	{
		if(!i_noanim_stunned)
		{
			if(@"univ" LIFE_HumainEtat[i_id_humain] == Life_ETAT_Agonisant)
			{
				@"univ" LIFE_HumainEtat[i_id_humain] = Life_ETAT_Conval
				f_time_conval  = TIME_Get()
			}
		}
	}

	// Je me releve
	if(@"univ" LIFE_HumainEtat[i_id_humain] == Life_ETAT_Conval || @"univ" LIFE_HumainEtat[i_id_humain] == Life_ETAT_Normal)
	{
		COL_ColSetActivationSet(C_bit_zdm_pied, 0)
		i_etat_state = 2
		ACT_ActionSet1(Action_Stress_Attente)
		f_etat_statetimer = 1.0
	}
	
	// Anim attente de soin
	else 
	{
		if(i_flag_hasbeen_carried || o_carry)
			ti_assezdeplace = vrai
		else
			ti_assezdeplace = AssezDePlacePourTomber(OBJ_PosGet())	
		if(!ti_assezdeplace && o_healer)
		{
			v_joy_dir = @o_healer OBJ_PosGet() - OBJ_PosGet()
			v_joy_dir.z = 0		
			OBJ_BankingGeneralSet(MATH_VecBlendRotate(OBJ_SightGet(), v_joy_dir, 4 * TIME_GetDt()), Cv_VerticalVector)
		}
//		else if(o_carry)
//		{
//			v_joy_dir = OBJ_PosGet() - @o_carry OBJ_PosGet()
//			v_joy_dir.z = 0		
//			OBJ_BankingGeneralSet(MATH_VecBlendRotate(OBJ_SightGet(), v_joy_dir, TIME_GetDt()), Cv_VerticalVector)
//		}
	
		if(o_healer && @"KingKong/Humain" o_healer i_flag_jesoigne)
		{
			if(ti_assezdeplace)
			{
				i_flag_crouch = vrai
				ACT_ActionSet1(Action_Humain_Blesse_Assis)
			}
			else
				ACT_ActionSet1(Action_SeFaitSoignerDebout)
		}
		else if(ti_assezdeplace)
		{
			i_flag_crouch = vrai
			if(i_id_humain == C_ID_Denham && o_ennemi_proche && OBJ_SqrDist(o_ennemi_proche) < 2 && !o_carry)
				ACT_ActionSet1(Action_Humain_Blesse_Repousse)
			else if(i_id_humain == C_ID_Ann && f_time_start_etat > 1 && !o_carry)
			{
				ACT_ActionSet1(Action_Ann_SeSoigneAssis)
				if(i_in_fight) AskText(TEXTE_ALaide, GeneAn_C_cure_herself, 10, nobody)
			}
			else if(!i_noanim_stunned)
				ACT_ActionSet1(Action_Humain_Blesse_Assis)
		}
		else if (i_id_humain == C_ID_Ann && f_time_start_etat > 1)
		{
			ACT_ActionSet1(Action_Ann_SeSoigne)
			if(i_in_fight) AskText(TEXTE_ALaide, GeneAn_C_cure_herself, 10, nobody)
		}
		else
		{
			ACT_ActionSet1(Action_Humain_Blesse_Attente)
		}
	}
}
// Je suis en train de me relever
else 
{
	COL_ColSetActivationSet(C_bit_zdm_pied, none)
	o_healer = LNK_ClientGet(Ci_LNK_INTERACTION, mid_healer, faux, nofunc, nofunc, nofunc)
	o_carry = LNK_ClientGet(Ci_LNK_INTERACTION, mid_carry, faux, nofunc, nofunc, nofunc)
	o_paf_sender = nobody
	f_etat_statetimer -= TIME_GetDt()
	if ( f_etat_statetimer <= 0 ) 
	{
		if(!ACT_ActionIsTransition())
		{
			macro_change_etat( fct_main_etat )
		}
	}
}
	
i_flag_suivi_regard = faux
float			tf_pond
vector		tv_pos
vector		tv_temp, tv_temp1
object		to_cache, to_next
int				ti_i, ti_terrain
float			tf_dot, tf_temp
messageid	tmid_info
object		to_obj

tv_pos = pop
to_cache = pop
tf_pond = 0

if(!o_ref_cache_0D) o_ref_cache_0D = OBJ_Me()

// Distance a ann
//if(i_id_humain != Ci_Personnage_Ann)
//{
//	to_next = @"univ" ao_AllHumains[Ci_Personnage_Ann]
//	if(to_next) tf_pond = MATH_VecNorm(tv_pos - @to_next OBJ_PosGet())
//}

// Distance a moi meme
tf_pond += MATH_VecNorm(tv_pos - @o_ref_cache_0D OBJ_PosGet())

// Sens de progression local
// Dans le sens de prog :  * 0.5
// Dans le sens opposé de prog : * 2
// Perpendiculaire : * 1
if(o_nearest_me)
{
	ti_i = (i_id_humain << 8) | NetNextWP_Mode_capa_aleatoire
	to_next = WAY_NetNextWP(move_network, o_nearest_me, ti_i, 0x00000001)
	if(to_next)
	{
		tv_temp = @to_next OBJ_PosGet() - @o_nearest_me OBJ_PosGet()
		MATH_VecSetHorzNormalize(tv_temp)
		tv_temp1 = tv_pos - OBJ_PosGet()
		MATH_VecSetHorzNormalize(tv_temp1)
		tf_dot = MATH_VecDotProduct(tv_temp, tv_temp1)
		tf_dot = ((tf_dot + 1) * 0.75) + 0.5
		tf_pond /= tf_dot
	}
}

// Si jack est dans la cache, et jack est safe, et j'en suis pas loin
to_obj = AI_MainActorGet(0)
if(@to_obj COL_Pivot_BVCollide(to_cache))
{
	if(OBJ_SqrDist(to_cache) < 10 * 10)
	{
		if(!EVENT_InfoCanGaoSeeGao(nobody, to_obj, tmid_info))
		{
			tf_pond /= 100
		}
	}
}

// Si on cherche une cache visible, une cache non visible est moins bonne
if(i_cherche_cache_visible)
{
	tv_temp = tv_pos - @get_camera OBJ_PosGet()
	tf_temp = MATH_VecNorm(tv_temp)
	MATH_VecSetHorzNormalize(tv_temp)
	tf_dot = MATH_VecDotProduct(tv_temp, @get_camera OBJ_SightGet())
	if(tf_dot < 0.8)
		tf_pond *= 10
	else
		tf_pond *= 1 + (1 - tf_dot)
}

ti_terrain = GRID_CapaGet(tv_pos) & tag_grid_terrain
switch(ti_terrain)
{
//	case Ci_sol_herbe : 				tf_pond -= 4 	break
	case Ci_sol_herbe_haute : 	tf_pond -= 5 	break
	case Ci_sol_plafondbas : 		tf_pond -= 10 	break
}

push(tf_pond)
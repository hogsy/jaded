#include "H_action.var"
#include "H_defines.var"


int			ti_i, ti_temp
int			ti_numero
int			ti_index
int			ti_flag_grab
int			ti_context
float		tf_dist
float		tf_sign
float		tv_val
float		tf_offset
float		tf_temp
int			ti_move
int			ti_weapon

vector	tv_dest_pos
vector	tv_temp

object	to_trap
object	to_obj
object	to_enn

// SORTIE ETAT ===================================================================
if (i_sort_etat)
{
	if(LNK_InteractionTypeGet(amid_SRV_LIAISON_ID[Ci_LNK_INTERACTION]) == Ci_LNK_INTERACTION_MUNITION)
	{
		o_obj_interaction = ao_SRV[Ci_LNK_INTERACTION]
		ao_SRV[Ci_LNK_INTERACTION] = LNK_ServeurGet(Ci_LNK_INTERACTION, amid_SRV_LIAISON_ID[Ci_LNK_INTERACTION], faux, nofunc, nofunc)
	}
	
	i_sort_etat = faux	
	return
}

// INITIALISATION ETAT ==============================================================
if (i_etat_courant != ETAT_IA_grabmunition)
{
	i_dernier_etat = i_etat_courant
	i_etat_courant = ETAT_IA_grabmunition

	if (fct_last_etat)
	{
		i_sort_etat = vrai
		AI_Execute(fct_last_etat)
	}

	fct_last_etat = AI_TrackCurGet()

	for (ti_i = 0; ti_i < Ci_LNK_TYPE_NB; ti_i++)
		ai_SRV_ENABLE[ti_i] = faux

	// AUTORISATIONS DES GRABS
	ai_SRV_ENABLE[Ci_LNK_GRAB_RAPTOR]	= vrai
	
	// AUTORISATION DES AIDES
	ai_SRV_ENABLE[Ci_LNK_INTERACTION] 		= vrai
	ai_SRV_ENABLE[Ci_LNK_PSSS] 					= vrai

	DYN_SpeedSetVector( Cv_NullVector)
	DYN_TractionSet( Cv_NullVector)
	
	f_time_start_etat = 0.0	
	f_time_psss = 0
	
	to_obj = ao_CL[Ci_LNK_INTERACTION]
	if(OBJ_SqrDist(to_obj) > 5 * 5)
	{
		if(i_in_fight)
			i_request_txt = GeneHay_C_get_ammo_urg
		else
			i_request_txt = GeneHay_C_get_ammo_low
	}
}
else
{
	f_time_start_etat += TIME_GetDt()
}

// ANALYSE  =====================================================================
AI_Execute("H_exec_read_world")
AI_Execute("H_exec_serveur_get")
AI_Execute("H_exec_change_etat")
AI_Execute("H_exec_ch_Stimulus_Paf")
if (o_paf_sender) macro_change_etat("H_ETAT_IA_paf")

// INTERACTION
OBJ_Me().des_int1 = Ci_DISPLAY_PSSS
if (!ao_CL[Ci_LNK_INTERACTION]) macro_change_etat(fct_main_etat)

// COMPORTEMENT ==============================================================

// Je vais jusqu'à celui qui va me filer des munitions
to_obj = ao_CL[Ci_LNK_INTERACTION]
v_way_destpos = @to_obj OBJ_PosGet() 

CommonMove()

// Orientation
if(!f_joy_norm)
{
	OrienteVersEnnemiProche()
}

// Arrivée ?
if(i_flag_arrived)
{
	f_time_psss += TIME_GetDt()
	ACT_ActionSet1(Action_Stress_Attente)
	ForceRegardSur(to_obj, 0.3)
	
	if(f_time_psss > 1)
	{
		i_request_txt = GeneHay_C_get_from_jimmy
		
		// Recup munitions
		ti_weapon = @"univ" i_weapon_ID[i_id_humain]
		ti_index = Humains_GetIndex(to_obj)
		ti_temp = @"univ" i_weapon_ammunition_reserve[ti_weapon][i_id_humain]
		ti_temp = @"univ" i_weapon_ammunition_max[ti_weapon][i_id_humain] - ti_temp
		if(ti_temp > @"univ" i_weapon_ammunition_reserve[ti_weapon][ti_index])
			ti_temp = @"univ" i_weapon_ammunition_reserve[ti_weapon][ti_index]
		@"univ" i_weapon_ammunition_reserve[ti_weapon][i_id_humain] += ti_temp
		@"univ" i_weapon_ammunition_reserve[ti_weapon][ti_index] -= ti_temp
		macro_change_etat(fct_main_etat)
	}
}
else
{
	f_time_psss = 0
	AI_Execute("H_exec_select_action")
}


object	to_torse
vector	tv_temp, tv_temp1
object	to_obj
float		tf_temp
int			cbact
int			ti_i
message m_cine
float	tf_norm
int x, y
int ti_ronce

#include "H_defines.var"

if( @get_global i_Player_is_Kong )
	return

// Annule intention
//if(PRG_Intention && !COL_Pivot_BVCollide(PRG_Intention)) PRG_Intention = nobody
//if(PRG_Idle && !COL_Pivot_BVCollide(PRG_Idle)) PRG_Idle = nobody

/////// CHEAT
#ifndef _FINAL_
if(i_flag_cheat_move)
{
	to_obj = VIEW_GetObject(0)
	tv_temp = -IO_JoyGetMove()
	if(MATH_AbsFloat(tv_temp.x) < 0.2) tv_temp.x = 0
	if(MATH_AbsFloat(tv_temp.y) < 0.2) tv_temp.y = 0
	tv_temp = @to_obj MATH_VecLocalToGlobal(tv_temp)
	tv_temp.z = IO_JoyGetMove1().y
	if(MATH_AbsFloat(tv_temp.z) < 0.2) tv_temp.z = 0
	tv_temp *= TIME_GetDt() * 15
	OBJ_PosSet(OBJ_PosGet() + tv_temp)
	COL_StartMatrixSet(OBJ_PosGet())
	DYN_SpeedSetVector(Cv_NullVector)
	DYN_GravitySet(Cv_NullVector)
	COL_ColSetActivationSet(0, C_bit_zdm_pied)
	COL_ColSetActivationSet(0, C_bit_zdm_recalagespecial)
	i_flag_old_cheat_move = vrai
	i_flag_snipe = faux
	i_flag_tir = faux
}
else if(i_flag_old_cheat_move)
{
	i_flag_old_cheat_move = faux
	COL_ColSetActivationSet(C_bit_zdm_pied, 0)
	COL_ColSetActivationSet(C_bit_zdm_recalagespecial, 0)
}
#endif
//////////////////

// Vision
MakeVision()


// Sound
AI_Execute("H_exec_sound")

// ZONE
to_torse = ANI_CanalObjectGet(Anim_Canal_Torse)
if(!to_torse) 
{
	to_torse = ANI_CanalObjectGet(Anim_Canal_Tete)
	COL_ZonePosSet(C_zde_corps, MATH_VecGlobalToLocal(@to_torse OBJ_PosGet() - OBJ_PosGet() - cvector(0,0,0.5)))
}
else
{
	COL_ZonePosSet(C_zde_corps, MATH_VecGlobalToLocal(@to_torse OBJ_PosGet() - OBJ_PosGet()))
}

// DEUXIEME ZONE (CROUCH)
if(i_flag_crouch)
	COL_ZonePosSet(C_zdm_recalagespecial, cvector(0, 0, 0.6))
else
	COL_ZonePosSet(C_zdm_recalagespecial, cvector(0, 0, 1.5))

// Psst
if(@get_global i_flag_rage)
	OBJ_Me().des_int1 = Ci_DISPLAY_DUMMY



if( i_jump_from_KK_flag )
	return // !!!!!!


// BLOQUAGE TRANSLATION
/////////////////////////////////////////////////
if( @get_global i_cam_blocked_trans || i_soin_en_cours )
{
	if(@get_global i_cam_blocked_trans) f_disable_coup = 5.1
	if(MATH_VecNull(v_org_pos_trans)) 
	{
		if(WOR_GetKey() == 0x08006177)
		{
			v_org_pos_trans = cvector(318.259,336.35,9.48)
		}
		else
		{
			v_org_pos_trans = OBJ_PosGet()
		}
		
		tv_temp = v_org_pos_trans
		if(OBJ_FlagsIdentityTest(OBJ_C_IdentityFlag_Hierarchy))
		{
			to_obj = OBJ_HierarchyGet()
			v_org_pos_trans = @to_obj MATH_VecGlobalToLocal(v_org_pos_trans - @to_obj OBJ_PosGet())
		}
	}
	else
	{
		tv_temp = v_org_pos_trans
		if(OBJ_FlagsIdentityTest(OBJ_C_IdentityFlag_Hierarchy))
		{
			to_obj = OBJ_HierarchyGet()
			tv_temp = @to_obj MATH_VecLocalToGlobal(v_org_pos_trans)
			tv_temp += @to_obj OBJ_PosGet()
		}
	}
	
	if(@get_global i_cam_mode_recal)
	{
		OBJ_PosSet(MATH_VecBlend(OBJ_PosGet(), @get_global v_cam_original_pos, 3 * TIME_GetDt()))
	}
	else
	{
		tf_temp = 10 * TIME_GetDt()
		OBJ_PosSet(MATH_VecBlend(OBJ_PosGet(), tv_temp, tf_temp*tf_temp))
	}
	if(@get_global i_cam_mode_recal || WOR_GetKey() == 0x08006177)
		DYN_SpeedSetVector(Cv_NullVector)
	else
		DYN_SpeedSetVector(DYN_SpeedGetVector() * 0.1)
}
else
	v_org_pos_trans = Cv_NullVector
f_disable_coup -= TIME_GetDt()
if(f_disable_coup < 0) f_disable_coup = 0

// RESTORATION ORIENTATION
//////////////////////////////////////////////////
if(ACT_ActionFinished())
	cbact = 0
else
	cbact = ACT_CustomBitGet()
if( ! f_joy_norm && (cbact & 0x10) )
{
	OBJ_SightGeneralSet(v_org_sight, Cv_VerticalVector)
	return 
}

/////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
// RONCES
ti_ronce = faux
v_sum_ronce = DYN_SpeedGetVector()
v_org_speed = v_sum_ronce
tf_norm = MATH_VecNorm(DYN_SpeedGetVector()) + 0.6
if(IsSolPaf(OBJ_PosGet()) && !CaseBrulee(OBJ_PosGet()))
{
	ti_ronce = vrai
	for(x = -1; x <= 1; x++)
	{
		for(y = -1; y <= 1; y++)
		{
			if(x == 0 && y == 0) continue
	
			tv_temp = GRID_PosGet(OBJ_PosGet())
			tv_temp.x += x
			tv_temp.y += y
			
			// Mur a coté
			if(IsSolPaf(tv_temp) && !CaseBrulee(OBJ_PosGet()))
			{
				tv_temp = OBJ_PosGet() - tv_temp
				tv_temp.z = 0
				tf_temp = (1.5 - MATH_VecNorm(tv_temp))
				tf_temp *= 1.3
				if(tf_temp > 0)
				{
//						tf_norm = MATH_FloatMax(tf_norm, 2)
					MATH_VecSetNorm(tv_temp, tf_temp * tf_norm)
					v_sum_ronce += tv_temp
				}
			}
		}
	}
}

tf_temp = MATH_VecNorm(v_sum_ronce)
if(ti_ronce && MATH_VecNorm(IO_JoyGetMove()) > 0.8 && MATH_AbsFloat(tf_temp - tf_norm) > (80 * tf_norm) / 100)
	f_time_case_ronce += TIME_GetDt()
else
	f_time_case_ronce = 0

DYN_SpeedSetVector(v_sum_ronce)
/////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////

if(@"univ" LIFE_HumainEtat[i_id_humain] == Life_ETAT_Mort)
	DYN_SpeedSetVector(Cv_NullVector)


object			to_obj, to_obj1, to_pere, to_enn
float				pound
vector			tv_temp
int					ti_danger, ti_danger1
int					ti_ok
int					ti_rank
messageid		EVT_ID
int					ti_state
vector			tv_temp1
float				tf_temp
int					i, ifi, ifj
#define DEST_DIST	(25 * 25)

to_obj = WAY_ShortGet(0)
to_obj1 = WAY_ShortGet(1)
pound = 0
 
// Un ennemi proche d'un wp => poids plus fort
if(i_way_evite_ennemi && !i_flag_rush)
{
	ifi = -1
	ifj = -1
	ti_danger = 0
	ti_danger1 = 0
	
	for(i = 0; i < @get_global i_CB_way_num; i++)
	{
		if(@get_global i_CB_way_obj[i] == to_obj) ifi = i
		if(@get_global i_CB_way_obj[i] == to_obj1) ifj = i
		if(ifi >= 0 && ifj >= 0) break
	}
	
	if(ifi == -1)
	{
		if(OBJ_SqrDistHorz(to_obj) < DEST_DIST)
		{
//			DBG_RenderVector(@to_obj OBJ_PosGet(), cvector(0,0,10), 0xFF00FF00)
			ti_danger = HL_PosDangereuse(@to_obj OBJ_PosGet()) * 100
			@get_global i_CB_way_obj[@get_global i_CB_way_num] = to_obj
			@get_global i_CB_way_danger[@get_global i_CB_way_num] = ti_danger
			@get_global i_CB_way_num++
		}
	}
	else
	{
		ti_danger = @get_global i_CB_way_danger[ifi]
	}

	if(ifj == -1)
	{
		if(OBJ_SqrDistHorz(to_obj1) < DEST_DIST)
		{
//			DBG_RenderVector(@to_obj1 OBJ_PosGet(), cvector(0,0,10), 0xFF00FF00)
			ti_danger1 = HL_PosDangereuse(@to_obj1 OBJ_PosGet()) * 100
			@get_global i_CB_way_obj[@get_global i_CB_way_num] = to_obj1
			@get_global i_CB_way_danger[@get_global i_CB_way_num] = ti_danger1
			@get_global i_CB_way_num++
		}
	}
	else
	{
		ti_danger1 = @get_global i_CB_way_danger[ifj]
	}
	
	pound += MATH_FloatMax(ti_danger, ti_danger1)
	
	if(OBJ_SqrDistHorz(to_obj) < DEST_DIST || OBJ_SqrDistHorz(to_obj1) < DEST_DIST)
	{
		ti_rank = -1
		EVT_ID = MSG_GlobalScan(C_EVENT_TYPE_Enemy, &ti_rank)
		while(MSG_GlobalIsValid(EVT_ID))
		{
			to_enn = EVENT_PereGet( EVT_ID )
			ti_state = EVENT_EnemyStateGet( EVT_ID )
			if (ti_state == C_EVENT_EnemyState_Fight || ti_state == C_EVENT_EnemyState_Wait || ti_state == C_EVENT_EnemyState_Neutral) 
			{
				// Un ennemi entre les deux wps
				tv_temp = @to_obj OBJ_PosGet() - @to_enn OBJ_PosGet()
				tv_temp1 = @to_obj1 OBJ_PosGet() - @to_enn OBJ_PosGet()
				if(MATH_VecDotProduct(tv_temp, tv_temp1) < 0)
				{				
					tv_temp = @to_obj OBJ_PosGet() - @to_obj1 OBJ_PosGet()
					if(MATH_VecNullEpsilon(tv_temp))
						tv_temp = cvector(1,0,0)
					else
						MATH_VecSetHorzNormalize(tv_temp)
					tv_temp1 = @to_obj OBJ_PosGet() - @to_enn OBJ_PosGet()
					if(MATH_VecNullEpsilon(tv_temp1))
						tv_temp1 = cvector(1,0,0)
					else
						MATH_VecSetHorzNormalize(tv_temp1)
					tf_temp = MATH_AbsFloat(MATH_VecDotProduct(tv_temp, tv_temp1))
					if(tf_temp > 0.9)
					{
						pound += 100 * @"univ" Enemy_af_Danger[EVENT_EnemyTypeGet( EVT_ID )]
					}
				}		
			}
			
			EVT_ID = MSG_GlobalScan(C_EVENT_TYPE_Enemy, &ti_rank)
		}
	}
}

WAY_ShortSet(0, pound)

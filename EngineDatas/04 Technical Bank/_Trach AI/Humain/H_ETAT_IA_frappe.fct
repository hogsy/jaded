#include "H_defines.var"
#include "H_action.var"


int			ti_context
int			ti_i
float		tf_liferatio
object	to_who

// SORTIE ETAT ===================================================================
if (i_sort_etat)
{
	f_time_enjoue = 3.0
	i_sort_etat = faux
	i_flag_tir_secondaire = faux
	COL_ColSetActivationSet(0, C_bit_zde_fight)	
	return
}

// INITIALISATION ETAT ==============================================================
if (i_etat_courant != ETAT_IA_frappe)
{
	i_dernier_etat = i_etat_courant
	i_etat_courant = ETAT_IA_frappe
	i_coup_gauche = 1
	COL_ColSetActivationSet(C_bit_zde_fight, 0)

	if (fct_last_etat)
	{
		i_sort_etat = vrai
		AI_Execute(fct_last_etat)
	}

	fct_last_etat = AI_TrackCurGet()
	for (ti_i = 0; ti_i < Ci_LNK_TYPE_NB; ti_i++)
		ai_SRV_ENABLE[ti_i] = faux
		
	ai_SRV_ENABLE[Ci_LNK_GRAB_RAPTOR]	= vrai
	ai_SRV_ENABLE[Ci_LNK_PSSS]					= vrai

	switch(@"univ" i_weapon_ID[i_id_humain])
	{
		case Ci_weapon_ID_bambou :
		case Ci_weapon_ID_bambou_moy :
		case Ci_weapon_ID_bambou_petit :
			if(i_id_humain == C_ID_Ann)
			{
				if(MATH_RandInt(0, 2) == 1)
					ACT_ActionSet(Action_FrappeBambou1)
				else
					ACT_ActionSet(Action_FrappeBambou)
			}
			else
			{
				ACT_ActionSet( Action_FrappeBambou)
			}
			
			v_ZdeFight_Pos = cvector(0,0,0.4)
			f_ZdeFight_Size = 0.6
			break
			
		default:
			ACT_ActionSet( Action_Hayes_Attaque_CoupDeCrosse )
			v_ZdeFight_Pos = cvector(0,0,0.2)
			f_ZdeFight_Size = 0.4
//			SND_RequestPlay( SND_Jack_CoupDeCrosse )
			break
	}
	
	// ZDE Fight POSITIONNEMENT -------------------------------------------------
	o_ZdeFight_Pos = o_canal_maind
	// ZDE Fight POSITIONNEMENT -------------------------------------------------	

	i_flag_zde_fight_enable = vrai
	f_time_start_etat = 0.0
	
//	AskText(TEXTE_Fighting, Gene_C_fighting, 30, nobody)
	if(i_request_txt == -1) i_request_txt = Gene_C_throw_spear
}
else
{
	f_time_start_etat += TIME_GetDt()
}

// ANALYSE ======================================================================

// Pssss	
OBJ_Me().des_int1 = Ci_DISPLAY_PSSS
if(ao_SRV[Ci_LNK_PSSS])
{
	ao_SRV[Ci_LNK_PSSS] = LNK_ServeurGet(Ci_LNK_PSSS, amid_SRV_LIAISON_ID[Ci_LNK_PSSS], faux, nofunc, nofunc)
	return
}

AI_Execute("H_exec_read_world")
AI_Execute("H_exec_serveur_get")
AI_Execute("H_exec_change_etat")
AI_Execute("H_exec_ch_Stimulus_Paf")
if (o_paf_sender) macro_change_etat("H_ETAT_IA_paf")
	
// COMPORTEMENT ================================================================
to_who = nobody
if(!joueur && o_cine_shoot)
{
	o_target_stimulus_vis = o_cine_shoot
	to_who = o_target_stimulus_vis
	OrienteVersTolerReal(o_target_stimulus_vis, 0.9, 8.0)
}
else if(!joueur && (!f_time_start_etat || !o_target_stimulus_vis))
{
	ComputeCibleAShooter( -1.0 )
	if(o_target_stimulus_vis) 
	{
		to_who = o_target_stimulus_vis
		OrienteVersTolerReal(o_target_stimulus_vis, 0.9, 8.0)
	}
	else if(o_ennemi_proche)
	{
		to_who = o_ennemi_proche
		OrienteVersTolerReal(o_ennemi_proche, 0.85, 8.0)
	}
}

if(to_who && OBJ_SqrDistHorz(to_who) < 2.0 * 2.0)
{
	DYN_SpeedSetVector(Cv_NullVector)
}

//if(IsWater(i_ground_ID))
//{
//	DYN_SpeedSetVector(Cv_NullVector)
//}

if((ACT_ActionGet() == Action_Hayes_Attaque_CoupDeCrosse && ACT_ActionItemGet() == 1))
	AI_Execute("H_exec_test_ZDE_FIGHT")
else if(ACT_ActionGet() == Action_FrappeBambou )
{
	i_flag_zde_fight_enable = vrai
	AI_Execute("H_exec_test_ZDE_FIGHT")
}

if(ACT_ActionFinished())
{
	if(o_pref_target_stimulus_vis)
	{
		o_pref_target_stimulus_vis = nobody
		AI_TrackChange(Ci_Track_Etat, fct_prev_etat)
	}
	else
		AI_TrackChange(Ci_Track_Etat, fct_main_etat)
}
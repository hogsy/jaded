#include "CM_Defines.var"

object	to_obj
vector	tv_pos, tv_pos_actor
vector	tv_sight
vector	tv_joy
vector	tv_temp

to_obj = o_mf_actor
if(!to_obj) return

// Sight
MATH_VecSetHorzNormalize(v_mf_req_sight)
tv_sight  = MATH_VecRotate(v_mf_req_sight, OBJ_HorizonGet(), f_mf_anglez)
if(i_mf_cut)
{
	v_mf_sight = tv_sight
	f_mf_dist = f_mf_req_dist
	f_mf_z = f_mf_req_z
	v_mf_tgt = v_mf_req_tgt
}
else
{
	v_mf_sight = MATH_VecBlendRotate(v_mf_sight, tv_sight, f_mf_blend_sight * TIME_GetDt())
	f_mf_dist = MATH_FloatBlend(f_mf_dist, f_mf_req_dist, f_mf_blend_dist * TIME_GetDt())
	f_mf_z = MATH_FloatBlend(f_mf_z, f_mf_req_z, f_mf_blend_z * TIME_GetDt())
	v_mf_tgt = MATH_VecBlend(v_mf_tgt, v_mf_req_tgt, f_mf_blend_tgt * TIME_GetDt())
}

// Position
tv_pos_actor = @to_obj OBJ_PosGet() + cvector(0,0,f_mf_perso_z)
tv_sight = v_mf_sight
MATH_VecSetNorm(tv_sight, f_mf_dist)
tv_pos = tv_pos_actor + tv_sight
tv_pos.z += (f_mf_z - f_mf_perso_z)
if(CM_Ray(tv_pos_actor, tv_pos))
{
	tv_pos = COL_RayObject_PosGet()
	tv_temp = tv_pos_actor - tv_pos
	MATH_VecSetNorm(tv_temp, 0.1)
	tv_pos += tv_temp
}

if(!i_mf_cut) tv_pos = MATH_VecBlend(OBJ_PosGet(), tv_pos, f_mf_blend_pos * TIME_GetDt())
OBJ_PosSet(tv_pos)

// Target
///////////////////////////////////////
if(!MATH_VecNull(v_mf_tgt_req_pos))
{
	if(i_mf_cut)
		v_mf_tgt_pos = v_mf_tgt_req_pos
	else
		v_mf_tgt_pos = MATH_VecBlend(v_mf_tgt_pos, v_mf_tgt_req_pos, f_mf_blend_pos_tgt * TIME_GetDt())	
}
else
{
	v_mf_tgt_pos = @to_obj OBJ_PosGet() + @to_obj MATH_VecLocalToGlobal(v_mf_tgt)
}

tv_pos = v_mf_tgt_pos - OBJ_PosGet()
OBJ_SightGeneralSet(tv_pos, cvector(0,0,1))

i_mf_cut = faux
VIEW_AssignObject(0)
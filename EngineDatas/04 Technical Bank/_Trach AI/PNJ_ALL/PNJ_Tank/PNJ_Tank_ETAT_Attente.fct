#include "PNJ_Tank_defines.var"

Include_UltraProcedure_Header

int			ti_canal
int			ti_sound
vector	tv_impact
vector	tv_normal
int			ti_flag_move
vector	tv_new_sight
vector	tv_pos
object	to_tourelle
vector	tv_axis
vector	tv_ray_start_pos
int			ti_shoot_in_one_seconde
object	to_hotspot
object	to_ray_object
object	to_head
object	to_torse
vector	tv_ray_dir
int			ti_filter 


// SORTIE ETAT =============================================================================
if (i_sort_etat)
{
	i_sort_etat = faux

	if (i_snd_loop != -1)
	{
		SND_Destroy(i_snd_loop)
		i_snd_loop = -1
	}

	 if (@"PNJ_Pacifique/PNJ_Car_Manager" o_car_manager o_shooting_car == OBJ_Me())
		@"PNJ_Pacifique/PNJ_Car_Manager" o_car_manager o_shooting_car	= nobody

	return
}

// INITIALISATION ETAT ========================================================================
if (i_etat_courant != ETAT_Attente)
{
	i_dernier_etat = i_etat_courant
	i_etat_courant = ETAT_Attente

	if (fct_last_etat)
	{
		i_sort_etat = vrai
		AI_Execute(fct_last_etat)
	}

	ODE_Enable(vrai)

	fct_last_etat = AI_TrackCurGet()
	fct_main_etat = fct_last_etat

	f_time_start_etat = 0.0
}
else
{
	f_time_start_etat += TIME_GetDt()
	f_delai_after_shoot += TIME_GetDt()
}

// ANALYSE ===============================================================================
if (dodged_by_car)
	@o_car_manager PNJ_Car_Manager_Add_Gao(OBJ_Me(), 7.0, i_my_index)

if (!i_flag_Shoot2Kill)
{
	AI_Execute("PNJ_Tank_CheckPaf")
	if (i_flag_paf)
		AI_TrackCurChangeNow("PNJ_Tank_ETAT_Paffed")
		
#ifndef _FINAL_
 	if (IO_KeyPressed(65) && AI_GetCurSystem() == 10)
 		AI_TrackCurChangeNow("PNJ_Tank_ETAT_Paffed")
#endif
}
	
// COMPORTEMENT =========================================================================

#ifndef _FINAL_
if (i_render_dist)
	DBG_RenderSphere(OBJ_PosGet(),MATH_FloatSqrt(f_SQRdistKill),color_rouge)
#endif

OBJ_CapaSet(none,Obj_Capa_Switch)
if ( f_time_min || ( AI_TriggerIsValid(t_activation) && call_trigger(t_activation) ) )
{
	// TEXT "IL EST Là" ==============
	if (!i_trig)
	{
		SPEECH_RequestPost
		(
			OBJ_Me(),
			OBJ_Me(), 
			SPEECH_CteTxg_SpeNewYork,
			GeneNY_C_tank_see_kong, 
			1.0, 
			SPEECH_Cte_PriorityDefault, 
			5,
			0,
			0,
			0
		)
		f_time_min = 2.0
	}
	// TEXT "IL EST Là" ==============

	f_time_min -= MATH_FloatMin(f_time_min, TIME_GetDt())
		
	i_trig = vrai	

	OBJ_CapaSet(Obj_Capa_Switch, none)

	@"PNJ_Pacifique/PNJ_Car_Manager" o_car_manager f_delay_since_last_car_shot = 0.0
	@"PNJ_Pacifique/PNJ_Car_Manager" o_car_manager f_delay_since_change_camera = 0.0
	@"PNJ_Pacifique/PNJ_Car_Manager" o_car_manager o_shooting_car	= OBJ_Me()

//	EVENT_AddEventLockCam(o_tourelle, C_EVENT_LockCamStatus_Fight, Cv_NullVector)
	EVENT_AddEventLockCam(OBJ_Me(), C_EVENT_LockCamStatus_Fight, Cv_NullVector)

	f_delai_avant_tir -= MATH_FloatMin(f_delai_avant_tir, TIME_GetDt())
	
	ti_shoot_in_one_seconde = faux

	if ( i_flag_Shoot2Kill )
	{
		if ( @o_main_actor Proc_KK_Test_Mode(ETAT_Kong_desequilibre_paf_fort) )
		{
			i_shoot = faux
			f_delai_after_shoot = 0.5
			f_delai_avant_tir = 0.2
			ti_shoot_in_one_seconde = faux
		}
	}
	else if ( @o_main_actor Proc_KK_JumpStatusGet() || @o_main_actor Proc_KK_Test_Mode(ETAT_Kong_walling) || @o_main_actor Proc_KK_Test_Mode(ETAT_Kong_desequilibre_paf_fort) ) 
	{
		i_shoot = faux
		f_delai_after_shoot = 0.5
		f_delai_avant_tir = 0.2
		ti_shoot_in_one_seconde = faux
	}
	else if (@o_main_actor ACT_ActionGet() == 66)
	{
		i_shoot = faux
		f_delai_after_shoot = 0.5
		f_delai_avant_tir = 0.2
		ti_shoot_in_one_seconde = faux
	}

	to_hotspot = @o_main_actor Proc_KK_HotSpotFightGet()
	if (to_hotspot == OBJ_Me() && !i_flag_jump_on_me )
	{
		i_flag_jump_on_me = vrai
		// AUSECOUR" ====================
		SPEECH_RequestPost
		(
			OBJ_Me(),
			OBJ_Me(), 
			SPEECH_CteTxg_SpeNewYork, 
			GeneNY_C_tank_panic, 
			1.0, 
			SPEECH_Cte_PriorityDefault, 
			5,
			0,
			0,
			0
		)
		// AUSECOUR" ====================
	}
	if (!i_shoot)
	{	
		if ( f_me_to_main_dist < f_SQRdistKill && f_delai_after_shoot > 1.5 )
			f_delai_avant_tir = MATH_FloatMin(f_delai_avant_tir,0.02) 

		if ( !f_delai_avant_tir )
			ti_shoot_in_one_seconde = vrai
	}

	if (ti_shoot_in_one_seconde)
	{
		// Tir si delai = 0
		i_shoot = vrai
		f_shoot = 1.0					// Tir esquivable (tir dans 1s)

		SND_RequestPlay(Ci_Snd_Tank_Reload)	// Reload

		// TEXT "FEU !!!" ====================
		SPEECH_RequestPost
		(
			OBJ_Me(),
			OBJ_Me(), 
			SPEECH_CteTxg_SpeNewYork, 
			GeneNY_C_tank_shoot, 
			1.0, 
			SPEECH_Cte_PriorityDefault, 
			5,
			0,
			0,
			0
		)
		// TEXT "FEU !!!" ====================
	}

	if (i_shoot)
		f_shoot -= MATH_FloatMin(f_shoot, TIME_GetDt())

	if (i_shoot && !f_shoot)
	{
		// TIR !!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
		
		i_shoot = faux
		f_delai_avant_tir = 2.0
		f_delai_after_shoot = 0

		tv_ray_start_pos = @o_tourelle OBJ_PosGet()
		tv_ray_start_pos += @o_tourelle OBJ_BankingGet() 
		tv_ray_start_pos += @o_tourelle OBJ_SightGet() * 1.5

		to_torse = @o_main_actor ANI_CanalObjectGet(Anim_Canal_Torse)
		tv_ray_dir = @to_torse OBJ_PosGet() - tv_ray_start_pos
		MATH_VecSetNormalize(tv_ray_dir)

		to_ray_object = COL_RayObject_Dist(tv_ray_start_pos, tv_ray_dir, 1000.0, all, none, Ci_Filter_IdentityFlag, COL_C_Ray_on_visuel_and_bone_volumes | COL_C_Ray_use_SpecificCrossableSet)
		if (to_ray_object)
		{
			DBG_RenderVector(tv_ray_start_pos, COL_RayObject_PosGet() - tv_ray_start_pos, color_rouge)	
	
 			to_ray_object = COL_RayObject_ActorGet()
			
			ti_canal = COL_RayObject_CanalGet()
			tv_impact = COL_RayObject_PosGet()
			tv_normal = COL_RayObject_NormalGet()
			ti_sound = COL_RayObject_SoundGet()


			if ( to_ray_object != o_main_actor 
			|| !	@o_main_actor Proc_KK_RefusePafFrom(OBJ_Me()))
				PNJ_Tank_GFX_GunsImpact( tv_impact, tv_normal, to_ray_object, ti_canal, 0 , ti_sound, @"PNJ_Pacifique/PNJ_Car_Manager" o_car_manager f_car_ground_Z)

			if (to_ray_object == o_main_actor )
			{
				if ( i_flag_Shoot2Kill )
					ti_filter = C_PAF_KK_NON_DODGEABLE
				else
					ti_filter = 0

				EVENT_AddEventPaf(C_EVENT_FILTER_All, (C_PAF_KK_Fort | ti_filter), OBJ_Me(), Cf_EVENT_Duree_1Trame, o_main_actor, 70.0 * PAF_Unit, MATH_VecNormalize(@o_main_actor OBJ_PosGet() - OBJ_PosGet()))
				if ( ! @o_main_actor Proc_KK_RefusePafFrom(OBJ_Me()))
				{
					// TEXT "TOUCHé" ====================
					SPEECH_RequestPost
					(
						OBJ_Me(),
						OBJ_Me(), 
						SPEECH_CteTxg_SpeNewYork, 
						GeneNY_C_tank_hit_kong, 
						1.0, 
						SPEECH_Cte_PriorityDefault, 
						5,
						0,
						0,
						0
					)
					// TEXT "FEU !!!" ====================
				}
			}
		}

		if (o_militaire)
			@o_militaire ACT_ActionSet(2)
		
		SND_RequestPlay(Ci_Snd_Tank_Shoot)

		Proc_Tank_GunStartFX(tv_ray_start_pos, 0.5, 1.5, 0.1, 0.2)
	}
}
else
{
	i_trig = faux

	f_delai_avant_tir = 1.2 // On ne tire qu'apres xs si on est loin

	if (@"PNJ_Pacifique/PNJ_Car_Manager" o_car_manager o_shooting_car == OBJ_Me())
		@"PNJ_Pacifique/PNJ_Car_Manager" o_car_manager o_shooting_car	= nobody
}


if (! (@o_main_actor OBJ_FlagsControlGet() & OBJ_C_ControlFlag_ForceInactive) && f_me_to_main_dist < 10000.0)
{
	tv_pos = OBJ_PosGet()
	tv_pos += OBJ_BankingGet() * 2.0

	if (OBJ_CapaTest(Obj_Capa_Switch))
		Proc_KongFight_TargetAdd(OBJ_Me(), tv_pos, 3.0, C_AIDE_AU_TIR_Prio_GrabAnn, 0)
	else
		Proc_KongFight_TargetAdd(OBJ_Me(), tv_pos, 3.0, C_AIDE_AU_TIR_Prio_Ennemi, 0)
}

// DEPLACEMENT =====================================================================
//ti_flag_move = faux
//if (o_start_wp)
//{
//	if (AI_TriggerIsValid(trigger_move) && call_trigger(trigger_move))
//	{
//		if (MATH_VecDotProduct(OBJ_SightGet(), @o_start_wp OBJ_PosGet() - OBJ_PosGet()) <= 0.0)
//			o_start_wp = WAY_NetNextWP(n_net, o_start_wp, 0, 0)
//	
//		if (o_start_wp)
//			ti_flag_move = vrai
//	}
//}
//	
//if (ti_flag_move)
//{
//	tv_new_sight = @o_start_wp OBJ_PosGet()
//	tv_new_sight -= OBJ_PosGet()
//	
//	tv_new_sight = MATH_VecBlendRotate(OBJ_SightGet(), tv_new_sight, 2.0 * TIME_GetDt())
//	OBJ_BankingGeneralSet(tv_new_sight, Cv_VerticalVector)
//	
//	tv_pos = OBJ_PosGet()
//	tv_pos += OBJ_SightGet() * (speed * TIME_GetDt())
//	OBJ_PosSet(tv_pos)
//}
//else
// DEPLACEMENT =====================================================================
{
	if (o_tourelle)
		to_tourelle = o_tourelle
	else
		to_tourelle = OBJ_Me()	

	tv_new_sight = @o_main_actor OBJ_PosGet()
	tv_new_sight -= @to_tourelle OBJ_PosGet()
	MATH_VecSetHorzNormalize(tv_new_sight)

	// SND LOOP Tourelle ==========================================
	tv_axis = @to_tourelle OBJ_SightGet()
	MATH_VecSetHorzNormalize(tv_axis)
	
	if (i_trig && MATH_VecDotProduct(tv_new_sight,tv_axis) < 0.999)
	{
		if (i_snd_loop == -1)
			i_snd_loop = SND_RequestPlayLoop(Ci_Snd_Tank_Tourelle)
	}
	else
	{
		if (i_snd_loop != -1)
		{
			SND_Stop(i_snd_loop)
			i_snd_loop = -1
		}
	}
	// SND LOOP Tourelle ==========================================
	
	tv_new_sight = MATH_VecBlendRotate(@to_tourelle OBJ_SightGet(), tv_new_sight, 2.0 * TIME_GetDt())

	@to_tourelle OBJ_BankingGeneralSet(tv_new_sight, Cv_VerticalVector)
}

if (o_militaire)
{
	@o_militaire OBJ_PosSet( @o_tourelle OBJ_PosGet() + @o_tourelle MATH_VecLocalToGlobal(v_offet_militaire) )
	@o_militaire OBJ_BankingGeneralSet(@o_tourelle OBJ_SightGet(),@o_tourelle OBJ_BankingGet())

	if (@o_militaire ACT_ActionGet() == 2 && @o_militaire ACT_ActionFinished())
		@o_militaire ACT_ActionSet(0)

	to_torse = @o_main_actor ANI_CanalObjectGet(Anim_Canal_Tete)
	to_head = @o_militaire ANI_CanalObjectGet(Anim_Canal_Tete)
	@to_head OBJ_Rotate_FromTo(cvector(0.0, -1.0, 0.0), @to_head MATH_VecGlobalToLocal(@to_torse OBJ_PosGet() - @to_head OBJ_PosGet()))
}
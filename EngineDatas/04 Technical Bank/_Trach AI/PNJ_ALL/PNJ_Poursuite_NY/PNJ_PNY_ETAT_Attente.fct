
#include "PNJ_PNY_defines.var"

Include_UltraProcedure_Header

int			ti_flag_ok

float		tf_norm
float		tf_speed

vector	tv_new_sight
vector	tv_quat
vector	tv_pos

message	tm_mes

object	to_canal
object	to_camera
object	to_special_cam
object	to_bone
object	to_annex

//if (IO_KeyPressed(65))
//	PNJ_PNY_GFX_Explode()

if (i_flag_sortie_etat)
{
	i_flag_sortie_etat = faux

	if (i_SND_bzzz_loop != -1)
	{
		SND_Stop(i_SND_bzzz_loop)
		i_SND_bzzz_loop = -1
	}

	if (GFX_Halo != -1)
	{
		GFX_Del(GFX_Halo)
		GFX_Halo = -1
	}
	return
}

if (i_etat_courant != ETAT_Attente)
{
	// INTIALISATION
	i_dernier_etat = i_etat_courant
	i_etat_courant = ETAT_Attente
	
	if (fct_last_etat)
	{
		i_flag_sortie_etat = vrai
		AI_Execute(fct_last_etat)
	}

	if (GFX_Halo == -1)
	{
		GFX_Halo = GFX_Add(21)                                 	 //création du halo
		GFX_MaterialSet(GFX_Halo, get_SFX_light_and_smoke,0) 	// affectation du matériau
		GFX_Setf(GFX_Halo, 21000, f_halo_size )             // taille du halo
		GFX_Setf(GFX_Halo, 21004, 20.0 )                       // taille du halo
		GFX_Seti(GFX_Halo, 21101, 0xFFFFFFFF)          // couleur du halo
		GFX_Seti(GFX_Halo, 21100, 4)
	}

	fct_last_etat = AI_TrackCurGet()
	
	v_target_pos = @o_BV BV_RandomPosGet(1)
	v_target_dest_pos = v_target_pos
	v_new_sight = OBJ_SightGet()
	f_time_start_etat = 0.0
}
else
{
	f_time_start_etat += TIME_GetDt()
}

// ANALYSE ==============================================================================
AI_Execute("PNJ_PNY_exec_check_paf")
if (i_flag_paf || PNJ_PNY_ODE_Collision())
	macro_change_etat("PNJ_PNY_ETAT_Mort")

// COMPORTEMENT ========================================================================
to_camera = @get_global o_camera


// SND BZZZZZZZZZZZZZZZZZzzzzzzzzzzzzzzzzzzzzzzzzzzzzzzzzzzzzzzzzzzzzzzzzzzzzzz
f_camera_sqr_dist = MATH_VecSquareNorm(@o_main_actor OBJ_PosGet() - OBJ_PosGet())
if (f_camera_sqr_dist < Cf_SND_Sqr_Dist_On)
{
	if (i_SND_bzzz_loop == -1)
		i_SND_bzzz_loop = SND_RequestPlayLoop(Ci_SND_bzzz_loop)
}
else if (f_camera_sqr_dist > Cf_SND_Sqr_Dist_Off)
{
	if (i_SND_bzzz_loop != -1)
	{
		SND_Stop(i_SND_bzzz_loop)
		i_SND_bzzz_loop = -1
	}
}
// SND BZZZZZZZZZZZZZZZZZzzzzzzzzzzzzzzzzzzzzzzzzzzzzzzzzzzzzzzzzzzzzzzzzzzzzzz

to_canal = ANI_CanalObjectGet(1)

f_before_shot_delay -= TIME_GetDt()

if (o_look_here && !OBJ_CapaTest(OBJ_Capa_0) )
{
	// Ciné ======================

	f_inside_bv_duration = 0.0

	// Target & Speed ================================
	v_target_dest_pos = @o_look_here OBJ_PosGet()
	tf_speed = f_look_here_speed
	// Target & Speed ================================

	if (o_militaire_tommy)	
		@o_militaire_tommy ACT_ActionSet(10)			// Attente

	ti_flag_ok = faux
}
else
{
	// GAME ======================
	ti_flag_ok = vrai

	if (o_BV_Proxim && @o_main_actor COL_Pivot_BVCollide(o_BV_Proxim) )
		ti_flag_ok = 666			// On est sur EUX (cas je ne veux pas de cam mais je me defend qd meme)
	else
	{
		if ( ! @o_main_actor COL_Pivot_BVCollide(o_BV) )
			ti_flag_ok = faux		// Hors BV
		else if (@to_camera Proc_Kam_FamilyGet() == 12)
			ti_flag_ok = faux		// Cam Voiture en cours
		else if (o_car_manager && @"PNJ_Pacifique/PNJ_Car_Manager" o_car_manager o_shooting_car != OBJ_Me())
		{
			if (@"PNJ_Pacifique/PNJ_Car_Manager" o_car_manager o_shooting_car)
				ti_flag_ok = faux
			else if (@"PNJ_Pacifique/PNJ_Car_Manager" o_car_manager f_delay_since_change_camera < 6.0)
				ti_flag_ok = faux
			else if (@"PNJ_Pacifique/PNJ_Car_Manager" o_car_manager f_delay_since_last_car_shot < 3.0)
				ti_flag_ok = faux
		}
	}
}

if (ti_flag_ok || f_duree_min_active)
{
	f_inside_bv_duration	+= TIME_GetDt()

	if (ti_flag_ok && !i_flag_ok_old)
		f_duree_min_active = 2.0
		
	f_duree_min_active -= MATH_FloatMin(f_duree_min_active,TIME_GetDt())

	tm_mes = MSG_GetById(msg_id_KK_send_projectile)
	if (o_militaire_tommy && tm_mes.msg_id)
	{
		f_duree_min_active = 0.5
		f_slowdown = 1.0
		
		// "Ho Mon Dieux"=====================
		SPEECH_RequestPost
		(
			OBJ_Me(),
			OBJ_Me(), 
			SPEECH_CteTxg_SpeNewYork, 
			GeneNY_C_snipe_panic, 
			1.0, 
			SPEECH_Cte_PriorityDefault, 
			5,
			0,
			0,
			0
		)
	}
	
	if (ti_flag_ok != 666)
	{
		// La cam prend la main
		if (o_car_manager)
		{
			@"PNJ_Pacifique/PNJ_Car_Manager" o_car_manager o_shooting_car	= OBJ_Me()
			@"PNJ_Pacifique/PNJ_Car_Manager" o_car_manager f_delay_since_last_car_shot = 0.00
			@"PNJ_Pacifique/PNJ_Car_Manager" o_car_manager f_delay_since_change_camera = 0.0
		}
		// Demande de cam
		OBJ_CapaSet(Obj_Capa_Switch, none)
		
		if ( i_flag_ok_old != ti_flag_ok )
		{
			// "Attention"=====================
			SPEECH_RequestPost
			(
				OBJ_Me(),
				OBJ_Me(), 
				SPEECH_CteTxg_SpeNewYork, 
				GeneNY_C_snipe_see_kong, 
				1.0, 
				SPEECH_Cte_PriorityDefault, 
				5,
				0,
				0,
				0
			)
		}
	}
	else
	{
		if (ti_flag_ok != ti_flag_ok)
		{
			if (o_car_manager && @"PNJ_Pacifique/PNJ_Car_Manager" o_car_manager o_shooting_car != OBJ_Me())
			{
				// Panique =====================
				SPEECH_RequestPost
				(
					OBJ_Me(),
					OBJ_Me(), 
					SPEECH_CteTxg_SpeNewYork, 
					GeneNY_C_snipe_panic, 
					1.0, 
					SPEECH_Cte_PriorityDefault, 
					5,
					0,
					0,
					0
				)
			}
		}
		OBJ_CapaSet(OBJ_Capa_9, none)
	}

	// Target & Speed ================================
	v_target_dest_pos = @o_main_actor_lightpoint OBJ_PosGet()
	tf_speed	 = 3.0
	// Target & Speed ================================
	
	if (o_militaire_tommy)
	{
		object	to_gao
		to_gao = @o_militaire_tommy ANI_CanalObjectGet(19)
		tv_pos = @to_gao OBJ_PosGet()
		@o_militaire_tommy OBJ_FlagInvisibleSet(faux)
	}
	else
		tv_pos = @to_canal OBJ_PosGet() + cvector(0.0, 0.0, 1.0)

	EVENT_AddEventLockCam(OBJ_Me(), C_EVENT_LockCamStatus_Fight, Cv_NullVector)
	Proc_KongFight_TargetAdd(OBJ_Me(), tv_pos, 3.0, C_AIDE_AU_TIR_Prio_AttackAnn, 0)

	f_slowdown -= MATH_FloatMin(f_slowdown, TIME_GetDt())
	if (f_slowdown)
	{
		@get_global f_game_speed = 0.4
		f_inside_bv_duration = 0.5
		@o_militaire_tommy ACT_ActionSet(13)
	}
	else if (f_inside_bv_duration > 1.5 && (o_militaire_tommy && @o_militaire_tommy ACT_ActionGet() != 12))
	{
		if (o_militaire_tommy)	
		{
			@o_militaire_tommy ACT_ActionSet(11)		// Au bout d'un moment on tire
			
			to_bone = @o_militaire_tommy ANI_CanalObjectGet(Anim_Canal_Torse)
			tv_quat = @to_bone MATH_VecGlobalToLocal(@o_militaire_tommy OBJ_SightGet())
			@to_bone OBJ_Rotate_FromTo(@to_bone MATH_VecGlobalToLocal(@o_militaire_tommy OBJ_SightGet()), MATH_VecBlendRotate(tv_quat, @to_bone MATH_VecGlobalToLocal(@o_main_actor OBJ_PosGet() - OBJ_PosGet()), 0.25))
			
			to_annex = @o_militaire_tommy ANI_CanalObjectGet(Anim_Canal_Annex1)
			to_bone = @o_militaire_tommy ANI_CanalObjectGet(Anim_Canal_EpauleDroite)
			tv_quat = @to_annex OBJ_SightGet()
			@to_bone OBJ_Rotate_FromTo(@to_bone MATH_VecGlobalToLocal(tv_quat), @to_bone MATH_VecGlobalToLocal(@o_main_actor OBJ_PosGet() - OBJ_PosGet()))
			to_bone = @o_militaire_tommy ANI_CanalObjectGet(Anim_Canal_EpauleGauche)
			@to_bone OBJ_Rotate_FromTo(@to_bone MATH_VecGlobalToLocal(tv_quat), @to_bone MATH_VecGlobalToLocal(@o_main_actor OBJ_PosGet() - OBJ_PosGet()))
		}

		PNJ_PNY_Shoot()
	}
	else if (o_militaire_tommy)	
	{
		if (	@o_militaire_tommy ACT_ActionGet() != 12 || ( @o_militaire_tommy ACT_ActionGet() == 12 && @o_militaire_tommy ACT_ActionFinished() ) )
			@o_militaire_tommy ACT_ActionSet(10)			// Attente
	}
	
}
else
{
	f_inside_bv_duration = 0.0
	
	if (o_militaire_tommy)	
	{
		@o_militaire_tommy ACT_ActionSet(10)		// Attente
		if (o_secto_tommy && @o_main_actor COL_Pivot_BVCollide(o_secto_tommy))
			@o_militaire_tommy OBJ_FlagInvisibleSet(faux)
		else
			@o_militaire_tommy OBJ_FlagInvisibleSet(vrai)
	}

	if (o_car_manager && @"PNJ_Pacifique/PNJ_Car_Manager" o_car_manager o_shooting_car == OBJ_Me())
		@"PNJ_Pacifique/PNJ_Car_Manager" o_car_manager o_shooting_car = nobody

	// Target & Speed ================================
	v_target_dest_pos = @o_BV OBJ_PosGet() + cvector( MATH_Sin(TIME_Get() * 1.2) * 5.0, MATH_Cos(TIME_Get()) * 5.0, 0)
	tf_speed	 = 1.0
	// Target & Speed ================================
}

i_flag_ok_old = ti_flag_ok

// ORIENTATION DU SPOT =================================================
tv_new_sight = v_target_dest_pos - @to_canal OBJ_PosGet()
v_new_sight = MATH_VecBlendRotate(v_new_sight,tv_new_sight, tf_speed * TIME_GetDt())

OBJ_BankingGeneralSet(v_new_sight, Cv_VerticalVector)
@to_canal OBJ_SightGeneralSet(v_new_sight, Cv_VerticalVector)

GFX_Setv(GFX_Halo, 21200, @to_canal OBJ_PosGet() + (@to_canal OBJ_SightGet() * 0.7))
GFX_Setv(GFX_Halo, 21201, @to_canal OBJ_SightGet())
// ORIENTATION DU SPOT =================================================

if (o_militaire_tommy)
{
	@o_militaire_tommy OBJ_BankingGeneralSet(@o_tourelle OBJ_SightGet(),Cv_VerticalVector)
}
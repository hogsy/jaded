#include "PNJ_PNY_defines.var"

float		tf_norm

vector	tv_speed

object	to_camera
object	to_bone

if (i_flag_sortie_etat)
{
	i_flag_sortie_etat = faux

	return
}

if (i_etat_courant != ETAT_Mort)
{
	// INTIALISATION
	i_dernier_etat = i_etat_courant
	i_etat_courant = ETAT_Mort
	
	if (fct_last_etat)
	{
		i_flag_sortie_etat = vrai
		AI_Execute(fct_last_etat)
	}

	SpecialFlag_set(i_SF_AlreadyPlayed) 

	OBJ_CapaSet(OBJ_Capa_15,none)
	COL_ColSetActivationSet( C_bit_zdm_pied, none)

	SND_RequestPlay(20)	// Cri buffer
	
	if (o_militaire_tommy)
	{
		@o_militaire_tommy COL_ColSetActivationSet(C_bit_zdm_pied,none)
		@o_militaire_tommy DYN_On()	
		@o_militaire_tommy DYN_FlagsSet(DYN_C_BasicForces, none)
		@o_militaire_tommy DYN_GravitySet(Cv_NormalGravity)
		@o_militaire_tommy ACT_ActionSet(1)
		@o_militaire_tommy OBJ_BankingGeneralSet(-v_paf_dir, Cv_VerticalVector)
	}

	f_slowdown = 0.3

	SND_RequestPlay(Ci_SND_destroyed)

	PNJ_PNY_GFX_Explode()

	fct_last_etat = AI_TrackCurGet()
	
	f_time_start_etat = 0.0
}
else
{
	f_time_start_etat += TIME_GetDt()
	f_invisible += TIME_GetDt()

	if (o_car_manager && f_time_start_etat > 1.5 && @"PNJ_Pacifique/PNJ_Car_Manager" o_car_manager o_shooting_car == OBJ_Me())
		@"PNJ_Pacifique/PNJ_Car_Manager" o_car_manager o_shooting_car = nobody
}

// ANALYSE ==========================================================================
AI_Execute("PNJ_PNY_exec_check_paf")

f_slowdown -= MATH_FloatMin(f_slowdown, TIME_GetDt())
if (f_slowdown)
{
	@get_global f_game_speed = 0.4
}

// COMPORTEMENT ====================================================================
if (o_car_manager && @"PNJ_Pacifique/PNJ_Car_Manager" o_car_manager o_shooting_car == OBJ_Me())
{
	to_bone	= @o_main_actor ANI_CanalObjectGet(Anim_Canal_Bassin)

	to_camera = @get_global o_camera
	if (COL_LIB_Can_See_Actor(o_main_actor, @to_camera OBJ_PosGet(), @to_bone OBJ_PosGet(), all, OBJ_C_IdentityFlag_AI, Ci_Filter_IdentityFlag, COL_C_Ray_on_ColMap_NoCrossable))
	{
		to_camera = @get_global i_Global_KamID[0]
		@to_camera Proc_Kam_SpecialParameterSet(OBJ_Me(), nobody, Cv_NullVector, Cv_NullVector)
	}
	else
	{
		@"PNJ_Pacifique/PNJ_Car_Manager" o_car_manager o_shooting_car = nobody
	}
}

tv_speed = ODE_Getv(3)
tf_norm = MATH_VecSquareNorm(tv_speed)
if (tf_norm > 40000.0)
{
	tf_norm = MATH_FloatSqrt(tf_norm)
	tv_speed /= tf_norm * 200.0
	ODE_Setv(3, tv_speed)
}


if (o_militaire_tommy && @o_militaire_tommy ACT_ActionItemGet() == 2 
&& ( (@o_militaire_tommy COL_CollideType(COL_C_Ground) && @o_militaire_tommy OBJ_FlagsIdentityTest(OBJ_C_IdentityFlag_Dyna)) 
|| !@o_militaire_tommy OBJ_FlagsIdentityTest(OBJ_C_IdentityFlag_Dyna) ) )
{
	if (@o_militaire_tommy OBJ_FlagsIdentityTest(OBJ_C_IdentityFlag_Dyna))
	{
		@o_militaire_tommy DYN_Off()
		@o_militaire_tommy COL_ColSetActivationSet(none, C_bit_zdm_pied)
	}
	@o_militaire_tommy OBJ_PosSet( @o_militaire_tommy OBJ_PosGet() - cvector(0,0,0.15 * TIME_GetDt()) )	
	f_profondeur += TIME_GetDt()
	if (f_profondeur >= 3)
	{
		// Fin !!
		@o_militaire_tommy OBJ_Destroy()
		o_militaire_tommy = nobody
		OBJ_Destroy()
	}
}

if ( i_invisible)
{
	if( f_invisible > 0.02)
		OBJ_FlagInvisibleSet(vrai)
}
else if ( f_time_start_etat  > 0.2
&& ( COL_CollideType(COL_C_Ground)  || COL_CollideType(COL_C_Wall) || COL_CollideType(COL_C_Extra_ODE)))
{
	PNJ_PNY_GFX_Explode()
	i_invisible = vrai
}
else
	f_invisible  = 0.0

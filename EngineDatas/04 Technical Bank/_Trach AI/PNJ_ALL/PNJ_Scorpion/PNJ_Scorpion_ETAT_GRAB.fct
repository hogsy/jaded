#include "PNJ_Scorpion_defines.var"

object	to_client

vector	tv_new_sight
vector	tv_new_banking

// SORTIE ETAT ====================================================================
if (i_sort_etat)
{
	i_sort_etat = faux

	o_grab_actor = LNK_ThisClientGet(o_grab_actor, Ci_LNK_GRAB_RAPTOR, mid_grab_actor_LNK_ID, faux, nofunc, nofunc, nofunc)
	
	if (SND_grab_loop != -1)
	{
		SND_Stop(SND_grab_loop)
		SND_grab_loop = -1
	}

	return
}

// INITIALISATION ETAT ==============================================================
if (i_etat_courant != ETAT_GRAB)
{
	i_dernier_etat = i_etat_courant
	i_etat_courant = ETAT_GRAB
	
	if (fct_last_etat)
	{
		i_sort_etat = vrai
		AI_Execute(fct_last_etat)
	}

	fct_last_etat = AI_TrackCurGet()
	
	if (SND_grab_loop == -1)
		SND_grab_loop = SND_RequestPlayLoop(SND_GRAB)
		
	i_flag_grab_hit_cpt = 0

	f_time_start_etat = 0.0
}
else
{
	if ( ! f_time_start_etat )
	{
		if (ai_perceived_ID[i_perceived_best_actor_index] != C_ID_Joueur)
			EVENT_AddEventPaf(C_EVENT_FILTER_All, C_PAF_KK_Fort, OBJ_Me(), Cf_EVENT_Duree_1Trame, o_grab_actor, 0.0 * PAF_Unit, OBJ_SightGet())
		else
			EVENT_AddEventPaf(C_EVENT_FILTER_All, C_PAF_KK_Moyen, OBJ_Me(), Cf_EVENT_Duree_1Trame, o_grab_actor, 10.0 * PAF_Unit, OBJ_SightGet())
	}
	
	f_time_start_etat += TIME_GetDt()
}

// ANALYSE ========================================================================
AI_Execute("PNJ_Scorpion_exec_check_vision")

AI_Execute("PNJ_Scorpion_exec_check_paf")
if (i_flag_paf_faible)
	macro_change_etat("PNJ_Scorpion_ETAT_PAF")

AI_Execute("PNJ_Scorpion_exec_check_best_interet")

if (! MSG_GlobalIsValid(mid_best_interet))
	macro_change_etat("PNJ_Scorpion_ETAT_IDLE")

if ((GRID_CapaGet(@o_grab_actor OBJ_PosGet()) & tag_grid_terrain) == Ci_sol_herbe_ronce)
	to_client = LNK_ThisClientGet(o_grab_actor, Ci_LNK_GRAB_RAPTOR, mid_grab_actor_LNK_ID, faux, "PNJ_Scorpion_exec_GRAB_init", nofunc, "PNJ_Scorpion_exec_GRAB_init")
else
	to_client = LNK_ThisClientGet(o_grab_actor, Ci_LNK_GRAB_RAPTOR, mid_grab_actor_LNK_ID, vrai, "PNJ_Scorpion_exec_GRAB_init", nofunc, "PNJ_Scorpion_exec_GRAB_init")

if (!to_client)
	macro_change_etat(fct_main_etat)

// COMPORTEMENT ==================================================================
PNJ_Scorpion_UncollideAdd(to_client, Cf_EVENT_Duree_1Trame)

@get_global f_raptor_last_grab_time = TIME_Get()

//f_joy_norm = 0.0
//
//AI_Execute("PNJ_Scorpion_exec_select_action")

switch(ACT_ActionGet())
{
	case Action_Grab_deb :
		if (ACT_ActionFinished())
			ACT_ActionSet(Action_Grab_fin)
		break
		
	case Action_Grab_fin :
		if (ACT_ActionFinished())
		{
//			f_duration_before_next_paf = MATH_RandFloat(0.75, 1.5)
			ACT_ActionSet(Action_Grab_cycl)
			i_grab_hit_enable = faux
		}
		break	

	case Action_Grab_cycl :
		f_duration_before_next_paf -= MATH_FloatMin(f_duration_before_next_paf, TIME_GetDt())
		if (!f_duration_before_next_paf)
			ACT_ActionSet(Action_Attaque_deb)
		break
		
	case Action_Attaque_deb :
		if (ACT_ActionFinished())
		{
			SND_RequestPlay(SND_ATTACK)
			i_grab_hit_enable = vrai
			ACT_ActionSet(Action_Attaque_fin)
		}
		break
		
	case Action_Attaque_fin :

		if (i_grab_hit_enable && ANI_CurrentFrameGet(0) < 10)
		{
			SND_RequestPlay(SND_BITE)
			i_grab_hit_enable = faux
			EVENT_AddEventPaf(C_EVENT_FILTER_All, C_PAF_KK_Fort, OBJ_Me(), Cf_EVENT_Duree_1Trame, o_grab_actor, 10.0 * PAF_Unit, OBJ_SightGet())
		}	

		if (ACT_ActionFinished())
		{
			f_duration_before_next_paf = MATH_RandFloat(0.5, 0.75)
			ACT_ActionSet(Action_Grab_cycl)
		}
		break
		
	default:
		ACT_ActionSet(Action_Grab_cycl)
}

if (ai_perceived_ID[i_perceived_best_actor_index] == C_ID_Joueur)
{
	if (f_time_start_etat > Cf_Delay_Before_Grabbed_Jack_Die)
		EVENT_AddEventPaf(C_EVENT_FILTER_All, C_PAF_KK_KiTue, OBJ_Me(), Cf_EVENT_Duree_1Trame, o_grab_actor, 0.0 * PAF_Unit, OBJ_SightGet())
}
else if (IsThis_ID_Humain(ai_perceived_ID[i_perceived_best_actor_index]))
{
	if (f_time_start_etat > 10.0)
		EVENT_AddEventPaf(C_EVENT_FILTER_All, C_PAF_KK_KiTue, OBJ_Me(), Cf_EVENT_Duree_1Trame, o_grab_actor, 0.0 * PAF_Unit, OBJ_SightGet())
}

tv_new_sight = OBJ_SightGet()
tv_new_banking = MATH_VecBlendRotate(OBJ_BankingGet(), v_ground_normale, 6.0 * TIME_GetDt())

OBJ_BankingGeneralSet(tv_new_sight, tv_new_banking)

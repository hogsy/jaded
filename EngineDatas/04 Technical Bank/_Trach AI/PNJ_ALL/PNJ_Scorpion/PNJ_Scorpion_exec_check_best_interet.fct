#include "PNJ_Scorpion_defines.var"

int				ti_i
int				ti_flag_ok
int				ti_new_interet_index
int				ti_flag_choose_best_interest

float			tf_interet
float			tf_best_interet
float			tf_best_dist

object		to_last_best_interet_gao

message	tmsg_trigger

if (MSG_GlobalIsValid(mid_best_interet))
	to_last_best_interet_gao = EVENT_InteretTargetGet(mid_best_interet)
else
	to_last_best_interet_gao = nobody

ti_new_interet_index = -1

// TRIGGER ATTAQUE ==================================================================================
if (AI_TriggerIsValid(trigger_attaque) && ! i_flag_trigger_used)
{
	if (call_trigger(trigger_attaque))
	{
		switch(target_selection)	
		{
			case 0 :

				i_flag_trigger_used = vrai
			
				break
				
			case 1 :
	
				tmsg_trigger = AI_TriggerGetMsg(trigger_attaque)
				Check_This_Gao5(tmsg_trigger.msg_gao5)

				for (ti_i = 0; ti_i < i_perceived_actor_nb; ti_i++)
				{
					if (ao_perceived_actor[ti_i] == tmsg_trigger.msg_gao5)
					{
						i_flag_trigger_used = vrai
						
						ti_new_interet_index = PNJ_Scorpion_Best_Interet_Update(ti_i)
						break
					}
				}
				
				break
		}
	}
	
	if (!i_flag_trigger_used)
		return
}

// REGLE PAF DU MAIN =================================================================================
if (ti_new_interet_index == -1)
{
	if (i_perceived_main_actor_index != -1 && ai_perceived_status[i_perceived_main_actor_index] & Ci_PERCEIVED_PAF)
	{
		ti_new_interet_index = PNJ_Scorpion_Best_Interet_Update(i_perceived_main_actor_index)

		if (ti_new_interet_index != -1 && to_last_best_interet_gao != ao_perceived_actor[ti_new_interet_index])
		{
#ifndef _FINAL_
			DBG_TraceFloat(TIME_Get())
			DBG_TraceString(" => ")
			DBG_TraceObject(OBJ_Me())
			DBG_TraceString(" préfère ")
			DBG_TraceObject(ao_perceived_actor[ti_new_interet_index])
			if (to_last_best_interet_gao)
			{
				DBG_TraceString(" à ")
				DBG_TraceObject(to_last_best_interet_gao)
			}
			DBG_TraceString(" PAF DU MAIN ACTOR ")
			DBG_TraceEOL()
#endif
		}
	}
}


// REGLE CADAVRE : SI JE VOIS UN PERSO INANIME, MIAM MIAM ===================================================
if (ti_new_interet_index == -1)
{
	ti_flag_ok = vrai

	switch(i_etat_courant)
	{
		case ETAT_CATCH :
		case ETAT_GRAB :
		case ETAT_BITE :
			ti_flag_ok = faux
			break
			
//		default:
//			if (i_perceived_best_actor_index  != -1 && ai_perceived_status[i_perceived_best_actor_index] & Ci_PERCEIVED_IS_DEAD)
//				ti_flag_ok = faux
	}

	if (ti_flag_ok)
	{
		tf_best_dist = Cf_Infinit

		for (ti_i = 0; ti_i < i_perceived_actor_nb; ti_i++)
		{
			// JE PARSE TOUT CADAVRE VU OU ENTENDU ET ACCESSIBLE
//			if (ao_perceived_actor[ti_i] == o_last_cadavre)
//				continue
			
			if ( ! ai_perceived_accessible[ti_i] )
				continue

			if (ai_perceived_status[ti_i] & Ci_PERCEIVED_BODY_MEMORISED)
				continue

			if ( ! ( ai_perceived_status[ti_i] & Ci_PERCEIVED_IS_DEAD ) )
				continue

			if (af_perceived_dist[ti_i] < tf_best_dist)
			{
				ti_new_interet_index = ti_i
				tf_best_dist = af_perceived_dist[ti_i]
			}
		}

		ti_new_interet_index = PNJ_Scorpion_Best_Interet_Update(ti_new_interet_index)

		if (ti_new_interet_index != -1 && to_last_best_interet_gao != ao_perceived_actor[ti_new_interet_index])
		{
#ifndef _FINAL_
			DBG_TraceFloat(TIME_Get())
			DBG_TraceString(" => ")
			DBG_TraceObject(OBJ_Me())
			if (ai_perceived_seen[ti_new_interet_index])
				DBG_TraceString(" a vu ")
			else
				DBG_TraceString(" a entendu ")
			DBG_TraceObject(ao_perceived_actor[ti_new_interet_index])
			DBG_TraceString(" en train d'agoniser (NOUVEAU CADAVRE)")
			DBG_TraceEOL()
#endif
		}
	}
}

// REGLE PROXIMITE DU MAIN =================================================================================
if (ti_new_interet_index == -1)
{
	ti_flag_ok = vrai

	if (i_perceived_main_actor_index == -1)
		ti_flag_ok = faux
	else if (ai_perceived_status[i_perceived_main_actor_index] & Ci_PERCEIVED_ALREADY_GRABBED)
		ti_flag_ok = faux
	else if (af_perceived_dist[i_perceived_main_actor_index] > 4.0)
		ti_flag_ok = faux
	else
	{
		switch(i_etat_courant)
		{
			case ETAT_DEVORE :
			case ETAT_BITE :
			case ETAT_GRAB :
			case ETAT_CATCH :
				ti_flag_ok = faux
				break
		}
	}
		
	if (ti_flag_ok)
	{
		ti_new_interet_index = PNJ_Scorpion_Best_Interet_Update(i_perceived_main_actor_index)

		if (ti_new_interet_index != -1 && to_last_best_interet_gao != ao_perceived_actor[ti_new_interet_index])
		{
#ifndef _FINAL_
			DBG_TraceFloat(TIME_Get())
			DBG_TraceString(" => ")
			DBG_TraceObject(OBJ_Me())
			DBG_TraceString(" préfère ")
			DBG_TraceObject(ao_perceived_actor[ti_new_interet_index])
			if (to_last_best_interet_gao)
			{
				DBG_TraceString(" à ")
				DBG_TraceObject(to_last_best_interet_gao)
			}
			DBG_TraceString(" MAIN ACTOR A MOINS DE 4 M")
			DBG_TraceEOL()
#endif
		}
	}
}
// REGLE PAF =========================================================================================
if (ti_new_interet_index == -1)
{
	ti_flag_ok = vrai

	if (! TIME_Elapsed(f_time_change_target, Cf_delay_between_target_switching))
		ti_flag_ok = faux
	
	if (ti_flag_ok)
	{
		for (ti_i = 0; ti_i < i_perceived_actor_nb; ti_i++)
		{
			if (ai_perceived_status[ti_i] & Ci_PERCEIVED_PAF)
			{
				ti_new_interet_index = PNJ_Scorpion_Best_Interet_Update(ti_i)
	
				if (ti_new_interet_index != -1 && to_last_best_interet_gao != ao_perceived_actor[ti_new_interet_index])
				{
#ifndef _FINAL_
					DBG_TraceFloat(TIME_Get())
					DBG_TraceString(" => ")
					DBG_TraceObject(OBJ_Me())
					DBG_TraceString(" préfère ")
					DBG_TraceObject(ao_perceived_actor[ti_new_interet_index])
					if (to_last_best_interet_gao)
					{
						DBG_TraceString(" à ")
						DBG_TraceObject(to_last_best_interet_gao)
					}
					DBG_TraceString(" PAF D'UN ACTOR")
					DBG_TraceEOL()
#endif
				}
	
				break
			}
		}
	}
}


if (i_perceived_best_actor_index == -1)
{
}
else if ( ! ai_perceived_accessible[i_perceived_best_actor_index] )
{
	// - SI MA CIBLE EST INACCESSIBLE
#ifndef _FINAL_
	if ( EVENT_InteretGet(mid_best_interet))
	{
		DBG_TraceFloat(TIME_Get())
		DBG_TraceString(" => ")
		DBG_TraceObject(OBJ_Me())
		DBG_TraceString(" constate que ")
		DBG_TraceObject(ao_perceived_actor[i_perceived_best_actor_index])
		DBG_TraceString(" est inaccessible => INTERET NULL")
		DBG_TraceEOL()
	}
#endif

	EVENT_InteretSet(mid_best_interet, 0.0)
}
else if (ai_perceived_status[i_perceived_best_actor_index] & Ci_PERCEIVED_ALREADY_GRABBED)
{
	// - SI MA CIBLE EST DEJA GRABBEE 
#ifndef _FINAL_
	if ( EVENT_InteretGet(mid_best_interet))
	{
		DBG_TraceFloat(TIME_Get())
		DBG_TraceString(" => ")
		DBG_TraceObject(OBJ_Me())
		DBG_TraceString(" constate que ")
		DBG_TraceObject(ao_perceived_actor[i_perceived_best_actor_index])
		DBG_TraceString(" est deja grabe par un autre acteur => INTERET NULL")
		DBG_TraceEOL()
	}
#endif

	EVENT_InteretSet(mid_best_interet, 0.0)
}
else if (ai_perceived_status[i_perceived_best_actor_index] & Ci_PERCEIVED_STUNNED)
{
	// - SI MA CIBLE EST HS
#ifndef _FINAL_
	if (EVENT_InteretGet(mid_best_interet))
	{
		DBG_TraceFloat(TIME_Get())
		DBG_TraceString(" => ")
		DBG_TraceObject(OBJ_Me())
		DBG_TraceString(" constate que ")
		DBG_TraceObject(ao_perceived_actor[i_perceived_best_actor_index])
		DBG_TraceString(" est inanimé => INTERET NULL")
		DBG_TraceEOL()
	}
#endif

	EVENT_InteretSet(mid_best_interet, 0.0)
}
else if (MSG_GlobalIsValid(mid_best_interet))
{
	EVENT_InteretSet(mid_best_interet, af_perceived_interest[i_perceived_best_actor_index])
}

if (MSG_GlobalIsValid(mid_best_interet))
	tf_best_interet = EVENT_InteretGet(mid_best_interet)
else
	tf_best_interet = -1.0

ti_flag_choose_best_interest	= vrai

switch(i_etat_courant)
{
	case ETAT_GRAB :
	case ETAT_CATCH :
	case ETAT_BITE :
		ti_flag_choose_best_interest	= faux
		break
		
	default:
		if (tf_best_interet > 0.0 && i_perceived_best_actor_index != -1)
			ti_flag_choose_best_interest	= faux
}

if (ti_flag_choose_best_interest)
{
	// RECHERCHE DE LA MEILLEURE CIBLE PARMIS CELLES QUE JE VOIS, QUE J'ENTENDS ET DONT JE ME SOUVIENS
	for (ti_i = 0; ti_i < i_perceived_actor_nb; ti_i++)
	{
		if ( ! ai_perceived_accessible[ti_i] )
			continue

		if (ai_perceived_status[ti_i] & Ci_PERCEIVED_BODY_UNREACHABLE)
			continue

		if (ai_perceived_seen[ti_i] || ai_perceived_status[ti_i] & Ci_PERCEIVED_IS_DEAD)
		{
			tf_interet = af_perceived_interest[ti_i]
		
			if (ai_perceived_status[ti_i] & Ci_PERCEIVED_ALREADY_GRABBED)
				tf_interet = 0.0
			else if (ai_perceived_status[ti_i] & Ci_PERCEIVED_STUNNED)
				tf_interet = 0.0
			else if (ai_perceived_status[ti_i] & Ci_PERCEIVED_IS_DEAD)
				tf_interet = 10.0

			if (tf_interet > tf_best_interet)
			{
				ti_new_interet_index = ti_i
				tf_best_interet = tf_interet
			}
		}
	}

	if (ti_new_interet_index != -1)
		PNJ_Scorpion_Best_Interet_Update(ti_new_interet_index)
}

if (ti_new_interet_index == -1)
	ti_new_interet_index = PNJ_Scorpion_Best_Interet_Update(i_perceived_best_actor_index)

if (MSG_GlobalIsValid(mid_best_interet) && to_last_best_interet_gao != EVENT_InteretTargetGet(mid_best_interet))
{
	i_flag_change_target = vrai
	f_time_change_target = TIME_Get()

#ifndef _FINAL_
	if (to_last_best_interet_gao)
	{
		DBG_TraceFloat(TIME_Get())
		DBG_TraceString(" => ")
		DBG_TraceObject(OBJ_Me())
		DBG_TraceString(" préfère ")
		DBG_TraceObject(ao_perceived_actor[ti_new_interet_index])
		DBG_TraceString(" à ")
		DBG_TraceObject(to_last_best_interet_gao)
		DBG_TraceString(" MEILLEUR INTERET")
		DBG_TraceEOL()
	}
	else
	{
		DBG_TraceFloat(TIME_Get())
		DBG_TraceString(" => ")
		DBG_TraceObject(OBJ_Me())
		DBG_TraceString(" choisit  ")
		DBG_TraceObject(ao_perceived_actor[ti_new_interet_index])
		DBG_TraceEOL()
	}	
#endif
}
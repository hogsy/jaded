#include "KBC_defines.var"

vector	tv_ma_pos
vector	tv_kong_pos


// SORTIE ETAT
if (i_sort_etat)
{
	DYN_FrictionVectorSet(Cv_Bat_friction)
	i_sort_etat = faux
	return
}

// ENTREE ETAT
if (i_etat_courant != ETAT_Chute_Libre )
{
	i_etat_ancien_ancien = i_etat_ancien
	i_etat_ancien = i_etat_courant
	i_etat_courant = ETAT_Chute_Libre
	if (fct_last_etat)
	{
		i_sort_etat = vrai
		AI_Execute(fct_last_etat)
	}
	fct_last_etat = AI_TrackCurGet()
	f_time_start_etat = 0.0
	KBC_DBG_Trace(i_DBG_trace_etat, "etat chute libre")
	
	AI_Execute("KBC_exec_dyn_on")
	DYN_GravitySet(Cv_NormalGravity)
	DYN_FrictionVectorSet(Cv_Bat_throw_friction)
	
	if( i_chute_wp_deja_dedans )
		ACT_ActionSet(ACTION_Vol_Blesse)
	else if( o_chute_bats_wp )
		ACT_ActionSet(ACTION_Vol_Blesse)
	else
	{
		// ne pas sortir de la BV du wp de chute
		DYN_SpeedSetVector(DYN_SpeedGetVector() + (f_speed * OBJ_SightGet()))
		
		i_mort_au_sol_sur_le_dos = faux
		if( i_paf_mur )
			ACT_ActionSet(ACTION_Vol_Blesse)
		else if( i_big_bat )
			ACT_ActionSet(ACTION_Vol_Vrille)
		else
		{
			if( i_killed_by_kong )
			{
				// tuée par kong -> modulo mort sur le dos / sur le ventre
				@get_global i_KSmallBats_death_action = MATH_Modulo(@get_global i_KSmallBats_death_action + 1, 2)
				if( @get_global i_KSmallBats_death_action )
					i_mort_au_sol_sur_le_dos = vrai		// 1 sur 2 meurt sur le dos
			}
			else
				i_mort_au_sol_sur_le_dos = vrai		// tuée par jack -> meurt sur le dos
			ACT_ActionSet(ACTION_Vol_Vrille)
		}
	}
	
	if( ! i_big_bat )
	{
		OBJ_CapaSet(CAPA_Dead, none)
	}
}
else
{
	f_time_start_etat += TIME_GetDt()
}

// ANALYSE ============================================================================

if( f_time_start_etat >= 10.0 )
{
	OBJ_Destroy()
}

MACRO_CHECK_PAFS

//KBC_Check_WaterZ()
//IsInWater(OBJ_PosGet(), f_water_Z)

// Big Bat : se renvoller si on tombe trop bas sans toucher de face de mort car Kong doit me finir, tomber dans le vide c nul...
if( i_big_bat )
{
	tv_ma_pos = OBJ_PosGet()
	tv_kong_pos = @o_Kong OBJ_PosGet()
	if( tv_ma_pos.z < ( tv_kong_pos.z - 10.0 ) )
	{
		EVENT_LIFE_CurLifeSet(ID_LIFE, MATH_FloatMax(EVENT_LIFE_CurLifeGet(ID_LIFE), Cf_BigBat_AntiMashingLifePoints) )
		AI_Execute("KBC_exec_init_attente")
	}
}

if( COL_CollideType(COL_C_Ground) )
	macro_change_etat("KBC_ETAT_Sol_Ecrasement")

if( i_splash_flag )
	macro_change_etat("KBC_ETAT_Sol_Ecrasement")		// jouer l'anim d'écrasement au contact de l'eau


// COMPORTEMENT ==================================================

i_flag_neck = faux

if( i_chute_wp_deja_dedans )
	OBJ_BankingGeneralSet(OBJ_SightGet(), MATH_VecBlendRotate(OBJ_BankingGet(), Cv_VerticalVector, 5 * TIME_GetDt()) )
else
	OBJ_BankingGeneralSet(OBJ_SightGet(), KBC_Paf_BankingGet())


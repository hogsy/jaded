#include "KBC_defines.var"

messageid				tmid_vision


// SORTIE ETAT
if (i_sort_etat)
{
	i_interaction_Ann_fake_paf = faux		// raz da	ns tous les cas
	f_bigbat_delai_no_attack = Cf_bigbat_delai_no_attack
	f_bigbat_delai_no_kamera = Cf_bigbat_delai_no_kamera
	f_speed = 0.0
	if( ! i_paf_repousse_flag )
		f_big_bat_no_paf = 4.0		// délai pas de paf sauf si c'était un paf repousse
	i_paf_repousse_flag = faux
	
	i_sort_etat = faux
	return
}

// ENTREE ETAT
if (i_etat_courant != ETAT_Paf )
{
	i_etat_ancien_ancien = i_etat_ancien
	i_etat_ancien = i_etat_courant
	i_etat_courant = ETAT_Paf
	if (fct_last_etat)
	{
		i_sort_etat = vrai
		AI_Execute(fct_last_etat)
	}
	fct_last_etat = AI_TrackCurGet()
	f_time_start_etat = 0.0
	KBC_DBG_Trace(i_DBG_trace_etat, "etat paf (air)")
	
	AI_Execute("KBC_exec_dyn_on")
//	if( EVENT_TL_LifeGet(ID_LIFE) <= 0.0 )
//		DYN_GravitySet(cvector(0,0,-20))
//	else
//		DYN_GravitySet(Cv_NullVector)
	
//	if( i_interaction_Ann_fake_paf )
//		DYN_GravitySet(cvector(0,0,-5))
//	else
//		DYN_GravitySet(Cv_NormalGravity)

	if( i_big_bat )
		DYN_GravitySet(Cv_NormalGravity)
	else
		DYN_GravitySet(Cv_NullVector)
	
	if( i_paf_mur )
	{
		SND_RequestPlay(Ci_SND_Ecrasement)
		ACT_ActionSet(ACTION_Paf_Mur | Ci_ActionSet_Force_SameAction | Ci_ActionSet_Force_No_Blend )
	}
	else
		ACT_ActionSet(ACTION_Paf_Air_Moyen | Ci_ActionSet_Force_SameAction | Ci_ActionSet_Force_No_Blend )
	KBC_action_frequency_set(1.0)
	
	o_paf_backup_actor = o_best_paf_pere
	
	if( i_big_bat )
	{
		// BOSS : la bat noire ne conserve pas l'intérêt après un moyen/gros paf, afin que les bats grises descendent attaquer à leur tour
		@get_global f_time_bat_noire_escape = TIME_Get()
		OBJ_CapaSet(none, CAPA_BigBat_attaque_en_cours)
		COL_CrossableSet(none, Gmat_KK_Cross_All_But_Kong_And_Raptors)		// ne sort pas de la zone de KONG
	}
	
	// CADAVRES
	o_cadavre = nobody
	
	KBC_Paf_BankingReset()
	
	// Réactivation wp chute pour les autres bats ----------------
	KBC_Chute_WP_reservation_free(o_chute_bats_wp)
}
else
{
	f_time_start_etat += TIME_GetDt()
}

// ANALYSE ============================================================================
if( f_time_start_etat >= 10.0 )
{
	OBJ_Destroy()
}

MACRO_CHECK_PAFS


// COMPORTEMENT =====================================================================

// INTERET ---------------------------------------------------------------------------------------------------------------------
if( EVENT_LIFE_CurLifeGet(ID_LIFE) > 0.0 && o_cible_attaque )
	KBC_Interet_Update(mid_interet_attaque, C_EVENT_InteretStatusAttack, o_cible_attaque, tmid_vision)

i_flag_neck = faux

if( ! ACT_ActionIsTransition() && ACT_ActionFinished() )
{
	if( EVENT_LIFE_CurLifeGet(ID_LIFE) <= 0.0 )
	{
		COL_CrossableSet(Gmat_KK_Face_eau, none)		// maintenant traverse l'eau...
		if( KBC_Chute_WP_Select() )
			macro_change_etat("KBC_ETAT_Chute_WP")
		else
			macro_change_etat("KBC_ETAT_Chute_Libre")
	}
	else if( i_big_bat )
	{
		COL_CrossableSet(Gmat_KK_Cross_All_But_Kong_And_Raptors, none)		// peut ressortir de la zone de KONG
		f_paf_pause_time = 0.75
		macro_change_etat("KBC_ETAT_BigBat_Fight_Dodged")
	}
	else
	{
		if( i_interaction_Ann_fake_paf )
		{		
			o_cible_attaque = o_paf_backup_actor
			macro_change_etat("KBC_ETAT_Fight_Contact")
		}
		else if( o_cible_attaque )
			macro_change_etat("KBC_ETAT_Fight_Contact")
		else if( o_paf_backup_actor && KBC_Seen_Actor_Test(o_paf_backup_actor, tmid_vision) )
		{
			o_cible_attaque = o_paf_backup_actor
			macro_change_etat("KBC_ETAT_Fight_Contact")
		}
		else
			AI_Execute("KBC_exec_init_attente")
	}
}

OBJ_BankingGeneralSet(MATH_VecBlendRotate(OBJ_SightGet(), - v_last_paf_sight, 5 * TIME_GetDt()), KBC_Paf_BankingGet())


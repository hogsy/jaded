
#include "KBC_defines.var"

//=============================================================================
// LA BAT OBSERVE UNE TRACE DE SANG
//=============================================================================

messageid		tmid_tmp
messageid		tmid_vision


// SORTIE ETAT
if (i_sort_etat)
{
	v_decalage = Cv_NullVector
	f_cible_life_coef = 0
	
	i_sort_etat = faux
	return
}

// ENTREE ETAT
if (i_etat_courant != ETAT_Observe_Trace)
{
	i_etat_ancien = i_etat_courant
	i_etat_courant = ETAT_Observe_Trace
	
	if (fct_last_etat)
	{
		i_sort_etat = vrai
		AI_Execute(fct_last_etat)
	}
	fct_last_etat = AI_TrackCurGet()
	f_time_start_etat = 0.0
	
	f_time_action = TIME_Get()			// init durée d'observation
	
	// vitesse
	KBC_ref_speed_set(Cf_speed_observe)
	f_speed = 0.0		// bat à l'arrêt
	
	ACT_ActionSet(i_ACTION_Vol_Standard)
	KBC_action_frequency_set(Cf_freq_sur_place)
	
	if( i_DBG_trace_etat )
	{
		DBG_TraceObject(OBJ_Me())
		DBG_TraceString(" : etat observe trace ")
		DBG_TraceEOL()
	}
}
else
{
	f_time_start_etat += TIME_GetDt()
}


// ========== OBSERVATION =============


// INTERET ====================================
i_flag_neck = faux
if( MSG_GlobalIsValid(mid_sang) )
{
	KBC_Interet_Update(mid_interet_charogne, C_EVENT_InteretStatusAttack, EVENT_PereGet(mid_sang), tmid_vision)
	i_flag_neck = vrai
	v_look_pos = EVENT_PositionGet(mid_sang)
}



if( ! TIME_Elapsed(f_time_action, Cf_observ_trace) )
{
	// le bat observe la trace et cherche des persos blessés autour

	// ========= STIMULUS VISION ===========
	
	// Si le bat a un perso blessé en visuel, il le suit plutôt que de pister l'odeur de sang
	AI_Execute("KBC_exec_suit_perso_blesse")
	
	
	// ========= STIMULUS SANG ===========
	
	// sinon le bat renifle le sang
	tmid_tmp = mid_sang
	AI_Execute("KBC_exec_check_sang")
	if( MSG_GlobalIsValid(mid_sang) && (mid_sang != tmid_tmp) )
		macro_change_etat("KBC_ETAT_Move_Olfactif")		// tache de sang + fraiche => on recommence l'état Suivi Odeur
	else
		mid_sang = tmid_tmp		// récup ancienne trace de sang
}
else
{
	// le bat n'a rien vu
	i_flag_check_sang = faux
	f_time_flags_check_sang = TIME_Get()
	AI_Execute("KBC_exec_init_attente")
}



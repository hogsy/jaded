#include "KBC_defines.var"

messageid	tmid_vision

object		to_head


// SORTIE ETAT ========================================================================
if (i_sort_etat)
{
	f_rotation_angle_courant = 0.0		// Rotation
//	AI_Execute("KBC_exec_dyn_on")
//	DYN_GravitySet(Cv_NormalGravity)
	f_bigbat_delai_no_attack = Cf_bigbat_delai_no_attack
	f_bigbat_delai_no_kamera = Cf_bigbat_delai_no_kamera

	i_sort_etat = faux
	return
}


// ENTREE ETAT =======================================================================
if (i_etat_courant != ETAT_Fight_Pause)
{
	i_etat_ancien_ancien = i_etat_ancien
	i_etat_ancien = i_etat_courant
	i_etat_courant = ETAT_Fight_Pause
	if (fct_last_etat)
	{
		i_sort_etat = vrai
		AI_Execute(fct_last_etat)
	}
	fct_last_etat = AI_TrackCurGet()
	f_time_start_etat = 0.0
	KBC_DBG_Trace(i_DBG_trace_etat, "etat fight pause")
	
	DYN_Off()
	
	ACT_ActionSet(i_ACTION_Vol_Standard)
	KBC_ref_speed_set(0.0)
	KBC_action_frequency_set(1.0)
	
	v_fight_wait_pos = OBJ_PosGet()
}
else
{
	f_time_start_etat += TIME_GetDt()
}

// ANALYSE ==========================================================================================================
MACRO_CHECK_PAFS


// COMPORTEMENT ==========================================================================================
if( o_cible_attaque )
{
	// INTERET 
	if( EVENT_LIFE_CurLifeGet(ID_LIFE) > 0.0 )
		KBC_Interet_Update(mid_interet_attaque, C_EVENT_InteretStatusAttack, o_cible_attaque, tmid_vision)

	// REGARD -----------------------------------------------------------
	to_head = @o_cible_attaque ANI_CanalObjectGet(Anim_Canal_Tete)
	if( ! to_head )
		to_head = o_cible_attaque
	
	i_flag_neck = vrai
	v_look_pos = @to_head OBJ_PosGet()
}


// DELAI PAUSE ---------------------------------------------------
if( MATH_VecNullToler(v_vitesse_derapage, Cf_vitesse_derapage_null_toler) )
	f_paf_pause_time -= MATH_FloatMin(f_paf_pause_time, TIME_GetDt())


// FIN DE LA PAUSE --------------------------------------------

if( f_paf_pause_time <= 0.0 )
{
	if( o_cible_attaque_contact )
	{
		o_cible_attaque = o_cible_attaque_contact
		macro_change_etat("KBC_ETAT_Fight_Approche")
	}
	else if( o_cible_attaque )
		AI_Execute("KBC_exec_init_attaque")
	else
		AI_Execute("KBC_exec_init_attente")
}


// ORIENTATION --------------------------------------------------
AI_Execute("KBC_exec_MOVE_fight_wait")


if( ! MATH_VecNullToler(v_vitesse_derapage, Cf_vitesse_derapage_null_toler) )
	ACT_ActionSet(ACTION_Falaise_Vol)
else
	ACT_ActionSet(i_ACTION_Vol_Standard)

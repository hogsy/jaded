#include "KBC_defines.var"

int			ti_test_ground

vector	tv_inclinaison


// SORTIE ETAT
if (i_sort_etat)
{	
	i_sort_etat = faux
	return
}

// ENTREE ETAT
if (i_etat_courant != ETAT_Sol_Ecrasement )
{
	i_etat_ancien_ancien = i_etat_ancien
	i_etat_ancien = i_etat_courant
	i_etat_courant = ETAT_Sol_Ecrasement
	if (fct_last_etat)
	{
		i_sort_etat = vrai
		AI_Execute(fct_last_etat)
	}
	fct_last_etat = AI_TrackCurGet()
	f_time_start_etat = 0.0
	KBC_DBG_Trace(i_DBG_trace_etat, "etat sol ecrasement")
	
	AI_Execute("KBC_exec_dyn_on")
	DYN_GravitySet(Cv_NormalGravity)
	
	v_paf_offset = Cv_NullVector				// plus de recul !!! (anim spécifique)
	
	if( i_big_bat )
		ACT_ActionSet(ACTION_Paf_Tombe_Sol_Dos_tr_Agonie )
	else if( i_mort_au_sol_sur_le_dos )
		ACT_ActionSet(ACTION_Tombe_au_sol_sur_le_dos)
	else
		ACT_ActionSet(ACTION_Ecrase)
	SND_RequestPlay(Ci_SND_Ecrasement)

	if( ! i_big_bat )
	{
		OBJ_CapaSet(CAPA_Dead_on_Ground, none)			// si pas passée dans l'état chute
		OBJ_CapaSet(CAPA_Dead, none)
	}
}
else
{
	f_time_start_etat += TIME_GetDt()
}

i_flag_neck = faux

ti_test_ground = faux

switch( ACT_ActionGet() )
{
	case ACTION_Tombe_au_sol_sur_le_dos :
		if( ANI_CurrentFrameGet(0) > 60 )
			ti_test_ground = vrai
		break
	default:
		if( ACT_ActionFinished() )
			ti_test_ground = vrai
		break
}

if( ti_test_ground && ( COL_CollideType(COL_C_Ground) || i_splash_flag ) )
{
	if( i_big_bat )
		macro_change_etat("KBC_ETAT_BigBat_Sol_Wait_Finished")
	else
		macro_change_etat("KBC_ETAT_Sol_Mort")
}

if( i_splash_flag )
	tv_inclinaison = Cv_VerticalVector
else if( ! MATH_VecNullEpsilon(v_ground_normale) )
	tv_inclinaison = v_ground_normale
else
	tv_inclinaison = Cv_VerticalVector
OBJ_BankingSet(MATH_VecBlendRotate(OBJ_BankingGet(), tv_inclinaison, 10.0 * TIME_GetDt()))


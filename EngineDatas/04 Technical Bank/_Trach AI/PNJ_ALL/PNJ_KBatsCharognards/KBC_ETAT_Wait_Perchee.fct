#include "KBC_defines.var"

messageid		tmid_vision


// SORTIE ETAT
if (i_sort_etat)
{
	i_sort_etat = faux
	return
}

// ENTREE ETAT
if (i_etat_courant != ETAT_Wait_Perche )
{
	i_etat_ancien_ancien = i_etat_ancien
	i_etat_ancien = i_etat_courant
	i_etat_courant = ETAT_Wait_Perche
	if (fct_last_etat)
	{
		i_sort_etat = vrai
		AI_Execute(fct_last_etat)
	}
	fct_last_etat = AI_TrackCurGet()
	f_time_start_etat = 0.0
	KBC_DBG_Trace(i_DBG_trace_etat, "etat wait perchee")
	
	ACT_ActionSet(ACTION_Perchee_Attente)
	
	DYN_Off()
	f_time_action = 0.0
	i_perchee_delai_envol = faux
	
	f_perchee_move_time = MATH_RandFloat(0.5, 4.5)
}
else
{
	f_time_start_etat += TIME_GetDt()
	
	if( i_perchee_delai_envol )
		f_perchee_delai_envol -= MATH_FloatMin(f_perchee_delai_envol, TIME_GetDt())
}

// ANALYSE ===============================================================================
MACRO_CHECK_PAFS

i_flag_neck = faux

// SOUND -----------------------------------------------------------------------------------------
AI_Execute("KBC_exec_check_sound")


switch( ACT_ActionGet() )
{
	case ACTION_Perchee_Attente :
		f_perchee_move_time -= MATH_FloatMin(f_perchee_move_time, TIME_GetDt())
		if( ! f_perchee_move_time )
		{
			f_perchee_move_time = MATH_RandFloat(5.0, 10.0)
			i_perchee_next_action = 1 - i_perchee_next_action
			if( i_perchee_next_action )
				ACT_ActionSet(ACTION_Perchee_Panic)
			else
				ACT_ActionSet(ACTION_Perchee_Panic_var)
		}
		break
		
	case ACTION_Perchee_Panic :
	case ACTION_Perchee_Panic_var :
		if( ACT_ActionFinished() )
			ACT_ActionSet(ACTION_Perchee_Attente)
		break
		
	default: 
		DBG_Error("état perchée, anim ????")
		break
}


// TRIGGER D'ATTAQUE ---------------------------------------------------------------------
if( ! i_perchee_delai_envol )
{
	if( i_flag_check_sound_valide || KBC_Test_Attaque())
	{
		i_perchee_delai_envol = vrai
		f_perchee_delai_envol = MATH_RandFloat(0.25,1.0)		// ne pas toutes décoller en même temps
	}
}
else
{
	if( ! f_perchee_delai_envol )
	{
		SND_RequestPlay(Ci_SND_Cri_Attack_Loin)			// je crie pour annoncer que je vais attaquer
		AI_Execute("KBC_exec_dyn_on")
		DYN_SpeedSetVector(Cv_NullVector)
		tmid_vision = EVENT_FindEventPereTarget(C_EVENT_TYPE_Visibility, o_cible_attaque, nobody)
		if( MSG_GlobalIsValid(tmid_vision) )
			KBC_Seen_Actor_Add(tmid_vision)
		macro_change_etat("KBC_ETAT_Fight_Approche")
	}
}


OBJ_BankingGeneralSet(
	MATH_VecBlendRotate(OBJ_SightGet(), v_pendu_init_sight, 5 * TIME_GetDt()),
	MATH_VecBlendRotate(OBJ_BankingGet(), v_pendu_init_banking, 5 * TIME_GetDt()))
	

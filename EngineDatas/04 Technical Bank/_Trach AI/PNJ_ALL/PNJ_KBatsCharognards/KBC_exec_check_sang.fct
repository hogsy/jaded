#include "KBC_defines.var"

messageid		tmid_olf_ID
messageid		tmid_vis_ID

float				tf_time					// date de la trace de sang

int					ti_olf_rank
int					ti_ID

object			to_pere

vector			tv_position 				// position de la trace de sang


if( ! i_flag_check_sang ) 
	return			// le bat n'est plus intéressé par les odeurs de sang

if( i_check_sang_already_done )
	return
	
i_check_sang_already_done = vrai

tv_position = Cv_NullVector
tf_time = 0.0

ti_olf_rank = -1
for ( 	tmid_olf_ID = MSG_GlobalScan(C_EVENT_TYPE_Odor, &ti_olf_rank);
		MSG_GlobalIsValid(tmid_olf_ID);
		tmid_olf_ID = MSG_GlobalScan(C_EVENT_TYPE_Odor, &ti_olf_rank) )
{
	// lecture des infos de l'event
	to_pere = EVENT_PereGet(tmid_olf_ID)
	
	if( to_pere == OBJ_Me() )
		continue
	
	tf_time = EVENT_LifeGet(tmid_olf_ID)
	tv_position = EVENT_PositionGet(tmid_olf_ID)
	
	// il est dans mon rayon de détection olfactive
	if( KBC_Pos_in_Territory(tv_position) || KBC_Pos_in_BV(tv_position, o_zone_food_chain) )
	{
		// il est dans ma zone d'activité
		if( (! MSG_GlobalIsValid(mid_sang)) || (tf_time >= EVENT_LifeGet(mid_sang)) )		// ou = pour raffraichir la position de sang courante
		{
			// le bat n'est pas en train de suivre une trace ou cette trace est + fraîche
			tmid_vis_ID = EVENT_FindEventPereTarget(C_EVENT_TYPE_Visibility, to_pere, nobody)
			if( MSG_GlobalIsValid(tmid_vis_ID) )
			{
				ti_ID = EVENT_VisionIDGet(tmid_vis_ID)
				if( ti_ID == C_ID_Tyranosaure )
				{
					if( EVENT_VisionLifeStateGet(tmid_vis_ID) <= Cf_Life_Dead )
					{
						mid_sang = tmid_olf_ID		// cet acteur est mort => le bat le choisit
						break
					}
				}
			}
		}
	}
}


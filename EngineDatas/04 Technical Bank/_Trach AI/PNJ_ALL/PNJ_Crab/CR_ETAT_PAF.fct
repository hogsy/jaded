#include "CR_defines.var"

messageid		tmid_vision

vector			tv_sight


// SORTIE ETAT ==============================================================================
if (i_sort_etat)
{
	f_time_force_requin = Cf_time_force_requin
	o_paf_actor = nobody
	
	i_sort_etat = faux
	return
}


// ENTREE ETAT ==============================================================================
if (i_etat_courant != ETAT_PAF )
{
	i_etat_ancien = i_etat_courant
	i_etat_courant = ETAT_PAF
	str_etat = "Paf"
	if (fct_etat_courant)
	{
		i_sort_etat = vrai
		AI_Execute(fct_etat_courant)
	}
	
	fct_etat_courant = AI_TrackCurGet()
	f_time_start_etat = 0.0

	CR_Paf_Select_Action()

	if( CR_Target_Change_Enabled(o_paf_actor) && CR_Target_Check(o_paf_actor) )
	{
		if( i_DBG_trace_etat )
		{
			DBG_TraceObject(OBJ_Me())
			DBG_TraceString(" : laisse ")
			DBG_TraceObject(o_fight_actor)
			DBG_TraceString(" et préfère ")
			DBG_TraceObject(o_paf_actor)
			DBG_TraceString(" (gros paf)")
			DBG_TraceEOL()
		}	
	}
	
	o_fight_actor = o_paf_actor
	
//	v_paf_recul_backup = v_paf_recul
	v_paf_recul_backup = v_paf_dyn_speed
	DBG_RenderVector(OBJ_PosGet(), v_paf_recul_backup, color_rouge)
	
	if( i_DBG_trace_etat )
	{
		DBG_TraceObject(OBJ_Me())
		DBG_TraceString(" : ETAT PAF")
		DBG_TraceEOL()
	}
	
	i_antre_move_flag = faux
	
	if( ! f_HP_cur )
		CR_InfosMort()
}
else
{
	f_time_start_etat += TIME_GetDt()
}


// ANALYSE ===================================================================================

AI_Execute("CR_exec_CHECK_Vision")

if( f_time_start_etat > 0.0 )
{
	AI_Execute("CR_exec_CHECK_Paf")
	if( CR_Paf_Check_Change_Etat() )
	{
		CR_Paf_Select_Action()
		v_paf_recul_backup = v_paf_dyn_speed
	}
}

// INTERET -----------------------------------------
CR_Interet_Update(mid_best_interet, C_EVENT_InteretStatusAttack, o_fight_actor, tmid_vision)

// COMPORTEMENT =============================================================================
if( ! CR_Is_Paf_Action() || ACT_ActionFinished() )		// pas d'anim pour un paf faible
{
	if( ! CR_Is_Paf_Action() )
	{
		if( f_HP_cur )
			macro_change_etat("CR_ETAT_FIGHT")
		else
		{
			ACT_ActionSet(ACTION_Mort)
			macro_change_etat("CR_ETAT_MORT")
		}
	}
	else if( ACT_ActionGet() == ACTION_Paf_Lance_deb )
	{
		if( f_HP_cur )
			ACT_ActionSet(ACTION_Paf_Lance_fin_std)
		else
			ACT_ActionSet(ACTION_Paf_Lance_fin_Mort)
	}
	else if( ACT_ActionGet() == ACTION_Paf_Lance_fin_Mort )
	{
		ACT_ActionSet(ACTION_Mort_Paf_Lance)
		macro_change_etat("CR_ETAT_MORT")
	}
	else if( ACT_ActionGet() == ACTION_Paf_Lance_fin_std )
	{
		if( f_HP_cur )
			macro_change_etat("CR_ETAT_FIGHT")
		else
			ACT_ActionSet(ACTION_Paf_Lance_fin_Mort)
	}
	else
	{
		// autres anims de paf
		if( f_HP_cur )
			macro_change_etat("CR_ETAT_FIGHT")
		else
		{
			if( ACT_ActionGet() == ACTION_Paf_Mortel_Variante )
				ACT_ActionSet(ACTION_Mort_Variante)
			else
				ACT_ActionSet(ACTION_Mort)
			macro_change_etat("CR_ETAT_MORT")
		}
	}
}

// ORIENTATION ------------------------------------------
if (MATH_VecNullEpsilon(v_paf_recul_backup))
	v_paf_recul_backup = OBJ_SightGet()
if( i_modele == MODELE_Geant )
	tv_sight = OBJ_SightGet()
else
	tv_sight = MATH_VecBlendRotate(OBJ_SightGet(), - v_paf_recul_backup, 5 * TIME_GetDt())
OBJ_BankingGeneralSet(tv_sight, macro_banking)

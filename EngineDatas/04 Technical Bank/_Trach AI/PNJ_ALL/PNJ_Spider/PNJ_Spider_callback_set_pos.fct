#include "PNJ_Spider_defines.var"

int				ti_index
int				ti_flag_collision

float			tf_norm
float			tf_coef
float			tf_dist
float			tf_target_speed

vector		tv_pos	
vector		tv_speed
vector		tv_new_sight
vector		tv_new_banking

vector		tv_point_A
vector		tv_OBBOX_center

object		to_bone
object		to_camera

to_camera = @get_global o_camera

if (o_predateur)
{
	to_bone = @o_predateur ANI_CanalObjectGet(Anim_Canal_Machoire)

	tv_speed = OBJ_PosGet()

	tf_coef = f_time_start_etat / 0.2

	if (tf_coef > 1.0)
	{
		tv_pos = @to_bone OBJ_PosGet()
		tv_pos += @to_bone OBJ_BankingGet() * 0.5
	
		OBJ_PosSet(tv_pos)
		OBJ_BankingGeneralSet(@to_bone OBJ_HorizonGet(), -@to_bone OBJ_SightGet())
	}
	else
	{
		tv_pos = @to_bone OBJ_PosGet()
		tv_pos += @to_bone OBJ_BankingGet() * 0.5
		tv_pos = MATH_VecBlend(OBJ_PosGet(), tv_pos, tf_coef)

		tv_new_sight = MATH_VecBlendRotate(OBJ_SightGet(), @to_bone OBJ_HorizonGet(), tf_coef)
		tv_new_banking = MATH_VecBlendRotate(OBJ_BankingGet(), -@to_bone OBJ_SightGet(), tf_coef)

		OBJ_PosSet(tv_pos)
		OBJ_BankingGeneralSet(tv_new_sight, tv_new_banking)
	}

//	av_modules_speed[ti_index] = @ao_modules[ti_index] OBJ_PosGet() - tv_speed
//	av_modules_speed[ti_index] /= TIME_GetDt()
}
else if (ai_perceived_ID[i_perceived_best_actor_index] == C_ID_Jack)
{
	OBJ_PosSet(@o_snap_bone OBJ_PosGet() + @o_snap_bone MATH_VecLocalToGlobal(v_snap_pos))
	OBJ_BankingGeneralSet(@o_snap_bone MATH_VecLocalToGlobal(v_snap_sight), @o_snap_bone MATH_VecLocalToGlobal(v_snap_banking))
	
	tv_new_banking = OBJ_PosGet()
	tv_new_banking -= @o_snap_bone OBJ_PosGet()
	f_grab_Z = MATH_VecDotProduct(tv_new_banking, @o_snap_bone OBJ_BankingGet())
	tv_new_banking -= f_grab_Z * @o_snap_bone OBJ_BankingGet()
	MATH_VecSetNormalize(tv_new_banking)
	tv_new_banking = MATH_VecRotate(tv_new_banking, @o_snap_bone OBJ_BankingGet(), TIME_GetDt() * 3.0)

	f_grab_Z = MATH_FloatBlend(f_grab_Z, 1.0 + MATH_Sin(2.0 * TIME_Get()), 4.0 * TIME_GetDt())

	tv_pos = tv_new_banking * 0.65
	tv_pos += @o_snap_bone OBJ_PosGet()
	tv_pos += f_grab_Z * @o_snap_bone OBJ_BankingGet()
	
	tv_speed = tv_pos - OBJ_PosGet()
	OBJ_PosSet(tv_pos)
	OBJ_BankingGeneralSet(tv_speed, tv_new_banking)
	
	v_snap_pos = @o_snap_bone MATH_VecGlobalToLocal(OBJ_PosGet() - @o_snap_bone OBJ_PosGet()) 
	v_snap_sight = @o_snap_bone MATH_VecGlobalToLocal(OBJ_SightGet())
	v_snap_banking = @o_snap_bone MATH_VecGlobalToLocal(OBJ_BankingGet())
}
else if (IsThis_ID_Humain(ai_perceived_ID[i_perceived_best_actor_index]))
{
	OBJ_PosSet(@o_snap_bone OBJ_PosGet() + @o_snap_bone MATH_VecLocalToGlobal(v_snap_pos))
	OBJ_BankingGeneralSet(@o_snap_bone MATH_VecLocalToGlobal(v_snap_sight), @o_snap_bone MATH_VecLocalToGlobal(v_snap_banking))
	
	if (f_time_start_etat < 0.65)
	{
		tv_new_banking = OBJ_PosGet()
		tv_new_banking -= @o_snap_bone OBJ_PosGet()
		f_grab_Z = MATH_VecDotProduct(tv_new_banking, @o_snap_bone OBJ_BankingGet())
		tv_new_banking -= f_grab_Z * @o_snap_bone OBJ_BankingGet()
		MATH_VecSetNormalize(tv_new_banking)
		tv_new_banking = MATH_VecRotate(tv_new_banking, @o_snap_bone OBJ_BankingGet(), TIME_GetDt() * 8.0)
	
		if (o_proie == o_main_actor)
			f_grab_Z = MATH_FloatBlend(f_grab_Z, MATH_Sin(2.0 * TIME_Get()), 4.0 * TIME_GetDt())
		else
			f_grab_Z = MATH_FloatBlend(f_grab_Z, 0.0, f_time_start_etat)
	
		tv_pos = tv_new_banking * 0.25
		tv_pos += @o_snap_bone OBJ_PosGet()
		tv_pos += f_grab_Z * @o_snap_bone OBJ_BankingGet()
		
		tv_speed = tv_pos - OBJ_PosGet()
		OBJ_PosSet(tv_pos)
		OBJ_BankingGeneralSet(tv_speed, tv_new_banking)
		
		v_snap_pos = @o_snap_bone MATH_VecGlobalToLocal(OBJ_PosGet() - @o_snap_bone OBJ_PosGet()) 
		v_snap_sight = @o_snap_bone MATH_VecGlobalToLocal(OBJ_SightGet())
		v_snap_banking = @o_snap_bone MATH_VecGlobalToLocal(OBJ_BankingGet())
	}
}
else if (ai_perceived_ID[i_perceived_best_actor_index] == C_ID_Spider)
{
	OBJ_PosSet(@o_snap_bone OBJ_PosGet() + @o_snap_bone MATH_VecLocalToGlobal(v_snap_pos))
	OBJ_BankingGeneralSet(@o_snap_bone MATH_VecLocalToGlobal(v_snap_sight), @o_snap_bone MATH_VecLocalToGlobal(v_snap_banking))	

	if (f_delay_before_moving_again)
	{
//		f_move_speed	= 0.0
//		f_delay_before_moving_again -= MATH_FloatMin(f_delay_before_moving_again, TIME_GetDt())
	}
	else
	{
		f_move_speed  = MATH_FloatBlend(f_move_speed , f_snaped_speed, 8.0 * TIME_GetDt())
		f_on_snaped_bone_length += f_move_speed * TIME_GetDt()

		while(i_snaped_bone_index < 13 && f_on_snaped_bone_length > f_snaped_bone_length * 0.5)
		{
			i_snaped_bone_index++
			f_on_snaped_bone_length -= f_snaped_bone_length
		}

		if ( ! f_move_speed || i_snaped_bone_index == 13)
		{
			f_snaped_speed = -1.5
			f_delay_before_moving_again = MATH_RandFloat(0.5, 1.0)
		}
	
		o_snap_bone = @o_proie ANI_CanalObjectGet(i_snaped_bone_index)

		tv_pos = OBJ_PosGet() - @o_snap_bone OBJ_PosGet()
		tv_pos -= MATH_VecDotProduct(tv_pos, @o_snap_bone OBJ_SightGet()) * @o_snap_bone OBJ_SightGet()
		tv_pos = MATH_VecRotate(tv_pos, @o_snap_bone OBJ_SightGet(), MATH_AbsFloat(f_move_speed) * 5.0 * TIME_GetDt())

		OBJ_BankingSet(tv_pos)
		MATH_VecSetNorm(tv_pos, @o_proie OBJ_ZoomGet() * 0.35)
		tv_pos -= @o_snap_bone OBJ_SightGet() * f_on_snaped_bone_length
		tv_pos += @o_snap_bone OBJ_PosGet()
		OBJ_BankingGeneralSet(tv_pos - OBJ_PosGet(), OBJ_BankingGet())
		OBJ_PosSet(tv_pos)

		v_snap_pos = @o_snap_bone MATH_VecGlobalToLocal(OBJ_PosGet() - @o_snap_bone OBJ_PosGet()) 
		v_snap_sight = @o_snap_bone MATH_VecGlobalToLocal(OBJ_SightGet())
		v_snap_banking = @o_snap_bone MATH_VecGlobalToLocal(OBJ_BankingGet())
	}
}


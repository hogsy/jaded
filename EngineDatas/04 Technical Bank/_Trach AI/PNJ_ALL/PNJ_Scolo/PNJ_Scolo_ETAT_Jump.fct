#include "PNJ_Scolo_defines.var"

int			ti_i
int			ti_flag_collision

float		tf_dist
float		tf_coef
float		tf_speed
float		tf_norm
float		tf_pow_coef
float		tf_delai_annonce
float		tf_jump_impulsion_max	
float		tf_jump_Z_offset

object	to_target
object	to_head_target
object	to_bassin

vector	tv_head_pos
vector	tv_head_sight
vector	tv_impulsion
vector	tv_new_sight
vector	tv_horizon
vector	tv_start_pos
vector	tv_speed
vector	tv_me_to_target
vector	tv_collide_pos
vector	tv_ik_start_pos
vector	tv_ik_start_sight

vector	tv_ray_start_pos
vector	tv_ray_dir
vector	tv_ray_col_normal

#define Cf_phase_0_duration				0.5
#define Cf_phase_1_duration				1.0

if (i_flag_sortie_etat)
{
	i_flag_sortie_etat = faux

	DYN_On()
	DYN_FlagsSet(DYN_C_BasicForces | DYN_C_VectorFriction | DYN_C_GlobalFriction | DYN_C_NeverDynamicFather, DYN_C_NeverDynamicHierarchy)
	DYN_GravitySet(v_Scolo_Gravity)

	COL_ColSetActivationSet(C_bit_zdm_pied, none)
	
	if (i_etat_courant != ETAT_Snap)
		o_proie = LNK_ThisClientGet(o_proie, Ci_LNK_GRAB_RAPTOR, mid_proie_LNK, faux, nofunc, nofunc, nofunc)

	return
}

if (i_etat_courant != ETAT_Jump)
{
	// INTIALISATION
	i_dernier_etat = i_etat_courant
	i_etat_courant = ETAT_Jump
	
	if (fct_last_etat)
	{
		i_flag_sortie_etat = vrai
		AI_Execute(fct_last_etat)
	}
	
	fct_last_etat = "PNJ_Scolo_ETAT_Jump"
	
	PNJ_Scolo_IK_Init(i_ik_modules_nb)

//	DYN_Off()
//	COL_ColSetActivationSet(none, C_bit_zdm_pied)

//	f_rot_sign = MATH_FloatSign(MATH_RandFloat(-1.0, 1.0))

	SND_RequestPlay(SND_GETUP)

	DYN_On()
	DYN_FlagsSet(DYN_C_BasicForces | DYN_C_VectorFriction | DYN_C_GlobalFriction | DYN_C_NeverDynamicFather, DYN_C_NeverDynamicHierarchy)
	DYN_GravitySet(v_Scolo_Gravity)
	DYN_FrictionVectorSet(Cv_Scolo_Jump_Friction)	

	COL_ColSetActivationSet(C_bit_zdm_pied, none)
	COL_StartMatrixSet(OBJ_PosGet())

	f_jump_duration = 0.0
	tv_impulsion = DYN_LIB_ImpulsionGet_Friction(OBJ_PosGet(), v_jump_dest_pos, v_Scolo_Gravity, Cf_Scolo_Jump_Friction, f_jump_duration, faux, 1.5)
	
	tf_norm = MATH_VecNorm(tv_impulsion)	
	if (tf_norm > Cf_impulsion_max_humain)
	{
		tv_impulsion /= tf_norm
		tv_impulsion *= Cf_impulsion_max_humain
	}

	DYN_SpeedSetVector(tv_impulsion)

//	f_delay_until_last_ground_col = 0.0	
//	PNJ_Scolo_Virtual_Net_Init(faux, nobody)

	PNJ_Scolo_Virtual_Net_Init(vrai, nobody)

	f_time_start_etat = 0.0
}
else
{
	f_time_start_etat += TIME_GetDt()
}

// ANALYSE ==============================================================================
o_KK_Grabbed_actor = LNK_ServeurGet(Ci_LNK_GRAB_KONG, mid_kong_LNK, vrai, nofunc, nofunc)
if (o_KK_Grabbed_actor)
	macro_change_etat("PNJ_Scolo_ETAT_Grabbed_By_Kong")

o_predateur = LNK_ServeurGet(Ci_LNK_GRAB_RAPTOR, mid_predateur_LNK, vrai, "PNJ_Scolo_exec_LNK_predateur", nofunc)
if (o_predateur)
{
	switch(LNK_GrabServeurVisionIDGet(mid_predateur_LNK))
	{
		case C_ID_Scolo :
			macro_change_etat("PNJ_Scolo_ETAT_Wait_For_Grab")
			break	
	
		default:
			macro_change_etat("PNJ_Scolo_ETAT_Grabbed_Predateur")
	}
}

AI_Execute("PNJ_Scolo_exec_check_vision")
 
AI_Execute("PNJ_Scolo_exec_check_paf")
if (i_flag_paf  || ! f_lifecur)
	macro_change_etat("PNJ_Scolo_ETAT_Paf")

AI_Execute("PNJ_Scolo_exec_check_best_interet")

// COMPORTEMENT ========================================================================
if (i_perceived_best_actor_index != -1)
{
	EVENT_InteretStatusSet(mid_best_interet, C_EVENT_InteretStatusLock)
	
	to_target = EVENT_InteretTargetGet(mid_best_interet)
	if (ai_perceived_ID[i_perceived_best_actor_index] == C_ID_Joueur)
		to_head_target = @get_global o_camera
	else
		to_head_target = @to_target ANI_CanalObjectGet(Anim_Canal_Tete)
	
	to_bassin = @to_target ANI_CanalObjectGet(Anim_Canal_Bassin)
	
	PNJ_Scolo_UncollideAdd(to_target, Cf_EVENT_Duree_1Trame)
}

if (f_time_start_etat > 0.1 && (COL_CollideType(COL_C_Ground) || COL_GMatReportGet(i_gmat_water_bit) != -1))
	macro_change_etat("PNJ_Scolo_ETAT_Sol")
		
tf_speed = DYN_SpeedGet()

if (tf_speed > 0.001)
{
	tv_new_sight = DYN_SpeedGetVector()
	tv_new_sight /= tf_speed
	tv_new_sight = MATH_VecBlendRotate(OBJ_SightGet(), tv_new_sight, 6.0 * TIME_GetDt())

	OBJ_SightGeneralSet(tv_new_sight, MATH_VecBlend(OBJ_BankingGet(), Cv_VerticalVector, 6.0 * TIME_GetDt()))
}

PNJ_Scolo_Modules_Update(0, nobody)

#include "PNJ_Raptor_KK_defines.var"

object	to_collide_object
object	to_camera

float		tf_sqr_hor_dist
float		tf_interet
float		tf_imprecision
float		tf_delay_since_last_update
float		tf_dist

int			ti_i
int			ti_flag_go_to_grid_center
int			ti_followed_ground_ID
int			ti_flag_try_to_move

vector	tv_offset
vector	tv_temp

object	to_interet_gao

// SORTIE ETAT ===================================================================
if (i_sort_etat)
{
	i_sort_etat = faux
	return
}

// INITIALISATION ETAT ==============================================================
if (i_etat_courant != ETAT_search)
{
	i_dernier_etat = i_etat_courant
	i_etat_courant = ETAT_search

	if (fct_last_etat)
	{
		i_sort_etat = vrai
		AI_Execute(fct_last_etat)
	}

	fct_last_etat = AI_TrackCurGet()

	i_flag_head_search = faux

	f_time_start_etat = 0.0
	i_propose_camera_fight = faux
}
else
{
	f_time_start_etat += TIME_GetDt()
}

// ANALYSE =======================================================================================================

o_grabbed_actor_KK = LNK_ServeurGet(Ci_LNK_GRAB_KONG, mid_grabbed_by_Kong_LNK_ID, vrai, nofunc, nofunc)
if (o_grabbed_actor_KK)
	macro_change_etat("PNJ_Raptor_KK_grabbed")

macro_check_proie_groupe
if( ! o_proie_groupe )
{
	// recherche d'une attaque solo
	AI_Execute("PNJ_Raptor_KK_exec_check_sound")
	AI_Execute("PNJ_Raptor_KK_exec_check_vision")
	AI_Execute("PNJ_Raptor_KK_exec_check_interet")
	if (MSG_GlobalIsValid(mid_new_interet) || (MSG_GlobalIsValid(mid_best_interet) && mid_best_interet != mid_last_interet))	
		macro_change_etat("PNJ_Raptor_KK_hesite")
}
else
	macro_change_etat("PNJ_Raptor_KK_fight")		// attaque groupée détectée -> mode fight

macro_check_FA_ann_kkgrab

macro_check_FA_valide_interet

//AI_Execute("PNJ_Raptor_KK_exec_check_sound")
//AI_Execute("PNJ_Raptor_KK_exec_check_vision")

i_flag_zde_fight_enable = vrai

AI_Execute("PNJ_Raptor_KK_exec_check_paf")
if( o_paf_actor )
{
	KR_Paf_Resolution(fct_track_change)
	if( fct_track_change != nofunc )
		macro_change_etat(fct_track_change)
}
	

AI_Execute("PNJ_Raptor_KK_exec_check_fury")
if ( o_flee_actor )
	macro_change_etat("PNJ_Raptor_KK_intimide")


//AI_Execute("PNJ_Raptor_KK_exec_check_interet")
//if (MSG_GlobalIsValid(mid_new_interet) || (MSG_GlobalIsValid(mid_best_interet) && mid_best_interet != mid_last_interet))
//	macro_change_etat("PNJ_Raptor_KK_hesite")


if ( ! MSG_GlobalIsValid(mid_best_interet))
	macro_change_etat(fct_main_etat)
else
{
	if( EVENT_FilterGet(mid_best_interet) & C_EVENT_FILTER_KingKong )
		i_fight_actor_type = Ci_fight_actor_KONG
	else if( EVENT_FilterGet(mid_best_interet) & C_EVENT_FILTER_Tyranosaure )
		i_fight_actor_type = Ci_fight_actor_TREX
	else
		i_fight_actor_type = Ci_fight_actor_Humain
}

EVENT_InteretStatusSet(mid_best_interet, C_EVENT_InteretStatusLock)

// TEST POUR MORDRE
if (i_flag_head_contact && ( i_fight_actor_type == Ci_fight_actor_Humain ) )
	macro_change_etat("PNJ_Raptor_KK_mord")				// le raptor ne mord pas le TREX (sinon double paf -> TREX au sol KO)

tf_delay_since_last_update = TIME_Get() - EVENT_InteretUpdateTimeGet(mid_best_interet)

if( tf_delay_since_last_update < 0.5)
{
	// VU CETTE TRAME
	macro_change_etat("PNJ_Raptor_KK_fight")
}

v_way_destpos = EVENT_InteretPositionGet(mid_best_interet)
v_way_destpos.z += 1.0

if (tf_delay_since_last_update < 2.0 && EVENT_InteretGet(mid_best_interet) > 0.0)
{
	// On avait vu quelque chose mais il n'est plus là
	i_flag_look = vrai
	v_look_pos = v_way_destpos
	
	ACT_ActionSet(Action_Observe)
	returntrack
}

// SUIVI REGARD =========================
i_flag_look = vrai

if (i_flag_head_search)
{
	tv_temp = OBJ_PosGet()
	tv_temp += OBJ_SightGet() * 3.0
	tv_temp += OBJ_HorizonGet() * (MATH_Sin(2.0 * TIME_Get()) * 3.0)
	
	if (tf_delay_since_last_update < 1.0)
		v_look_pos = MATH_VecBlend(v_way_destpos, tv_temp, tf_delay_since_last_update)
	else
		v_look_pos = tv_temp
}
else
{
	v_look_pos = v_way_destpos
	if ( i_way_moving)
		EVENT_LifeSet(mid_best_interet, 5.0)
	else if ( EVENT_LifeGet(mid_best_interet) < 4.8) // autorise 0.2sec de search avant d arreter.
		EVENT_LifeSet(mid_best_interet, 0.0)
}

tf_imprecision = EVENT_InteretPrecisionGet(mid_best_interet)
DBG_RenderCircle(v_way_destpos + Cv_VerticalVector, tf_imprecision, Cv_VerticalVector, color_rouge)

tv_temp = v_way_destpos - OBJ_PosGet()
tv_temp.z = 0.0

if (!i_flag_head_search && MATH_VecDotProduct(tv_temp, tv_temp) < tf_imprecision * tf_imprecision)
	i_flag_head_search = vrai

tv_temp = MATH_VecCrossProduct(tv_temp, Cv_VerticalVector)
MATH_VecSetNormalize(tv_temp)
tv_temp *= tf_imprecision
v_way_destpos += tv_temp

tv_temp = v_way_destpos
tv_temp -= OBJ_PosGet()
tv_temp.z = 0.0

tf_sqr_hor_dist = MATH_VecDotProduct(tv_temp, tv_temp)

// COMPORTEMENT ==================================================================================================	
ti_flag_go_to_grid_center = vrai

DBG_RenderVector(v_way_destpos, cvector(0.0, 0.0, 5.0), color_bleu)

ti_flag_try_to_move = faux

if (tf_sqr_hor_dist > 4.0)
	ti_flag_try_to_move = vrai

if (ti_flag_try_to_move)
{
	AI_Execute("PNJ_Raptor_KK_exec_way_find")

	v_joy_dir = v_way_case_dest
	v_joy_dir -= OBJ_PosGet()
	v_joy_dir.z = 0.0

	f_joy_norm = MATH_VecNorm(v_joy_dir)

	if (i_way_moving && f_joy_norm)
	{
		v_joy_dir /= f_joy_norm
		
		f_joy_norm = MATH_FloatLimit(f_joy_norm, 0.0, 1.0)
		
		ti_flag_go_to_grid_center = faux
	}
}

if (i_way_moving)
{
	if (!i_flag_run && i_way_case_nbr > 8)
		i_flag_run = vrai
	else if (i_flag_run && i_way_case_nbr < 5)
		i_flag_run = faux
}
else
{
	if (!i_flag_run && tf_sqr_hor_dist > 64.0)
		i_flag_run = vrai
	else if (i_flag_run && tf_sqr_hor_dist < 25.0)
		i_flag_run = faux
}
			
if (ti_flag_go_to_grid_center)
{
	i_flag_run = faux

//	if (i_flag_way_ok)
//	{
//		// On est arrivé, on se centre sur la case
//		v_joy_dir = v_way_case_dest
//		v_joy_dir -= OBJ_PosGet()
//		v_joy_dir.z = 0.0
//		
//		f_joy_norm = MATH_VecNorm(v_joy_dir)
//	
//		if (f_joy_norm)
//			v_joy_dir /= f_joy_norm
//		else
//			v_joy_dir = OBJ_SightGet()
//	
//		if (f_joy_norm > 0.25)
//			f_joy_norm = MATH_FloatLimit(f_joy_norm, 0.0, 1.0)
//		else
//			f_joy_norm = 0.0
//	}
//	else
	{
		f_joy_norm = 0.0
		v_joy_dir	 = OBJ_SightGet()
	}
}

//AI_Execute("PNJ_Raptor_KK_exec_select_action")


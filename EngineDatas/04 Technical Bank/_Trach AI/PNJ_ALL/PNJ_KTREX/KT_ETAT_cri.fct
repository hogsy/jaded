#include "KT_defines.var"

//procedure_ultra int	LIb_Kong_Attack( object po_object);
//Include_UltraProcedure_Header

#define	Cf_Time_Force_Wait		0.5
object	to_followed_actor
object	to_collide_object
object	to_camera

float		tf_sqr_hor_dist
float		tf_interet
float		tf_delay_since_last_update
float		tf_dot

int			ti_i
int			ti_flag_go_to_grid_center
int			ti_followed_ground_ID
int			ti_flag_try_to_move

vector	tv_offset
vector	tv_temp


// SORTIE ETAT ===================================================================
if (i_sort_etat)
{
//	if( o_fury_actor == o_ANN )
//		KT_ReAttackDelay()			// script attaques ANN / REX
//	
	if (i_etat_courant != ETAT_JumpAttak)
		o_fury_actor = nobody		// conservé pour le test AwareOfKong
	
	i_stunned_flag = faux
	i_flag_crie_au_lieu_de_charger = faux
	
	i_sort_etat = faux
	return
}

// INITIALISATION ETAT ==============================================================
if (i_etat_courant != ETAT_cri)
{
	i_dernier_etat = i_etat_courant
	i_etat_courant = ETAT_cri

	if (fct_last_etat)
	{
		i_sort_etat = vrai
		AI_Execute(fct_last_etat)
	}

	fct_last_etat = AI_TrackCurGet()
	if ( fct_main_etat == nofunc )
		fct_main_etat = "KT_ETAT_fight_KONG"
	ai_grabbed_availibility[ Ci_LNK_GRAB_KONG] = vrai
	
	f_time_start_etat = 0.0
	
	if( ! o_fury_actor )
		DBG_BreakPoint()
	
	KT_Action_Cri_Select(o_fury_actor)
	
	// STUNNED ?
	if( i_stunned_flag )
		f_stunned_last_delay = 0.0
}
else
{
	f_time_start_etat += TIME_GetDt()
}

// ANALYSE =============================================================================

// Attack sur Ann
//if( o_interaction_target && @o_ANN KAnn_Death_Attack_Enabled() )
//{
//	o_fight_actor = o_interaction_target
//	f_fury_last_time_hurlement = TIME_Get()
//	macro_change_etat(fct_main_etat)
//}

// Detection du vide ==========================
macro_checkchute

// PAFS ==================================
AI_Execute("KT_exec_check_paf")
if (o_paf_actor)
{
	macro_change_etat("KT_ETAT_paf")
}

// Jack fait diversion
Macro_Check_Paf_Jauge_Crier

// GRAB KONG ============================
o_grabbed_actor_KK = LNK_ServeurGet(Ci_LNK_GRAB_KONG, mid_grabbed_by_Kong_LNK_ID, vrai, nofunc, nofunc)
if (o_grabbed_actor_KK)
{
	macro_change_etat("KT_ETAT_Grabbed_by_Kong")
}

// COMPORTEMENT =======================================================================

KT_Interet_Update(o_fury_actor, C_EVENT_InteretStatusLock)

if( (KT_Action_Cri_EnCours() && ACT_ActionFinished()) 
	|| f_time_start_etat > 3.0 
	|| ( i_Kong_Mashing && ! i_flag_crie_au_lieu_de_charger ) )
{
	o_fight_actor = o_fury_actor
	f_fury_last_time_hurlement = TIME_Get()
	macro_change_etat(fct_main_etat)		// retourne dans l'état appelant
}

if( KT_Action_Cri_EnCours() )
{
	// Suivi du regard pour le cri (pas pour l'attente blessée)
	i_flag_look = vrai
	v_look_pos = KT_GetActorPosToLook(o_fury_actor)
}



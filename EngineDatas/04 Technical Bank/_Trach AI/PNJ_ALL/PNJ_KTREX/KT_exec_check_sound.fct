#include "KT_defines.var"

//event sound
int					ti_rank
messageid		tmid_sound_event
object			EVT_Pere
vector			tv_sound_pos
// event vision
messageid		EVT_Visibility_ID
int					ti_index


if (i_flag_audio_check_done)
	return

i_flag_audio_check_done = vrai

ti_rank = -1
for (	tmid_sound_event = MSG_GlobalScan(C_EVENT_TYPE_Sound, &ti_rank); 
		MSG_GlobalIsValid(tmid_sound_event);
		tmid_sound_event = MSG_GlobalScan(C_EVENT_TYPE_Sound, &ti_rank)	)
{
	EVT_Pere = EVENT_PereGet(tmid_sound_event)
	if (EVT_Pere != OBJ_Me())
	{
		tv_sound_pos = EVENT_PositionGet(tmid_sound_event)
		if (MATH_VecDistance(OBJ_PosGet(), tv_sound_pos) < EVENT_SoundSizeGet(tmid_sound_event))
		{
			// ON ENTEND
			EVT_Visibility_ID = EVENT_FindEventPereTarget(C_EVENT_TYPE_Visibility, EVT_Pere, nobody)
			if( ! MSG_GlobalIsValid(EVT_Visibility_ID) )
			{
				DBG_Error("J'entends un acteur qui n'a pas d'événement de vision !")
				continue
			}
			
			// ACTEUR PERCU
			ti_index = KT_Add_Perceived_Actor(EVT_Pere, 0, EVT_Visibility_ID)		// retour -1 impossible
			
			// MAJ MEILLEUR INTERET
			if (MSG_GlobalIsValid(mid_best_interet) && EVT_Pere == EVENT_InteretTargetGet(mid_best_interet))
				i_perceived_best_actor_index = ti_index
			
			// MAJ STATUS PERCEPTION
			ai_perceived_status[ti_index] |= Ci_PERCEIVED_SOUND
		}
	}
}


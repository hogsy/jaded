#include "prs_RaptorSlim_defines.var"

float		tf_delay

object	to_target

// SORTIE ETAT ===================================================================
if (i_sort_etat)
{
	i_sort_etat = faux
	return
}

// INITIALISATION ETAT ==============================================================
if (i_etat_courant != ETAT_PRS_FIGHT) 
{
	i_dernier_etat = i_etat_courant
	i_etat_courant = ETAT_PRS_FIGHT
	if (fct_last_etat)
	{
		i_sort_etat = vrai
		AI_Execute(fct_last_etat)
	}
	
	PROC_WAY_CLEAR()
	fct_last_etat = AI_TrackCurGet()
	fct_main_etat = AI_TrackCurGet()	
}



// STIMULI ===========================================================================

if( DBG_Mount_Only )
	macro_change_etat("prs_ETAT_ATTENTE")
	
AI_Execute("prs_exec_check_cadavre")
AI_Execute("prs_exec_check_vision")

AI_Execute("prs_exec_check_paf")
if (i_flag_paf_fly)
	macro_change_etat("prs_ETAT_PAF_FLY")
if (i_flag_paf_fall)
	macro_change_etat("prs_ETAT_PAF_FALL")
if (i_flag_paf_repousse || i_flag_paf_slide || ! EVENT_LIFE_CurLifeGet(ID_LIFE))
	macro_change_etat("prs_ETAT_PAF_SLIDE")

AI_Execute("prs_exec_update_best_interest")


// GRABBED ----------------------------------------------------------
o_grabbed_actor_KK = LNK_ServeurGet(Ci_LNK_GRAB_KONG, mid_grabbed_by_Kong_LNK_ID, vrai, nofunc, nofunc)
if (o_grabbed_actor_KK)
	macro_change_etat("prs_ETAT_GRABBED")

	
if (i_flag_change_target)
	macro_change_etat("prs_ETAT_HESITE")

if (ai_perceived_accessible[i_perceived_best_actor_index] && ai_perceived_status[i_perceived_best_actor_index] & Ci_PERCEIVED_IS_DEAD)
	macro_change_etat("prs_ETAT_DEVORE")

if ( MSG_GlobalIsValid(mid_best_interet))
{
	if (i_perceived_best_actor_index == -1 
	|| (TIME_Get() - EVENT_InteretSeenTimeGet(mid_best_interet)) > Cf_delay_before_lose_target)
	{
		macro_change_etat("prs_ETAT_SEARCH")
	}
}
else
{
	macro_change_etat("prs_ETAT_ATTENTE")
}

// UPDATE NEW TARGET POS
switch ( i_way_computation_mode)
{
	case Ci_WAY_MODE_FLEE :
		if ( i_flag_in_my_territory)
			i_way_computation_mode = Ci_WAY_MODE_FIGHT
			
		if ( o_next_wp)
		{
			v_way_destpos = v_next_pos
			i_target_territory_ID = i_next_territory	
		}
		else
		{
			to_target = EVENT_InteretTargetGet( mid_best_interet)
			i_target_territory_ID = PROC_GAO_GET_TERRITORY( to_target)
			v_way_destpos = @to_target OBJ_PosGet()
		}
		break
	case Ci_WAY_MODE_RETURN :
		to_target = EVENT_InteretTargetGet( mid_best_interet)
		i_target_territory_ID = PROC_GAO_GET_TERRITORY( to_target)
		if ( PROC_WAY_TERRITORY_ID_ALLOWED(i_target_territory_ID)
		&& ! (ai_perceived_status[i_perceived_best_actor_index] & Ci_PERCEIVED_IN_HIDING_PLACE) )
			i_way_computation_mode = Ci_WAY_MODE_FIGHT		
		else
		{
			i_way_computation_mode = Ci_WAY_MODE_RETURN		
			to_target = O_Return		// Waiting too long with no target
			i_target_territory_ID = PROC_GAO_GET_TERRITORY( to_target)
		}
		v_way_destpos = @to_target OBJ_PosGet()
		break	
	default:
		i_way_computation_mode = Ci_WAY_MODE_FIGHT
		to_target = EVENT_InteretTargetGet( mid_best_interet)
		i_target_territory_ID = PROC_GAO_GET_TERRITORY( to_target)
	
	
		if ( i_target_territory_ID != i_old_good_target_territory_ID
		&& ! PROC_WAY_TERRITORY_ID_ALLOWED(i_target_territory_ID))
		{
			// NEW TERRITORY IS NOT AVAILABLE
//			v_retour_pos = v_way_destpos
//			i_retour _territory_ID = i_old_good_target_territory_ID
			f_time_target_not_reachable += TIME_GetDt()
			i_target_territory_ID = i_old_good_target_territory_ID
			v_way_destpos = v_way_old_good_destpos
			if ( f_time_target_not_reachable > 6.0 )
			{
				i_way_computation_mode = Ci_WAY_MODE_RETURN
			}
		}
		else
		{
			f_time_target_not_reachable = 0.0
			v_way_destpos = @to_target OBJ_PosGet()
		}	
}


// COMPORTEMENT ===========================================================================

AI_Execute("prs_exec_test_bite")


// SI UN RAPTOR OU UN GALIMINUS ATTAQUE LE MAIN HORS ECRAN
if (to_target == o_main_actor && f_on_screen_pourcent == -1.0)
	i_flag_run = faux
else if (af_perceived_dist[i_perceived_best_actor_index] > 6.0 * OBJ_ZoomGet()) 
	i_flag_run = vrai
else
	i_flag_run = faux

tf_delay	= TIME_Get() - EVENT_InteretSeenTimeGet(mid_best_interet)
if (tf_delay > Cf_delay_before_lose_target)
	macro_change_etat("prs_ETAT_SEARCH")


AI_Execute("prs_exec_way_find")

AI_Execute("prs_exec_way_move")

AI_Execute("prs_exec_select_action")

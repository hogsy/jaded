#include "prs_RaptorSlim_defines.var"

int				ti_i
int				ti_action

float			tf_dot_X
float			tf_dot_Y
float			tf_sight_speed

vector		tv_new_sight
vector		tv_dest_sight
vector		tv_speed

// SORTIE ETAT ===================================================================
if (i_sort_etat)
{
	i_sort_etat = faux

	COL_ZoneSizeSet(C_zdm_pied, cvector(0.75, 0.75, 0.75))
	COL_ZonePosSet(C_zdm_pied, cvector(0.0, 0.0, 0.75))

	return
}

// INITIALISATION ETAT ==============================================================
if (i_etat_courant != ETAT_PRS_PAF_SLIDE)
{
	i_dernier_etat = i_etat_courant
	i_etat_courant = ETAT_PRS_PAF_SLIDE

	if (fct_last_etat)
	{
		i_sort_etat = vrai
		AI_Execute(fct_last_etat)
	}

	fct_last_etat = AI_TrackCurGet()
	
	if( i_dernier_etat == ETAT_PRS_RIDED )
		i_flag_Rided_paf = vrai
	else
		i_flag_Rided_paf = faux
	
	PROC_WAY_CLEAN()

//	o_grab_actor = LNK_ClientGet(Ci_LNK_GRAB_EAGLE, mid_grab_actor_LNK_ID, faux, nofunc, nofunc, nofunc)

	DYN_FrictionVectorSet(cvector(f_friction, f_friction, 0.0))	

	i_flag_snap_pos = faux
	i_flag_cri = faux

	SND_RequestPlayOnObjCanal(Ci_SND_Paf_Moyen, Anim_Canal_Tete)

	v_paf_speed = DYN_SpeedGetVector()
	v_paf_speed += v_paf_dir * 10.0
	DYN_SpeedSetVector(v_paf_speed)

	if (f_lifecur)
	{
		tf_dot_X = MATH_VecDotProduct(v_paf_dir, OBJ_HorizonGet())
		tf_dot_Y = MATH_VecDotProduct(v_paf_dir, OBJ_SightGet())
		
		if (MATH_AbsFloat(tf_dot_X) > MATH_AbsFloat(tf_dot_Y))
		{
			if (tf_dot_X < 0.0)
				PRS_ActionSet(Action_Paf_Petit_Sol_G)
			else
				PRS_ActionSet(Action_Paf_Petit_Sol_D)
		}
		else
		{
			if (tf_dot_Y > 0.0)
				PRS_ActionSet(Action_Paf_Petit_Sol_Dos)
			else
				PRS_ActionSet(Action_Paf_Petit_Sol_Face)
		}
	}
	else
	{
		PRS_ActionSet(Action_Paf_Sol_sur_place_tr_Mort)
	}

	COL_ZoneSizeSet(C_zdm_pied, cvector(1.25, 1.25, 1.25))
	COL_ZonePosSet(C_zdm_pied, cvector(0.0, 0.0, 1.25))

	f_time_start_etat = 0.0
	f_paf_agonie_time = MATH_RandFloat(1.5, 3.0)
}
else
{
	f_time_start_etat += TIME_GetDt()

//	DYN_SpeedSetVector(v_dyn_speed)
}

//DYN_SpeedSetVector(DYN_SpeedGetVector() + v_paf_speed)
//DYN_LIB_Position_After_N_Seconds(OBJ_PosGet(), v_paf_speed, DYN_FrictionVectorGet(), DYN_GravityVectorGet(), TIME_GetDt(), 0.0)

// ANALYSE =======================================================================================================
//AI_Execute("prs_exec_check_msg")

AI_Execute("prs_exec_check_cadavre")
AI_Execute("prs_exec_check_vision")
//AI_Execute("PNJ_Raptor_exec_check_sound")

//AI_Execute("PNJ_Raptor_exec_check_collision")

AI_Execute("prs_exec_check_paf")
if (i_flag_paf_fly)
	macro_change_etat("prs_ETAT_PAF_FLY")
if (i_flag_paf_fall)
	macro_change_etat("prs_ETAT_PAF_FALL")

//AI_Execute("PNJ_Raptor_exec_check_shoot")

AI_Execute("prs_exec_update_best_interest")

// GRABBED ----------------------------------------------------------
o_grabbed_actor_KK = LNK_ServeurGet(Ci_LNK_GRAB_KONG, mid_grabbed_by_Kong_LNK_ID, vrai, nofunc, nofunc)
if (o_grabbed_actor_KK)
	macro_change_etat("prs_ETAT_GRABBED")
	
// RIDED -----------------------------------------------------------------
o_Rided_Actor = LNK_ServeurGet(Ci_LNK_RIDE_ON_NMI, mid_Rided_LNK_ID, vrai, nofunc, nofunc)
if( o_Rided_Actor )
{
	if( ! i_flag_Rided_paf )
		macro_change_etat("prs_ETAT_RIDED")
}
else
{
	i_flag_Rided_paf = faux
}


//AI_Execute("PNJ_Raptor_exec_check_client")

// COMPORTEMENT =================================================================================================
ti_action = ACT_ActionGet()
switch(ti_action)
{
	case Action_Paf_Sol_sur_place_tr_Mort :
		if( ACT_ActionFinished() )
			f_paf_agonie_time = 0.0
		break
		
	case Action_Agonie_cycl :
		f_paf_agonie_time -= MATH_FloatMin(f_paf_agonie_time, TIME_GetDt())
		break
			
	default:
		if ( ! f_lifecur )
		{
			PRS_ActionSet(Action_Paf_Sol_sur_place_tr_Mort)
		}
		else if( ACT_ActionFinished() )
		{
			PRS_ActionSet(Action_Agonie_cycl)
		}
		else
		{
			ACT_LIB_ActionFrequencyMultiply(0.5)
	
			switch(ti_action)
			{
				case Action_Paf_Petit_Sol_G :
					tv_new_sight = cvector(1.0, 0.0, 0.0)
					break
				case Action_Paf_Petit_Sol_D :
					tv_new_sight = cvector(-1.0, 0.0, 0.0)
					break
				case Action_Paf_Petit_Sol_Face :
					tv_new_sight = cvector(0.0, -1.0, 0.0)
					break
				case Action_Paf_Petit_Sol_Dos :
					tv_new_sight = cvector(0.0, 1.0, 0.0)
					break
			
			}
			
			tv_dest_sight = MATH_VecGlobalToLocal(-v_paf_dir)
			tv_dest_sight.z = 0.0
			OBJ_Rotate_FromTo(tv_new_sight, MATH_VecBlendRotate(tv_new_sight, tv_dest_sight, 6.0 * TIME_GetDt()))
		}
}


if( ! f_paf_agonie_time )
{
	if ( ! MATH_FloatNullEpsilon(f_burning_duration))
		macro_change_etat("prs_ETAT_BURN")
	else if (f_lifecur <= 0.0)
		macro_change_etat("prs_ETAT_MORT")
	else if (f_ground_col_time)
		macro_change_etat("prs_ETAT_FALL")
	else if( i_flag_Rided_paf )
		macro_change_etat("prs_ETAT_RIDED")
	else
		macro_change_etat(fct_main_etat)
}


#include "prs_RaptorSlim_defines.var"

int			ti_i
int			ti_rank
int			ti_rank2
int			ti_context
int			ti_type_sol
int			ti_flag_target_is_hided
int			ti_index
int			ti_flag_best_interest_finded
int			ti_flag_ok

float		tf_dot_product
float		tf_dist
float		tf_best_dist

vector	tv_ray_start_pos
vector	tv_target_pos
vector	tv_me_to_target
vector	tv_temp

object	EVT_Pere
object	to_best_interet_target
object	to_cam

messageid		EVT_Visibility_ID
messageid		EVT_Interet_ID
messageid		EVT_InfoSeen_ID

message		tmsg_filter

int				ti_sphere_color
int				ti_cone_color
color			tc_vision_color

//return

if (i_flag_visual_check_done)
	return

i_flag_visual_check_done = vrai

// COLORS --------------------------------------
ti_cone_color = 0x0
ti_sphere_color = 0x0

tf_best_dist = Cf_Infinit

tv_ray_start_pos = v_look_head_pos
if (ACT_ActionGet() == Action_Mange_Au_Sol)
	tv_ray_start_pos.z = OBJ_PosGet().z + 0.5

if (MSG_GlobalIsValid(mid_best_interet))
	to_best_interet_target = EVENT_InteretTargetGet(mid_best_interet)
else
	to_best_interet_target = nobody

ti_rank = -1
for (	EVT_Visibility_ID = MSG_GlobalScan(C_EVENT_TYPE_Visibility, &ti_rank);
		MSG_GlobalIsValid(EVT_Visibility_ID);
		EVT_Visibility_ID = MSG_GlobalScan(C_EVENT_TYPE_Visibility, &ti_rank))
{
	// SI CE N'EST PAS UN ACTEUR QUI M'INTERESSE, JE PASSE AU SUIVANT
	if (PRS_Invalid_Vision_Event(EVT_Visibility_ID))
		continue

	EVT_Pere = EVENT_PereGet(EVT_Visibility_ID)

	// I DON T CHECK SAME ACTORS
	if ( AI_HaveSameModel( EVT_Pere))
	{
		if (EVT_Pere != OBJ_Me() )
		{
			if( AI_HaveSameModel(EVT_Pere) )
			{
				ao_raptors[i_raptors_nb] = EVT_Pere
				i_raptors_nb++
			}
		}
		continue
	}
	
	// RECUPERATION DE L'INDEX DE CE PERSO DANS LE TABLEAU DES ACTEURS PERCUS
	ti_index = PRS_Add_Perceived_Actor(EVT_Pere, 0, EVT_Visibility_ID)		// Je veux un status pour l attaquer

	// SI POUR UNE RAISON OU POUR UNE AUTRE, CET ACTEUR N'A PAS ETE VALIDE...
	if (ti_index == -1)
		continue

	// ON MET A JOUR L'INDEX DES INFOS DE NOTRE MEILLEUR INTERET
	if (EVT_Pere == to_best_interet_target)
		i_perceived_best_actor_index = ti_index

	// LES CADAVRES SONT PERCUS DE TOUTE MANIERE
	if (ai_perceived_status[ti_index] & Ci_PERCEIVED_IS_DEAD)
	{
		ai_perceived_seen[ti_index] = vrai
		continue
	}

	tv_target_pos = EVENT_PositionGet(EVT_Visibility_ID)
	tv_target_pos.z += 1.0

	// RECUPERATION DU CONTEXT ET DU TERRAIN SUR LEQUEL LE PERSO EST
	ti_context = EVENT_VisionContextGet(EVT_Visibility_ID)
	ti_type_sol = ti_context / 10
	ti_context -= ti_type_sol * 10

	if (ai_perceived_status[ti_index] & Ci_PERCEIVED_IS_DEAD)
	{
		// UN CADAVRE N'EST PAS CACHE !!!
		ti_flag_target_is_hided	= faux
	}
	else
	{
		// EST-CE QUE LE PERSO EST CACHE ?
		ti_flag_target_is_hided	= faux
	
		switch(ti_type_sol)
		{
			case Ci_sol_herbe :
				tv_temp = v_look_head_pos - tv_target_pos
				tv_temp.z = 0.0
		
				if (ACT_ActionGet() == Action_Observe && MATH_VecDotProduct(tv_temp, tv_temp) < 16.0)
					ti_flag_target_is_hided = faux
				else if (ti_context == C_EVENT_CONTEXT_ACCROUPI || ti_context == C_EVENT_CONTEXT_ALLONGE)
					ti_flag_target_is_hided = vrai
				break
			
			case Ci_sol_herbe_haute :
					ti_flag_target_is_hided = vrai
				break
		}
	}
	
	if (ti_flag_target_is_hided)
		continue

	if (ai_perceived_accessible[ti_index] && ai_perceived_ID[ti_index] == C_ID_Rayman )
	{
		EVT_InfoSeen_ID = EVENT_AddEventInfo(OBJ_Me(), EVT_Pere, C_EVENT_INFOTYPE_SEEN)
	}

	if (ai_perceived_status[ti_index] & Ci_PERCEIVED_IDENTIFIED)
	{
		// PERSO DEJA VU => TOUJOURS VU
		ai_perceived_seen[ti_index] = vrai
		if (MATH_LIB_ZoneInCone(OBJ_PosGet(), OBJ_SightGet(), Cf_Cos30, f_vision_near_in_move_dist, tv_target_pos, 1.0, vrai, tf_dot_product, 0, 0) )
			ai_perceived_status[ti_index] |= Ci_PERCEIVED_NEAR_IN_MOVE_CONE
	}
	else
	{
		// PERSO PAS ENCORE IDENTIFIE
		ti_flag_ok = faux	
		if( ai_perceived_ID[ti_index] != C_ID_Rayman )
			ti_flag_ok = vrai
		else if( MATH_LIB_ZoneInCone(v_look_head_pos, v_look_banking, Cf_cos_angle_vision, Cf_dist_vision, tv_target_pos, 1.0, vrai, tf_dot_product, ti_cone_color, ti_sphere_color) )
			ti_flag_ok = vrai
		
		if (ti_flag_ok)
		{
			// ON CONSIDERE QUE CET ACTEUR EST DANS MON CHAMPS DE VISION	
		
			if ( ai_perceived_ID[ti_index] == C_ID_Rayman )
			{
				@get_global i_raptor_ray_on_colmap_nb++
				COL_SpecificCrossableSet(all)
				ti_flag_ok = COL_LIB_Can_See_Actor(EVT_Pere, tv_ray_start_pos, tv_target_pos, all, OBJ_C_IdentityFlag_Anims, Ci_Filter_IdentityFlag, COL_C_Ray_on_ColMap_NoCrossable + COL_C_Ray_use_SpecificCrossableSet)
			}
			else
			{
				ti_flag_ok = vrai
			}

			if (ti_flag_ok)
			{
				// ON CONSIDERE QUE CET ACTEUR N'EST PAS CACHE PAR UNE COLMAP OU DE L'HERBE	
				ai_perceived_seen[ti_index] = vrai
				ai_perceived_status[ti_index] |= Ci_PERCEIVED_IDENTIFIED
			
				// EST-CE QUE CET ACTEUR EST DANS MON CONE DE DEPLACEMENT ?
				if (MATH_LIB_ZoneInCone(OBJ_PosGet(), OBJ_SightGet(), Cf_Cos30, f_vision_near_in_move_dist, tv_target_pos, 1.0, vrai, tf_dot_product, 0, 0) )
					ai_perceived_status[ti_index] |= Ci_PERCEIVED_NEAR_IN_MOVE_CONE
			}
		}
	}

	if (ai_perceived_accessible[ti_index] && ai_perceived_seen[ti_index] && ai_perceived_ID[ti_index] == C_ID_Rayman )
		EVENT_Info_SeenSet(EVT_InfoSeen_ID, vrai)
}

PRS_Check_Perceived_Actor()

if (MSG_GlobalIsValid(mid_best_interet) && i_perceived_best_actor_index == -1)
{
	PRS_Del_Interest()
}

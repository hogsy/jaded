int		ti_rank
int		ti_ind
int		ti_ind2
int		ti_test
int		ti_color 
int		ti_detect[5]
int		ti_fiter
float	tf_dist
object	to_stimulus
vector	tv_stimulus
vector	tv_vct

object	EVT_Pere
vector	EVT_Position

messageid		tmid_event


ti_test = faux

for( ti_ind = 0; ti_ind < 5; ti_ind++)
	ti_detect[ti_ind] = 0
// Précalcul des zones triggers
for( ti_ind = 0; ti_ind < i_nb_zone; ti_ind++)
	av_centerGlob[ ti_ind] = ( cvector(0,0,0.6) + OBJ_PosGet()) + MATH_VecLocalToGlobal( av_center[ ti_ind])


// TEST VISION
to_stimulus = nobody

ti_rank = -1
tmid_event = MSG_GlobalScan(i_type, &ti_rank)
while(MSG_GlobalIsValid(tmid_event))
{
	// On a une cible visible, test si ds zone triger
	EVT_Pere = EVENT_PereGet(tmid_event)
	EVT_Position = EVENT_PositionGet(tmid_event)
	if ( MATH_VecNullEpsilon(EVT_Position))
		EVT_Position = @EVT_Pere OBJ_PosGet()

	for( ti_ind2 = 0; ti_ind2 < i_nb_zone; ti_ind2++)
	{
		tv_vct = EVT_Position - av_centerGlob[ti_ind2]
		if ( tv_vct.z < 1.2)
		{
			tv_vct.z = 0.0
			tf_dist = MATH_VecNorm( tv_vct)  
			if ( tf_dist < af_rayon[ ti_ind2] )
			{
				// La Cible est dans 1 des triggers
				af_delaiDsTrigger[ti_ind2] += TIME_GetDt()
				ti_detect[ti_ind2] = 1
				if ( af_delaiDsTrigger[ti_ind2] > af_delai_avt_trigger[ti_ind2])
				{
					ti_test =  vrai
					to_stimulus = EVT_Pere
					tv_stimulus = EVT_Position
					ti_fiter = EVENT_FilterGet(tmid_event)
				}
			}
		}
	}
	
	tmid_event = MSG_GlobalScan(i_type, &ti_rank)
}

for( ti_ind = 0; ti_ind < 5; ti_ind++)
{
	if ( !ti_detect[ti_ind])
		af_delaiDsTrigger[ti_ind] = 0.0
}



// --- AFFICHAGE ---
if ( ti_test)
{
	// Détection d'une nouvelle trace de sang
	ti_color = color_rouge
	o_target_stimulus_trigger = to_stimulus
	v_target_stimulus_triger = tv_stimulus
	i_attaque_filtre = ti_fiter
}
else
	ti_color = color_noire
	
for( ti_ind = 0; ti_ind < i_nb_zone; ti_ind++)
	DBG_RenderCircle( av_centerGlob[ ti_ind], af_rayon[ ti_ind], Cv_VerticalVector, ti_color )
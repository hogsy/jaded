#include "KBM_defines.var"
Include_UltraProcedure_Header

procedure_local int KBM_GetNearestAttackWPindex(object to_bigbat)
{
	float		tf_dist
	float		tf_best_dist
	int			ti_best_index
	int			ti_k
	if( i_DBG_Manager )
	{
		DBG_TraceString("********** Calcul du nearest attack wp pour ")
		DBG_TraceObject(to_bigbat)
		DBG_TraceString(" -> ")
	}
	tf_best_dist = 9999
	ti_best_index = -1
	for( ti_k = 0; ti_k < ci_attack_wp_nb; ti_k++ )
	{
		if( ao_init_attack_wp[ti_k] )	// il y a un wp
		{
			if( ! AI_TriggerIsValid(at_cond_attack_wp[ti_k]) || call_trigger(at_cond_attack_wp[ti_k]) )	// pas de condition ou condition valide
			{
				tf_dist = MATH_VecNorm(@ao_init_attack_wp[ti_k] OBJ_PosGet() - @to_bigbat OBJ_PosGet())
				if( tf_dist < tf_best_dist )
				{
					tf_best_dist = tf_dist
					ti_best_index = ti_k
				}
			}
		}
	}
	if( i_DBG_Manager )
	{
		DBG_TraceObject(ao_init_attack_wp[ti_best_index])
		DBG_TraceEOL()
	}
	return ti_best_index
}

procedure_local int KBM_CetteBigBatAttaqueDeja(object to_bigbat)
{
	if( ! to_bigbat )
		return faux
	else
		return @to_bigbat OBJ_CapaTest(CAPA_BigBat_Attack_En_Cours)
}

procedure_local int KBM_UneBigBatAttaqueDeja(int ti_excluded_bigbat_index)
{
	int		ti_i
	for( ti_i = 0; ti_i < ci_bigbats_nb; ti_i++ )
	{
		if( ti_i != ti_excluded_bigbat_index )
		{
			if( ao_bigbats[ti_i] )
			{
				if( KBM_CetteBigBatAttaqueDeja(ao_bigbats[ti_i]) )
					return vrai
			}
		}
	}
	return faux
}

procedure_local int KBM_PafsNbMaxGet()
{
	if( i_attack_1ere_phase )
		return i_1ere_sequence_pafs_max_nb
	else
		return i_nieme_sequence_pafs_max_nb
}

procedure_local int KBM_PassagesNbMaxGet()
{
	if( i_attack_1ere_phase )
		return i_1ere_sequence_passages_max_nb
	else
		return i_nieme_sequence_passages_max_nb
}

procedure_local void KBM_CAPA_Kam_CC_Kong_to_Bat(int ti_bigbat_index)
{
	if( ! ti_bigbat_index )
		OBJ_CapaSet(CAPA_Kam_CC_Kong_to_1eBigBat, none)
	else
		OBJ_CapaSet(CAPA_Kam_CC_Kong_to_2eBigBat, none)
}

procedure_local void KBM_CAPA_Kam_CC_Bat_to_Kong(int ti_bigbat_index)
{
	if( ! ti_bigbat_index )
		OBJ_CapaSet(CAPA_Kam_CC_1eBigBat_to_Kong, none)
	else
		OBJ_CapaSet(CAPA_Kam_CC_2eBigBat_to_Kong, none)
}


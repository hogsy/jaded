#include "PNJ_Worm_defines.var"

float			tf_dot_X
float			tf_dot_Y

vector		tv_start_sight
vector		tv_dest_sight
vector		tv_new_banking

// SORTIE ETAT ====================================================================
if (i_flag_sort_etat)
{
	i_flag_sort_etat = faux
	return
}

// INITIALISATION ETAT ==============================================================
if (i_etat_courant != ETAT_PAF)
{
	i_dernier_etat = i_etat_courant
	i_etat_courant = ETAT_PAF
	
	if (fct_last_etat)
	{
		i_flag_sort_etat = vrai
		AI_Execute(fct_last_etat)
	}

	fct_last_etat = AI_TrackCurGet()

	if (f_lifecur)
	{
		if (i_flag_paf_faible)
			ACT_ActionSet(Action_Paf_Rafale)	
		else
			ACT_ActionSet(Action_Paf)
	}
	else
		ACT_ActionSet(Action_Mort_Paf)
	
	f_time_start_etat = 0.0
}
else
{
	f_time_start_etat += TIME_GetDt()
}

// ANALYSE ========================================================================
AI_Execute("PNJ_Worm_exec_check_vision")

//AI_Execute("PNJ_Worm_exec_check_paf")

AI_Execute("PNJ_Worm_exec_check_best_target")

// COMPORTEMENT ==================================================================
if (ACT_ActionFinished())
{
	if (f_lifecur)
		macro_change_etat(fct_main_etat)
	else
		macro_change_etat("PNJ_Worm_ETAT_MORT")
}

tv_new_banking = MATH_VecBlendRotate(OBJ_BankingGet(), v_ground_normale, 6.0 * TIME_GetDt())
OBJ_BankingSet(tv_new_banking)

tf_dot_X = MATH_VecDotProduct(v_paf_dir, OBJ_HorizonGet())
tf_dot_Y = MATH_VecDotProduct(v_paf_dir, OBJ_SightGet())

if (MATH_AbsFloat(tf_dot_X) > MATH_AbsFloat(tf_dot_Y))
{
	if (tf_dot_X > 0.0)
		tv_start_sight = cvector(1.0, 0.0, 0.0)
	else
		tv_start_sight = cvector(-1.0, 0.0, 0.0)
}
else
{
	if (tf_dot_Y > 0.0)
		tv_start_sight = cvector(0.0, 1.0, 0.0)
	else
		tv_start_sight = cvector(0.0, -1.0, 0.0)
}

tv_dest_sight = MATH_VecGlobalToLocal(-v_paf_dir)
tv_dest_sight.z = 0.0
OBJ_Rotate_FromTo(tv_start_sight, MATH_VecBlendRotate(tv_start_sight, tv_dest_sight, 6.0 * TIME_GetDt()))

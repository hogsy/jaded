#include "PNJ_Worm_defines.var"

object	to_target

vector	tv_new_sight
vector	tv_new_banking

// SORTIE ETAT ====================================================================
if (i_flag_sort_etat)
{
	i_flag_sort_etat = faux

	return
}

// INITIALISATION ETAT ==============================================================
if (i_etat_courant != ETAT_CATCH)
{
	i_dernier_etat = i_etat_courant
	i_etat_courant = ETAT_CATCH
	
	if (fct_last_etat)
	{
		i_flag_sort_etat = vrai
		AI_Execute(fct_last_etat)
	}

	fct_last_etat = AI_TrackCurGet()
	
//	ACT_ActionSet(Action_Grab_appel)
	ACT_ActionSet(Action_Grab_deb)

	f_time_start_etat = 0.0
}
else
{
	f_time_start_etat += TIME_GetDt()
}

// ANALYSE ========================================================================
AI_Execute("PNJ_Worm_exec_check_vision")

AI_Execute("PNJ_Worm_exec_check_paf")
if (o_paf_actor)
	macro_change_etat("PNJ_Worm_ETAT_PAF")

AI_Execute("PNJ_Worm_exec_check_best_target")

if (! MSG_GlobalIsValid(mid_best_interet))
	macro_change_etat("PNJ_Worm_ETAT_IDLE")

// COMPORTEMENT ==================================================================
//f_joy_norm = 0.0
//
//AI_Execute("PNJ_Worm_exec_select_action")

to_target = EVENT_InteretTargetGet(mid_best_interet)

switch(ACT_ActionGet())
{
	case Action_Grab_appel :
		if (ACT_ActionFinished())
			ACT_ActionSet(Action_Grab_deb)
		break

	case Action_Grab_deb :

		AI_Execute("PNJ_Worm_exec_test_grab")
		
		if (ACT_ActionFinished())
			ACT_ActionSet(Action_Grab_fin)
		break
		
	case Action_Grab_fin :

		AI_Execute("PNJ_Worm_exec_test_grab")	

		if (ACT_ActionFinished())
			macro_change_etat(fct_main_etat)
		break	
}

if (to_target == o_main_actor)
	ACT_LIB_ActionFrequencyMultiply(3.0)

tv_new_sight = @to_target OBJ_PosGet() - OBJ_PosGet()
tv_new_sight = MATH_VecBlendRotate(OBJ_SightGet(), tv_new_sight, 6.0 * TIME_GetDt())
tv_new_banking = MATH_VecBlendRotate(OBJ_BankingGet(), v_ground_normale, 6.0 * TIME_GetDt())

OBJ_BankingGeneralSet(tv_new_sight, tv_new_banking)

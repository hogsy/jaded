#include "PNJ_Worm_defines.var"

object	to_client

vector	tv_new_sight
vector	tv_new_banking

// SORTIE ETAT ====================================================================
if (i_flag_sort_etat)
{
	i_flag_sort_etat = faux

	o_grab_actor = LNK_ThisClientGet(o_grab_actor, Ci_LNK_GRAB_RAPTOR, mid_grab_actor_LNK_ID, faux, nofunc, nofunc, nofunc)
	
	return
}

// INITIALISATION ETAT ==============================================================
if (i_etat_courant != ETAT_GRAB)
{
	i_dernier_etat = i_etat_courant
	i_etat_courant = ETAT_GRAB
	
	if (fct_last_etat)
	{
		i_flag_sort_etat = vrai
		AI_Execute(fct_last_etat)
	}

	fct_last_etat = AI_TrackCurGet()
	
	f_time_start_etat = 0.0
}
else
{
	f_time_start_etat += TIME_GetDt()
}

// ANALYSE ========================================================================
AI_Execute("PNJ_Worm_exec_check_vision")

AI_Execute("PNJ_Worm_exec_check_paf")
if (o_paf_actor)
	macro_change_etat("PNJ_Worm_ETAT_PAF")

AI_Execute("PNJ_Worm_exec_check_best_target")

if (! MSG_GlobalIsValid(mid_best_interet))
	macro_change_etat("PNJ_Worm_ETAT_IDLE")

to_client = LNK_ThisClientGet(o_grab_actor, Ci_LNK_GRAB_RAPTOR, mid_grab_actor_LNK_ID, vrai, nofunc, nofunc, nofunc)
if (!to_client)
	macro_change_etat(fct_main_etat)

// COMPORTEMENT ==================================================================
PNJ_Worm_UncollideAdd(to_client, Cf_EVENT_Duree_1Trame)

@get_global f_raptor_last_grab_time = TIME_Get()

//f_joy_norm = 0.0
//
//AI_Execute("PNJ_Worm_exec_select_action")

switch(ACT_ActionGet())
{
	case Action_Grab_deb :
		if (ACT_ActionFinished())
			ACT_ActionSet(Action_Grab_fin)
		break
		
	case Action_Grab_fin :
		if (ACT_ActionFinished())
		{
			f_duration_before_next_paf = MATH_RandFloat(0.75, 1.5)
			ACT_ActionSet(Action_Grab_cycl)
		}
		break	

	case Action_Grab_cycl :
		f_duration_before_next_paf -= MATH_FloatMin(f_duration_before_next_paf, TIME_GetDt())
		if (!f_duration_before_next_paf)
			ACT_ActionSet(Action_Attaque_deb)
		break
		
	case Action_Attaque_deb :
		if (ACT_ActionFinished())
		{
			i_grab_hit_enable = vrai
			ACT_ActionSet(Action_Attaque_fin)
		}
		break
		
	case Action_Attaque_fin :

		if (i_grab_hit_enable && ANI_CurrentFrameGet(0) > 10)
		{
			i_grab_hit_enable = faux
			EVENT_AddEventPaf(C_EVENT_FILTER_All, C_PAF_KK_Fort, OBJ_Me(), 0.01, o_grab_actor, 10.0 * PAF_Unit, OBJ_SightGet())
		}	

		if (ACT_ActionFinished())
		{
			f_duration_before_next_paf = MATH_RandFloat(0.75, 1.5)
			ACT_ActionSet(Action_Grab_cycl)
		}
		break
		
	default:
		ACT_ActionSet(Action_Grab_cycl)
}

tv_new_sight = OBJ_SightGet()
tv_new_banking = MATH_VecBlendRotate(OBJ_BankingGet(), v_ground_normale, 6.0 * TIME_GetDt())

OBJ_BankingGeneralSet(tv_new_sight, tv_new_banking)

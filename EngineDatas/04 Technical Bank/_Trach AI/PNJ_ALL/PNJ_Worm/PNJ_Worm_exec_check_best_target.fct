#include "PNJ_Worm_defines.var"

int				ti_i
int				ti_flag_ok
int				ti_new_target_index
int				ti_flag_choose_best_interest

float			tf_interet
float			tf_best_interet

object		to_last_target

message	tmsg_trigger

if (MSG_GlobalIsValid(mid_best_interet))
	to_last_target = EVENT_InteretTargetGet(mid_best_interet)
else
	to_last_target = nobody

ti_new_target_index = -1

// TRIGGER ATTAQUE ==================================================================================
if (AI_TriggerIsValid(trigger_attaque) && ! i_flag_trigger_used)
{
	if (call_trigger(trigger_attaque))
	{
		switch(target_selection)	
		{
			case 0 :

				i_flag_trigger_used = vrai
			
				break
				
			case 1 :
	
				tmsg_trigger = AI_TriggerGetMsg(trigger_attaque)
		
				for (ti_i = 0; ti_i < i_perceived_actor_nb; ti_i++)
				{
					if (ao_perceived_actor[ti_i] == tmsg_trigger.msg_gao5)
					{
						i_flag_trigger_used = vrai
						
						ti_new_target_index = PNJ_Worm_Best_Interet_Update(ti_i)
						break
					}
				}
				
				break
		}
	}
	
	if (!i_flag_trigger_used)
		return
}

// REGLE PAF DU MAIN =================================================================================
if (ti_new_target_index == -1)
{
	if (i_perceived_main_actor_index != -1 && ai_perceived_status[i_perceived_main_actor_index] & Ci_PERCEIVED_PAF)
	{
		ti_new_target_index = PNJ_Worm_Best_Interet_Update(i_perceived_main_actor_index)

		if (ti_new_target_index != -1 && to_last_target != ao_perceived_actor[ti_new_target_index])
		{
#ifndef _FINAL_
			DBG_TraceFloat(TIME_Get())
			DBG_TraceString(" => ")
			DBG_TraceObject(OBJ_Me())
			DBG_TraceString(" préfère ")
			DBG_TraceObject(ao_perceived_actor[ti_new_target_index])
			if (to_last_target)
			{
				DBG_TraceString(" à ")
				DBG_TraceObject(to_last_target)
			}
			DBG_TraceString(" PAF DU MAIN ACTOR ")
			DBG_TraceEOL()
#endif
		}
	}
}

// REGLE PROXIMITE DU MAIN =================================================================================
if (ti_new_target_index == -1)
{
	ti_flag_ok = vrai

	if (i_perceived_main_actor_index == -1)
		ti_flag_ok = faux
	else if (ai_perceived_status[i_perceived_main_actor_index] & Ci_PERCEIVED_ALREADY_GRABBED)
		ti_flag_ok = faux
	else if (af_perceived_dist[i_perceived_main_actor_index] > 4.0)
		ti_flag_ok = faux
		
	if (ti_flag_ok)
	{
		ti_new_target_index = PNJ_Worm_Best_Interet_Update(i_perceived_main_actor_index)

		if (ti_new_target_index != -1 && to_last_target != ao_perceived_actor[ti_new_target_index])
		{
#ifndef _FINAL_
			DBG_TraceFloat(TIME_Get())
			DBG_TraceString(" => ")
			DBG_TraceObject(OBJ_Me())
			DBG_TraceString(" préfère ")
			DBG_TraceObject(ao_perceived_actor[ti_new_target_index])
			if (to_last_target)
			{
				DBG_TraceString(" à ")
				DBG_TraceObject(to_last_target)
			}
			DBG_TraceString(" MAIN ACTOR A MOINS DE 4 M")
			DBG_TraceEOL()
#endif
		}
	}
}
// REGLE PAF =========================================================================================
if (ti_new_target_index == -1)
{
	ti_flag_ok = vrai

	if (! TIME_Elapsed(f_time_change_target, Cf_delay_between_target_switching))
		ti_flag_ok = faux
	
	if (ti_flag_ok)
	{
		for (ti_i = 0; ti_i < i_perceived_actor_nb; ti_i++)
		{
			if (ai_perceived_status[ti_i] & Ci_PERCEIVED_PAF)
			{
				ti_new_target_index = PNJ_Worm_Best_Interet_Update(ti_i)
	
				if (ti_new_target_index != -1 && to_last_target != ao_perceived_actor[ti_new_target_index])
				{
#ifndef _FINAL_
					DBG_TraceFloat(TIME_Get())
					DBG_TraceString(" => ")
					DBG_TraceObject(OBJ_Me())
					DBG_TraceString(" préfère ")
					DBG_TraceObject(ao_perceived_actor[ti_new_target_index])
					if (to_last_target)
					{
						DBG_TraceString(" à ")
						DBG_TraceObject(to_last_target)
					}
					DBG_TraceString(" PAF D'UN ACTOR")
					DBG_TraceEOL()
#endif
				}
	
				break
			}
		}
	}
}


if (i_perceived_best_actor_index == -1)
{
}
else if (ai_perceived_status[i_perceived_best_actor_index] & Ci_PERCEIVED_ALREADY_GRABBED)
{
	// - SI MA CIBLE EST DEJA GRABBEE 
#ifndef _FINAL_
	if ( EVENT_InteretGet(mid_best_interet))
	{
		DBG_TraceFloat(TIME_Get())
		DBG_TraceString(" => ")
		DBG_TraceObject(OBJ_Me())
		DBG_TraceString(" constate que ")
		DBG_TraceObject(ao_perceived_actor[i_perceived_best_actor_index])
		DBG_TraceString(" est deja grabe par un autre acteur => INTERET NULL")
		DBG_TraceEOL()
	}
#endif

	EVENT_InteretSet(mid_best_interet, 0.0)
}


if (MSG_GlobalIsValid(mid_best_interet))
	tf_best_interet = EVENT_InteretGet(mid_best_interet)
else
	tf_best_interet = -1.0

if (tf_best_interet > 0.0 && i_perceived_best_actor_index != -1)
{
	// MISE A JOUR DE NOTRE CIBLE COURANTE	
	ti_flag_choose_best_interest	= faux

	PNJ_Worm_Best_Interet_Update(i_perceived_best_actor_index)
}
else
{
	// RECHERCHE DE LA MEILLEURE CIBLE PARMIS CELLES QUE JE VOIS, QUE J'ENTENDS ET DONT JE ME SOUVIENS
	ti_flag_choose_best_interest	= vrai

	for (ti_i = 0; ti_i < i_perceived_actor_nb; ti_i++)
	{
		if (ai_perceived_status[ti_i] & Ci_PERCEIVED_STUNNED)
		{
			// CADAVRE
			continue
		}

		if (ai_perceived_seen[ti_i] || ai_perceived_status[ti_i] & (Ci_PERCEIVED_STUNNED | Ci_PERCEIVED_HEARD))
		{
			tf_interet = af_perceived_interest[ti_i]
		
			if (ai_perceived_status[ti_i] & Ci_PERCEIVED_ALREADY_GRABBED)
			{
				// DEJA GRABE PAR UN AUTRE RAPTOR
				tf_interet = 0.0
			}

			if (tf_interet > tf_best_interet)
			{
				ti_new_target_index = ti_i
				tf_best_interet = tf_interet
			}
		}
	}

	if (ti_new_target_index != -1)
		PNJ_Worm_Best_Interet_Update(ti_new_target_index)
	else
		PNJ_Worm_Best_Interet_Update(i_perceived_best_actor_index)
}

if (MSG_GlobalIsValid(mid_best_interet) && to_last_target != EVENT_InteretTargetGet(mid_best_interet))
{
	i_flag_change_target = vrai
	f_time_change_target = TIME_Get()

#ifndef _FINAL_
	if (to_last_target)
	{
		DBG_TraceFloat(TIME_Get())
		DBG_TraceString(" => ")
		DBG_TraceObject(OBJ_Me())
		DBG_TraceString(" préfère ")
		DBG_TraceObject(ao_perceived_actor[ti_new_target_index])
		DBG_TraceString(" à ")
		DBG_TraceObject(to_last_target)
		DBG_TraceString(" MEILLEUR INTERET")
		DBG_TraceEOL()
	}
	else
	{
		DBG_TraceFloat(TIME_Get())
		DBG_TraceString(" => ")
		DBG_TraceObject(OBJ_Me())
		DBG_TraceString(" choisit  ")
		DBG_TraceObject(ao_perceived_actor[ti_new_target_index])
		DBG_TraceEOL()
	}	
#endif
}
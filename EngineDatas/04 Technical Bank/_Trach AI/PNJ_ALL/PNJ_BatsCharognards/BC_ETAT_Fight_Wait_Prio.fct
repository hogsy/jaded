#include "BC_defines.var"

messageid		tmid_vision

object			to_head

int					ti_cpt


// SORTIE ETAT ==================================================================================================
if (i_sort_etat)
{
	if( i_etat_courant != ETAT_Fight_Wait_Begin )
		BC_WP_Reservation_Del(o_backup_wp_depl_utilise)
	i_sort_etat = faux
	return
}

// ENTREE ETAT ==================================================================================================
if (i_etat_courant != ETAT_Fight_Wait_Prio)
{
	i_etat_ancien_ancien = i_etat_ancien
	i_etat_ancien = i_etat_courant
	i_etat_courant = ETAT_Fight_Wait_Prio
	if (fct_last_etat)
	{
		i_sort_etat = vrai
		AI_Execute(fct_last_etat)
	}
	fct_last_etat = AI_TrackCurGet()
	f_time_start_etat = 0.0
	BC_DBG_Trace_Etat("etat fight wait prio")
	
	ACT_ActionSet(i_ACTION_Vol_Standard)		// vol attente
	BC_action_frequency_set(Cf_freq_sur_place)
	BC_ref_speed_set(0.0)
	
	// wp d'attaque
	o_backup_wp_depl_utilise	= o_backup_nearest_cible_wp

	AI_Execute("BC_exec_dyn_on")
	
	v_fight_wait_pos = OBJ_PosGet()
	
	i_vitesse_derapage_init = vrai
	if( BC_Boss() )
		OBJ_CapaSet(CAPA_Boss_attaque, none)
}
else
{
	f_time_start_etat += TIME_GetDt()
}

// EMERGENCY ABORT =============================
if( o_cible_attaque && ! BC_TargetActive(o_cible_attaque) )
{
	DBG_TraceObject(OBJ_Me())
	DBG_TraceString(" : EMERGENCY ABORT : Target non active (")
	DBG_TraceObject(o_cible_attaque)	
	DBG_TraceString(")")
	DBG_TraceEOL()	
	
	BC_Cadavre_Del(o_cible_attaque)
	o_cible_attaque = nobody
	AI_Execute("BC_exec_init_attente")
}

// ANALYSE ==================================================================================================
if( 0 )
{
	ANNULE_ATTAQUE:
	AI_Execute("BC_exec_init_attente")
}

AI_Execute("BC_exec_check_fight_exit")
if( i_fight_exit_flag )
{
	goto ANNULE_ATTAQUE
}

// COMPORTEMENT =====================================================================================================

// REGARD 
to_head = @o_cible_attaque ANI_CanalObjectGet(Anim_Canal_Tete)
if( ! to_head )
	to_head = o_cible_attaque

i_flag_neck = vrai
v_look_pos = @to_head OBJ_PosGet()

// INTERET 
if( i_flag_reserve_interet )
{
	BC_Interet_Update(mid_interet_attaque, C_EVENT_InteretStatusLock, o_cible_attaque, tmid_vision)
}

// DUREE MAX D'ATTENTE SUR UN WP 
if( f_time_start_etat > f_attaque_delai_change_wp )
{
	DBG_TraceObject(OBJ_Me())
	DBG_TraceString(" : délai change wp (")
	DBG_TraceFloat(f_attaque_delai_change_wp)
	DBG_TraceString(" sec) => repasse en idle")
	DBG_TraceEOL()
	
	BC_reset_attaque()
	f_speed = 0.0
	AI_Execute("BC_exec_init_attente")
}


// FEU VERT POUR ATTAQUER 
ti_cpt = BC_Get_Interet_Status_Model_Nb(o_cible_attaque, -1, vrai)
if( ti_cpt < i_nb_attaques_simultanees )
	macro_change_etat("BC_ETAT_Fight_Wait_Begin")


// ORIENTATION 
AI_Execute("BC_exec_MOVE_fight_wait")


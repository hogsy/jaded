#include "BC_defines.var"

messageid	tmid_vision

object		to_head


// SORTIE ETAT ========================================================================
if (i_sort_etat)
{
	i_sort_etat = faux
	return
}


// ENTREE ETAT =======================================================================
if (i_etat_courant != ETAT_Fight_Pause)
{
	i_etat_ancien_ancien = i_etat_ancien
	i_etat_ancien = i_etat_courant
	i_etat_courant = ETAT_Fight_Pause
	if (fct_last_etat)
	{
		i_sort_etat = vrai
		AI_Execute(fct_last_etat)
	}
	fct_last_etat = AI_TrackCurGet()
	f_time_start_etat = 0.0
	BC_DBG_Trace_Etat("etat fight pause")
	
	DYN_Off()
	
	ACT_ActionSet(i_ACTION_Vol_Standard)
	BC_action_frequency_set(2.0)
	
	if( BC_Boss() )
		BC_ref_speed_set(4.0)		// vitesse de remontée verticale après un paf
	else
		BC_ref_speed_set(0.0)
	
	v_fight_wait_pos = OBJ_PosGet()
}
else
{
	f_time_start_etat += TIME_GetDt()
}

// EMERGENCY ABORT =============================
if( o_cible_attaque && ! BC_TargetActive(o_cible_attaque) )
{
	DBG_TraceObject(OBJ_Me())
	DBG_TraceString(" : EMERGENCY ABORT : Target non active (")
	DBG_TraceObject(o_cible_attaque)	
	DBG_TraceString(")")
	DBG_TraceEOL()	
	
	BC_Cadavre_Del(o_cible_attaque)
	o_cible_attaque = nobody
	AI_Execute("BC_exec_init_attente")
}

// ANALYSE ==================================================================================================
if( 0 )
{
	ANNULE_ATTAQUE:
	AI_Execute("BC_exec_init_attente")
}

if( o_cible_attaque )		// fight exit si j'étais en fight, pas si j'ai pris un paf alors que j'étais pas en fight
{
	AI_Execute("BC_exec_check_fight_exit")
	if( i_fight_exit_flag )
	{
		goto ANNULE_ATTAQUE
	}
}

// COMPORTEMENT ==========================================================================================
if( o_cible_attaque )
{
	// INTERET 
	if( i_agressive && f_HP )
		BC_Interet_Update(mid_interet_attaque, C_EVENT_InteretStatusAttack, o_cible_attaque, tmid_vision)

	// REGARD -----------------------------------------------------------
	to_head = @o_cible_attaque ANI_CanalObjectGet(Anim_Canal_Tete)
	if( ! to_head )
		to_head = o_cible_attaque
	
	i_flag_neck = vrai
	v_look_pos = @to_head OBJ_PosGet()
}

// DELAI PAUSE ---------------------------------------------------
f_paf_pause_time -= MATH_FloatMin(f_paf_pause_time, TIME_GetDt())


// FIN DE LA PAUSE --------------------------------------------

//if( i_modele == Ci_MODELE_Bat_Noire )
//	f_paf_pause_time = 0.0		// jamais de pause pour la bat noire

if( ! f_paf_pause_time )
{
	if( BC_Follower() )
	{
		macro_change_etat("BC_ETAT_Fight_Follower")
	}
	else if( BC_Boss() && i_etat_ancien == ETAT_Fight_Approche )
	{
//		f_boss_ronde_delai_take_paf = 2.0
//		AI_Execute("BC_exec_init_attente")
		AI_Execute("BC_exec_decolle_sans_proie")
	}
	else if( o_cible_attaque_contact )
	{
		o_cible_attaque = o_cible_attaque_contact
		macro_change_etat("BC_ETAT_Fight_Approche")
	}
	else if( ! BC_Boss() && o_cible_attaque )
		AI_Execute("BC_exec_init_attaque")
	else
		AI_Execute("BC_exec_init_attente")
}

// ORIENTATION --------------------------------------------------
AI_Execute("BC_exec_MOVE_fight_wait")


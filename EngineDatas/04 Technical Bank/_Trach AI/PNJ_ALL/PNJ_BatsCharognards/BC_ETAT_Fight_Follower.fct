#include "BC_defines.var"

messageid	tmid_vision

object		to_head
object		to_look_actor

vector		tv_offset
vector		tv_pos
vector		tv_sin


// SORTIE ETAT ========================================================================
if (i_sort_etat)
{
	i_sort_etat = faux
	return
}


// ENTREE ETAT =======================================================================
if (i_etat_courant != ETAT_Fight_Follower)
{
	i_etat_ancien_ancien = i_etat_ancien
	i_etat_ancien = i_etat_courant
	i_etat_courant = ETAT_Fight_Follower
	if (fct_last_etat)
	{
		i_sort_etat = vrai
		AI_Execute(fct_last_etat)
	}
	fct_last_etat = AI_TrackCurGet()
	f_time_start_etat = 0.0
	BC_DBG_Trace_Etat("etat fight follower")
	
	DYN_Off()
	
	ACT_ActionSet(i_ACTION_Vol_Standard)
	BC_action_frequency_set(MATH_RandFloat(1.0, 2.0))
	f_follow_blend_coef = 0.0
}
else
{
	f_time_start_etat += TIME_GetDt()
}

// ANALYSE ==================================================================================================
if( f_time_start_etat > 1.0 )
{
	tmid_vision = EVENT_FindEventPereTarget(C_EVENT_TYPE_Visibility, o_follow_gao, nobody)
	if( MSG_GlobalIsValid(tmid_vision) )
	{
		i_follow_already_seen = vrai		// attention secto différente entre le follower et le follow_gao !!!
		if( EVENT_VisionLifeStateGet(tmid_vision) <= 0.0 )
			f_follow_not_visible_duration += TIME_GetDt()
	}
	else
	{
		f_follow_not_visible_duration += TIME_GetDt()
	}
	if( i_follow_already_seen && f_follow_not_visible_duration > 2.0 )
	{
//		BC_DBG_Trace_Etat("o_follow_gao mort => devient une bat agressive standard")

		DBG_TraceObject(OBJ_Me())
		DBG_TraceString(" : o_follow_gao ")
		DBG_TraceObject(o_follow_gao)
		DBG_TraceString(" mort => devient une bat agressive standard")
		DBG_TraceEOL()
	
		o_follow_gao = nobody
		i_config_boss = Ci_config_boss_default
		AI_Execute("BC_exec_init_attente")
	}
}

// REGARD -----------------------------------------------------------
to_look_actor = o_jack
to_head = @to_look_actor ANI_CanalObjectGet(Anim_Canal_Tete)
if( ! to_head )
	to_head = to_look_actor
i_flag_neck = vrai
v_look_pos = @to_head OBJ_PosGet()

// ORIENTATION --------------------------------------------------
OBJ_BankingGeneralSet(
	MATH_VecBlendRotate(OBJ_SightGet(), @o_follow_gao OBJ_SightGet(), 1 * TIME_GetDt()),
	MATH_VecBlendRotate(OBJ_BankingGet(), @o_follow_gao OBJ_BankingGet(), 1 * TIME_GetDt()))

// POSITION --------------------------------------------------------
tv_offset = @o_follow_gao MATH_VecLocalToGlobal(v_follow_offset)
DBG_RenderVector(@o_follow_gao OBJ_PosGet(), tv_offset, color_rouge)
tv_pos = @o_follow_gao OBJ_PosGet()
tv_pos += tv_offset
tv_sin = cvector(MATH_Sin(f_follow_sin_desynchro * TIME_Get()), MATH_Sin(f_follow_sin_desynchro * 1.5 * TIME_Get()), MATH_Sin(f_follow_sin_desynchro * 3 * TIME_Get()))
tv_pos += tv_sin
f_follow_blend_coef = MATH_FloatBlend(f_follow_blend_coef, 2.0, TIME_GetDt())
OBJ_PosSet(MATH_VecBlend(OBJ_PosGet(), tv_pos, f_follow_blend_coef * TIME_GetDt()))


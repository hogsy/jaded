#include "PNJ_Raptor_defines.var"

procedure_local void PNJ_Raptor_ActionSet(int ti_action_num);

// PRECAL GRID LINE TEST ==============================================
procedure_local void PNJ_Raptor_Add_Precal_Grid_Line(vector tv_start_pos, vector tv_dest_pos, int ti_flag)
{
	int		ti_i
	int		ti_index
	
	ti_index = -1

	tv_start_pos = GRID_PosGet(tv_start_pos)
	tv_dest_pos = GRID_PosGet(tv_dest_pos)

	for (ti_i = 0; ti_i < @get_global i_grid_line_nb; ti_i++)
	{
		if (@get_global an_grid_line_net[ti_i] != n_net)
			continue

		if (@get_global ai_grid_line_bit_field[ti_i] != i_net_territory_bit_field)
			continue
			
		ti_index = ti_i
		break
	}
	
	if (ti_index == -1)
	{
		ti_index = @get_global i_grid_line_nb
		@get_global an_grid_line_net[ti_index] = n_net
		@get_global ai_grid_line_bit_field[ti_index] = i_net_territory_bit_field
		@get_global i_grid_line_nb ++
	}
	
	ti_i = @get_global ai_grid_line_tested_nb[ti_index]
	@get_global av_grid_line_tested_start[ti_index][ti_i] = tv_start_pos
	@get_global av_grid_line_tested_dest[ti_index][ti_i] = tv_dest_pos
	@get_global ai_grid_line_tested_flag[ti_index][ti_i] = ti_flag

	@get_global ai_grid_line_tested_nb[ti_index] ++
}

procedure_local int PNJ_Raptor_Get_Precal_Grid_Line(vector tv_start_pos, vector tv_dest_pos)
{
	int		ti_i
	int		ti_index
	
	ti_index = -1

	for (ti_i = 0; ti_i < @get_global i_grid_line_nb; ti_i++)
	{
		if (@get_global an_grid_line_net[ti_i] != n_net)
			continue

		if (@get_global ai_grid_line_bit_field[ti_i] != i_net_territory_bit_field)
			continue
			
		ti_index = ti_i
		break
	}
	
	if (ti_index != -1)
	{
		tv_start_pos = GRID_PosGet(tv_start_pos)
		tv_dest_pos = GRID_PosGet(tv_dest_pos)
	
		for (ti_i = 0; ti_i < @get_global ai_grid_line_tested_nb[ti_index]; ti_i++)
		{
			if (@get_global av_grid_line_tested_start[ti_index][ti_i].x != tv_start_pos.x)
				continue

			if (@get_global av_grid_line_tested_start[ti_index][ti_i].y != tv_start_pos.y)
				continue
	
			if (@get_global av_grid_line_tested_dest[ti_index][ti_i].x != tv_dest_pos.x)
				continue

			if (@get_global av_grid_line_tested_dest[ti_index][ti_i].y != tv_dest_pos.y)
				continue
		}
		
		return(@get_global ai_grid_line_tested_flag[ti_index][ti_i])
	}
	
	return(-1)
}

// GRID LINE TEST OPTIMISATION =========================================
procedure_local int PNJ_Raptor_Test_Grid_Line(int ti_index, vector tv_start_pos, vector tv_dest_pos, int ti_bit_field, float tf_dist, int ti_display_info, int ti_grid_width)
{
	tv_start_pos = GRID_PosGet(tv_start_pos)
	tv_dest_pos = GRID_PosGet(tv_dest_pos)

	tv_start_pos.z = OBJ_PosGet().z
	tv_dest_pos.z = tv_start_pos.z

	if (tv_start_pos.x != av_grid_line_start_pos[ti_index].x || tv_start_pos.y != av_grid_line_start_pos[ti_index].y || tv_dest_pos.x != av_grid_line_dest_pos[ti_index].x || tv_dest_pos.y != av_grid_line_dest_pos[ti_index].y)
	{
		av_grid_line_start_pos[ti_index] = tv_start_pos
		av_grid_line_dest_pos[ti_index] = tv_dest_pos
		
		ai_grid_line_flag[ti_index] = GRID_LIB_IsReachableFrom(tv_start_pos, tv_dest_pos, ti_bit_field, tf_dist, ti_display_info, ti_grid_width)
		
// FRED
//#ifndef _FINAL_
//		if (DBG_Display_Way && ! ai_grid_line_flag[ti_index] )
//		{
//			GRID_LIB_IsReachableFrom(tv_start_pos, tv_dest_pos, ti_bit_field, tf_dist, vrai, ti_grid_width)
//		}
//#endif
	}
	
	return(ai_grid_line_flag[ti_index])
}

// INT FILE ===========================================================
procedure_local void PNJ_Raptor_Clean_File()
{
	i_file_size = 0
	i_file_start_index = 0
	i_file_end_index = 0
}

procedure_local void PNJ_Raptor_Enfile(int ti_index)
{
	i_file_size++
	ai_file[i_file_end_index] = ti_index

//	i_file_end_index = MATH_Modulo(i_file_end_index + 1, 100)
	i_file_end_index++
	if (i_file_end_index == 100)
		i_file_end_index = 0
}

procedure_local int PNJ_Raptor_Defile()
{
	int		ti_val
	
	i_file_size--
	ti_val = ai_file[i_file_start_index]
	ai_file[i_file_start_index] = -1

//	i_file_start_index = MATH_Modulo(i_file_start_index + 1, 100)
	i_file_start_index++
	if (i_file_start_index == 100)
		i_file_start_index = 0

	return(ti_val)
} 

procedure_local int PNJ_Raptor_File_Size_Get()
{
	return(i_file_size)
}


// GAO FILE ========================================================
procedure_local void PNJ_Raptor_Clean_Gao_File()
{
	i_gao_file_size = 0
	i_gao_file_start_index = 0
	i_gao_file_end_index = 0
}

procedure_local void PNJ_Raptor_Gao_Enfile(object to_gao)
{
	i_gao_file_size++
	ao_gao_file[i_gao_file_end_index] = to_gao

//	i_gao_file_end_index = MATH_Modulo(i_gao_file_end_index + 1, 100)
	i_gao_file_end_index++
	if (i_gao_file_end_index == 100)
		i_gao_file_end_index = 0
}

procedure_local object PNJ_Raptor_Gao_Defile()
{
	object	to_gao
	
	i_gao_file_size--
	to_gao = ao_gao_file[i_gao_file_start_index]
	ao_gao_file[i_gao_file_start_index] = nobody

//	i_gao_file_start_index = MATH_Modulo(i_gao_file_start_index + 1, 100)
	i_gao_file_start_index++
	if (i_gao_file_start_index == 100)
		i_gao_file_start_index = 0

	return(to_gao)
} 

procedure_local int PNJ_Raptor_Gao_File_Size_Get()
{
	return(i_gao_file_size)
}


// TERRITORY =======================================================
procedure_local int PNJ_Raptor_Territory_ID_Allowed(int ti_territory_ID)
{
	if (ti_territory_ID == -1)
		return(faux)	

	if ( i_net_territory_ID_bit_field & (1 << ti_territory_ID) )
		return(vrai)

	return(faux)
}

procedure_local int PNJ_Raptor_Territory_Num_Allowed(int ti_territory_num)
{
	if (i_net_territory_bit_field & (1 << ti_territory_num) )
		return(vrai)
	else
		return(faux)
}


procedure_local int PNJ_Raptor_Wp_Is_Invalid(object to_wp)
{
	int		ti_wp_territory_ID	

	if (@to_wp OBJ_CapaTest(Ci_Capa_Exclusion))
		return(vrai)
	
	ti_wp_territory_ID	= to_wp.des_int2 
	
	if ( ti_wp_territory_ID != i_my_territory_ID && ! PNJ_Raptor_Territory_ID_Allowed(ti_wp_territory_ID) )
		return(vrai)

	return(faux)
}

procedure_local int PNJ_Raptor_Can_Jump()
{
	if (always_jumping)
		return(vrai)
	
	if (@"univ" Cheat_i_Flags & CHEAT_Raptor_Jump)
		return(vrai)

	if (i_flag_blesse)
		return(faux)

	if (raptor_category == Ci_Raptor_Light)
		return(faux)

	return(vrai)
}
procedure_local int PNJ_Raptor_Link_Is_Invalid(object to_start_wp, object to_dest_wp)
{
	int		ti_link_capa
	
	ti_link_capa = WAY_LinkCapaGet(n_net, to_start_wp, to_dest_wp)
	
//	if (raptor_type == C_ID_Tyranosaure && ti_link_capa & Ci_Capa_Link_Jump)
//		return(vrai)

	if (ti_link_capa & Ci_Capa_Link_Invalid)
		return(vrai)
		
	if (ti_link_capa & Ci_Capa_Link_Jump && ! PNJ_Raptor_Can_Jump() )
		return(vrai)
		
	if (ti_link_capa & Ci_Capa_Link_Test_Grid && ! GRID_LIB_IsReachableFrom(@to_start_wp OBJ_PosGet(), @to_dest_wp OBJ_PosGet(), 0, 0.0, faux, 0) )
		return(vrai)
		
	return(faux)
}

procedure_local void PNJ_Raptor_Best_Sight_Index(byrefarr int index, int ti_array_size, object to_wp)
{
	int			ti_i
	
	float		taf_dot_product[20]

	vector	tv_wp_to_next_wp

	object	to_next_wp

	for (ti_i = 0; ti_i < ti_array_size; ti_i++)
	{
		to_next_wp = WAY_NetNextWP(n_net, to_wp, 6, ti_i)
		tv_wp_to_next_wp = @to_next_wp OBJ_PosGet() - @to_wp OBJ_PosGet()
		MATH_VecSetNormalize(tv_wp_to_next_wp)
		
		taf_dot_product[ti_i] = MATH_VecDotProduct(OBJ_SightGet(), tv_wp_to_next_wp)
	}
	
	ARR_LIB_QuickSort_OrderIndex(&index[0], &taf_dot_product[0], ti_array_size)
}

procedure_local void PNJ_Raptor_Rand_Index(byrefarr int index, int ti_array_size)
{
	int			ti_i
	int			tai_index[10]
	int			ti_size
	int			ti_index
	
	for (ti_i = 0; ti_i < ti_array_size; ti_i++)
		tai_index[ti_i] = ti_i

	ti_size = ti_array_size
	for (ti_i = 0; ti_i < ti_array_size; ti_i++)
	{
		ti_index = MATH_RandInt(0, ti_size)
		index[ti_i] = tai_index[ti_index]

		ti_size--
		tai_index[ti_index] = tai_index[ti_size]
	}
}

// PARCOURS EN LARGEUR POUR ATTEINDRE UN WP
procedure_local int PNJ_Raptor_Way(object to_start_wp, object to_dest_wp)
{
	int			ti_i
	int			ti_index
	int			tai_index_array[10]
	int			ti_link_nb

	color		tc_color

	object	to_wp
	object	to_next_wp

	if (to_start_wp == to_dest_wp)
	{
		ao_way_wp[0] = to_start_wp
		i_way_wp_nb = 1
		return(i_way_wp_nb)
	}

	@get_global i_raptor_short_way_call_nb ++

	PNJ_Raptor_Clean_Gao_File()
	PNJ_Raptor_Gao_Enfile(to_start_wp)
	
	to_start_wp.des_object2 = nobody
	@to_start_wp OBJ_CapaSet(Ci_Capa_Skip_This_Wp, none)

	while(PNJ_Raptor_Gao_File_Size_Get())
	{
		to_wp = PNJ_Raptor_Gao_Defile()
		
		ti_link_nb = WAY_GetNumLinks(n_net, to_wp)

//		if (to_wp == to_start_wp)
//			PNJ_Raptor_Best_Sight_Index(&tai_index_array[0], ti_link_nb, to_wp)
//		else
			PNJ_Raptor_Rand_Index(&tai_index_array[0], ti_link_nb)

		for (ti_i = 0; ti_i < ti_link_nb; ti_i++)
		{
			ti_index = tai_index_array[ti_i]
		
			to_next_wp = WAY_NetNextWP(n_net, to_wp, 6, ti_index)	
		
			if (@to_next_wp OBJ_CapaTest(Ci_Capa_Skip_This_Wp))
				continue

			if (PNJ_Raptor_Wp_Is_Invalid(to_next_wp))
				continue
				
			if (PNJ_Raptor_Link_Is_Invalid(to_wp, to_next_wp))
				continue
	
			@to_next_wp OBJ_CapaSet(Ci_Capa_Skip_This_Wp, none)
			to_next_wp.des_object2 = to_wp

			if (to_next_wp == to_dest_wp	)
			{
				PNJ_Raptor_Clean_Gao_File()
				break
			}
			
			PNJ_Raptor_Gao_Enfile(to_next_wp)
		}
	}

	i_way_wp_nb = 0

	if (to_next_wp == to_dest_wp	)
	{
		while(to_next_wp != to_start_wp)
		{
			ao_way_wp[i_way_wp_nb] = to_next_wp
			to_next_wp = to_next_wp.des_object2
			i_way_wp_nb++
		}
		ao_way_wp[i_way_wp_nb] = to_start_wp
		i_way_wp_nb++
	}

	for (ti_i = 0; ti_i < i_net_wp_nb; ti_i++)
	{
		@ao_net_wp[ti_i] OBJ_CapaSet(none, Ci_Capa_Skip_This_Wp)
		ao_net_wp[ti_i].des_object2 = nobody
	}

#ifndef _FINAL_
	for (ti_i = i_way_wp_nb - 1; ti_i > 0; ti_i--)
		DBG_RenderVector(@ao_way_wp[ti_i] OBJ_PosGet(), @ao_way_wp[ti_i - 1] OBJ_PosGet() - @ao_way_wp[ti_i] OBJ_PosGet(), color_cyan)
#endif

	return(i_way_wp_nb)
}

#ifndef _FINAL_
// AFFICHAGE DES WPS ACCESSIBLES
procedure_local void PNJ_Raptor_DBG_Display_Accessible(object to_start_wp)
{
	int			ti_i
	int			ti_index
	int			tai_index_array[10]
	int			ti_link_nb

	color		tc_color

	object	to_wp
//	object	to_start_wp
	object	to_next_wp

//	ti_index = PNJ_Raptor_Territory_Index_Get(i_my_territory_ID)
//	ti_index = ai_net_territory_first_wp_index[ti_index]
//	to_start_wp = ao_net_wp[ti_index]

	if (AI_GetCurSystem() != 10)
		return

	if ( ! to_start_wp )
		return

	PNJ_Raptor_Clean_Gao_File()
	PNJ_Raptor_Gao_Enfile(to_start_wp)
	
	to_start_wp.des_object2 = nobody
	@to_start_wp OBJ_CapaSet(Ci_Capa_Skip_This_Wp, none)

	while(PNJ_Raptor_Gao_File_Size_Get())
	{
		to_wp = PNJ_Raptor_Gao_Defile()
		
		ti_link_nb = WAY_GetNumLinks(n_net, to_wp)

		for (ti_i = 0; ti_i < ti_link_nb; ti_i++)
		{
			to_next_wp = WAY_NetNextWP(n_net, to_wp, 6, ti_i)	
		
			if (@to_next_wp OBJ_CapaTest(Ci_Capa_Skip_This_Wp))
				continue

			if (PNJ_Raptor_Wp_Is_Invalid(to_next_wp))
				continue
				
			if (PNJ_Raptor_Link_Is_Invalid(to_wp, to_next_wp))
				continue
	
			@to_next_wp OBJ_CapaSet(Ci_Capa_Skip_This_Wp, none)
			to_next_wp.des_object2 = to_wp

			DBG_RenderVector(@to_wp OBJ_PosGet() + Cv_VerticalVector, @to_next_wp OBJ_PosGet() - @to_wp OBJ_PosGet(), color_blanc)
			
			PNJ_Raptor_Gao_Enfile(to_next_wp)
		}
	}

	for (ti_i = 0; ti_i < i_net_wp_nb; ti_i++)
	{
		@ao_net_wp[ti_i] OBJ_CapaSet(none, Ci_Capa_Skip_This_Wp)
		ao_net_wp[ti_i].des_object2 = nobody
	}
}
#endif

// PARCOURS EN LARGEUR POUR ATTEINDRE UN TERRITOIRE
procedure_local int PNJ_Raptor_Way_To_Territory(object to_start_wp, int ti_dest_territory)
{
	int			ti_i
	int			ti_index
	int			tai_index_array[10]
	int			ti_link_nb

	color		tc_color

	object	to_wp
	object	to_next_wp

	@get_global i_raptor_short_way_call_nb ++

	PNJ_Raptor_Clean_Gao_File()
	PNJ_Raptor_Gao_Enfile(to_start_wp)
	
	to_start_wp.des_object2 = nobody
	@to_start_wp OBJ_CapaSet(Ci_Capa_Skip_This_Wp, none)

	while(PNJ_Raptor_Gao_File_Size_Get())
	{
		to_wp = PNJ_Raptor_Gao_Defile()
		
		ti_link_nb = WAY_GetNumLinks(n_net, to_wp)

		PNJ_Raptor_Rand_Index(&tai_index_array[0], ti_link_nb)

		for (ti_i = 0; ti_i < ti_link_nb; ti_i++)
		{
			ti_index = tai_index_array[ti_i]
		
			to_next_wp = WAY_NetNextWP(n_net, to_wp, 6, ti_index)	
		
			if (@to_next_wp OBJ_CapaTest(Ci_Capa_Skip_This_Wp))
				continue

			if (PNJ_Raptor_Wp_Is_Invalid(to_next_wp))
				continue
				
			if (PNJ_Raptor_Link_Is_Invalid(to_wp, to_next_wp))
				continue
	
			@to_next_wp OBJ_CapaSet(Ci_Capa_Skip_This_Wp, none)
			to_next_wp.des_object2 = to_wp

			if (to_next_wp.des_int2 == ti_dest_territory && @to_next_wp OBJ_CapaTest(Ci_Capa_Use_Occluder))
			{
				PNJ_Raptor_Clean_Gao_File()
				break
			}
			
			PNJ_Raptor_Gao_Enfile(to_next_wp)
		}
	}

	i_way_wp_nb = 0

	if (to_next_wp.des_int2 == ti_dest_territory && @to_next_wp OBJ_CapaTest(Ci_Capa_Use_Occluder))
	{
		o_path_dest_wp = to_next_wp

		while(to_next_wp != to_start_wp)
		{
			ao_way_wp[i_way_wp_nb] = to_next_wp
			to_next_wp = to_next_wp.des_object2
			i_way_wp_nb++
		}
		ao_way_wp[i_way_wp_nb] = to_start_wp
		i_way_wp_nb++

		o_path_start_wp = to_start_wp
	}

	for (ti_i = 0; ti_i < i_net_wp_nb; ti_i++)
	{
		@ao_net_wp[ti_i] OBJ_CapaSet(none, Ci_Capa_Skip_This_Wp)
		ao_net_wp[ti_i].des_object2 = nobody
	}

#ifndef _FINAL_
	for (ti_i = i_way_wp_nb - 1; ti_i > 0; ti_i--)
		DBG_RenderVector(@ao_way_wp[ti_i] OBJ_PosGet(), @ao_way_wp[ti_i - 1] OBJ_PosGet() - @ao_way_wp[ti_i] OBJ_PosGet(), color_cyan)
#endif

	return(i_way_wp_nb)
}


// PARCOURS EN PROFONDEUR
procedure_local object PNJ_Raptor_Deep_Way(object to_start_wp, object to_dest_wp, vector tv_sight)
{
	int			ti_i
	int			ti_index
	int			ti_wp_nb
	int			ti_wp_index[10]

	float		tf_wp_ponderation[10]
	
	color		tc_color

	vector	tv_link_dir[10]

	object	tao_wp[10]
	object	to_wp
	object	to_next_wp

	to_wp = nobody

	@to_start_wp OBJ_CapaSet(Ci_Capa_Skip_This_Wp, none)

//	DBG_RenderVector(@to_start_wp OBJ_PosGet(), tv_sight, color_vert)

	ti_wp_nb = WAY_GetNumLinks(n_net, to_start_wp)
	for (ti_i = 0; ti_i < ti_wp_nb; ti_i++)
	{
		tao_wp[ti_i] = WAY_NetNextWP(n_net, to_start_wp, 6, ti_i)
	
		tv_link_dir[ti_i] = @tao_wp[ti_i] OBJ_PosGet() - @to_start_wp OBJ_PosGet()
		tv_link_dir[ti_i] /= WAY_LinkWeightGet(n_net, to_start_wp, tao_wp[ti_i])
		
//		DBG_RenderVector(@tao_wp[ti_i] OBJ_PosGet(), tv_link_dir[ti_i], color_rouge)	

		tf_wp_ponderation[ti_i] = 1.0 - MATH_VecDotProduct(tv_sight, tv_link_dir[ti_i])
		
		if (WAY_LinkCapaGet(n_net, to_start_wp, tao_wp[ti_i]) & Ci_Capa_Link_Jump)
			tf_wp_ponderation[ti_i] += 2.0
	}

	ARR_LIB_QuickSort_OrderIndex(&ti_wp_index[0], &tf_wp_ponderation[0], ti_wp_nb)
	
	for (ti_i = 0; ti_i < ti_wp_nb; ti_i++)
	{
		ti_index = ti_wp_index[ti_i]
		to_next_wp = tao_wp[ti_index]
	
		if (@to_next_wp OBJ_CapaTest(Ci_Capa_Skip_This_Wp))
			continue

		if (PNJ_Raptor_Wp_Is_Invalid(to_next_wp))
			continue
			
		if (PNJ_Raptor_Link_Is_Invalid(to_start_wp, to_next_wp))
			continue

		if (to_next_wp == to_dest_wp	)
		{
			to_wp = 	to_dest_wp
		
			ao_way_wp[i_way_wp_nb] = to_dest_wp
			i_way_wp_nb++
			break
		}
		
		to_wp = PNJ_Raptor_Deep_Way(to_next_wp, to_dest_wp, tv_link_dir[ti_index])
		if (to_wp == to_dest_wp)
		{
			ao_way_wp[i_way_wp_nb] = to_next_wp
			i_way_wp_nb++
			break
		}
	}

	@to_start_wp OBJ_CapaSet(none, Ci_Capa_Skip_This_Wp)

	return(to_wp)
}

procedure_local void PNJ_Raptor_Deep_Way_Main(object to_start_wp, object to_dest_wp)
{
	int		ti_i	

	i_way_wp_nb = 0

	PNJ_Raptor_Deep_Way(to_start_wp, to_dest_wp, OBJ_SightGet())

	if (i_way_wp_nb)
	{
		ao_way_wp[i_way_wp_nb] = to_start_wp
		i_way_wp_nb++
	}

#ifndef _FINAL_
	for (ti_i = i_way_wp_nb - 1; ti_i > 0; ti_i--)
		DBG_RenderVector(@ao_way_wp[ti_i] OBJ_PosGet() + Cv_VerticalVector, @ao_way_wp[ti_i - 1] OBJ_PosGet() - @ao_way_wp[ti_i] OBJ_PosGet(), color_jaune)
#endif
}

procedure_local void PNJ_Raptor_Update_Flag(object to_wp)
{
	int			ti_i
	
	int			ti_flag_dont_use_grid
	int			ti_flag_stalk
	int			ti_flag_alien_move
	
	if (!to_wp)
		return

	ti_flag_dont_use_grid = i_flag_dont_use_grid
	ti_flag_stalk = i_flag_stalk
	ti_flag_alien_move = i_flag_alien_move
	
	f_current_wp_speed = to_wp.des_float2

	// USE GRID OR DONT USE GRID ?
	if (@to_wp OBJ_CapaTest(Ci_Capa_Out_Of_Grid))
		i_flag_dont_use_grid = vrai
	else
		i_flag_dont_use_grid = faux
	
	// USE OCCLUDER ?
	if (@to_wp OBJ_CapaTest(Ci_Capa_Use_Occluder))
	{
		i_flag_occluder_alllowed = vrai
		i_occluder_territory = to_wp.des_int2
	}
	else
	{
		i_flag_occluder_alllowed = faux
	}
	
	// STALK OU PAS STALK ?
	if (@to_wp OBJ_CapaTest(Ci_Capa_Stalk))
		i_flag_stalk = vrai
	else
		i_flag_stalk = faux
	
	// ALIEN MOVE OU PAS ?
	if (@to_wp OBJ_CapaTest(Ci_Capa_Alien))
	{
		i_flag_alien_move = vrai
		
		COL_ColSetActivationSet(none, C_bit_zdm_pied)
		DYN_GravitySet(Cv_NullVector)
	}
	else
	{
		i_flag_alien_move = faux
	
		COL_StartMatrixSet(OBJ_PosGet())
		COL_ColSetActivationSet(C_bit_zdm_pied, none)
	
		DYN_GravitySet(v_jump_gravity)
	}
	
	#ifndef _FINAL_
	if (DBG_Log_Allowed)
	{
		if (!ti_flag_alien_move && i_flag_alien_move)
		{
			DBG_TraceFloat(TIME_Get())
			DBG_TraceString(" => ")
			DBG_TraceObject(OBJ_Me())
			DBG_TraceString(" PASSE EN MODE ALIEN")
			DBG_TraceEOL()
		}
		else if (ti_flag_alien_move && !i_flag_alien_move)
		{	
			DBG_TraceFloat(TIME_Get())
			DBG_TraceString(" => ")
			DBG_TraceObject(OBJ_Me())
			DBG_TraceString(" QUITTE LE MODE ALIEN")
			DBG_TraceEOL()
		}
		
		if (ti_flag_stalk && !i_flag_stalk)
		{
			DBG_TraceFloat(TIME_Get())
			DBG_TraceString(" => ")
			DBG_TraceObject(OBJ_Me())
			DBG_TraceString(" QUITTE LE STALK => ")
			DBG_TraceObject(to_wp)
			DBG_TraceEOL()
		}
		else if (!ti_flag_stalk && i_flag_stalk)
		{
			DBG_TraceFloat(TIME_Get())
			DBG_TraceString(" => ")
			DBG_TraceObject(OBJ_Me())
			DBG_TraceString(" PASSE EN STALK => ")
			DBG_TraceObject(to_wp)
			DBG_TraceEOL()
		}
	}
	#endif
	
	if (!i_flag_stalk && i_flag_display_i_am_back)
	{
		i_flag_display_i_am_back	= faux
		i_flag_outside_territory = faux
	
	#ifndef _FINAL_	
		if (DBG_Display_Text)
			STR_CreateText("\h.07\LE MONSTRE REVIENT", cvector(0.35, 0.65, 0.0), 2.0)
	#endif
		EVENT_AddEventInfo(OBJ_Me(), nobody, C_EVENT_INFOTYPE_BACK_ON_FIGHT)
	}
	else if (i_flag_stalk && i_flag_find_safe_pos)
	{
		i_flag_outside_territory = vrai
	
	#ifndef _FINAL_
		if (DBG_Display_Text)
			STR_CreateText("\h.07\LE MONSTRE FUIT", cvector(0.38, 0.65, 0.0), 2.0)	
	#endif
	}
}

// ULTRA TAG

procedure_local int PNJ_Raptor_Pos_In_Ultra_Tag(vector tv_pos)
{
	int		ti_i
	
	for (ti_i = 0; ti_i < i_ultra_tag_nb; ti_i++)
	{
		if (GRID_LIB_Pos_In_Ultra_Tag(ao_ultra_tag[ti_i], tv_pos))
			return(vrai)
	}
	
	return(faux)
}

procedure_local int PNJ_Raptor_Blocked(vector tv_pos, int ti_flag)
{
	if ( GRID_LIB_Blocked(tv_pos, ti_flag) )
		return(vrai)
	else if (@get_global i_ultra_tag_last_id != raptor_type && PNJ_Raptor_Pos_In_Ultra_Tag(tv_pos) )
		return(vrai)

	return(faux)
}

procedure_local int PNJ_Raptor_On_Wall_Pos(vector tv_pos)
{
	int			ti_grid_capa

	GRID_CurrentSet(1)
	ti_grid_capa = GRID_CapaGet(tv_pos) & tag_grid_terrain
	GRID_CurrentSet(0)

	if (ti_grid_capa == Ci_Grid2_MuretJack)
		return(vrai)
	else
		return(faux)
}

procedure_local int PNJ_Raptor_Find_Free_Grid_Pos(byref vector tv_pos)
{
	int			ti_i

	if (PNJ_Raptor_On_Wall_Pos(tv_pos))
		return(vrai)

	if (! PNJ_Raptor_Blocked(tv_pos, 0))
		return(vrai)

	tv_pos.x += 1.0
	if (! PNJ_Raptor_Blocked(tv_pos, 0))
		return(vrai)

	tv_pos.y += 1.0
	if (! PNJ_Raptor_Blocked(tv_pos, 0))
		return(vrai)

	tv_pos.x -= 1.0
	if (! PNJ_Raptor_Blocked(tv_pos, 0))
		return(vrai)

	tv_pos.x -= 1.0
	if (! PNJ_Raptor_Blocked(tv_pos, 0))
		return(vrai)

	tv_pos.y -= 1.0
	if (! PNJ_Raptor_Blocked(tv_pos, 0))
		return(vrai)

	tv_pos.y -= 1.0
	if (! PNJ_Raptor_Blocked(tv_pos, 0))
		return(vrai)

	tv_pos.x += 1.0
	if (! PNJ_Raptor_Blocked(tv_pos, 0))
		return(vrai)

	tv_pos.x += 1.0
	if (! PNJ_Raptor_Blocked(tv_pos, 0))
		return(vrai)

	return(faux)	
}

procedure_local int PNJ_Raptor_Is_Accessible(byref vector tv_pos, int ti_territory_ID)
{
	vector	tv_free_pos	

	// EST-CE CETTE POSITION EST DANS MA LISTE DE TERRITOIRES ?
	if (! PNJ_Raptor_Territory_ID_Allowed(ti_territory_ID))
		return(faux)

	//EST-CE QUE CETTE CASE EST INACCESSIBLE ?
	if (PNJ_Raptor_Find_Free_Grid_Pos(tv_pos))
	{
//		DBG_RenderVector(tv_pos, Cv_VerticalVector * 2.0, color_vert)
		return(vrai)
	}
	else
	{
		return	(faux)
	}

	return(vrai)
}

procedure_local void PNJ_Raptor_Clean_Way()
{
	o_next_wp = nobody
	o_next_next_wp = nobody

	o_path_start_wp = nobody
	o_path_dest_wp = nobody
	while(i_way_wp_nb)
	{
		i_way_wp_nb--
		ao_way_wp[i_way_wp_nb] = nobody
	}
}

procedure_local void PNJ_Raptor_Way_Reset()
{
	i_way_status = 0
	
	i_flag_stalk = faux
	i_flag_dont_use_grid = faux
	i_flag_alien_move = faux

	i_way_computation_old_mode = -1

	o_next_wp = nobody
	o_next_next_wp = nobody
	o_current_wp = nobody
	o_balade_1st_wp = nobody

	i_way_force_recompute = vrai
	
	while(i_way_case_nbr > 0)
	{
		i_way_case_nbr--
		v_way_case[i_way_case_nbr] = Cv_NullVector
	}
	
	f_use_current_wp_action_duration = 0.0

	PNJ_Raptor_Clean_Way()
}

procedure_local void PNJ_Raptor_Ulltra_Tag_Init()
{
	GRID_LIB_Ultra_Tag_Get(raptor_type, &ao_ultra_tag[0], i_ultra_tag_nb)
}

//procedure_local void PNJ_Raptor_Raptor_Budy_TagOn()
//{
//	int		ti_i
//	
//	for (ti_i = 0; ti_i < i_budy_nb; ti_i++)
//		@ao_budy[ti_i] AI_CBExecute(CallBack_TagOn, 0)
//}
//
//procedure_local void PNJ_Raptor_Raptor_Budy_TagOff()
//{
//	int		ti_i
//	
//	for (ti_i = 0; ti_i < i_budy_nb; ti_i++)
//		@ao_budy[ti_i] AI_CBExecute(CallBack_TagOff, 0)
//}

// TEST SI ON N'A PAS UN LIEN OU UN WP SUR LE RESEAU QUI DEVIENT INVALIDE
procedure_local void PNJ_Raptor_Check_Way()
{
	int			ti_i	
	int			ti_link_capa

	for (ti_i = i_way_wp_nb - 1; ti_i > 0; ti_i--)
	{
		if (PNJ_Raptor_Wp_Is_Invalid(ao_way_wp[ti_i]))
		{
	 		PNJ_Raptor_Clean_Way()	
	 		return
		}
	
		if (PNJ_Raptor_Link_Is_Invalid(ao_way_wp[ti_i], ao_way_wp[ti_i - 1]))
		{
	 		PNJ_Raptor_Clean_Way()	
	 		return
		}
	}
}

// CHECK SI ON NE VA PAS DANS UNE IMPASSE
procedure_local int PNJ_Raptor_Invalid_Requin_Way(object to_current_wp, object to_next_wp)
{
	int			ti_i
	int			ti_good_wp_nb
	int			ti_link_nb
	int			ti_iteration_nb
	int			tai_good_wp_index[10]
	int			ti_link_capa

	object	to_next_link_0
	object	to_next_link_1

	object	to_first_wp
	
	if (to_next_wp == nobody)
		DBG_Error("Pas de wp destination en mode requin ?")

	ti_iteration_nb = 0
	ti_good_wp_nb = 0
	to_first_wp = to_current_wp

	for (ti_link_nb = WAY_GetNumLinks(n_net, to_next_wp); to_next_wp && ti_link_nb; ti_link_nb = WAY_GetNumLinks(n_net, to_next_wp))
	{
//		DBG_RenderVector(@to_current_wp OBJ_PosGet(), @to_next_wp OBJ_PosGet() - @to_current_wp OBJ_PosGet(), color_blanc)	
	
		for (ti_i = 0; ti_i < ti_link_nb; ti_i++)
		{
			to_next_link_0	 = WAY_NetNextWP(n_net, to_next_wp, 6, ti_i)
		
			if (PNJ_Raptor_Wp_Is_Invalid(to_next_link_0))
				continue

			if (@to_next_link_0 OBJ_CapaGet() & Ci_Capa_Stalk)
				continue

			if (PNJ_Raptor_Link_Is_Invalid(to_next_wp, to_next_link_0))
				continue
				
			// OK, j'ai le droit de prendre ce wp
			tai_good_wp_index[ti_good_wp_nb] = ti_i
			ti_good_wp_nb++
		}

		ti_link_nb = ti_good_wp_nb

		ti_iteration_nb++

		switch(ti_link_nb)
		{
			case 0 :
				// WP bizarre, on ne peut pas aller plus loin
//				DBG_RenderVector(@to_next_wp OBJ_PosGet(), Cv_VerticalVector * 100.0, color_rouge)
//				DBG_BreakPoint()
				return(faux)	
				
			case 1 :
				// Un seul lien, soit il ramène vers le point d'ou on vient, soit ce n'est pas une impasse
				to_next_link_0 = WAY_NetNextWP(n_net, to_next_wp, 6, tai_good_wp_index[0])
				if (ti_iteration_nb > 1 && to_next_link_0 == to_first_wp)
					return(faux) // BOUCLE														FIRST --LINKS--> CURRENT -> NEXT -> LINK 0 -> FIRST
				else if (to_next_link_0 == to_current_wp)
					return(vrai) // IMPASSE														CURRENT -> NEXT -> LiNK 0 -> CURRENT

				to_current_wp = to_next_wp
				to_next_wp = to_next_link_0
					
				break

			case 2 :
				to_next_link_0 = WAY_NetNextWP(n_net, to_next_wp, 6, tai_good_wp_index[0])
				to_next_link_1 = WAY_NetNextWP(n_net, to_next_wp, 6, tai_good_wp_index[1])
			
				if (ti_iteration_nb > 1 && to_next_link_0 == to_first_wp)
				{
//					FIRST --LINKS--> CURRENT -> NEXT -> LINK 0 -> FIRST
					return(faux)	
				}
				else if (to_next_link_0 == to_current_wp)
				{
//					CURRENT -> NEXT -> LINK 0 -> CURRENT
					to_current_wp = to_next_wp
					to_next_wp = to_next_link_1
					continue
				}

				if (ti_iteration_nb > 1 && to_next_link_1 == to_first_wp)
				{
//					FIRST --LINKS--> CURRENT -> NEXT -> LINK 1 -> FIRST
					return(faux)	
				}
				else if (to_next_link_1 == to_current_wp)
				{
//					CURRENT -> NEXT -> LINK 1 -> CURRENT
					to_current_wp = to_next_wp
					to_next_wp = to_next_link_0
					continue
				}

				// SI ON EST ICI, C'EST QU'ON A UNE BIFURCATION VERS 2 WPS
				// SI UN DES DEUX WPS N'EST PAS UN WP DE STALK, C'EST BON
				
			default:
				// PLUS DE 2 LIENS
				for (ti_i = 0; ti_i < ti_link_nb; ti_i++)
				{
					to_next_link_0 = WAY_NetNextWP(n_net, to_next_wp, 6, tai_good_wp_index[ti_i])
					if (to_next_link_0 == to_current_wp)
						continue
					else
						return(faux)
				}
				
				return(vrai)
		}
	}
	
	return(faux)
}

procedure_local int PNJ_Raptor_Territory_Index_Get(int ti_territory_ID)
{
	int		ti_index

	if (ti_territory_ID < 0 || ti_territory_ID > 31)
		return(-1)
	else
		return(ai_net_territory_ID_index[ti_territory_ID])
	
//	ti_index = ARR_LIB_IntSearch(&ai_net_territory_ID[0], i_net_territory_ID_nb, ti_territory_ID)
//	return(ti_index)
}

// PRECAL WP
procedure_local void PNJ_Raptor_Add_Precal_Wp(vector tv_pos, object to_wp)
{
	if (to_wp)
	{
		@get_global an_raptor_way_net[@get_global i_raptor_way_wp_nb] = n_net
		@get_global av_raptor_way_pos[@get_global i_raptor_way_wp_nb] = tv_pos
		@get_global ao_raptor_way_wp[@get_global i_raptor_way_wp_nb] = to_wp
		@get_global i_raptor_way_wp_nb = MATH_Modulo(@get_global i_raptor_way_wp_nb + 1, 10)
	}
}

procedure_local object PNJ_Raptor_Get_Precal_Wp(vector tv_pos, int ti_territory)
{
	int				ti_i

	vector		tv_precal_pos	
	vector		tv_temp

	object		to_wp
	object		to_precal_wp

	to_precal_wp = nobody

	for (ti_i = 0; ti_i < 10; ti_i++)
	{
		if (@get_global an_raptor_way_net[ti_i] != n_net)
			continue
	
		to_wp = @get_global ao_raptor_way_wp[ti_i]

		if (!to_wp)
			continue

		if (to_wp.des_int2 != ti_territory)
			continue

		tv_precal_pos	= @get_global av_raptor_way_pos[ti_i]

		tv_temp = tv_precal_pos
		tv_temp -= tv_pos		

		if (MATH_VecDotProduct(tv_temp, tv_temp) < 9.0 && GRID_LIB_IsReachableFrom(@to_wp OBJ_PosGet(), tv_pos, 0, -1.0, faux, 0))
		{
			@get_global i_raptor_way_precal_wp_used++	
			to_precal_wp = to_wp
			break		
		}
	}
	
	return(to_precal_wp)
}

// PRECAL NET WAY
procedure_local void PNJ_Raptor_Add_Precal_Net_Way()
{
	int		ti_i
	int		ti_precal_index
	
	if (i_net_wp_nb > 5)
	{
		ti_precal_index = @get_global i_raptor_net_way_nb
	
		for (ti_i = 0; ti_i < i_way_wp_nb; ti_i++)
			@get_global ao_raptor_net_way_wp[ti_precal_index][ti_i] = ao_way_wp[ti_i]
	
		@get_global i_raptor_net_way_wp_nb[ti_precal_index] = i_way_wp_nb
		@get_global i_raptor_net_way_nb = MATH_Modulo(@get_global i_raptor_net_way_nb + 1, 5)
	}
}

procedure_local int PNJ_Raptor_Get_Precal_Net_Way(object to_start_wp, object to_dest_wp)
{
	int			ti_i	
	int			ti_k

	for (ti_i = 0; ti_i < 5; ti_i++)
	{
		if (@get_global ao_raptor_net_way_wp[ti_i][0] != to_dest_wp)
			continue

		i_way_wp_nb = 0

		for (ti_k = 0; ti_k < @get_global i_raptor_net_way_wp_nb[ti_i]; ti_k++)
		{
			i_way_wp_nb++
			ao_way_wp[ti_k] = @get_global ao_raptor_net_way_wp[ti_i][ti_k]
			if (ao_way_wp[ti_k] == to_start_wp)
			{
				@get_global i_raptor_net_way_precal_used ++
				return(vrai)
			}
		}
	}
	
	return(faux)
}

// RETOURNE LE WP LE PLUS PROCHE SUR MON TERRITOIRE
procedure_local object PNJ_Raptor_Nearest_Wp_Get(vector tv_pos, int ti_capa_a_vrai, int ti_capa_a_faux, int ti_flag_rand)
{
	int		ti_i
	int		ti_k
	int		ti_index
	int		ti_wp_capa
	
	float			tf_dist
	float			tf_best_dist

	object		to_wp
	object		to_nearest_wp

	to_nearest_wp = nobody

	tf_best_dist = Cf_Infinit

	ti_capa_a_faux |= Ci_Capa_Exclusion

//	switch(raptor_type)
//	{
//		case C_ID_Raptor :
//			// Si je fuis pour tout autre raison que "je ne suis pas sur mon territoire", je vais privilégier les points "out of grid"
//			if ( ti_capa_a_vrai != all && ti_capa_a_vrai & Ci_Capa_Fuite && (! i_flag_blesse && raptor_category != Ci_Raptor_Light) )
//				ti_capa_a_vrai |= Ci_Capa_Out_Of_Grid
//			break
//			
//		case C_ID_Galiminus :
//			if ( ! i_flag_dont_use_grid )
//				ti_capa_a_faux |= Ci_Capa_Out_Of_Grid
//	}

	// POUR CHAQUE ID DE MON TERRITOIRE
	for (ti_i = 0; ti_i < i_net_territory_ID_nb; ti_i++)
	{
		if ( ! PNJ_Raptor_Territory_Num_Allowed(ti_i) )
			continue
			
		// POUR CHAQUE WP DE CE TERRITOIRE
		ti_index = ai_net_territory_first_wp_index[ti_i]

		for (ti_k = 0; ti_k < ai_net_territory_wp_nb[ti_i]; ti_k++)
		{
			to_wp = ao_net_wp[ti_index + ti_k]

			ti_wp_capa = @to_wp OBJ_CapaGet()
	
			if (ti_wp_capa & ti_capa_a_faux)
				continue
	
			if (ti_capa_a_vrai != all && (ti_wp_capa & ti_capa_a_vrai) != ti_capa_a_vrai)
				continue

			if (i_flag_dijkstra_way && ! to_wp.des_object2)
				continue
			
			if (ti_flag_rand)
				tf_dist = MATH_RandFloat(0.0, 100.0)			// On prend une valeur au hasard
			else if (i_flag_dijkstra_way)
				tf_dist = af_net_wp_dist[to_wp.des_int3]		// On prend la distance sur le réseau
			else
				tf_dist = MATH_VecSquareNorm(@to_wp OBJ_PosGet() - tv_pos)	// On prend la distance dans l'espace
				
			// Dans les point de fuite, on essaye toujours d'atteindre les points hors grille en priorité
			if (ti_capa_a_vrai != all && ti_capa_a_vrai & Ci_Capa_Fuite && ! (ti_wp_capa & Ci_Capa_Out_Of_Grid) )
				tf_dist += 1000000.0
		
			if (tf_dist < tf_best_dist)
			{
				tf_best_dist = tf_dist
				to_nearest_wp = to_wp
			}
		}
	}
	
	return(to_nearest_wp)
}

// MET DANS LE TABLEAU L'ENSEMBLE DES WPS DE MES TERRITOIRES
procedure_local int PNJ_Raptor_Build_Wp_List(byrefarr object tao_wp, int ti_size)
{
	int		ti_good_wp
	int		ti_i
	int		ti_k
	int		ti_territory_ref_wp_nb
	int		ti_territory_first_wp_index
	int		ti_net_territory_index

	ti_good_wp = 0

	// Pour chaque territoire connu
	for (ti_i = 0; ti_i < i_net_territory_ID_nb; ti_i++)
	{
		// Je traite ce territoire s'il est autorisé ou si c'est mon territoire courant
		if (! PNJ_Raptor_Territory_Num_Allowed(ti_i) && ai_net_territory_ID[ti_i] != i_my_territory_ID)
			continue
	
		// OK, on va rajouter tous les wps qui sont sur ce territoire
		ti_territory_first_wp_index = ai_net_territory_first_wp_index[ti_i]
		ti_territory_ref_wp_nb = ai_net_territory_wp_nb[ti_i]

		for (ti_k = 0; ti_k < ti_territory_ref_wp_nb; ti_k++)
		{
			if (ti_good_wp + 1 > ti_size)
				DBG_BreakPoint()
			
			tao_wp[ti_good_wp] = ao_net_wp[ti_territory_first_wp_index + ti_k]
			ti_good_wp++
		}
	}
	
	return(ti_good_wp)
}

procedure_local object PNJ_Raptor_Full_Start_Wp_Get()
{
	int				ti_index
	int				ti_on_my_territory_wp_nb

	object 		to_start_wp	
	object		to_nearest_wp
	object		tao_wp[100]

	ti_index = PNJ_Raptor_Territory_Index_Get(i_my_territory_ID)
	
	if (ti_index != -1 && i_flag_in_my_territory)
	{
		// OK, je suis sur mon territoire, je cherche parmis tous les points de ce territoire	
		to_start_wp = ARR_LIB_NearestSightReachableWaypoint(&ao_net_wp[0], ai_net_territory_first_wp_index[ti_index], ai_net_territory_wp_nb[ti_index], OBJ_PosGet(), OBJ_SightGet(), 0.0, all, Ci_Capa_Out_Of_Grid  | Ci_Capa_Exclusion | Ci_Capa_Skip_This_Wp, to_nearest_wp, 0, vrai)
		if (!to_start_wp)
			to_start_wp = to_nearest_wp
	}
	else
	{
		// Je ne suis pas sur mon territoire, je cherche parmis tous les points de mes territoires
		ti_on_my_territory_wp_nb = PNJ_Raptor_Build_Wp_List(&tao_wp[0], 100)
		to_start_wp = ARR_LIB_NearestSightReachableWaypoint(&tao_wp[0], 0, ti_on_my_territory_wp_nb, OBJ_PosGet(), OBJ_SightGet(), 0.0, all, Ci_Capa_Out_Of_Grid  | Ci_Capa_Exclusion | Ci_Capa_Skip_This_Wp, to_nearest_wp, 0, vrai)
		if (!to_start_wp)
			to_start_wp = to_nearest_wp
	}
	
	return(to_start_wp)
}

// RETOURNE LE WP LE PLUS PROCHE
procedure_local object PNJ_Raptor_Start_Wp_Get()
{
	int				ti_i
	int				ti_k
	int				ti_index
	int				ti_wp_nb
	int				ti_good_wp_nb

	int				ti_flag_ok
	int				ti_my_territory_index

	object 		to_start_wp	
	object		to_nearest_wp
	object		to_neighbor
	object		tao_neighbor_wp[10]

	if (i_flag_occluder_alllowed && i_my_territory_ID == i_occluder_territory)
	{
		// JE NE SUIS PAS SUR LA GRILLE
		if (o_next_wp && ! PNJ_Raptor_Wp_Is_Invalid(o_next_wp) && o_next_wp.des_int2 == i_occluder_territory)
		{
			i_way_status |= Ci_WAY_STATUS_START_WP_IS_NEXT_WP
			return(o_next_wp)
		}
		else
		{
			ti_index = PNJ_Raptor_Territory_Index_Get(i_my_territory_ID)
			for (ti_i = 0; ti_i < ai_net_territory_wp_nb[ti_index]; ti_i++)
			{
				to_nearest_wp = ao_net_wp[ai_net_territory_first_wp_index[ti_index] + ti_i]

				if (PNJ_Raptor_Wp_Is_Invalid(to_nearest_wp))
					continue
			
				if (@to_nearest_wp OBJ_CapaTest(Ci_Capa_Use_Occluder))
				{
					i_way_status |= Ci_WAY_STATUS_START_WP_IS_OCCLUDER_WP
					return(to_nearest_wp)
				}
			}
		}
	}
	
	if (i_flag_dont_use_grid)
	{
		// JE NE SUIS PAS SUR LA GRILLE
		if (o_next_wp && ( o_next_wp.des_int2 == i_my_territory_ID || ! PNJ_Raptor_Wp_Is_Invalid(o_next_wp) ) )
		{
			i_way_status |= Ci_WAY_STATUS_START_WP_IS_NEXT_WP
			to_start_wp = o_next_wp
		}
		else
		{
			ti_my_territory_index = PNJ_Raptor_Territory_Index_Get(i_my_territory_ID)
		
			if ( ! i_flag_in_my_territory	)
			{
				i_net_territory_bit_field |= (1 << ti_my_territory_index)
				i_net_territory_ID_bit_field |= (1 << i_my_territory_ID)
			}

			i_way_status |= Ci_WAY_STATUS_START_WP_IS_NEAREST_OUT_OF_GRID_WP
			to_start_wp = PNJ_Raptor_Nearest_Wp_Get(OBJ_PosGet(), Ci_Capa_Out_Of_Grid, Ci_Capa_Exclusion, faux)

			if ( ! i_flag_in_my_territory	)
			{
				i_net_territory_bit_field &= ~(1 << ti_my_territory_index)
				i_net_territory_ID_bit_field &= ~(1 << i_my_territory_ID)
			}
		}
	}
	else if (o_next_wp)
	{
		// JE SUIS SUR LA GRILLE ET JE ME DIRIGE VERS UN WP
		ti_flag_ok = vrai	
	
		if (PNJ_Raptor_Wp_Is_Invalid(o_next_wp))
			ti_flag_ok = faux
		else if (@o_next_wp OBJ_CapaTest(Ci_Capa_Out_Of_Grid))
			ti_flag_ok = faux
		else if (MATH_VecDotProduct(@o_next_wp OBJ_PosGet() - OBJ_PosGet(), OBJ_SightGet()) < 0.0)
			ti_flag_ok = faux
		else if ( ! GRID_LIB_IsReachableFrom(OBJ_PosGet(), @o_next_wp OBJ_PosGet(), 0, 0.0, faux, 0) )
			ti_flag_ok = faux
	
		if (ti_flag_ok)
		{
			// MON PROCHAIN WP EST DEVANT MOI ET ACCESSIBLE
			i_way_status |= Ci_WAY_STATUS_START_WP_IS_NEXT_WP
			to_start_wp = o_next_wp
		}
		else
		{
			// ON VA REGARDER PARMIS LES VOISINS DU WP VERS LEQUEL ON ALLAIT
			ti_good_wp_nb = 0
			ti_wp_nb = WAY_GetNumLinks(n_net, o_next_wp)
			for (ti_i = 0; ti_i < ti_wp_nb; ti_i++)
			{
				to_neighbor = WAY_NetNextWP(n_net, o_next_wp, 6, ti_i)

				if (PNJ_Raptor_Wp_Is_Invalid(to_neighbor))
					continue

				tao_neighbor_wp[ti_good_wp_nb] = to_neighbor
				ti_good_wp_nb++
			}

			to_start_wp = ARR_LIB_NearestSightReachableWaypoint(&tao_neighbor_wp[0], 0, ti_good_wp_nb, OBJ_PosGet(), OBJ_SightGet(), 0.0, all, Ci_Capa_Out_Of_Grid | Ci_Capa_Exclusion, to_nearest_wp, 0, faux)
			if (!to_start_wp)
				to_start_wp = to_nearest_wp

			if (to_start_wp)
			{
				i_way_status |= Ci_WAY_STATUS_START_WP_IS_NEXT_WP_NEIGHBOR
			}
			else
			{
				// ON VA RECHERCHER PARMIS TOUS LES WPS
				for (ti_i = 0; ti_i < ti_good_wp_nb; ti_i++)
					@tao_neighbor_wp[ti_i] OBJ_CapaSet(Ci_Capa_Skip_This_Wp, none)
				
				to_start_wp = PNJ_Raptor_Full_Start_Wp_Get()

				for (ti_i = 0; ti_i < ti_good_wp_nb; ti_i++)
					@tao_neighbor_wp[ti_i] OBJ_CapaSet(none, Ci_Capa_Skip_This_Wp)
			}
		}
	}
	else
	{
		// JE SUIS SUR LA GRILLE, ON VA RECHERCHER PARMIS TOUS LES WPS
		to_start_wp = PNJ_Raptor_Full_Start_Wp_Get()
	}

	return(to_start_wp)
}

procedure_local void PNJ_Raptor_Skip_Wp_If_Occluder_Enable()
{
	if ( i_flag_occluder_alllowed )
	{
		while(i_way_wp_nb > 1 && @ao_way_wp[i_way_wp_nb - 1] OBJ_CapaTest(Ci_Capa_Use_Occluder) && @ao_way_wp[i_way_wp_nb - 2] OBJ_CapaTest(Ci_Capa_Use_Occluder))
		{
			i_way_wp_nb--
			ao_way_wp[i_way_wp_nb] = nobody
		}
	}
}

procedure_local object PNJ_Raptor_Dijkstra_Extraire_Min(byrefarr object tao_net_wp, byref int ti_net_wp_nb, byrefarr object tao_wp_to_reach, byref int ti_wp_to_reach_nb)
{
	int			ti_i
	int			ti_k
	int			ti_best_index

	float		tf_min
	float		tf_dist
	float		tf_dist_min
	
	vector	tv_temp

	object	to_wp

	tf_min = Cf_Infinit
	ti_best_index = -1

	for (ti_i = 0; ti_i < ti_net_wp_nb; ti_i++)
	{
		if (ti_wp_to_reach_nb > 0)
		{
			tf_dist_min = Cf_Infinit
			for (ti_k = 0; ti_k < ti_wp_to_reach_nb; ti_k++)
			{
				tv_temp = @tao_net_wp[ti_i] OBJ_PosGet() - @tao_wp_to_reach[ti_k] OBJ_PosGet()
//				tf_dist = MATH_VecNorm(tv_temp)
				tf_dist = MATH_AbsFloat(tv_temp.x) + MATH_AbsFloat(tv_temp.y) + MATH_AbsFloat(tv_temp.z)
				if (tf_dist < tf_dist_min)
					tf_dist_min = tf_dist
			}	

			tf_dist = af_net_wp_dist[tao_net_wp[ti_i].des_int3]		
			tf_dist += tf_dist_min
		}
		else
		{
			tf_dist = af_net_wp_dist[tao_net_wp[ti_i].des_int3]		
		}

		if (tf_dist < tf_min)
		{
			tf_min = tf_dist
			ti_best_index = ti_i
		}
	}
	
	if (ti_best_index != -1)
	{
		to_wp = tao_net_wp[ti_best_index]

		ti_net_wp_nb--
		tao_net_wp[ti_best_index] = tao_net_wp[ti_net_wp_nb]

		for (ti_i = 0; ti_i < ti_wp_to_reach_nb; ti_i++)
		{
			if (tao_wp_to_reach[ti_i] == to_wp)
			{
				DBG_RenderVector(@tao_wp_to_reach[ti_i] OBJ_PosGet(), cvector(0.0, 0.0, af_net_wp_dist[tao_wp_to_reach[ti_i].des_int3]), color_cyan)	
			
                 ti_wp_to_reach_nb-- 
				tao_wp_to_reach[ti_i] = tao_wp_to_reach[ti_wp_to_reach_nb]
			}
		}

		return(to_wp)
	}
	else
	{
		return(nobody)
	}
}

procedure_local void PNJ_Raptor_Dijkstra_Display()
{
	int		ti_i	
	int		ti_link_nb
	int		ti_string_ID
	int		ti_color

	float		tf_length

	object	to_wp
	object	to_next_wp

	if ( ! DBG_Display_Way )
		return

	if ( AI_GetCurSystem() != 10 )
		return

	for (ti_i = 0; ti_i < i_net_wp_nb; ti_i++)
	{
		to_wp = ao_net_wp[ti_i].des_object2 
		if (to_wp)
		{
			ti_color = COLOR_Blend(color_vert, color_rouge, af_net_wp_dist[ti_i] / 50.0)
			DBG_RenderVector(@to_wp OBJ_PosGet(), @ao_net_wp[ti_i] OBJ_PosGet() - @to_wp OBJ_PosGet(), ti_color)

//			ti_string_ID = STR_CreateText("\h.04\", VIEW_3dWorldTo2d(0, @ao_net_wp[ti_i] OBJ_PosGet() + cvector(0.0, 0.0, 0.5)), 0.0)
//			STR_AppendFloat(ti_string_ID, af_net_wp_dist[ti_i], 1)
//			
//			ti_string_ID = STR_CreateText("\c000000FF\\h.03\", VIEW_3dWorldTo2d(0, MATH_VecBlend(@ao_net_wp[ti_i] OBJ_PosGet(), @to_wp OBJ_PosGet(), 0.5) + cvector(0.0, 0.0, 0.5)), 0.0)
//			STR_AppendFloat(ti_string_ID, WAY_LinkWeightGet(n_net, ao_net_wp[ti_i], to_wp), 1)
		}
	}

//	DBG_BreakPoint()
}

procedure_local void PNJ_Raptor_Dijkstra_Clean()
{
	int		ti_i	

	i_flag_dijkstra_way = faux

	for (ti_i = 0; ti_i < i_net_wp_nb; ti_i++)
	{
		ao_net_wp[ti_i].des_object2 = nobody
		af_net_wp_dist[ti_i] = Cf_Infinit
	}
}

procedure_local void PNJ_Raptor_Dijkstra_Build_Way(object to_dest_wp)
{
	int			ti_i

	object	to_prev_wp	

	PNJ_Raptor_Clean_Way()

	if (to_dest_wp)
	{
		ao_way_wp[0] = to_dest_wp
		i_way_wp_nb = 1
	
		to_prev_wp = to_dest_wp.des_object2
		while(to_prev_wp && to_prev_wp != OBJ_Me())
		{
			ao_way_wp[i_way_wp_nb] = to_prev_wp
			to_prev_wp = to_prev_wp.des_object2
			i_way_wp_nb++
		}
	
#ifndef _FINAL_
		for (ti_i = i_way_wp_nb - 1; ti_i > 0; ti_i--)
			DBG_RenderVector(@ao_way_wp[ti_i] OBJ_PosGet() + cvector(0.0, 0.0, 0.1), @ao_way_wp[ti_i - 1] OBJ_PosGet() - @ao_way_wp[ti_i] OBJ_PosGet(), color_jaune)
#endif
		
		o_path_dest_wp = to_dest_wp
		o_path_start_wp = ao_way_wp[i_way_wp_nb - 1]
	}
	
	return
}

procedure_local void PNJ_Raptor_Fill_Flee_Wp_Array(byrefarr object tao_wp_to_reach, byref int ti_wp_to_reach_nb)
{
	int		ti_i
	int		ti_k
	int		ti_index	

	ti_wp_to_reach_nb = 0

	for (ti_i = 0; ti_i < i_net_territory_ID_nb; ti_i++)
	{
		if ( ! PNJ_Raptor_Territory_Num_Allowed(ti_i) )
			continue

		ti_index	= ai_net_territory_first_wp_index[ti_i]
	
		for (ti_k = 0; ti_k < ai_net_territory_wp_nb[ti_i]; ti_k++)
		{
			if (@ao_net_wp[ti_index + ti_k] OBJ_CapaTest(Ci_Capa_Fuite))
			{
				tao_wp_to_reach[ti_wp_to_reach_nb] = ao_net_wp[ti_index + ti_k]
				ti_wp_to_reach_nb++
			}
		}
	}
}

procedure_local void PNJ_Raptor_Fill_Territory_Wp_Array(byrefarr object tao_wp_to_reach, byref int ti_wp_to_reach_nb, int ti_territory)
{
	int		ti_i
	int		ti_k
	int		ti_index	

	ti_wp_to_reach_nb = 0

	ti_i = PNJ_Raptor_Territory_Index_Get(ti_territory)
	if (ti_i == -1)
		return

	ti_index = ai_net_territory_first_wp_index[ti_i]

	for (ti_k = 0; ti_k < ai_net_territory_wp_nb[ti_i]; ti_k++)
	{
		if (PNJ_Raptor_Wp_Is_Invalid(ao_net_wp[ti_index + ti_k]))
			continue	
	
		if (@ao_net_wp[ti_index + ti_k] OBJ_CapaTest(Ci_Capa_Use_Occluder))
		{
			tao_wp_to_reach[ti_wp_to_reach_nb] = ao_net_wp[ti_index + ti_k]
			ti_wp_to_reach_nb++
		}
	}
}


procedure_local void PNJ_Raptor_Dijkstra(object to_start_wp, byrefarr object tao_wp_to_reach2, byref int ti_wp_to_reach_nb2, int ti_stop_when_nb)
{
	int		ti_i
	int		ti_k
	int		ti_index
	int		ti_link_nb
	int		ti_string_ID
	int		ti_net_wp_nb
	int		ti_wp_to_reach_nb

	float		tf_length

	object	to_last_reach_wp
	object	to_wp
	object	to_next_wp
	object	tao_net_wp[100]
	object 	tao_wp_to_reach[100]

	if ( ! to_start_wp )
		return

	ti_wp_to_reach_nb = ti_wp_to_reach_nb2
	for (ti_i = 0; ti_i < ti_wp_to_reach_nb; ti_i++)
		tao_wp_to_reach[ti_i] = tao_wp_to_reach2[ti_i]

	ti_stop_when_nb = ti_wp_to_reach_nb2 - ti_stop_when_nb

	i_flag_dijkstra_way = vrai

	ti_net_wp_nb = 0

	// On ajoute le point de départ à la liste des ouverts...
	@to_start_wp OBJ_CapaSet(Ci_Capa_Skip_This_Wp, none)
	to_start_wp.des_object2 = OBJ_Me()
	tao_net_wp[ti_net_wp_nb] = to_start_wp
	ti_net_wp_nb++

	af_net_wp_dist[to_start_wp.des_int3] = 0.0
	
	to_wp = PNJ_Raptor_Dijkstra_Extraire_Min(&tao_net_wp[0], ti_net_wp_nb, &tao_wp_to_reach[0], ti_wp_to_reach_nb)
	while(to_wp)
	{
		AI_ClearStack()	
	
		// Ce point est visité
		@to_wp OBJ_CapaSet(Ci_Capa_Skip_This_Wp, none)

		ti_link_nb = WAY_GetNumLinks(n_net, to_wp)
		for (ti_i = 0; ti_i < ti_link_nb; ti_i++)
		{
			to_next_wp = WAY_NetNextWP(n_net, to_wp, 6, ti_i)
		
			if (@to_next_wp OBJ_CapaTest(Ci_Capa_Skip_This_Wp))
				continue

			if ( PNJ_Raptor_Wp_Is_Invalid(to_next_wp) )
				continue
				
			if ( PNJ_Raptor_Link_Is_Invalid(to_wp, to_next_wp) )
				continue
	
//			DBG_RenderVector(@to_wp OBJ_PosGet() + Cv_VerticalVector, @to_next_wp OBJ_PosGet() - @to_wp OBJ_PosGet(), color_blanc)	

//			for (ti_k = 0; ti_k < ti_wp_to_reach_nb; ti_k++)
//			{
//				if (tao_wp_to_reach[ti_k] == to_next_wp)
//				{
//					ti_wp_to_reach_nb--
//					tao_wp_to_reach[ti_k] = tao_wp_to_reach[ti_wp_to_reach_nb]
//				}
//			}

			tf_length	 = WAY_LinkWeightGet(n_net, to_wp, to_next_wp)

			if ( @to_next_wp OBJ_CapaTest(Ci_Capa_Wp_Already_Added) )
			{
				if (af_net_wp_dist[to_next_wp.des_int3] > af_net_wp_dist[to_wp.des_int3] + tf_length)
				{
					to_next_wp.des_object2 = to_wp
					af_net_wp_dist[to_next_wp.des_int3] = af_net_wp_dist[to_wp.des_int3] + tf_length
				}
			}
			else
			{
				// On rajoute ce wp à la liste des ouverts	
				@to_next_wp OBJ_CapaSet(Ci_Capa_Wp_Already_Added, none)
				tao_net_wp[ti_net_wp_nb] = to_next_wp
				ti_net_wp_nb++

				to_next_wp.des_object2 = to_wp
				af_net_wp_dist[to_next_wp.des_int3] = af_net_wp_dist[to_wp.des_int3] + tf_length
			}
		}

		to_wp = PNJ_Raptor_Dijkstra_Extraire_Min(&tao_net_wp[0], ti_net_wp_nb, &tao_wp_to_reach[0], ti_wp_to_reach_nb)
		
		if (ti_wp_to_reach_nb == ti_stop_when_nb)
			break
	}
	
 	PNJ_Raptor_Dijkstra_Display()

	for (ti_i = 0; ti_i < i_net_wp_nb; ti_i++)
		@ao_net_wp[ti_i] OBJ_CapaSet(none, Ci_Capa_Skip_This_Wp | Ci_Capa_Wp_Already_Added)
}

procedure_local void PNJ_Raptor_Reach_This_WP(object to_dest_wp)
{
	int					ti_i
	int					ti_flag_ok
	int					ti_flag_skip_next_wp
	int					ti_flag_find_way_to_territory
	int					ti_flag_net_computation
	int					ti_wp_to_reach_nb
	
	object			to_nearest_wp
	object			to_start_wp
	object			tao_wp_to_reach[100]
	
	// A == ma position
	// B == le wp le plus proche de moi
	// C == le wp destination
	// A -----> B chemin entre A et B ok
	// A --X--> B chemin entre A et B bloqué par la grille
	// A --?--> B chemin entre A et dépendant du reseau 

	i_way_status |= Ci_WAY_STATUS_USE_NETWORK	

	ti_flag_net_computation = faux

	if ( ! to_dest_wp )
	{
		// PAS DE WP => ON NE CALCULE PAS DE CHEMIN
		i_way_status |= Ci_WAY_STATUS_NO_NET_WAY	
	
		PNJ_Raptor_Clean_Way()
	}
	else
	{
		// DOIT-ON RECALCULER UN CHEMIN ?
		if (i_way_status & Ci_WAY_STATUS_REACH_TERRITORY)
		{
			if (!o_path_dest_wp || o_path_dest_wp.des_int2 != i_target_territory_ID || ! @o_path_dest_wp OBJ_CapaTest(Ci_Capa_Use_Occluder))
			{
				to_start_wp = PNJ_Raptor_Start_Wp_Get()

				PNJ_Raptor_Fill_Territory_Wp_Array(&tao_wp_to_reach[0], ti_wp_to_reach_nb, i_target_territory_ID)
				PNJ_Raptor_Dijkstra(to_start_wp, &tao_wp_to_reach[0], ti_wp_to_reach_nb, 1)
				for (ti_i = 0; ti_i < ti_wp_to_reach_nb; ti_i++)
				{
					if (tao_wp_to_reach[ti_i].des_object2)
					{
						PNJ_Raptor_Dijkstra_Build_Way(tao_wp_to_reach[ti_i])
						break
					}
				}
				PNJ_Raptor_Dijkstra_Clean()

//				PNJ_Raptor_Way_To_Territory(to_start_wp, i_target_territory_ID)
				if (i_way_wp_nb)
				{
					// On a un chemin
					i_way_status &= ~Ci_WAY_STATUS_NO_NET_WAY
				}
				else
				{
					// Echec, on n'a pas de chemin
					i_way_status |= Ci_WAY_STATUS_NO_NET_WAY
					
#ifndef _FINAL_
					if (DBG_Display_Way)
						PNJ_Raptor_DBG_Display_Accessible(to_start_wp)
#endif				
//					DBG_BreakPoint()
				}
			}
		}
	 	else if (to_dest_wp != o_path_dest_wp || ( ! i_way_wp_nb && (o_current_wp != o_path_dest_wp || i_my_territory_ID != o_path_dest_wp.des_int2)))
		{	
			to_start_wp = PNJ_Raptor_Start_Wp_Get()
					
			if (to_start_wp && to_dest_wp)
			{
				// RECHERCHE DE CHEMIN
				PNJ_Raptor_Clean_Way()
			
				o_path_start_wp = to_start_wp
				o_path_dest_wp = to_dest_wp

				ti_flag_net_computation = vrai

				switch(raptor_type)
				{
					case C_ID_Galiminus :
						if ( ! PNJ_Raptor_Get_Precal_Net_Way(o_path_start_wp, o_path_dest_wp) )
						{
							PNJ_Raptor_Way(to_start_wp, to_dest_wp)
							PNJ_Raptor_Add_Precal_Net_Way()
						}
						break

//					case C_ID_Raptor :
//						if (i_way_computation_mode == Ci_WAY_MODE_VALA)
//							i_way_wp_nb = WAY_ShortWay(n_net, &ao_way_wp[0], to_start_wp, to_dest_wp, all, Ci_Capa_Link_Invalid)
////						else if (i_way_computation_mode == Ci_WAY_MODE_REQUIN)
////							PNJ_Raptor_Deep_Way_Main(to_start_wp, to_dest_wp)
//						else
//							PNJ_Raptor_Way(to_start_wp, to_dest_wp)
//						break
						
					case C_ID_Tyranosaure :
					case C_ID_Raptor :
						i_way_wp_nb = WAY_ShortWay(n_net, &ao_way_wp[0], to_start_wp, to_dest_wp, all, Ci_Capa_Link_Invalid)
						break	
				}

				if (i_way_wp_nb)
				{
					// On a un chemin
					i_way_status &= ~Ci_WAY_STATUS_NO_NET_WAY
				}
				else
				{
					// Echec, on n'a pas de chemin
					i_way_status |= Ci_WAY_STATUS_NO_NET_WAY
#ifndef _FINAL_
					DBG_TraceFloat(TIME_Get())
					DBG_TraceString(" => ")
					DBG_TraceObject(OBJ_Me())
					DBG_TraceString(" ne trouve pas de chemin entre les wps ")
					DBG_TraceObject(to_start_wp)
					DBG_TraceString(" et ")
					DBG_TraceObject(to_dest_wp)
					DBG_TraceEOL()
#endif
					
//					DBG_BreakPoint()
				}
			}
		}
	
		// ON VERIFIE LE CHEMIN
		PNJ_Raptor_Check_Way()
	
		// OPTIMISATION POUR ENLEVER UN WP DU RESAU
		ti_flag_skip_next_wp = vrai
		
		if (i_flag_dont_use_grid)
		{
			if (!ti_flag_net_computation)
				ti_flag_skip_next_wp = faux
			else if (i_way_wp_nb < 2)
				ti_flag_skip_next_wp = faux
			else if (WAY_LinkCapaGet(n_net, ao_way_wp[i_way_wp_nb - 1], ao_way_wp[i_way_wp_nb - 2]) & (Ci_Capa_Link_Jump | Ci_Capa_Link_Test_Grid))
				ti_flag_skip_next_wp = faux
			else if (MATH_VecDotProduct(@ao_way_wp[i_way_wp_nb - 1] OBJ_PosGet() - OBJ_PosGet(), @ao_way_wp[i_way_wp_nb - 2] OBJ_PosGet() - OBJ_PosGet()) > 0.0)
				ti_flag_skip_next_wp = faux
		}
		else
		{
			if (i_way_wp_nb < 2)
				ti_flag_skip_next_wp = faux
			else if (OBJ_SqrDistHorz(ao_way_wp[i_way_wp_nb - 2]) > 400.0)
				ti_flag_skip_next_wp = faux
			else if (@ao_way_wp[i_way_wp_nb - 2] OBJ_CapaTest(Ci_Capa_Out_Of_Grid))
				ti_flag_skip_next_wp = faux
			else if (WAY_LinkCapaGet(n_net, ao_way_wp[i_way_wp_nb - 1], ao_way_wp[i_way_wp_nb - 2]) & (Ci_Capa_Link_Jump | Ci_Capa_Link_Test_Grid))
				ti_flag_skip_next_wp = faux
			else if (! PNJ_Raptor_Test_Grid_Line(Ci_GRID_LINE_ME_TO_NEXT_NEXT_WP, OBJ_PosGet(), @ao_way_wp[i_way_wp_nb - 2] OBJ_PosGet(), 0, 0.0, faux, i_grid_width))
				ti_flag_skip_next_wp = faux
		}
		
		if (ti_flag_skip_next_wp)
		{
			i_way_wp_nb--
			o_last_skiped_wp = ao_way_wp[i_way_wp_nb]
			o_current_wp = ao_way_wp[i_way_wp_nb]

			PNJ_Raptor_Update_Flag(o_current_wp)

			ao_way_wp[i_way_wp_nb] = nobody
		}
	}

	// ON VA ENLEVER DES WPS SI ON PEUT UTILISER LES OCCLUDERS	
	PNJ_Raptor_Skip_Wp_If_Occluder_Enable()

	if (i_way_wp_nb)
		o_next_wp = ao_way_wp[i_way_wp_nb - 1]
	else
		o_next_wp = nobody
	
	if (i_way_wp_nb > 1)
		o_next_next_wp = ao_way_wp[i_way_wp_nb - 2]
	else
		o_next_next_wp = nobody
}


procedure_local void PNJ_Raptor_Way_Add_Wp(object	to_add_wp)
{
	int		ti_i
	
	if (PNJ_Raptor_Link_Is_Invalid(o_path_dest_wp, to_add_wp))
		return

	if (i_way_wp_nb > 2 && ao_way_wp[i_way_wp_nb - 2] == to_add_wp)
	{
		i_way_wp_nb--

		for (ti_i = 0; ti_i < i_way_wp_nb; ti_i++)
			ao_way_wp[ti_i] = ao_way_wp[ti_i + 1]
	
		ao_way_wp[i_way_wp_nb] = nobody

		ao_way_wp[0] = to_add_wp
		o_path_dest_wp = o_path_dest_wp
	}
	else
	{
		for (ti_i = i_way_wp_nb; ti_i > 0; ti_i--)
			ao_way_wp[ti_i] = ao_way_wp[ti_i - 1]
	
		ao_way_wp[0] = to_add_wp
		o_path_dest_wp = to_add_wp

		i_way_wp_nb++
	}
}

procedure_local object PNJ_Raptor_Nearest_Dest_Pos_Wp_Get(vector tv_pos, int ti_territory_ID)
{
	int				ti_i
	int				ti_k
	int				ti_wp_nb	
	int				ti_good_wp_nb
	int				ti_territory_index
	int				ti_flag_precal_wp
	int				ti_index	
	int				ti_flag

	object 		to_dest_wp	
	object		to_nearest_wp
	object		to_neighbor
	object		tao_neighbor_wp[10]
	object		tao_wp[100]
	object		to_nearest_neighbor_wp

	ti_flag_precal_wp = faux

	ti_territory_index = PNJ_Raptor_Territory_Index_Get(ti_territory_ID)

	// ON REGARDE SI ON PEUT PARTIR D'UN WP OCCLUDER
	for (ti_i = 0; ti_i < ai_net_territory_wp_nb[ti_territory_index]; ti_i++)
	{
		ti_index = ai_net_territory_first_wp_index[ti_territory_index]
		ti_index += ti_i
		
		to_dest_wp = 	ao_net_wp[ti_index]
		if (@to_dest_wp OBJ_CapaTest(Ci_Capa_Use_Occluder))
			return(to_dest_wp)
	}

	if (o_path_dest_wp)
	{
		if (! (@o_path_dest_wp OBJ_CapaTest(Ci_Capa_Out_Of_Grid)) )
		{
//			ti_index	= PNJ_Raptor_Get_Precal_Grid_Line(@o_path_dest_wp OBJ_PosGet(), tv_pos)
//			if (ti_index == -1)
//			{
//				ti_flag = GRID_LIB_IsReachableFrom(@o_path_dest_wp OBJ_PosGet(), tv_pos, 0, -1.0, faux, 0)
				ti_flag = PNJ_Raptor_Test_Grid_Line(Ci_GRID_LINE_LAST_WP_TO_DEST, @o_path_dest_wp OBJ_PosGet(), tv_pos, 0, -1.0, faux, 0)
//				PNJ_Raptor_Add_Precal_Grid_Line(@o_path_dest_wp OBJ_PosGet(), tv_pos, ti_flag)
//			}
//			else
//			{
//				ti_flag = ti_index
//			}
		
			if (ti_flag)
			{
				// ON PEUT ALLER DU WP VERS LEQUEL ON ALLAIT VERS LA POSITION DEST
				i_way_status |= Ci_WAY_STATUS_USE_LAST_DEST_WP
				return(o_path_dest_wp)
			}
		}

		// ON VA REGARDER PARMIS LES VOISINS DU WP VERS LEQUEL ON ALLAIT
		ti_good_wp_nb = 0
		ti_wp_nb = WAY_GetNumLinks(n_net, o_path_dest_wp)
		for (ti_i = 0; ti_i < ti_wp_nb; ti_i++)
		{
			to_neighbor = WAY_NetNextWP(n_net, o_path_dest_wp, 6, ti_i)

			if (PNJ_Raptor_Wp_Is_Invalid(to_neighbor))
				continue

			tao_neighbor_wp[ti_good_wp_nb] = to_neighbor
			ti_good_wp_nb++
		}

		to_dest_wp = ARR_LIB_NearestSightReachableWaypoint(&tao_neighbor_wp[0], 0, ti_good_wp_nb, tv_pos, OBJ_SightGet(), -1.0, all, Ci_Capa_Out_Of_Grid | Ci_Capa_Exclusion, to_nearest_wp, 0, faux)
		if (to_dest_wp)
		{
			// ON AJOUTE UN WP DANS LE CHEMIN
			i_way_status |= Ci_WAY_STATUS_USE_DEST_WP_NEIGHBOR
			PNJ_Raptor_Way_Add_Wp(to_dest_wp)
		}
		else
		{
			// ON REGARDE PARMIS LES POSITIONS DEJA EVALUEES SI UNE CONVIENT
			to_dest_wp = PNJ_Raptor_Get_Precal_Wp(tv_pos, ti_territory_ID)
			if (to_dest_wp)
			{
				ti_flag_precal_wp = vrai
			}
			else
			{
//				// ON VA RECHERCHER PARMIS TOUS LES WPS MOINS CELUI VERS LEQUEL ON ALLAIT ET SES VOISINS
//				@o_path_dest_wp OBJ_CapaSet(Ci_Capa_Skip_This_Wp, none)
//				for (ti_i = 0; ti_i < ti_good_wp_nb; ti_i++)
//					@tao_neighbor_wp[ti_i] OBJ_CapaSet(Ci_Capa_Skip_This_Wp, none)
//
//				to_dest_wp = ARR_LIB_NearestSightReachableWaypoint(&ao_net_wp[0], ai_net_territory_first_wp_index[ti_territory_index], ai_net_territory_wp_nb[ti_territory_index], tv_pos, OBJ_SightGet(), -1.0, all, Ci_Capa_Out_Of_Grid | Ci_Capa_Exclusion | Ci_Capa_Skip_This_Wp, to_nearest_wp, 0, vrai)
//
//				@o_path_dest_wp OBJ_CapaSet(none, Ci_Capa_Skip_This_Wp)
//				for (ti_i = 0; ti_i < ti_good_wp_nb; ti_i++)
//					@tao_neighbor_wp[ti_i] OBJ_CapaSet(none, Ci_Capa_Skip_This_Wp)

				// ON CHERCHE PARMIS TOUS LES WPS QUI SONT SUR LE MEME TERRITOIRE
				to_dest_wp = ARR_LIB_NearestSightReachableWaypoint(&ao_net_wp[0], ai_net_territory_first_wp_index[ti_territory_index], ai_net_territory_wp_nb[ti_territory_index], tv_pos, OBJ_SightGet(), -1.0, all, Ci_Capa_Out_Of_Grid | Ci_Capa_Exclusion, to_nearest_wp, 0, vrai)
			}
		}
	}
	else
	{
		// ON REGARDE PARMIS LES POSITIONS DEJA EVALUEES SI UNE CONVIENT
		to_dest_wp = PNJ_Raptor_Get_Precal_Wp(tv_pos, ti_territory_ID)
		if (to_dest_wp)
		{
			ti_flag_precal_wp = vrai
		}
		else
		{
			// ON CHERCHE PARMIS TOUS LES WPS QUI SONT SUR LE MEME TERRITOIRE
			to_dest_wp = ARR_LIB_NearestSightReachableWaypoint(&ao_net_wp[0], ai_net_territory_first_wp_index[ti_territory_index], ai_net_territory_wp_nb[ti_territory_index], tv_pos, OBJ_SightGet(), -1.0, all, Ci_Capa_Out_Of_Grid | Ci_Capa_Exclusion, to_nearest_wp, 0, vrai)
		}
	}

//	if (!to_dest_wp)
//	{
//		// ON N'A PAS TROUVE DE WP PARMIS LES WPS DE CE TERRITOIRE, ON UTILISE LES WPS QUI SONT SUR LES TERRITROIRES VOISINS
//		for (ti_i = 0; ti_i < i_net_territory_ID_nb; ti_i++)
//		{
//			if (! ai_net_territory_allowed[ti_i] )
//				continue
//
//			if (! ai_net_territory_link[ti_index][ti_i] )
//				continue
//		
//			to_dest_wp = ARR_LIB_NearestSightReachableWaypoint(&ao_net_wp[0], ai_net_territory_first_wp_index[ti_i], ai_net_territory_wp_nb[ti_i], tv_pos, OBJ_SightGet(), -1.0, all, Ci_Capa_Out_Of_Grid | Ci_Capa_Exclusion, to_nearest_neighbor_wp, 0, faux)
//			if (to_dest_wp)
//				break
//		}
//	}


	if (to_dest_wp)
	{
		if (!ti_flag_precal_wp)
			PNJ_Raptor_Add_Precal_Wp(tv_pos, to_dest_wp)

		if (DBG_Display_Way)
			DBG_RenderVector(@to_dest_wp OBJ_PosGet() + Cv_VerticalVector, tv_pos - @to_dest_wp OBJ_PosGet(), color_vert)
	}
	else
	{
		i_way_status |= Ci_WAY_STATUS_USE_TERRITORY_NEAREST_WP

		to_dest_wp = to_nearest_wp

		if (DBG_Display_Way && to_dest_wp)
			DBG_RenderVector(@to_dest_wp OBJ_PosGet() + Cv_VerticalVector, tv_pos - @to_dest_wp OBJ_PosGet(), color_rouge)
	}

	return(to_dest_wp)
}

procedure_local void PNJ_Raptor_Reach_This_Position(vector tv_pos, int ti_territory_ID)
{
	int					ti_flag_position_accessible
	int					ti_flag_find_nearest_pos
	int					ti_flag_ok
	int					ti_i
	int					ti_index
	int					ti_territory_index

	object			to_nearest_wp
	object			to_dest_wp
	
	GRID_CurrentSet(0)

	i_way_status |= Ci_WAY_STATUS_REACH_POSITION

	v_way_position_to_reach = tv_pos

	v_way_destpos = tv_pos
	i_dest_pos_territory_ID = ti_territory_ID

	v_way_case_dest = tv_pos

	ti_flag_position_accessible = PNJ_Raptor_Is_Accessible(tv_pos, ti_territory_ID)

	if ( ! ti_flag_position_accessible )
	{
		// JE NE POURRAIS PAS ACCEDER A LA POSITION DEST, JE VAIS AU PLUS PRES
		i_way_status |= Ci_WAY_STATUS_UNREACHABLE_POSITION	
	
		v_way_last_invalid_research_pos = GRID_PosGet(tv_pos)
		
		DBG_RenderVector(tv_pos, Cv_VerticalVector * 1000.0, color_cyan)	

		to_dest_wp = PNJ_Raptor_Nearest_Wp_Get(tv_pos, all, Ci_Capa_Exclusion | Ci_Capa_Stalk, faux)
	}
	else
	{
		// =======================================================================
		// SI ON EST SUR LA GRILLE ET UNIQUEMENT SI ON EST SUR LA GRILLE	
		// =======================================================================
	
		if ( ! i_flag_dont_use_grid )
		{
			if (i_flag_occluder_alllowed && i_my_territory_ID == ti_territory_ID && i_my_territory_ID == i_occluder_territory)
			{
				// ON PEUT UTILISER LES OCCLUDER
				i_way_status |= Ci_WAY_STATUS_OCCLUDER_ENABLE	
			
				o_next_wp = nobody
				PNJ_Raptor_Clean_Way()
				return
			}

			ti_flag_ok = vrai

			if (raptor_type == C_ID_Galiminus && i_way_wp_nb > 1 && i_my_territory_ID == ti_territory_ID)
				ti_flag_ok = faux
			else if ( ! PNJ_Raptor_Test_Grid_Line(Ci_GRID_LINE_ME_TO_DEST_POS, OBJ_PosGet(), tv_pos, 0, -1.0, faux, i_grid_width) )
				ti_flag_ok = faux
			
			if ( ti_flag_ok )
			{
				// JE PEUX ACCEDER A MA CIBLE VIA LA GRILLE
				i_way_status |= Ci_WAY_STATUS_NO_WALL_BETWEEN_ME_AND_POS
		
				o_next_wp = nobody
				PNJ_Raptor_Clean_Way()

				// =======================================================================
				// EST-CE QUE J'AI UN WP EN USE OCCLUDER SUR LE TERRITOIRE DEST ?
				// =======================================================================
				if (i_my_territory_ID == ti_territory_ID)
				{
					ti_territory_index = PNJ_Raptor_Territory_Index_Get(ti_territory_ID)
	
					for (ti_i = 0; ti_i < ai_net_territory_wp_nb[ti_territory_index]; ti_i++)
					{
						ti_index = ai_net_territory_first_wp_index[ti_territory_index]
						ti_index += ti_i
						
						to_dest_wp = ao_net_wp[ti_index]
						if (@to_dest_wp OBJ_CapaTest(Ci_Capa_Use_Occluder))
						{
							// ON PEUT UTILISER LES OCCLUDER
							i_way_status |= Ci_WAY_STATUS_OCCLUDER_ENABLE	
				
							i_flag_occluder_alllowed = vrai
							i_occluder_territory = ti_territory_ID
						}
					}
				}

				return
			}
		}

		// =======================================================================
		// FIN : SI ON EST SUR LA GRILLE ET UNIQUEMENT SI ON EST SUR LA GRILLE	
		// =======================================================================

		// =======================================================================
		// EST-CE QUE J'AI UN WP EN USE OCCLUDER SUR LE TERRITOIRE DEST ?
		// =======================================================================
		ti_territory_index = PNJ_Raptor_Territory_Index_Get(ti_territory_ID)
		for (ti_i = 0; ti_i < ai_net_territory_wp_nb[ti_territory_index]; ti_i++)
		{
			ti_index = ai_net_territory_first_wp_index[ti_territory_index]
			ti_index += ti_i
			
			to_dest_wp = ao_net_wp[ti_index]
			if (PNJ_Raptor_Wp_Is_Invalid(to_dest_wp))	
				continue
		
			if (@to_dest_wp OBJ_CapaTest(Ci_Capa_Use_Occluder))
			{
				i_way_status |= Ci_WAY_STATUS_REACH_TERRITORY
				break
			}
		}

		if (i_way_status & Ci_WAY_STATUS_REACH_TERRITORY)
		{
			// JE PRENDRAI LE PREMIER WP EN USE OCLLUDER RENCONTRE 
			to_dest_wp = to_dest_wp
		}
		else
		{
			// JE DOIS RECHERCHER LE WP LE PLUS PROCHE DE MA CIBLE
			to_dest_wp = PNJ_Raptor_Nearest_Dest_Pos_Wp_Get(tv_pos, ti_territory_ID)
		}
	}

	PNJ_Raptor_Reach_This_WP(to_dest_wp)
}

procedure_local int PNJ_Raptor_Find_Way(vector tv_start_pos, vector tv_dest_pos)
{
	int			ti_i	
	int			ti_cpt_good_case
	int			tai_old_case[20]
	int			tai_new_case[20]
	int			ti_flag_dest_case_computed
	int			ti_flag_free_way
	int			ti_width
	int			ti_tested_width

	float		tf_dist	
	float		tf_tested_length
	float		tf_step_length

	vector 	tv_start_to_dest
	vector	tv_ortho_dir
	vector	tv_tested_pos
	vector	tv_pos

	object	to_main_actor

//	v_way_case_dest = tv_dest_pos
//	return	(vrai)

//	f_way_bezier_sight_weight = 0.0

	GRID_CurrentSet(0)

	i_way_case_nbr = 0

	ti_width = 1 << i_grid_width
	ti_width += 1

	ti_tested_width = ti_width << 1
	ti_tested_width += 1

	tv_start_pos = GRID_PosGet(tv_start_pos)
	tv_dest_pos = GRID_PosGet(tv_dest_pos)

 	tv_start_to_dest = tv_dest_pos - tv_start_pos
 	tv_start_to_dest.z = 0.0

	tf_dist = MATH_VecNorm(tv_start_to_dest)
	if (!tf_dist)
		return	(vrai)

	tf_dist = MATH_FloatMin(tf_dist, ti_tested_width)

	tf_step_length = MATH_FloatMax(MATH_AbsFloat(tv_start_to_dest.x), MATH_AbsFloat(tv_start_to_dest.y))
	tv_start_to_dest /= tf_step_length
	tf_step_length = MATH_VecNorm(tv_start_to_dest)

	tv_ortho_dir = MATH_VecCrossProduct(tv_start_to_dest, Cv_VerticalVector)
	tv_ortho_dir += tv_start_pos
	tv_ortho_dir = GRID_PosGet(tv_ortho_dir)
	tv_ortho_dir -= tv_start_pos

	tai_old_case[0] = vrai
	tai_old_case[1] = vrai
	tai_old_case[2] = vrai
	tai_old_case[3] = vrai
	tai_old_case[4] = vrai
	tai_old_case[5] = vrai
	tai_old_case[6] = vrai

	ti_flag_dest_case_computed = faux
	ti_flag_free_way = vrai

	tf_tested_length = 0.0
	tf_tested_length += tf_step_length

#ifndef _FINAL_
	to_main_actor = AI_MainActorGet(C_ID_Joueur)
	tv_start_pos.z = @to_main_actor OBJ_PosGet().z
	tv_dest_pos.z = tv_start_pos.z
#endif

	tv_tested_pos = tv_start_pos
	tv_tested_pos += tv_start_to_dest

	while(tf_tested_length < tf_dist)
	{
		ti_cpt_good_case = 0

		for (ti_i = -ti_width; ti_i <= ti_width; ti_i++)
		{
			tv_pos = tv_tested_pos
			tv_pos += ti_i * tv_ortho_dir

			if (PNJ_Raptor_Blocked(tv_pos, 0))
			{
				tai_new_case[ti_i + ti_width] = faux

#ifndef _FINAL_
				if (DBG_Display_Way)
					DBG_RenderVector(tv_pos, Cv_VerticalVector, color_rouge)	
#endif
			}
			else
			{
				ti_cpt_good_case++
				tai_new_case[ti_i + ti_width] = vrai
#ifndef _FINAL_
				if (DBG_Display_Way)
					DBG_RenderVector(tv_pos, Cv_VerticalVector, color_vert)	
#endif
			}
		}

		if (ti_cpt_good_case < ti_width - 1)
			return(faux)

		ti_cpt_good_case = 0

		for (ti_i = 0; ti_i < ti_tested_width; ti_i++)
		{
			if (tai_old_case[ti_i] && tai_new_case[ti_i])
				ti_cpt_good_case++
			else if (ti_cpt_good_case < ti_width)
				ti_cpt_good_case = 0
			
			tai_old_case[ti_i] = tai_new_case[ti_i]
		}
			
		if (ti_cpt_good_case < ti_width)
			return(faux)

		if (!ti_flag_dest_case_computed && (tf_tested_length && ti_cpt_good_case < ti_tested_width))
		{
			ti_flag_dest_case_computed = vrai
			ti_cpt_good_case	 = 0
		
			for (ti_i = 0; ti_i < ti_tested_width; ti_i++)
			{
				if (tai_new_case[ti_i])
					ti_cpt_good_case++
				else
					ti_cpt_good_case = 0

				if (ti_cpt_good_case == ti_width)
				{
					v_way_case_dest = tv_tested_pos
					v_way_case_dest += (ti_i - ti_width) * tv_ortho_dir
					
#ifndef _FINAL_
					if (DBG_Display_Way)
						DBG_RenderVector(v_way_case_dest, Cv_VerticalVector * 10.0, color_vert)
#endif
				}
			}
		}

		tf_tested_length += tf_step_length
		tv_tested_pos += tv_start_to_dest
	}

	if (!ti_flag_dest_case_computed)
		v_way_case_dest = v_way_destpos

	return(vrai)
}

procedure_local int PNJ_Raptor_Can_Reach_Dest_Pos()
{
	// LA POSITION DEST EST INACCESSIBLE
	if (i_way_status & Ci_WAY_STATUS_UNREACHABLE_POSITION)
		return(faux)

	// ON PASSE PAR LE RESEAU POUR ATTEINDRE LE WP OU LA POSITION DESTINATION
	if (i_way_status & Ci_WAY_STATUS_USE_NETWORK)
	{
		// ON NE TROUVE PAS DE CHEMIN
		if (i_way_status & Ci_WAY_STATUS_NO_NET_WAY)
			return(faux)	
	
		// ON NE PEUT PAS ALLER VIA LA GRILLE D'UN WP DU RESEAU A LA POSITION DEST
		if (i_way_status & Ci_WAY_STATUS_USE_TERRITORY_NEAREST_WP)
			return(faux)	
	}

	return(vrai)
}


procedure_local object PNJ_Raptor_Best_Requin_Wp_Get(object to_start_wp, object to_last_wp, vector tv_dodged_pos, vector tv_start_pos, vector tv_side_dir)
{
	int			ti_i
	int			ti_nb_wp	
	int			ti_flag_skip_slide_preference

	float		tf_dot_product
	float		tf_ponderation
	float		tf_best_ponderation
	float		tf_norm
	
	object	to_best_wp
	object	to_next_wp

	vector	tv_best_sight
	vector	tv_me_to_wp

	if (!to_start_wp)
		return(nobody)

	to_best_wp = nobody
	tf_best_ponderation = -Cf_Infinit

	tv_best_sight = tv_dodged_pos
	tv_best_sight -= tv_start_pos
	tv_best_sight.z = 0.0

//#ifndef _FINAL_
//	if (DBG_Display_Way)
//		DBG_RenderVector(tv_start_pos + Cv_VerticalVector, tv_best_sight, color_blanc)
//#endif

	tv_best_sight.z = 0.0
	tf_norm = MATH_VecNorm(tv_best_sight)
	if (tf_norm)
		tv_best_sight /= tf_norm
	else
	{
		// Houla, le point à éviter est moi même ?
		DBG_BreakPoint()
		return(nobody)
	}
	
	tv_side_dir.z = 0.0
	tf_norm = MATH_VecNorm(tv_side_dir)
	if (tf_norm)
		tv_side_dir /= tf_norm
	else
	{
		// Houla, c'est quoi ce sight ?
		DBG_BreakPoint()
		return(nobody)
	}

//	if (MATH_VecDotProduct(tv_side_dir, -tv_best_sight) > Cf_Cos30)
//		ti_flag_skip_slide_preference = vrai
//	else
//		ti_flag_skip_slide_preference = faux

	tv_best_sight = MATH_VecCrossProduct(tv_best_sight, Cv_VerticalVector) 
	tv_best_sight *= MATH_FloatSign(MATH_VecDotProduct(tv_best_sight, tv_side_dir))

//#ifndef _FINAL_
//	if (DBG_Display_Way)
//	{
//		DBG_RenderVector(tv_start_pos + Cv_VerticalVector, tv_best_sight * 10.0, color_cyan)
////		if (ti_flag_skip_slide_preference)
////			DBG_RenderVector(tv_start_pos + Cv_VerticalVector, -tv_best_sight * 10.0, color_cyan)
//	}
//#endif

	ti_nb_wp = WAY_GetNumLinks(n_net, to_start_wp)
	for (ti_i = 0; ti_i < ti_nb_wp; ti_i++)
	{
		to_next_wp = WAY_NetNextWP(n_net, to_start_wp, 6, ti_i)
		
		if (PNJ_Raptor_Wp_Is_Invalid(to_next_wp))
			continue

		if (PNJ_Raptor_Link_Is_Invalid(to_start_wp, to_next_wp))
			continue

		if (PNJ_Raptor_Invalid_Requin_Way(to_start_wp, to_next_wp))
		{
			// Tres tres mauvaise ponderation
			tf_ponderation = -100.0
		}
		else if (to_next_wp == to_last_wp)
		{
			// La pire des pondérations normales
			tf_ponderation = -1.0
		}
		else
		{
			tv_me_to_wp = @to_next_wp OBJ_PosGet() - tv_start_pos
			tv_me_to_wp.z = 0.0
			
			tf_norm = MATH_VecNorm(tv_me_to_wp)
			if (!tf_norm)
				continue	
		
			tv_me_to_wp /= tf_norm

			tf_dot_product = MATH_VecDotProduct(tv_me_to_wp, tv_best_sight)	
//			if (ti_flag_skip_slide_preference)	
//				tf_dot_product = MATH_AbsFloat(tf_dot_product)			
		
			tf_ponderation = 1.0 + tf_dot_product
			tf_ponderation *= MATH_AbsFloat(tf_dot_product)
		}

		if (@to_next_wp OBJ_CapaTest(Ci_Capa_Stalk))
		{
			// Normalement on ne doit jamais passer par un wp de stalk, mais si on n'a pas le choix...
			tf_ponderation -= 10.0
		}

		if (tf_ponderation > tf_best_ponderation)
		{
			to_best_wp = to_next_wp
			tf_best_ponderation = tf_ponderation
		}
	}
	
	return(to_best_wp)
}

procedure_local int PNJ_Raptor_Dodged_Pos_On_Requin_Way(object	to_next_wp, vector tv_start_pos, vector tv_dodged_pos, vector tv_side_dir)
{
	int			ti_iteration

	float		tf_link_length
	float		tf_dist
	float		tf_dot_product
	float		tf_tested_dist

	vector	tv_link_dir
	vector	tv_link_doged_pos
	

	object	to_current_wp
	object	to_last_next_wp

	ti_iteration = 5
	tf_tested_dist = 0.0
	
	to_current_wp = nobody
	tf_link_length = MATH_VecNorm(@to_next_wp OBJ_PosGet() - tv_start_pos)

	while(ti_iteration && tf_tested_dist < 10.0)
	{
		ti_iteration--
		tf_tested_dist += tf_link_length
	
		tv_link_doged_pos = tv_dodged_pos
		tv_link_doged_pos -= tv_start_pos
		tv_link_doged_pos.z = 0.0
		if (MATH_VecDotProduct(tv_link_doged_pos, tv_link_doged_pos) < 9.0)
			return(vrai)

		if (tf_link_length)
		{
			tv_link_dir = @to_next_wp OBJ_PosGet()
			tv_link_dir -= tv_start_pos
			tv_link_dir /= tf_link_length
	
			tf_dot_product = MATH_VecDotProduct(tv_dodged_pos - tv_start_pos, tv_link_dir)
	
			if (tf_dot_product >= 0.0 && tf_dot_product < tf_link_length)
			{
				// La position à éviter est "sur ce lien"
				tv_link_doged_pos = tv_start_pos
				tv_link_doged_pos += tf_dot_product * tv_link_dir
				
//				DBG_RenderVector(tv_start_pos, tf_dot_product * tv_link_dir, color_rouge)	
//				DBG_RenderVector(tv_start_pos + (tf_dot_product * tv_link_dir), tv_link_dir * (tf_link_length - tf_dot_product), color_vert)
	
	//			tv_link_doged_pos -= tv_dodged_pos
		
				tf_dist = MATH_VecDotProduct(tv_link_doged_pos - tv_dodged_pos, tv_link_doged_pos - tv_dodged_pos)
				if (tf_dist < 9.0)
				{
//					DBG_RenderVector(tv_dodged_pos, tv_link_doged_pos - tv_dodged_pos, color_rouge)
					return(vrai)
				}
//				else
//				{
//					DBG_RenderVector(tv_dodged_pos, tv_link_doged_pos - tv_dodged_pos, color_vert)
//				}
			}
	
//			DBG_RenderVector(tv_start_pos, tv_link_dir * tf_link_length, color_vert)
		}

		to_last_next_wp	= to_next_wp
		to_next_wp = PNJ_Raptor_Best_Requin_Wp_Get(to_next_wp, to_current_wp, tv_dodged_pos, @to_next_wp OBJ_PosGet(), tv_side_dir)

		if (!to_next_wp)
			return(faux)

		to_current_wp = to_last_next_wp
		tv_start_pos = @to_current_wp OBJ_PosGet() 

		tf_link_length = WAY_LinkWeightGet(n_net, to_current_wp, to_next_wp)
		
		tv_side_dir = @to_next_wp OBJ_PosGet() - @to_current_wp OBJ_PosGet()
		tv_side_dir /= tf_link_length
	}
	
	return(faux)
}

procedure_local object PNJ_Raptor_Balade_Wp_Get(object to_start_wp, vector tv_start_pos, vector tv_sight)
{
	int			ti_i
	int			ti_nb_wp	

	float		tf_ponderation
	float		tf_best_ponderation
	float		tf_dot_product
	float		tf_norm

	object	to_best_wp
	object	to_next_wp

	vector	tv_me_to_wp

	if (to_start_wp == nobody)
		return(nobody)

	to_best_wp = nobody
	tf_best_ponderation = -1.0

	ti_nb_wp = WAY_GetNumLinks(n_net, to_start_wp)
	for (ti_i = 0; ti_i < ti_nb_wp; ti_i++)
	{
		to_next_wp = WAY_NetNextWP(n_net, to_start_wp, 6, ti_i)
		
		if (PNJ_Raptor_Wp_Is_Invalid(to_next_wp))
			continue
	
		if (PNJ_Raptor_Link_Is_Invalid(to_start_wp, to_next_wp))
			continue

		if (@to_next_wp OBJ_CapaTest(Ci_Capa_Balade))
		{
			tv_me_to_wp = @to_next_wp OBJ_PosGet() - tv_start_pos
			tv_me_to_wp.z = 0.0
			
			tf_norm = MATH_VecNorm(tv_me_to_wp)
			if (tf_norm)
				tv_me_to_wp /= tf_norm

			tf_dot_product = MATH_VecDotProduct(tv_sight, tv_me_to_wp)	

			tf_ponderation = tf_dot_product
			if (tf_ponderation > tf_best_ponderation)
			{
				to_best_wp = to_next_wp
				tf_best_ponderation = tf_ponderation
			}
		}
	}
	
	return(to_best_wp)
}

procedure_local vector PNJ_Raptor_Bezier_Grid_Move(float tf_move_length, byref vector tv_sight)
{
	int			ti_i
	int			ti_grid_case_index	

	float		tf_link_length
	float		tf_coef	
	float		tf_dot_product

	vector	tv_point_A
	vector	tv_point_B
	vector	tv_point_C
	vector	tv_point_D
	vector	tv_last_grid_pos_backup

	vector	tv_virtual_link
	vector	tv_start_tangente
	vector	tv_dest_tangente

	GRID_CurrentSet(0)

	// SI ON EST SUR LA PREMIERE CASE DU CHEMIN, ON CONSIDERE QU'ELLE EST ATTEINTE
	if (GRID_PosGet(OBJ_PosGet()) == v_way_case[i_way_case_nbr - 1])
	{
		i_way_case_nbr--
		v_way_case[i_way_case_nbr] = Cv_NullVector
	}

	// ON ENLEVE LES CASES ADJACENTES QUI SONT DERRIERES
	tv_point_B = GRID_PosGet(OBJ_PosGet())

	while(i_way_case_nbr > 1)
	{
		tv_point_A = v_way_case[i_way_case_nbr - 1]
		tv_point_A -= OBJ_PosGet()
		tv_point_A.z = 0.0

		tf_dot_product = MATH_VecDotProduct(tv_point_A, OBJ_SightGet())
		if (tf_dot_product > 0.0)
			break

		// CETTE CASE FAIT FAIRE DEMI-TOUR
		
		tv_point_A = v_way_case[i_way_case_nbr - 1]
		tv_point_A -= tv_point_B
		tv_point_A.z = 0.0

		tf_dot_product = MATH_VecDotProduct(tv_point_A, tv_point_A)
		if (tf_dot_product > 2.25)
			break
		
		// CETTE CASE EST ADJACENTE => ON L'ENLEVE	

		i_way_case_nbr--
		v_way_case[i_way_case_nbr] = Cv_NullVector
	}

	ti_grid_case_index = i_way_case_nbr 
	tv_last_grid_pos_backup = v_way_case[ti_grid_case_index]

	v_way_case[ti_grid_case_index] = OBJ_PosGet()
	v_way_case[ti_grid_case_index].z = 0.0

#ifndef _FINAL
	if (DBG_Display_Way)
	{
		for (ti_i = ti_grid_case_index; ti_i > 0; ti_i--)
		{
			tv_virtual_link = v_way_case[ti_i - 1]
			tv_virtual_link -= v_way_case[ti_i]
			tv_virtual_link.z = 0.0
			
			tf_link_length = MATH_VecNorm(tv_virtual_link)
	
			tv_virtual_link /= tf_link_length
		
			tv_point_A = v_way_case[ti_i]
			tv_point_A.z = OBJ_PosGet().z + 1.0

			tv_point_B = tv_point_A
		
			tv_point_D = v_way_case[ti_i - 1]
			tv_point_D.z = tv_point_A.z

			tv_point_C = tv_point_D
		
			if (ti_i == ti_grid_case_index)
			{
				tv_start_tangente = tv_virtual_link
				tv_start_tangente *= tf_link_length * 0.5
			}
			else
			{
				tv_start_tangente = v_way_case[ti_i + 1]
				tv_start_tangente -= v_way_case[ti_i] 
				MATH_VecSetNormalize(tv_start_tangente)
				tv_start_tangente = MATH_VecBlendRotate(tv_start_tangente, tv_virtual_link, 0.5)
				tv_start_tangente = MATH_VecCrossProduct(tv_start_tangente, Cv_VerticalVector)
				tv_start_tangente *= MATH_FloatSign(MATH_VecDotProduct(tv_start_tangente, tv_virtual_link))

				tv_start_tangente *= tf_link_length * 0.33
			}
		
			if (ti_i > 1)
				tv_dest_tangente = v_way_case[ti_i - 2]
			else if (o_next_next_wp)
				tv_dest_tangente = @o_next_next_wp OBJ_PosGet()
			else if (i_way_status & Ci_WAY_STATUS_REACH_POSITION)
				tv_dest_tangente = v_way_position_to_reach
			else
				tv_dest_tangente = v_way_destpos
			
			tv_dest_tangente -= v_way_case[ti_i -1] 
			MATH_VecSetHorzNormalize(tv_dest_tangente)
	
			if (ti_i > 1)
			{
				// On veut la biscectrice
				tv_dest_tangente = MATH_VecBlendRotate(-tv_virtual_link, tv_dest_tangente, 0.5)
				tv_dest_tangente = MATH_VecCrossProduct(tv_dest_tangente, Cv_VerticalVector)
				tv_dest_tangente *= - MATH_FloatSign(MATH_VecDotProduct(tv_dest_tangente, tv_virtual_link))
			}
			else
			{
				tv_dest_tangente *= -1.0
			}
	
			DBG_RenderVector(tv_point_A, tv_point_D - tv_point_A, color_cyan)

//			DBG_RenderVector(tv_point_A, tv_start_tangente * (0.33 * tf_link_length), color_jaune)
			tv_point_B += tv_start_tangente

//			DBG_RenderVector(tv_point_D, tv_dest_tangente * (0.33 * tf_link_length), color_cyan)
			tv_point_C += tv_dest_tangente * (0.33 * tf_link_length)
		
			MATH_LIB_Bezier_Display(5, tv_point_A, tv_point_B, tv_point_C, tv_point_D)
		}
	}
#endif
	
	ti_i = ti_grid_case_index

	tv_virtual_link = v_way_case[ti_i - 1]
	tv_virtual_link -= v_way_case[ti_i]
	tv_virtual_link.z = 0.0

	tf_link_length = MATH_VecNorm(tv_virtual_link)
	
	while(tf_move_length >= tf_link_length && ti_i > 1)
	{
		// On a atteint cette case
		tf_move_length -= tf_link_length
		
		ti_i--

		tv_virtual_link = v_way_case[ti_i - 1]
		tv_virtual_link -= v_way_case[ti_i]
		tv_virtual_link.z = 0.0
	
		tf_link_length = MATH_VecNorm(tv_virtual_link)
	}
	
	tf_coef = tf_move_length / tf_link_length
	
	tv_virtual_link /= tf_link_length

	tv_point_A = v_way_case[ti_i]
	tv_point_A.z = OBJ_PosGet().z + 0.5

	tv_point_B = tv_point_A

	tv_point_D = v_way_case[ti_i - 1]
	tv_point_D.z = tv_point_A.z

	tv_point_C = tv_point_D

	// START TANGENTE
	if (ti_i == ti_grid_case_index)
	{
		tv_start_tangente = tv_virtual_link
		tv_start_tangente *= tf_link_length * 0.5
	}
	else
	{
		tv_start_tangente = v_way_case[ti_i + 1]
		tv_start_tangente -= v_way_case[ti_i] 
		MATH_VecSetNormalize(tv_start_tangente)
		tv_start_tangente = MATH_VecBlendRotate(tv_start_tangente, tv_virtual_link, 0.5)
		tv_start_tangente = MATH_VecCrossProduct(tv_start_tangente, Cv_VerticalVector)
		tv_start_tangente *= MATH_FloatSign(MATH_VecDotProduct(tv_start_tangente, tv_virtual_link))
	
		tv_start_tangente *= tf_link_length * 0.33
	}

	// DEST TANGENTE
	if (ti_i > 1)
		tv_dest_tangente = v_way_case[ti_i - 2]
	else if (o_next_next_wp)
		tv_dest_tangente = @o_next_next_wp OBJ_PosGet()
	else if (i_way_status & Ci_WAY_STATUS_REACH_POSITION)
		tv_dest_tangente = v_way_position_to_reach
	else
		tv_dest_tangente = v_way_destpos
	
	tv_dest_tangente -= v_way_case[ti_i -1] 
	MATH_VecSetHorzNormalize(tv_dest_tangente)

	if (ti_i > 1)
	{
		// On veut la biscectrice
		tv_dest_tangente = MATH_VecBlendRotate(-tv_virtual_link, tv_dest_tangente, 0.5)
		tv_dest_tangente = MATH_VecCrossProduct(tv_dest_tangente, Cv_VerticalVector)
		tv_dest_tangente *= - MATH_FloatSign(MATH_VecDotProduct(tv_dest_tangente, tv_virtual_link))
	}
	else
	{
		tv_dest_tangente *= -1.0
	}

	tv_point_B += tv_start_tangente 
	tv_point_C += tv_dest_tangente * (0.33 * tf_link_length)

	v_way_case[ti_grid_case_index] = tv_last_grid_pos_backup

	tv_point_A = MATH_LIB_Bezier_Pos_Get(tf_coef, tv_point_A, tv_point_B, tv_point_C, tv_point_D, tv_sight)

//	DBG_RenderVector(OBJ_PosGet() + cvector(0.0, 0.0, 0.5), Cv_VerticalVector * 5.0, color_rouge)
//	DBG_RenderVector(tv_point_A, Cv_VerticalVector * 5.0, color_vert)
//	DBG_RenderVector(tv_point_A + (tf_coef * tf_link_length * tv_virtual_link), Cv_VerticalVector * 5.0, color_bleu)

	return(tv_point_A)
}

procedure_local int PNJ_Raptor_Bezier_Move(float tf_wanted_speed)
{
	int			ti_flag_use_next_pos
	int			ti_flag_jump_link
	int			ti_flag_collision
	int			ti_return_value

	vector	tv_point_A
	vector	tv_point_B
	vector	tv_point_C
	vector	tv_point_D
	vector	tv_AD

	vector	tv_pos
	vector	tv_sight
	vector	tv_banking
	
	vector	tv_next_pos
	vector	tv_tangente
	vector	tv_temp	
	vector	tv_next_banking

	float		tf_link_coef
	float		tf_move_length
	float		tf_virtual_link_length
	float		tf_dot_product
	float		tf_norm
	float		tf_length

	object	to_collided_actor

	if (!f_wanted_speed)
		return(faux)

	if (i_flag_recul)
		OBJ_BankingGeneralSet(-OBJ_SightGet(), OBJ_BankingGet())

	// SI COLLISION MUR =======================================
	if (i_flag_saut)
	{
		f_force_slide_duration = 0.0
	}
	else if (COL_CollideType(COL_C_Wall))
	{
		to_collided_actor	= COL_ObjectGet(COL_C_Wall)
	
		if (! (@to_collided_actor OBJ_FlagsIdentityGet() & (OBJ_C_IdentityFlag_Anims | OBJ_C_IdentityFlag_Dyna)))
		{
			tv_point_A = OBJ_PosGet()
			tv_point_A -= v_way_last_before_dyn_pos
	
			if (MATH_VecDotProduct(tv_point_A, tv_point_A) < 0.01)
			{
				// On n'a pas beaucoup bougé en une trame, on doit être bloqué
				tv_point_A = COL_CollidedPointGet(COL_C_Wall)
				DBG_RenderVector(tv_point_A, COL_NormalGet(COL_C_Wall), color_jaune)

				ti_flag_collision = faux
         
				if ( COL_CollideType(COL_C_Ground) )
				{
					tv_temp = COL_CollidedPointGet(COL_C_Ground)

					if (tv_point_A.z > tv_temp.z)
					{
						ti_flag_collision = vrai
					}
					else
					{
						tv_temp -= COL_ZonePosGet(C_zdm_pied)
						tv_temp.z += COL_ZoneSizeGet(C_zdm_pied)
						
						if (tv_temp.z > f_max_step)
							ti_flag_collision = vrai
					}
				}
				else
				{
					ti_flag_collision = vrai
				}
				
				if (ti_flag_collision)
				{
					tv_point_A -= COL_ZonePosGet(C_zdm_pied)
					tv_point_A.z = 0.0
					MATH_VecSetNormalize(tv_point_A)
			
					if (MATH_VecDotProduct(OBJ_SightGet(), tv_point_A) > 0.0 && MATH_VecDotProduct(tv_point_A, v_way_case_dest - OBJ_PosGet()) > 0.0)
					{
						if (f_force_slide_duration)
							v_slide_dir = MATH_VecBlendRotate(v_slide_dir, tv_point_A, 4.0 * TIME_GetDt())
						else
							v_slide_dir = tv_point_A
					
						f_force_slide_duration = 0.3
					}
				}
			}
		}
	}

	if (f_force_slide_duration)
	{
		i_flag_look = faux	
	
		f_slide_duration += TIME_GetDt()

//		DBG_RenderVector(OBJ_PosGet() + Cv_VerticalVector, v_slide_dir * 3.0, color_cyan)

		tv_point_A.x = v_slide_dir.y
		tv_point_A.y = -v_slide_dir.x
		tv_point_A.z = 0.0
//		tv_point_A *= MATH_FloatSign(MATH_VecDotProduct(tv_point_A, OBJ_SightGet()))
		tv_point_A *= MATH_FloatSign(MATH_VecDotProduct(tv_point_A, v_way_case_dest - OBJ_PosGet()))

		DBG_RenderVector(OBJ_PosGet() + Cv_VerticalVector, tv_point_A * 3.0, color_cyan)
	
		OBJ_BankingGeneralSet(MATH_VecBlendRotate(OBJ_SightGet(), tv_point_A, MATH_FloatMin(f_slide_duration * 12.0, 6.0) * TIME_GetDt() ), v_virtual_banking)
		
		tv_point_A *= tf_wanted_speed
		tv_point_A.z = DYN_SpeedGetVector().z
		DYN_SpeedSetVector(tv_point_A)
	
		if (i_flag_recul)
			OBJ_BankingGeneralSet(-OBJ_SightGet(), OBJ_BankingGet())

		return(vrai)
	}

	i_way_status |= Ci_WAY_STATUS_BEZIER_MOVE
	
	f_slide_duration = 0.0
	tf_link_coef = -1.0

	tf_move_length = tf_wanted_speed * TIME_GetDt()

	if (i_way_status & Ci_WAY_STATUS_GRID_USED && i_way_case_nbr > 1)
	{
		// ON DOIT UTILISER LA GRILLE POUR S'ORIENTER
		f_way_bezier_sight_weight = 0.0
	
		tv_pos = PNJ_Raptor_Bezier_Grid_Move(tf_move_length, tv_sight)
	}
	else
	{
		if ( (i_way_status & Ci_WAY_STATUS_NO_WALL_BETWEEN_ME_AND_POS) != (i_way_last_status & Ci_WAY_STATUS_NO_WALL_BETWEEN_ME_AND_POS) )
		{
			f_way_bezier_sight_weight = 0.0
		}
		else
		{
			tv_point_A = v_way_case_dest
			tv_point_A -= v_way_last_case_dest
			if (MATH_VecDotProduct(tv_point_A, tv_point_A) > 4.0)
				f_way_bezier_sight_weight = 0.0
		}

		// ON A LE RESEAU POUR S'ORIENTER ====================================
		tv_point_A = OBJ_PosGet()
	
		tv_point_D = v_way_case_dest
		if (! i_flag_alien_move)
			tv_point_D.z = tv_point_A.z
	
		tv_AD = tv_point_D - tv_point_A

		tf_virtual_link_length	= MATH_VecNorm(tv_AD)

		if (tf_virtual_link_length > 0.01)
		{
			tv_AD /= tf_virtual_link_length
		}
		else
		{
			i_way_status |= Ci_WAY_STATUS_BEZIER_NO_MOVE
			
//			tv_point_D.z = tv_point_A.z
//			OBJ_PosSet(tv_point_D - (DYN_SpeedGetVector() * TIME_GetDt()))

			OBJ_PosSet(tv_point_D)
			tv_point_A = Cv_NullVector
			tv_point_A.z = DYN_SpeedGetVector().z
			DYN_SpeedSetVector(tv_point_A)

			if (i_flag_recul)
				OBJ_BankingGeneralSet(-OBJ_SightGet(), OBJ_BankingGet())
		
			return(faux)
		}

		// NATURE DE NOTRE DESTINATION ================================================================
		ti_flag_use_next_pos = faux
		ti_flag_jump_link = faux
		ti_return_value = vrai

		if (i_way_status & Ci_WAY_STATUS_OCCLUDER_USED)
		{
			// RIEN
		}
		else if (o_cache_best_wp)
		{
			ti_flag_jump_link = vrai
			ti_flag_use_next_pos = vrai
			tv_next_pos = @o_cache_best_wp OBJ_PosGet()
			if (i_flag_recul)
				tv_next_pos -= @o_cache_best_wp OBJ_SightGet() * 5.0
			else
				tv_next_pos += @o_cache_best_wp OBJ_SightGet() * 5.0
		}
		else if (i_flag_cine_vala_orient && (o_next_wp == o_cine_vala_wp || o_current_wp == o_cine_vala_wp))
		{
			ti_flag_jump_link = vrai
			ti_flag_use_next_pos = vrai
			tv_next_pos = @o_cine_vala_wp OBJ_PosGet()
			tv_next_pos += @o_cine_vala_wp OBJ_SightGet() * 5.0
		}
		else if (i_flag_saut)
		{
			if (o_next_wp)
			{
				ti_flag_jump_link = vrai
				ti_flag_use_next_pos = vrai
				tv_next_pos = @o_next_wp OBJ_PosGet()
			}
			else
			{
				if (i_flag_recul)
					OBJ_BankingGeneralSet(-OBJ_SightGet(), OBJ_BankingGet())

				return(faux)
			}
		}
		else if (i_way_status & Ci_WAY_STATUS_NO_WALL_BETWEEN_ME_AND_POS)
		{
			// RIEN
		}
		else if (o_next_next_wp)
		{	
			if (WAY_LinkCapaGet(n_net, o_next_wp, o_next_next_wp) & Ci_Capa_Link_Jump)
				ti_flag_jump_link = vrai

			ti_flag_use_next_pos = vrai
			tv_next_pos = @o_next_next_wp OBJ_PosGet()
		}
		else if (i_way_status & Ci_WAY_STATUS_REACH_POSITION)
		{
			// Il faudrait utiliser la position que l'on cherche à atteindre...
			ti_flag_use_next_pos = vrai
			tv_next_pos = v_way_position_to_reach
		}

		// GALIMINIUS ================================================================================		
		if (raptor_type == C_ID_Galiminus && ! ti_flag_jump_link)
		{
			tf_dot_product = MATH_VecDotProduct(OBJ_SightGet(), tv_AD)
		
			if (tf_dot_product < -Cf_Cos45)
			{	
				if (raptor_type != C_ID_Tyranosaure && ACT_ActionGet() == Action_Normal_Course && ACT_ActionItemGet())
				{
					PNJ_Raptor_ActionSet(Action_Demi_Tour_Rapide)
				}
				else
				{
					tv_sight = OBJ_SightGet()

					tf_dot_product = MATH_VecDotProduct(MATH_VecNormalize(v_look_axis), OBJ_HorizonGet())
					if (MATH_AbsFloat(tf_dot_product) > Cf_Cos60)
					{
						if (tf_dot_product > 0.0)
							PNJ_Raptor_ActionSet(Action_Demi_Tour_G_deb)
						else
							PNJ_Raptor_ActionSet(Action_Demi_Tour_D_deb)
					}
					else
					{
						tf_dot_product = MATH_VecDotProduct(tv_AD, OBJ_HorizonGet())
						if (tf_dot_product > 0.0)
							PNJ_Raptor_ActionSet(Action_Demi_Tour_G_deb)
						else
							PNJ_Raptor_ActionSet(Action_Demi_Tour_D_deb)
					}
				}

				if (i_flag_recul)
					OBJ_BankingGeneralSet(-OBJ_SightGet(), OBJ_BankingGet())
					
				return(vrai)
			}
			else if (tf_dot_product < 0.0)
			{
				// MON CHEMIN ME DEMANDE UN DEMI-TOUR
				if (MATH_VecDotProduct(OBJ_HorizonGet(), tv_AD) > 0.0)
					tv_sight = MATH_VecBlendRotate(OBJ_SightGet(), OBJ_HorizonGet(), 5.0 * TIME_GetDt())
				else
					tv_sight = MATH_VecBlendRotate(OBJ_SightGet(), -OBJ_HorizonGet(), 5.0 * TIME_GetDt())
			}
			else
			{
				tv_sight = MATH_VecBlendRotate(OBJ_SightGet(), tv_AD, 5.0 * TIME_GetDt())
			}

//			tv_sight -= MATH_VecDotProduct(tv_sight, v_virtual_banking) * v_virtual_banking
			OBJ_SightGeneralSet(tv_sight, v_virtual_banking)

			if (i_flag_recul)
				OBJ_BankingGeneralSet(-OBJ_SightGet(), OBJ_BankingGet())

			return(vrai)
		}
		// GALIMINIUS ================================================================================		
	
		tv_point_B = tv_point_A
		tf_dot_product = MATH_VecDotProduct(OBJ_SightGet(), tv_AD)
	
		if (tf_dot_product < 0.0)
			i_way_status |= Ci_WAY_STATUS_BEZIER_BIG_TURN	
		
		if (tf_dot_product < -Cf_Cos60)
			tv_point_B += tv_AD * (tf_virtual_link_length * 0.33)
		else
			tv_point_B += OBJ_SightGet() * MATH_FloatMin(0.33 * tf_virtual_link_length, 3.0)
	
		tv_point_C = tv_point_D
	
		tv_next_banking = Cv_VerticalVector
		if (o_next_wp)
			tv_next_banking = @o_next_wp OBJ_BankingGet()

		if (ti_flag_use_next_pos)
		{
			tv_tangente = tv_next_pos  - tv_point_D
			if ( ! i_flag_alien_move )
				tv_tangente.z = 0.0

			if (ti_flag_jump_link)
			{
				MATH_VecSetNormalize(tv_tangente)
			}
			else
			{
				tv_tangente = MATH_VecBlendRotate(tv_tangente, -tv_AD, 0.5)
				tv_temp = MATH_VecCrossProduct(tv_tangente, tv_AD)
				tv_temp = MATH_VecCrossProduct(tv_temp, tv_tangente)
				tf_length = MATH_VecNorm(tv_temp)
				if (tf_length)
					tv_tangente = tv_temp / tf_length
				else
					tv_tangente = tv_AD
			}

			tv_point_C -= tv_tangente * MATH_FloatMin(0.33 * tf_virtual_link_length, 3.0)
		}
		else
		{
			tv_point_C -= tv_AD * (tf_virtual_link_length * 0.33)
		}

#ifndef _FINAL_
		if (DBG_Display_Way)
		{
			if ( i_flag_alien_move)
			{
//				tv_point_A -= MATH_VecDotProduct(tv_point_A - @o_current_wp OBJ_PosGet(), v_virtual_banking) * v_virtual_banking
//				tv_point_B -= MATH_VecDotProduct(tv_point_B - @o_current_wp OBJ_PosGet(), v_virtual_banking) * v_virtual_banking
//				tv_point_C -= MATH_VecDotProduct(tv_point_C - @o_current_wp OBJ_PosGet(), v_virtual_banking) * v_virtual_banking
//				tv_point_D -= MATH_VecDotProduct(tv_point_D - @o_current_wp OBJ_PosGet(), v_virtual_banking) * v_virtual_banking
			}
			else 
			{
				tv_point_A.z = OBJ_PosGet().z + 1.0
				tv_point_B.z = tv_point_A.z
				tv_point_C.z = tv_point_A.z
				tv_point_D.z = tv_point_A.z
			}

			MATH_LIB_Bezier_Display(30, tv_point_A, tv_point_B, tv_point_C, tv_point_D)

			if (ti_flag_use_next_pos)
			{
				DBG_RenderVector(tv_point_D, tv_next_pos - tv_point_D, color_blanc)	
				DBG_RenderVector(tv_point_D, tv_point_C - tv_point_D, color_jaune)
			}
		}
#endif
	
		tf_link_coef = MATH_LIB_Bezier_Find_Move_Coef(tf_move_length, 10, tv_point_A, tv_point_B, tv_point_C, tv_point_D)
		tv_pos = MATH_LIB_Bezier_Pos_Get(tf_link_coef, tv_point_A, tv_point_B, tv_point_C, tv_point_D, tv_sight)
		
		if (i_flag_alien_move)
		{
			if (! i_flag_saut )
				v_virtual_banking = MATH_VecBlendRotate(OBJ_BankingGet(), tv_next_banking, tf_link_coef)
		}
	}

//	ti_flag_use_next_pos = STR_CreateText("\h.07\Move => Speed : ", cvector(0.1, 0.2, 0.0), 0.0)
//	STR_AppendFloat(ti_flag_use_next_pos, MATH_VecNorm(tv_point_A), 2)

	tf_norm = MATH_VecNorm(tv_sight)
	if (tf_norm > 0.01)
	{
		tv_sight /= tf_norm
//		DBG_RenderVector(tv_pos, tv_sight, color_vert)
	
		if (f_way_bezier_sight_weight && f_way_bezier_sight_weight < 1.0)
			tv_sight = MATH_VecBlendRotate(OBJ_SightGet(), tv_sight, f_way_bezier_sight_weight)
	
		tf_dot_product = MATH_VecDotProduct(OBJ_SightGet(), tv_sight)

		if (tf_dot_product < -Cf_Cos60)
		{	
			if (ACT_ActionGet() == Action_Normal_Course && ACT_ActionItemGet())
			{
				DYN_SpeedSetVector(v_dyn_speed)
				
				PNJ_Raptor_ActionSet(Action_Demi_Tour_Rapide)
			}
			else
			{
//				if (i_way_occluder_status & Ci_OCCLUDER_START_POS_IS_IN_OCCLUDER && tf_virtual_link_length < 2.0)
//				{
//					i_flag_recul = vrai
//					tv_sight = OBJ_SightGet()
//					OBJ_SightGeneralSet(tv_sight, v_virtual_banking)
//				}
//				else
				{
					DYN_SpeedSetVector(v_dyn_speed)	

					tf_dot_product = MATH_VecDotProduct(MATH_VecNormalize(v_look_axis), OBJ_HorizonGet())
					if (MATH_AbsFloat(tf_dot_product) > Cf_Cos70)
					{
						if (tf_dot_product > 0.0)
							PNJ_Raptor_ActionSet(Action_Demi_Tour_G_deb)
						else
							PNJ_Raptor_ActionSet(Action_Demi_Tour_D_deb)
					}
					else
					{
						tf_dot_product = MATH_VecDotProduct(tv_AD, OBJ_HorizonGet())
						if (tf_dot_product > 0.0)
							PNJ_Raptor_ActionSet(Action_Demi_Tour_G_deb)
						else
							PNJ_Raptor_ActionSet(Action_Demi_Tour_D_deb)
					}
				}
			}

			if (i_flag_recul)
				OBJ_BankingGeneralSet(-OBJ_SightGet(), OBJ_BankingGet())
				
			return(vrai)
		}
		else if (tf_dot_product < 0.0)
		{
			// MON CHEMIN ME DEMANDE UN DEMI-TOUR
			if (MATH_VecDotProduct(OBJ_HorizonGet(), tv_sight) > 0.0)
				tv_sight = MATH_VecBlendRotate(OBJ_SightGet(), OBJ_HorizonGet(), MATH_FloatMax(4.0 * TIME_GetDt(), f_way_bezier_sight_weight))
			else
				tv_sight = MATH_VecBlendRotate(OBJ_SightGet(), -OBJ_HorizonGet(), MATH_FloatMax(4.0 * TIME_GetDt(), f_way_bezier_sight_weight))
			tv_sight = MATH_VecInCone(tv_sight, OBJ_SightGet(), Cf_Pi * TIME_GetDt(), 0)
		}
		else
		{
			tv_sight = MATH_VecBlendRotate(OBJ_SightGet(), tv_sight, MATH_FloatMax(4.0 * TIME_GetDt(), f_way_bezier_sight_weight))
			tv_sight = MATH_VecInCone(tv_sight, OBJ_SightGet(), Cf_Pi * TIME_GetDt(), 0)
		}
	
		OBJ_SightGeneralSet(tv_sight, v_virtual_banking)
	}
	else
	{
		OBJ_SightGeneralSet(OBJ_SightGet(), v_virtual_banking)
	}

	tv_point_A = tv_pos - OBJ_PosGet()

	if ( ! i_flag_alien_move )
		tv_point_A.z = 0.0

	tv_point_A /= TIME_GetDt()
	
	if (tf_link_coef == 1.0)
	{
		// ON EST ARRIVE !!!
		i_way_status |= Ci_WAY_STATUS_BEZIER_NO_MOVE
		ti_return_value = faux

		tv_pos.z = OBJ_PosGet().z
//		OBJ_PosSet(tv_pos - (tv_point_A * TIME_GetDt()))
		OBJ_PosSet(tv_pos)
		tv_point_A = Cv_NullVector
	}

	if (! i_flag_alien_move)
		tv_point_A.z = DYN_SpeedGetVector().z
	DYN_SpeedSetVector(tv_point_A)

	if (i_flag_recul)
		OBJ_BankingGeneralSet(-OBJ_SightGet(), OBJ_BankingGet())
	
	return(ti_return_value)
}

//procedure_local void PNJ_Raptor_Reset_jump_Link()
//{
//	while(i_jump_link_nb)	
//	{
//		i_jump_link_nb--
//
//		WAY_LinkCapaSet(n_net, ao_jump_link[i_jump_link_nb][Ci_jump_link_start_wp], ao_jump_link[i_jump_link_nb][Ci_jump_link_dest_wp], none, Ci_Capa_Link_Reserved)	
//
//		ao_jump_link[i_jump_link_nb][Ci_jump_link_start_wp] = nobody
//		ao_jump_link[i_jump_link_nb][Ci_jump_link_dest_wp] = nobody
//	}
//}
//
//procedure_local void PNJ_Raptor_Flag_jump_Link(object to_current_wp, object to_next_wp)
//{
//	int			ti_i
//	int			ti_next_wp_link_nb
//	int			ti_link_capa	
//
//	object	to_next_next_wp
//
//	ao_jump_link[i_jump_link_nb][Ci_jump_link_start_wp] = to_current_wp
//	ao_jump_link[i_jump_link_nb][Ci_jump_link_dest_wp] = to_next_wp
//
//	i_jump_link_nb++
//
//	WAY_LinkCapaSet(n_net, to_current_wp, to_next_wp, Ci_Capa_Link_Reserved, none)	
//
//	DBG_RenderVector(@to_current_wp OBJ_PosGet(), @to_next_wp OBJ_PosGet() - @to_current_wp OBJ_PosGet(), color_jaune)
//
//	ti_next_wp_link_nb = WAY_GetNumLinks(n_net, to_next_wp)
//	for (ti_i = 0; ti_i < ti_next_wp_link_nb; ti_i++)
//	{
//		to_next_next_wp = WAY_NetNextWP(n_net, to_next_wp, 6, ti_i)
//		
//		if (PNJ_Raptor_Wp_Is_Invalid(to_next_next_wp))
//			continue
//
//		ti_link_capa = WAY_LinkCapaGet(n_net, to_next_wp, to_next_next_wp)
//	
//		if (ti_link_capa & Ci_Capa_Link_Invalid)
//			continue
//			
//		if (ti_link_capa & Ci_Capa_Link_Reserved)
//			continue
//	
//		if (ti_link_capa & Ci_Capa_Link_Jump)
//			PNJ_Raptor_Flag_jump_Link(to_next_wp, to_next_next_wp)
//	}
//}
//
//procedure_local int PNJ_Raptor_Pos_Is_Near_Wall(vector tv_pos, byref vector tv_first_wall_case)
//{
//	int			ti_i
//	int			ti_k	
//	int			ti_grid_capa
//
//	if (i_flag_dont_use_grid)
//		return(faux)
//
//	GRID_CurrentSet(0)
//	tv_first_wall_case = GRID_PosGet(tv_pos)
//
//	tv_first_wall_case.x += 1.0
//	if (PNJ_Raptor_Blocked(tv_first_wall_case, 0))
//		return(vrai)
//
//	tv_first_wall_case.y += 1.0
//	if (PNJ_Raptor_Blocked(tv_first_wall_case, 0))
//		return(vrai)
//
//	tv_first_wall_case.x -= 1.0
//	if (PNJ_Raptor_Blocked(tv_first_wall_case, 0))
//		return(vrai)
//
//	tv_first_wall_case.x -= 1.0
//	if (PNJ_Raptor_Blocked(tv_first_wall_case, 0))
//		return(vrai)
//
//	tv_first_wall_case.y -= 1.0
//	if (PNJ_Raptor_Blocked(tv_first_wall_case, 0))
//		return(vrai)
//
//	tv_first_wall_case.y -= 1.0
//	if (PNJ_Raptor_Blocked(tv_first_wall_case, 0))
//		return(vrai)
//
//	tv_first_wall_case.x += 1.0
//	if (PNJ_Raptor_Blocked(tv_first_wall_case, 0))
//		return(vrai)
//
//	tv_first_wall_case.x += 1.0
//	if (PNJ_Raptor_Blocked(tv_first_wall_case, 0))
//		return(vrai)
//
//	return(faux)	
//}

procedure_local void PNJ_Raptor_Hyper_Tag_On()
{
	int		ti_index
	int		ti_i	
	int		ti_k

	if (raptor_type != C_ID_Tyranosaure)
		return

	ti_index = ARR_ObjSearch(& @get_global o_hyper_tag_actor[0], 3, OBJ_Me())

	for (ti_i = 0; ti_i < 25; ti_i++)
	{
		for (ti_k = 0; ti_k < @get_global ai_hyper_tag_case_nb[ti_index][ti_i]; ti_k++)
			GRID_CapaSet(@get_global av_hyper_tag_case[ti_index][ti_i][ti_k], GRID_CapaGet(@get_global av_hyper_tag_case[ti_index][ti_i][ti_k]) | tag_grid_terraindyn_1)
	}
}

procedure_local void PNJ_Raptor_Hyper_Tag_Off()
{
	int		ti_index
	int		ti_i	
	int		ti_k

	if (raptor_type != C_ID_Tyranosaure)
		return

	ti_index = ARR_ObjSearch(& @get_global o_hyper_tag_actor[0], 3, OBJ_Me())

	for (ti_i = 0; ti_i < 25; ti_i++)
	{
		for (ti_k = 0; ti_k < @get_global ai_hyper_tag_case_nb[ti_index][ti_i]; ti_k++)
			GRID_CapaSet(@get_global av_hyper_tag_case[ti_index][ti_i][ti_k], GRID_CapaGet(@get_global av_hyper_tag_case[ti_index][ti_i][ti_k]) & ~tag_grid_terraindyn_1)
	}
}

procedure_local void PNJ_Raptor_Hyper_Tag_Compute(vector tv_pos, int ti_size)
{
	int			ti_i
	int			ti_k	
	int			ti_index
	int			ti_my_index
	int			ti_flag_ok
	int			ti_double_size

	vector	tv_tested_pos
	vector	tv_start_pos
	vector	tv_ref_pos

	if (raptor_type != C_ID_Tyranosaure)
		return

	if (i_flag_dont_use_grid)
		return

	tv_pos.x = MATH_FloatRound(tv_pos.x, ti_size << 1)
	tv_pos.y = MATH_FloatRound(tv_pos.y, ti_size << 1)
	tv_pos = GRID_PosGet(tv_pos)

	// RECHERCHE DE MON INDEX DANS LE GLOBAL
	ti_my_index = ARR_ObjSearch(& @get_global o_hyper_tag_actor[0], 3, OBJ_Me())
	if (ti_my_index == -1)
	{
		ti_my_index = ARR_ObjSearch(& @get_global o_hyper_tag_actor[0], 3, nobody)
		@get_global o_hyper_tag_actor[ti_my_index] = OBJ_Me()
	}

	ti_double_size = ti_size << 1

	// ETABLISSEMENT DE LA LISTE DES BLOCS
	if (@get_global av_hyper_tag_ref_pos[ti_my_index][12] != tv_pos)
	{
		@get_global i_hyper_tag_case_sector[ti_my_index] = 12
	
		ti_index = 0
		for (ti_i = -2; ti_i <= 2; ti_i++)
		{
			for (ti_k = -2; ti_k <= 2; ti_k++)
			{
				@get_global av_hyper_tag_ref_pos[ti_my_index][ti_index].x = tv_pos.x + (ti_i * ti_double_size)
				@get_global av_hyper_tag_ref_pos[ti_my_index][ti_index].y = tv_pos.y - (ti_k * ti_double_size)
				ti_index++
			}
		}
	}

	// ON RECUPERE LA POSITION DU CENTRE DU BLOC A EVALUER
	tv_pos = @get_global av_hyper_tag_ref_pos[ti_my_index][@get_global i_hyper_tag_case_sector[ti_my_index]]

	// EST-CE QUE CE BLOC A DEJA ETE EVALUE ?
	ti_index = -1
	for (ti_k = 0; ti_k < 25; ti_k++)
	{
		tv_start_pos = @get_global av_hyper_tag_start_pos[ti_my_index][ti_k] 
	
		if (tv_start_pos.x != tv_pos.x)
			continue

		if (tv_start_pos.y != tv_pos.y)
			continue	

		ti_index = ti_k
		break

//		if (MATH_VecNullToler(@get_global av_hyper_tag_start_pos[ti_my_index][ti_k] - tv_pos, 0.1))
//		{
//			ti_index = ti_k
//			break
//		}
	}
	
	if (ti_index == -1)
	{
		// SI LE BLOC COURANT N'A JAMAIS ETE EVALUE, JE CHERCHE UN INDEX D'UN BLOC INVALID
		for (ti_i = 0; ti_i < 25; ti_i++)
		{
			ti_index = -1
			for (ti_k = 0; ti_k < 25; ti_k++)
			{
				tv_start_pos = @get_global av_hyper_tag_start_pos[ti_my_index][ti_i]	
				tv_ref_pos = @get_global av_hyper_tag_ref_pos[ti_my_index][ti_k]
			
				if (tv_start_pos.x != tv_ref_pos.x)
					continue
		
				if (tv_start_pos.y != tv_ref_pos.y)
					continue	

				ti_index = ti_k
				break

//				if (MATH_VecNullToler(@get_global av_hyper_tag_ref_pos[ti_my_index][ti_k] - @get_global av_hyper_tag_start_pos[ti_my_index][ti_i], 0.1))
//				{
//					ti_index = ti_k
//					break
//				}
			}
			
			if (ti_index == -1)
			{
				ti_index = ti_i
				break
			}
		}
	}
	else if (! @get_global ai_hyper_tag_recompute_bloc[ti_my_index][ti_index] )
	{
		// CE BLOC A ETE EVALUE
		ti_index = -1
	}
	
	if (ti_index != -1)
	{
		@get_global av_hyper_tag_start_pos[ti_my_index][ti_index] = tv_pos
		@get_global ai_hyper_tag_case_nb[ti_my_index][ti_index] = 0
		@get_global ai_hyper_tag_recompute_bloc[ti_my_index][ti_index] = faux
	
		for (ti_i = 0; ti_i <= ti_double_size + 1; ti_i++)
		{
			for (ti_k = 0; ti_k <= ti_double_size + 1; ti_k++)
			{
				tv_tested_pos = tv_pos
				tv_tested_pos.x += ti_i - ti_size - 1
				tv_tested_pos.y += ti_k - ti_size - 1

				@get_global ai_hyper_tag[ti_i][ti_k] = PNJ_Raptor_Blocked(tv_tested_pos, 0)
			}
		}
	
		for (ti_i = 1; ti_i <= ti_double_size; ti_i++)
		{
			for (ti_k = 1; ti_k <= ti_double_size; ti_k++)
			{
				if (@get_global ai_hyper_tag[ti_i][ti_k])
					continue
	
				ti_flag_ok = faux
				
				if (@get_global ai_hyper_tag[ti_i - 1][ti_k - 1])
					ti_flag_ok = vrai
				else if (@get_global ai_hyper_tag[ti_i][ti_k - 1])
					ti_flag_ok = vrai
				else if (@get_global ai_hyper_tag[ti_i + 1][ti_k - 1])
					ti_flag_ok = vrai
				else if (@get_global ai_hyper_tag[ti_i - 1][ti_k])
					ti_flag_ok = vrai
				else if (@get_global ai_hyper_tag[ti_i + 1][ti_k])
					ti_flag_ok = vrai
				else if (@get_global ai_hyper_tag[ti_i - 1][ti_k + 1])
					ti_flag_ok = vrai
				else if (@get_global ai_hyper_tag[ti_i][ti_k + 1])
					ti_flag_ok = vrai
				else if (@get_global ai_hyper_tag[ti_i + 1][ti_k + 1])
					ti_flag_ok = vrai

				if (ti_flag_ok)
				{
					tv_tested_pos = tv_pos
					tv_tested_pos.x += ti_i - ti_size - 1
					tv_tested_pos.y += ti_k - ti_size - 1
				
					@get_global av_hyper_tag_case[ti_my_index][ti_index][@get_global ai_hyper_tag_case_nb[ti_my_index][ti_index]] = tv_tested_pos
					@get_global ai_hyper_tag_case_nb[ti_my_index][ti_index]++
				
#ifndef _FINAL_
					if (DBG_Display_Way)
					{
						if (o_main_actor)
							tv_tested_pos.z = @o_main_actor OBJ_PosGet().z
						DBG_RenderVector(tv_tested_pos, Cv_VerticalVector * 10.0, color_rouge)	
					}
#endif
				}
			}
		}
	}

//	PNJ_Raptor_Hyper_Tag_On()
	@get_global i_hyper_tag_case_sector[ti_my_index] = MATH_Modulo(@get_global i_hyper_tag_case_sector[ti_my_index] + 1, 25)
//	PNJ_Raptor_Hyper_Tag_Off()

	return
} 

procedure_local void PNJ_Raptor_Ultra_TagOn()
{
	GRID_LIB_Ultra_Tag_On(raptor_type, &ao_ultra_tag[0], i_ultra_tag_nb)
}

procedure_local void PNJ_Raptor_Local_Res()
{
	int				ti_i
	
	float			tf_dist	
	float			tf_sign

	vector		tv_me_to_dest
	vector		tv_pseudo_sight

	tv_me_to_dest = v_way_case_dest
	tv_me_to_dest -= OBJ_PosGet()
	tv_me_to_dest.z = 0.0
	tf_dist = MATH_VecNorm(tv_me_to_dest)

	if (i_way_status & Ci_WAY_STATUS_DESTROY_OCCLUDER)
		return

	if (i_way_status & Ci_WAY_STATUS_OCCLUDER_USED && tf_dist < 4.0)
		return

	if ( GRID_LIB_IsReachableFrom(OBJ_PosGet(), v_way_case_dest, 0b0, 4.0, faux, 0) )
	{
		if (i_way_status & Ci_WAY_STATUS_REACH_POSITION)
			i_way_status |= Ci_WAY_STATUS_NO_WALL_BETWEEN_ME_AND_POS

//		DBG_RenderVector(OBJ_PosGet() + Cv_VerticalVector, v_way_case_dest - OBJ_PosGet(), color_vert)
	}
	else
	{
		i_way_status |= Ci_WAY_STATUS_LOCAL_RES_USED
	
		tv_me_to_dest /= tf_dist

		tv_pseudo_sight = tv_me_to_dest

		tv_me_to_dest = OBJ_SightGet()
		MATH_VecSetHorzNormalize(tv_me_to_dest)	

		if (MATH_AbsFloat(tv_me_to_dest.x) > MATH_AbsFloat(tv_me_to_dest.y))
		{
			tv_me_to_dest.x = MATH_FloatSign(tv_me_to_dest.x)
			tv_me_to_dest.y = 0.0
		}
		else
		{
			tv_me_to_dest.x = 0.0
			tv_me_to_dest.y = MATH_FloatSign(tv_me_to_dest.y)
		}

		tf_sign = MATH_FloatSign(MATH_VecCrossProduct(tv_pseudo_sight, tv_me_to_dest).z)
		if (!tf_sign)
			tf_sign = 1.0

		for (ti_i = 0; ti_i < 4; ti_i++)
		{
			
#ifndef _FINAL_
			if (DBG_Display_Way)
			{
				if (!ti_i)
					DBG_RenderVector(OBJ_PosGet() + Cv_VerticalVector, tv_me_to_dest * 4.0, color_rouge)
				else if (ti_i == 1)
					DBG_RenderVector(OBJ_PosGet() + Cv_VerticalVector, tv_me_to_dest * 4.0, color_vert)
				else if (ti_i == 2)
					DBG_RenderVector(OBJ_PosGet() + Cv_VerticalVector, tv_me_to_dest * 4.0, color_bleu)
				else
					DBG_RenderVector(OBJ_PosGet() + Cv_VerticalVector, tv_me_to_dest * 4.0, color_jaune)
			}
#endif

			if (GRID_LIB_IsReachableFrom(OBJ_PosGet(), OBJ_PosGet() + (tv_me_to_dest * 4.0), 0b0, 4.0, faux, 0))
				break

			if (!ti_i || ti_i == 2)
			{
				tv_me_to_dest.z = tv_me_to_dest.x
				tv_me_to_dest.x = tf_sign * tv_me_to_dest.y
				tv_me_to_dest.y = -tf_sign * tv_me_to_dest.z
				tv_me_to_dest.z = 0.0
			}
			else
			{
				tv_me_to_dest *= -1.0
			}
		}
		
		tv_me_to_dest *= 4.0
		tv_me_to_dest += OBJ_PosGet()

		v_way_case_dest = tv_me_to_dest
	}
}

procedure_local int PNJ_Raptor_Get_Nearest_Occluder_Ode()
{
	int		ti_i
	int		ti_best_index	

	float	tf_dist
	float	tf_nearest_ode_dist
	float	tf_dot_product
	float	tf_best_dot

	object	to_object
	object	to_occluder	

	vector	tv_me_to_ode	

	o_destructible_occluder = nobody
	o_destructible_occluder_ode = nobody

	if (@get_global WAY_LIB_used_occluder_index >= @get_list_manager i_occluder_nb)
		return(faux)

	if (i_perceived_best_actor_index == -1)
		return(faux)
	 
	switch(raptor_type)
	{
		case C_ID_Galiminus :
			return(faux)
			
		case C_ID_Raptor :
			if (raptor_category != Ci_Raptor_Heavy)
				return(faux)
	}
					
	to_occluder  = @get_list_manager ao_occluder[@get_global WAY_LIB_used_occluder_index]
	if ( ! @to_occluder AI_IsModel("Interactive_Objects/ODE_Occluder") )
		return(faux)

	// BON, ON EST BLOQUE PAR UN OCCLUDER DESTRUCTIBLE
	if ( ! (@"Interactive_Objects/ODE_Occluder" to_occluder ode_a_activer[0]) )
		return(faux)

	// ON DIMINUE LA DUREE DE VIE DE CET OCCLUDER
	if (af_perceived_dist[i_perceived_best_actor_index] < 20.0)
		@"Interactive_Objects/ODE_Occluder" to_occluder f_occluder_life_time -= TIME_GetDt()

	// IL A ENCORE DE LA VIE
	if (@"Interactive_Objects/ODE_Occluder" to_occluder f_occluder_life_time > 0.0)
		return(faux)

	// L'ACTEUR N'EST PAS A L'ECRAN
	if (f_on_screen_duration < 0.3)
		return(faux)	

	ti_best_index = -1

	tf_nearest_ode_dist = @"Interactive_Objects/ODE_Occluder" to_occluder distance_destruction
	tf_nearest_ode_dist *= tf_nearest_ode_dist

	tf_best_dot = 0.0

	for (ti_i = 0; ti_i < 10; ti_i++)
	{
		to_object = @"Interactive_Objects/ODE_Occluder" to_occluder ode_a_activer[ti_i]
		
		if ( ! to_object )
			break
	
		tf_dist = OBJ_SqrDist(to_object)
		if (tf_dist > tf_nearest_ode_dist)
			continue

		tv_me_to_ode = @to_object OBJ_PosGet()
		tv_me_to_ode -= OBJ_PosGet()
		MATH_VecSetHorzNormalize(tv_me_to_ode)
		
		tf_dot_product = MATH_VecDotProduct(OBJ_SightGet(), tv_me_to_ode)
		if (tf_dot_product > tf_best_dot)
		{
			ti_best_index = ti_i
			tf_best_dot = tf_dot_product
		}
	}

	if (ti_best_index != -1)
	{
		v_way_case_dest = v_way_destpos

		o_destructible_occluder = to_occluder
		o_destructible_occluder_ode = @"Interactive_Objects/ODE_Occluder" to_occluder ode_a_activer[ti_best_index]

		DBG_RenderVector(@o_destructible_occluder OBJ_PosGet(), Cv_VerticalVector * 1000.0, color_rouge)
		DBG_RenderVector(@o_destructible_occluder_ode OBJ_PosGet(), Cv_VerticalVector * 1000.0, color_jaune)

		@"Interactive_Objects/ODE_Occluder" o_destructible_occluder ode_a_activer[ti_best_index] = @"Interactive_Objects/ODE_Occluder" o_destructible_occluder ode_a_activer[ti_i - 1]
		@"Interactive_Objects/ODE_Occluder" o_destructible_occluder ode_a_activer[ti_i - 1] = nobody
		
		return(vrai)
	}

	return(faux)
}

procedure_local void PNJ_Raptor_Test_Occluder()
{
	int			ti_i
	int			ti_index
	int			ti_flag_can_reach_pos

	object	to_occluder	
	object	to_nearest_wp

	vector	tv_temp

	network	tn_occluder_jump_net

	if (!i_flag_occluder_alllowed)
		return

	o_occluder_jump = nobody
	o_destructible_occluder = nobody
	n_occluder_jump_net = nonet

	i_way_occluder_status = WAY_LIB_Test_Occluder(OBJ_PosGet() + Cv_VerticalVector, OBJ_SightGet(), COL_ZoneSizeGet(C_zdm_pied) + 0.1, v_way_destpos, i_occluder_territory, v_way_case_dest, o_occluder_jump, & @get_global ao_bronto_leg[0], & @get_global af_bronto_leg_size[0], @get_global i_bronto_leg_nb, C_Occl_Type_All)

	switch(i_way_computation_mode)
	{
		case Ci_WAY_MODE_GRAB :
			break
			
		default:

			// UN OCCLUDER NOUS GENE, PEUT-ETRE FAUT-IL SAUTER PAR DESSUS ?
			if (i_way_occluder_status & (Ci_OCCLUDER_COLLISION | Ci_OCCLUDER_START_POS_IS_IN_OCCLUDER))
			{
				i_way_status |= Ci_WAY_STATUS_OCCLUDER_USED
		
				if (PNJ_Raptor_Get_Nearest_Occluder_Ode())
				{
					i_way_status |= Ci_WAY_STATUS_DESTROY_OCCLUDER
				}
				else
				{
					if (@get_global WAY_LIB_used_occluder_index < @get_list_manager i_occluder_nb)
						to_occluder  = @get_list_manager ao_occluder[@get_global WAY_LIB_used_occluder_index]
					else
						to_occluder = nobody
			
					if (to_occluder && @to_occluder AI_IsModel("Interactive_Objects/Occluder_Jump"))
					{
						o_occluder_jump = to_occluder	
					
						for (ti_i = 0; ti_i < 10; ti_i++)
						{
							to_occluder = @"Interactive_Objects/Occluder_Jump" o_occluder_jump jump_pos[ti_i]
							if (!to_occluder)
								break
			
							if (! (@to_occluder OBJ_CapaTest(OBJ_Capa_0)) )
							{
								tn_occluder_jump_net = @to_occluder WAY_NetOfObj()
								if (tn_occluder_jump_net)
									to_nearest_wp = WAY_WPNearestOfOBJ(tn_occluder_jump_net, all, none, Ci_Filter_IdentityFlag)
								else
									to_nearest_wp = to_occluder
		
								if (OBJ_SqrDistHorz(to_nearest_wp) < 9.0)
								{
									i_way_status |= Ci_WAY_STATUS_OCCLUDER_JUMP
									
									v_jump_impulsion_banking = OBJ_BankingGet()
		
									if (tn_occluder_jump_net)
										o_current_wp = to_nearest_wp
						
									o_occluder_jump_wp = to_occluder	
									n_occluder_jump_net = tn_occluder_jump_net
						
									@o_occluder_jump_wp OBJ_CapaSet(OBJ_Capa_0, none)
									v_jump_dest_pos = @o_occluder_jump_wp OBJ_PosGet()
									v_way_case_dest	 = v_jump_dest_pos
									v_jump_reception_banking = @o_occluder_jump_wp OBJ_BankingGet()
									
									break
								}
							}
						}
					}
				}
			}
			
			if (i_way_occluder_status & Ci_OCCLUDER_DEST_POS_IS_IN_OCCLUDER && i_way_status & Ci_WAY_STATUS_REACH_POSITION)
			{
				ti_flag_can_reach_pos = vrai
				
				if (@get_global WAY_LIB_used_occluder_index != -1 && @get_global WAY_LIB_used_occluder_index != @get_global WAY_LIB_dest_pos_occluder_index)
					ti_flag_can_reach_pos = faux
				else if (@get_global WAY_LIB_dest_pos_occluder_index >= @get_list_manager i_occluder_nb)
					ti_flag_can_reach_pos = faux
				else if (i_way_status & Ci_WAY_STATUS_DESTROY_OCCLUDER)
					ti_flag_can_reach_pos = faux
				else if (i_way_status &Ci_WAY_STATUS_OCCLUDER_JUMP)
					ti_flag_can_reach_pos = faux
			}
			else
			{
				ti_flag_can_reach_pos = faux
			}
	
			if (ti_flag_can_reach_pos)
			{
				// Ma position dest est dans cet occluder, je le zape si j'ai une ligne droite
//				tv_temp = v_way_destpos - OBJ_PosGet()
//				tv_temp.z = 0.0
//				if (MATH_VecDotProduct(tv_temp, tv_temp) < 100.0 && PNJ_Raptor_Test_Grid_Line(Ci_GRID_LINE_ME_TO_DEST_POS, OBJ_PosGet(), v_way_destpos, 0b0, -1.0, faux, i_grid_width))
//					v_way_case_dest = v_way_destpos

				if (PNJ_Raptor_Test_Grid_Line(Ci_GRID_LINE_ME_TO_DEST_POS, OBJ_PosGet(), v_way_destpos, 0b0, -1.0, faux, i_grid_width))
				{
					// On a un accès en ligne droite
					v_way_case_dest = v_way_destpos
				}
				else
				{
					// On va contourner
					to_occluder  = @get_list_manager ao_occluder[@get_global WAY_LIB_dest_pos_occluder_index]
					v_way_destpos = @to_occluder  OBJ_PosGet() - OBJ_PosGet()
					v_way_destpos.z = 0.0
					v_way_destpos *= 1000.0
					v_way_destpos += OBJ_PosGet()
					WAY_LIB_Test_Occluder(OBJ_PosGet() + Cv_VerticalVector, OBJ_SightGet(), COL_ZoneSizeGet(C_zdm_pied) + 0.1, v_way_destpos, i_occluder_territory, v_way_case_dest, o_occluder_jump, & @get_global ao_bronto_leg[0], & @get_global af_bronto_leg_size[0], @get_global i_bronto_leg_nb, C_Occl_Type_All)
				}
			}
	}
		
	PNJ_Raptor_Local_Res()

	if (i_way_status & Ci_WAY_STATUS_LOCAL_RES_USED)	
	{
		v_way_destpos = v_way_case_dest
		i_way_occluder_status |= WAY_LIB_Test_Occluder(OBJ_PosGet() + Cv_VerticalVector, OBJ_SightGet(), COL_ZoneSizeGet(C_zdm_pied) + 0.1, v_way_destpos, i_occluder_territory, v_way_case_dest, o_occluder_jump, & @get_global ao_bronto_leg[0], & @get_global af_bronto_leg_size[0], @get_global i_bronto_leg_nb, C_Occl_Type_All)
	}
}

procedure_local vector PNJ_Raptor_Circumvent_Wp_If_Bad_Sight(vector tv_start_pos, vector tv_dest_pos, vector tv_sight, vector tv_horizon, float tf_size)
{
	float		tf_dist	
	float		tf_dot_product

	vector	tv_pos
	vector 	tv_me_to_cache
	
	// POUR FORCER UN CONTOURNEMENT QUI FAVORISE LA BONNE ORIENTATION ===================================================				
#ifndef _FINAL_
	tv_pos = tv_dest_pos
	tv_pos.z = OBJ_PosGet().z + 1.0
	DBG_RenderCircle(tv_pos, tf_size, Cv_VerticalVector, color_rouge)
#endif

	tv_me_to_cache = tv_dest_pos
	tv_me_to_cache -= OBJ_PosGet()
	tv_me_to_cache.z = 0.0
	
	tf_dist = MATH_VecNorm(tv_me_to_cache)	
	
	if (MATH_FloatNullToler(tf_dist, 0.05))
	{
		tf_dot_product = 1.0
	}
	else
	{
		tv_me_to_cache /= tf_dist
		tf_dot_product = MATH_VecDotProduct(tv_sight, tv_me_to_cache)
	}

	if (tf_dot_product < Cf_Cos70)
	{
		i_way_status |= Ci_WAY_STATUS_OCCLUDER_USED
		tv_dest_pos = WAY_LIB_Get_Sphere_Pos(OBJ_PosGet(), tv_dest_pos + tv_me_to_cache, OBJ_SightGet(), 0.0, tv_dest_pos, tf_size, -tv_sight, -tv_horizon, vrai)
		DBG_RenderVector(OBJ_PosGet() + Cv_VerticalVector, tv_dest_pos - OBJ_PosGet(), color_rouge)
	}
	
	return(tv_dest_pos)
}



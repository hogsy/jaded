#include "PNJ_Raptor_defines.var"

int			ti_index
int			ti_state
int			ti_indice_gmat

float		tf_norm

vector	tv_pos
vector	tv_new_banking
vector	tv_temp

#ifdef SHOW_RASTER
DBG_StopRaster(8)
#endif

//PNJ_Raptor_Check_Body()

if (i_frame_nb < 10 && ! ACT_ActionIsTransition())
	PNJ_Raptor_ActionSet(ACT_ActionGet() | Ci_ActionSet_Force_SameAction | Ci_ActionSet_Force_No_Blend)

if (raptor_type == C_ID_Galiminus)
{
	switch(i_etat_courant)
	{
		case ETAT_A_TERRE :
		case ETAT_BURN :
		case ETAT_MORT :
		case ETAT_FADE :
			break
			
		default:
			tv_new_banking = MATH_VecCrossProduct(OBJ_SightGet(), Cv_VerticalVector)
			MATH_VecSetNormalize(tv_new_banking)
			tv_new_banking = v_virtual_banking - (MATH_VecDotProduct(v_virtual_banking, tv_new_banking) * tv_new_banking)
			OBJ_BankingGeneralSet(OBJ_SightGet(), tv_new_banking)
	}
}

switch(i_etat_courant)
{
	case ETAT_PAF_SLIDE :
	case ETAT_PAF_FALL :
	case ETAT_PAF_FLY :
	case ETAT_A_TERRE :
	case ETAT_BURN :
	case ETAT_MORT :
	case ETAT_FADE :
		break
	
	default:
		if ( ! i_flag_stalk && ! i_flag_saut && OBJ_FlagsIdentityTest(OBJ_C_IdentityFlag_Dyna) )
		{
			if (f_ground_col_time > 0.5)
				AI_TrackChange(Ci_Track_Etat, "PNJ_Raptor_ETAT_FALL")
		}
}

switch(i_etat_courant)
{
	case ETAT_ATTAQUE_CACHE :
	case ETAT_PAF_SLIDE :
	case ETAT_PAF_FALL :
	case ETAT_PAF_FLY :
	case ETAT_A_TERRE :
	case ETAT_BURN :
	case ETAT_MORT :
	case ETAT_FADE :
	case ETAT_MORD :
	case ETAT_DEVORE :
		break
	
	default:
		if ( ! i_flag_stalk && ! i_flag_saut && OBJ_FlagsIdentityTest(OBJ_C_IdentityFlag_Dyna) )
		{
			if (f_neck_col_duration)
			{
				tv_temp = @o_ventre OBJ_PosGet()
				tv_temp -= v_neck_col_pos
				tv_temp.z = 0.0
				tf_norm = MATH_VecNorm(tv_temp)
				if (tf_norm)
					tv_temp /= tf_norm
				else
					tv_temp = -OBJ_SightGet()
					
				if (tf_norm < 3.0 * OBJ_ZoomGet())
				{
					tv_temp *= 20.0 * DYN_FrictionGet()
					tv_temp += DYN_TractionVectorGet()
					DYN_TractionSet(tv_temp)
				}
			}
		}
}


v_way_last_before_dyn_pos = OBJ_PosGet()

//PNJ_Raptor_UncollideCheck()

#ifdef SHOW_RASTER
DBG_StartRaster(6, "Collision Molles")
#endif
PNJ_Raptor_Soft_Collision()
//PNJ_Raptor_Main_Soft_Collision()
PNJ_Raptor_Bronto_Soft_Collision()
#ifdef SHOW_RASTER
DBG_StopRaster(6)
#endif

switch(raptor_type)
{
	case C_ID_Raptor :
	case C_ID_Galiminus :

		if (COL_CrossableGet() & Gmat_KK_Face_de_Bord)
		{
			if (TIME_Elapsed(f_time_last_shot_paf, 1.0) && COL_GMatReportGet(Gmat_KK_Face_de_Bord) == -1)
				COL_CrossableSet(none, Gmat_KK_Face_de_Bord)
		}
		else if (! TIME_Elapsed(f_time_last_shot_paf, 1.0) )
		{
			COL_CrossableSet(Gmat_KK_Face_de_Bord, none)
		}
	
		break
}

if (i_flag_dont_use_grid)
{
	macro_change_tag_size(Cv_NullVector, Cv_NullVector)
}
else
{
	switch(raptor_type)
	{
		case C_ID_Tyranosaure :
			macro_change_tag_size(cvector(-1.0, -2.0, 0.0), cvector(1.0, 2.0, 0.0))
			break

		case C_ID_Raptor :
			macro_change_tag_size(cvector(-0.5, -1.0, 0.0), cvector(0.5, 1.0, 0.0))
			break
			
		case C_ID_Galiminus :
			macro_change_tag_size(cvector(-0.25, -0.25, 0.0), cvector(0.5, 0.5, 0.0))
			break
	}
}

AI_Execute("PNJ_Raptor_callback_tagon")

switch(i_etat_courant)
{
	case ETAT_BURN :
	case ETAT_MORT:
	case ETAT_FADE :
		ti_state = C_EVENT_EnemyState_Die
		break

	case ETAT_FALL :
	case ETAT_PAF_SLIDE :
	case ETAT_PAF_FALL :
	case ETAT_PAF_FLY :
	case ETAT_A_TERRE :
		ti_state = C_EVENT_EnemyState_Neutral
		break

//	case ETAT_CINE_VALA :
	case ETAT_CINE_FIGHT :
		ti_state = C_EVENT_EnemyState_Fight
		break

	case ETAT_SEARCH :	
	case ETAT_FIGHT :
	case ETAT_HESITE :
	case ETAT_MORD :
	case ETAT_COODBOOL :
	case ETAT_GRAB :
	case ETAT_LANCE :
	case ETAT_ATTAQUE_CACHE :
	case ETAT_RODE :
		if (i_perceived_best_actor_index != -1 && IsThis_ID_Humain(ai_perceived_ID[i_perceived_best_actor_index]) )
			ti_state = C_EVENT_EnemyState_Fight
		else
			ti_state = C_EVENT_EnemyState_Wait
		break

	case ETAT_ATTENTE :
	case ETAT_DEVORE :
		ti_state = C_EVENT_EnemyState_Wait
		break

	case ETAT_VALA :
		if (i_flag_outside_territory)
			ti_state = C_EVENT_EnemyState_Flee
		else
			ti_state = C_EVENT_EnemyState_Fight
		break

	default:
		ti_state = C_EVENT_EnemyState_Quiet
//		DBG_Error("Pas bien !!!")
}

if (ti_state != -1 && ! i_flag_ennemi_fake)
	EVENT_AddEventEnemy(raptor_type, OBJ_Me(), ti_state)

#ifdef SHOW_RASTER
DBG_StopRaster(0)
#endif
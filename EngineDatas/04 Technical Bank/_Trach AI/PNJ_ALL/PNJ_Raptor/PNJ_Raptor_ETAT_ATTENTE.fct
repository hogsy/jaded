#include "PNJ_Raptor_defines.var"

float		tf_delay_since_last_update

int			ti_i

vector	tv_offset
vector	tv_temp

// SORTIE ETAT ===================================================================
if (i_sort_etat)
{
	i_sort_etat = faux

	return
}

// INITIALISATION ETAT ==============================================================
if (i_etat_courant != ETAT_ATTENTE)
{
	i_dernier_etat = i_etat_courant
	i_etat_courant = ETAT_ATTENTE

	if (fct_last_etat)
	{
		i_sort_etat = vrai
		AI_Execute(fct_last_etat)
	}

	fct_last_etat = AI_TrackCurGet()
	fct_main_etat = AI_TrackCurGet()

	f_time_start_etat = 0.0
}
else
{
	f_time_start_etat += TIME_GetDt()
}

// ANALYSE =======================================================================================================
if (!i_flag_activation && AI_TriggerIsValid(trigger_activation))
{
	if (call_trigger(trigger_activation))
	{
		i_flag_activation = vrai
	}
	else
	{
		i_frame_nb = 2
		f_time_start_etat = 0.0
		returntrack
	}
}

i_flag_activation = vrai

AI_Execute("PNJ_Raptor_exec_check_msg")

AI_Execute("PNJ_Raptor_exec_check_vision")

AI_Execute("PNJ_Raptor_exec_check_sound")

AI_Execute("PNJ_Raptor_exec_check_collision")

AI_Execute("PNJ_Raptor_exec_check_paf")
if (i_flag_paf_fly)
	macro_change_etat("PNJ_Raptor_ETAT_PAF_FLY")
if (i_flag_paf_fall)
	macro_change_etat("PNJ_Raptor_ETAT_PAF_FALL")
if (i_flag_paf_repousse || i_flag_paf_slide || !f_lifecur)
	macro_change_etat("PNJ_Raptor_ETAT_PAF_SLIDE")

//CB 10/09
//if (PNJ_Raptor_Check_Death())
//	macro_change_etat("PNJ_Raptor_ETAT_CINE_ROAR")

if (i_flag_force_fuite)
	macro_change_etat("PNJ_Raptor_ETAT_VALA")
if (! i_flag_in_my_territory)
	macro_change_etat("PNJ_Raptor_ETAT_VALA")

AI_Execute("PNJ_Raptor_exec_check_shoot")

AI_Execute("PNJ_Raptor_exec_check_client")

AI_Execute("PNJ_Raptor_exec_check_vala")
if (MSG_GlobalIsValid(mid_vala))
	macro_change_etat("PNJ_Raptor_ETAT_VALA")

AI_Execute("PNJ_Raptor_exec_update_best_interest")

AI_Execute("PNJ_Raptor_exec_check_requin")

if (i_flag_change_target)
	macro_change_etat("PNJ_Raptor_ETAT_HESITE")

if (MSG_GlobalIsValid(mid_best_interet))
{
	i_flag_look = vrai
	i_flag_look_best_interet = vrai	

	if (f_force_requin_mode_duration)
	{
		macro_change_etat("PNJ_Raptor_ETAT_FIGHT")
	}
	else if (i_perceived_best_actor_index == -1)
	{
		macro_change_etat("PNJ_Raptor_ETAT_SEARCH")
	}
	else if (ai_perceived_status[i_perceived_best_actor_index] & Ci_PERCEIVED_IN_HIDING_PLACE)
	{
		macro_change_etat("PNJ_Raptor_ETAT_ATTAQUE_CACHE")
	}
	else if (ai_perceived_status[i_perceived_best_actor_index] & Ci_PERCEIVED_ALREADY_CHECKED)
	{
		if (raptor_type == C_ID_Raptor && f_rode_duration < f_rode_duration_limit)
			macro_change_etat("PNJ_Raptor_ETAT_HESITE")
		else
			macro_change_etat("PNJ_Raptor_ETAT_VALA")
	}
	else
	{
		if (ai_perceived_accessible[i_perceived_best_actor_index] && ai_perceived_status[i_perceived_best_actor_index] & Ci_PERCEIVED_IS_DEAD)
			macro_change_etat("PNJ_Raptor_ETAT_DEVORE")
		
		if ((TIME_Get() - EVENT_InteretSeenTimeGet(mid_best_interet)) <= Cf_delay_before_lose_target)
			macro_change_etat("PNJ_Raptor_ETAT_FIGHT")
		else
			macro_change_etat("PNJ_Raptor_ETAT_SEARCH")
	}
}
else if (!i_flag_stalk && f_time_start_etat > 1.0 && raptor_type != C_ID_Tyranosaure)
{
	macro_change_etat("PNJ_Raptor_ETAT_VALA")
}

// COMPORTEMENT =================================================================================================
i_way_computation_mode = Ci_WAY_MODE_BALADE
AI_Execute("PNJ_Raptor_exec_way_find_dest_pos")

AI_Execute("PNJ_Raptor_exec_way_move")

AI_Execute("PNJ_Raptor_exec_select_action")




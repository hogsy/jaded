#include "PNJ_Raptor_defines.var"

int				ti_flag_hit
int				ti_flag_grab

float			tf_dot_product
float			tf_cone_length
float			tf_neck_length

vector		tv_cone_origin
vector		tv_hor_head_sight
vector		tv_bassin_to_head

object		to_target
object		to_machoire

if (i_flag_bite && !i_flag_bite_ok)
{
	if (i_CineStack && am_CineStack[0].msg_id == CINE_Bite)
	{
		to_target = am_CineStack[0].msg_gao2
		@to_target OBJ_CapaSet(OBJ_Capa_8, none)

		i_flag_bite_ok = vrai
		i_flag_last_chance_grab = faux
		o_IK_bassin_bite_actor = nobody
		SND_RequestPlayOnObjCanal(Ci_SND_Mord, Anim_Canal_Tete)

		return
	}

	ti_flag_hit = faux

	if (i_perceived_best_actor_index == -1)
		return
		
	if (ai_perceived_hiding_place_index[i_perceived_best_actor_index] != -1)
		return
	
	if (ai_perceived_status[i_perceived_best_actor_index] & Ci_PERCEIVED_ALREADY_GRABBED)
		return

	PNJ_Raptor_Target_Reachable()

	// Pas de coup porté à travers des murs...	
	if  (! i_flag_bite_no_wall )
		return

	to_target = EVENT_InteretTargetGet(mid_best_interet)

//	if (to_target == o_main_actor && !i_flag_fake_attack && i_fake_attack_counter))
//	{
//		if (i_flag_fake_attack)
//		{
//			i_flag_fake_attack = faux
//			i_flag_bite_ok = vrai
//			i_fake_attack_counter--
//		}
//		else
//		{
//			i_flag_fake_attack = faux
//		}
//	}	

	if (i_flag_head_contact)
	{
		ti_flag_hit = vrai
	}
	else if (raptor_type == C_ID_Raptor)
	{
		tv_hor_head_sight = @ao_head_bones[0] OBJ_BankingGet()
		MATH_VecSetHorzNormalize(tv_hor_head_sight)

		tf_cone_length = 2.5
		tv_cone_origin = @ao_head_bones[0] OBJ_PosGet()
		tv_cone_origin -= tv_hor_head_sight

		 if (MATH_LIB_ZoneInCone(tv_cone_origin, tv_hor_head_sight, Cf_Cos30, tf_cone_length, @to_target COL_ZonePosGet(C_zde_corps),  @to_target COL_ZoneSizeGet(C_zde_corps), vrai, tf_dot_product, 0, 0))
			ti_flag_hit = vrai

		DBG_RenderCone(tv_cone_origin, tv_hor_head_sight * tf_cone_length, MATH_ACos(Cf_Cos30), 0x80008000)
	}
	else if (raptor_type == C_ID_Tyranosaure) // && i_perceived_best_actor_index == i_perceived_main_actor_index)
	{
		to_machoire = ANI_CanalObjectGet(Anim_Canal_Machoire)
	
		if (i_etat_courant == ETAT_FIGHT)
			tf_cone_length = 2.5
		else
			tf_cone_length = 3.8
			
		tv_cone_origin = @ao_head_bones[0] OBJ_PosGet()
		tv_hor_head_sight = MATH_VecBlendRotate(@ao_head_bones[0] OBJ_BankingGet(), @to_machoire OBJ_BankingGet(), 0.5)

		 if (MATH_LIB_ZoneInCone(tv_cone_origin, tv_hor_head_sight, Cf_Cos40, tf_cone_length, @to_target COL_ZonePosGet(C_zde_corps),  @to_target COL_ZoneSizeGet(C_zde_corps), faux, tf_dot_product, 0x800000FF, 0x800000FF))
			ti_flag_hit = vrai
			
		if ( ! ti_flag_hit && ! i_flag_last_chance_grab )
		{
			tv_bassin_to_head = OBJ_PosGet()
			tv_bassin_to_head -= tv_cone_origin
			tv_bassin_to_head *= 0.5
			tv_cone_origin += tv_bassin_to_head
			tf_neck_length = MATH_VecNorm(tv_bassin_to_head)
			tv_bassin_to_head /= tf_neck_length

//			MATH_LIB_Intersection_Cylindre_Sphere(vector tv_cylinder_center, vector tv_cylinder_axis, float tf_cylinder_rayon, float tf_cylinder_length, vector tv_sphere_center, float tf_sphere_size, int ti_flag_display_info)
			if (MATH_LIB_Intersection_Cylindre_Sphere(tv_cone_origin, tv_bassin_to_head, 2.0, tf_neck_length, @to_target COL_ZonePosGet(C_zde_corps), @to_target COL_ZoneSizeGet(C_zde_corps), vrai))
				ti_flag_hit = vrai
		}
	}

	if (ti_flag_hit)		
	{
		i_flag_bite_ok = vrai
		i_flag_last_chance_grab = faux
		o_IK_bassin_bite_actor = nobody
		SND_RequestPlayOnObjCanal(Ci_SND_Mord, Anim_Canal_Tete)
	
		// Est-ce qu'on peut attraper le perso ou est-ce qu'on doit juste le mordre ?
		ti_flag_grab = faux

		switch(raptor_type)
		{
			case C_ID_Galiminus :
				ti_flag_grab = faux
				break

			case C_ID_Tyranosaure :
				ti_flag_grab = vrai
			
				if ( ! IsThis_ID_Humain(ai_perceived_ID[i_perceived_best_actor_index]) )
					ti_flag_grab = vrai
				else if (ai_perceived_status[i_perceived_best_actor_index] & Ci_PERCEIVED_CINE_TARGET || MSG_GlobalIsValid(mid_force_target))
					ti_flag_grab = vrai
				else if (af_perceived_life_ratio[i_perceived_best_actor_index] > Cf_Life_Agonisant && ! @ao_perceived_actor[i_perceived_best_actor_index] OBJ_HierarchyGet())
					ti_flag_grab = faux
				break
				
				
			default:

				// ON NE BLESSE PAS UN BLESSE
//					if (af_perceived_life_ratio[i_perceived_best_actor_index] == Cf_Life_Agonisant)
//					{
//						 switch(ai_perceived_ID[i_perceived_best_actor_index])
//						 {
//							case  C_ID_Joueur :
//							case C_ID_Indigene1 :
//							case C_ID_Indigene2 :
//							case C_ID_Indigene3 :
//							case C_ID_Indigene4 :
//			 				case C_ID_Indigene5 :
//				 			case C_ID_DummyHumain :
//				 				break
//				 				
//				 			default:
//				 				// EXIT !!!
//								return
//						}
//					}

				ti_flag_grab = vrai	
			
				// CONDITION POUR NE PAS GRABBER
				if (i_flag_dont_use_grid)
					ti_flag_grab = faux
				else if (to_target == o_main_actor && i_grab_disable)
					ti_flag_grab = faux
				else if (IsThis_ID_Humain(ai_perceived_ID[i_perceived_best_actor_index]) && to_target != o_main_actor && ! TIME_Elapsed(@get_global f_raptor_last_grab_time, delay_between_grab) )
					ti_flag_grab = faux
		}

	
		if (ti_flag_grab)
		{
			// Si le perso veut bien, on va se regarder dans le blanc des yeux puis je vais le bouffer !!!
			o_grab_actor = LNK_ThisClientGet(to_target, Ci_LNK_GRAB_RAPTOR, mid_grab_actor_LNK_ID, vrai, "PNJ_Raptor_exec_GRAB_init", nofunc, "PNJ_Raptor_exec_GRAB_init")
			if (o_grab_actor)
			{
				if (IsThis_ID_Marin(ai_perceived_ID[i_perceived_best_actor_index]))
					@get_global f_raptor_last_grab_time = TIME_Get()
				else if (to_target == o_main_actor && raptor_type == C_ID_Raptor)
					i_grab_disable = MATH_Modulo(i_grab_disable + 1, 3)

				macro_change_etat("PNJ_Raptor_ETAT_GRAB")
			}
			else
			{
				ti_flag_grab = faux
			}
		}

		switch(ai_perceived_ID[i_perceived_best_actor_index])
		{
			case C_ID_Scolo :
				break
				
			default:
				if (!ti_flag_grab)
				{
					switch(raptor_type)	
					{
						case C_ID_Tyranosaure :	
		//					EVENT_AddEventPaf(C_EVENT_FILTER_All, C_EVENT_PAF_ForceDir | C_EVENT_PAF_Tuant | C_EVENT_PAF_Fort, OBJ_Me(), Cf_EVENT_Duree_1Trame, to_target, PAF_Unit * 1000, OBJ_SightGet())
							EVENT_AddEventPaf(C_EVENT_FILTER_All, C_PAF_KK_Fort, OBJ_Me(), Cf_EVENT_Duree_1Trame, to_target, 1000.0 * PAF_Unit, OBJ_SightGet())
							if (i_perceived_best_actor_index == i_perceived_main_actor_index)
								PNJ_Raptor_Del_Interest()
							break
			
						case C_ID_Raptor :
							switch(raptor_category)
							{
								case Ci_Raptor_Light :
									EVENT_AddEventPaf(C_EVENT_FILTER_All, C_PAF_KK_Moyen, OBJ_Me(), Cf_EVENT_Duree_1Trame, to_target, 6.0 * PAF_Unit, OBJ_SightGet())
									break
									
								case Ci_Raptor_Standard :
									EVENT_AddEventPaf(C_EVENT_FILTER_All, C_PAF_KK_Fort, OBJ_Me(), Cf_EVENT_Duree_1Trame, to_target, 6.0 * PAF_Unit, OBJ_SightGet())
									break
									
								case Ci_Raptor_Heavy :
									EVENT_AddEventPaf(C_EVENT_FILTER_All, C_PAF_KK_Fort, OBJ_Me(), Cf_EVENT_Duree_1Trame, to_target, 10.0 * PAF_Unit, OBJ_SightGet())
									break
							}
									
							break
							
						case C_ID_Galiminus :
							@get_global f_time_last_gali_attack = TIME_Get()
							EVENT_AddEventPaf(C_EVENT_FILTER_All, C_PAF_KK_Faible, OBJ_Me(), Cf_EVENT_Duree_1Trame, to_target, 1.0 * PAF_Unit, OBJ_SightGet())
							break
					}
				}
		}
	}
}

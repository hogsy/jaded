#include "PNJ_Raptor_defines.var"

int		ti_i
int		ti_index
int		ti_flag_exit
int		ti_flag_a_table
int		ti_best_index
int		ti_flag_can_fight

float	tf_nearest_dist

// SORTIE ETAT ===================================================================
if (i_sort_etat)
{
	i_sort_etat = faux

	if ( ! i_flag_saut )
	{
		i_way_computation_mode = -1
		i_way_computation_old_mode = -1
	}

	return
}

// INITIALISATION ETAT ==============================================================
if (i_etat_courant != ETAT_BRINGITBACK)
{
	i_dernier_etat = i_etat_courant
	i_etat_courant = ETAT_BRINGITBACK

	if (fct_last_etat)
	{
		i_sort_etat = vrai
		AI_Execute(fct_last_etat)
	}

	fct_last_etat = AI_TrackCurGet()
	fct_main_etat = AI_TrackCurGet()

//	PNJ_Raptor_Uncollide_Humans()

	f_time_start_etat = 0.0
}
else
{
	f_time_start_etat += TIME_GetDt()
}

// ANALYSE =======================================================================================================
AI_Execute("PNJ_Raptor_exec_check_msg")

AI_Execute("PNJ_Raptor_exec_check_vision")

AI_Execute("PNJ_Raptor_exec_check_sound")

AI_Execute("PNJ_Raptor_exec_check_collision")

AI_Execute("PNJ_Raptor_exec_check_paf")
if (i_flag_paf_fly)
	macro_change_etat("PNJ_Raptor_ETAT_PAF_FLY")
if (i_flag_paf_fall)
	macro_change_etat("PNJ_Raptor_ETAT_PAF_FALL")
if (i_flag_paf_repousse || i_flag_paf_slide || !f_lifecur)
	macro_change_etat("PNJ_Raptor_ETAT_PAF_SLIDE")

AI_Execute("PNJ_Raptor_exec_check_shoot")

AI_Execute("PNJ_Raptor_exec_update_best_interest")

AI_Execute("PNJ_Raptor_exec_check_requin")

AI_Execute("PNJ_Raptor_exec_check_vala")

AI_Execute("PNJ_Raptor_exec_test_bite")

if ( ! o_javelin || i_perceived_best_actor_index != i_perceived_main_actor_index || @o_javelin OBJ_CapaTest(OBJ_Capa_13))
{
	o_javelin = nobody
	macro_change_etat("PNJ_Raptor_ETAT_ATTENTE")
}

// COMPORTEMENT =================================================================================================
i_way_computation_mode = Ci_WAY_MODE_FIGHT
AI_Execute("PNJ_Raptor_exec_way_find_dest_pos")

i_flag_run = vrai

AI_Execute("PNJ_Raptor_exec_way_move")
AI_Execute("PNJ_Raptor_exec_select_action")

// JE NE TROUVE PAS DE CHEMIN POUR ATTEINDRE MON POINT DE FUITE
if (! i_way_wp_nb && ! i_way_moving)
{
	if (OBJ_SqrDist(o_javelin) > 16.0)
	{
		o_javelin = nobody
		macro_change_etat("PNJ_Raptor_ETAT_ATTENTE")
	}
	else
	{
		macro_change_etat("PNJ_Raptor_ETAT_MORD")
	}
}



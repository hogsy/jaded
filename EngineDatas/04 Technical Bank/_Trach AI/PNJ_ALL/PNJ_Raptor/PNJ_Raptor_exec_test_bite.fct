#include "PNJ_Raptor_defines.var"

int			ti_i
int			ti_flag_ok

float		tf_test_dist
float		tf_dist
float		tf_dot_product
float		tf_sqr_dist
float		tf_gali_attack_dist
float		tf_norm
float		tf_me_to_target_dist

vector	tv_temp
vector	tv_sight
vector	tv_pos
vector	tv_me_to_target

object	to_target
object	to_target_ref_bone

//if (i_way_computation_mode == Ci_WAY_MODE_FIGHT && !i_flag_destpos_accessible)
//	return

if (i_CineStack && am_CineStack[0].msg_id == CINE_Bite)
	macro_change_etat("PNJ_Raptor_ETAT_MORD")

if (! i_flag_in_my_territory )
	return

if (i_flag_paf_repousse || i_flag_paf_visual)
	return

if (i_way_computation_mode == Ci_WAY_MODE_REQUIN && f_requin_mode_duration < 5.0)
	return

if (i_perceived_best_actor_index == -1)
	return

if (! MSG_GlobalIsValid(mid_best_interet))
	return

if (! ai_perceived_seen[i_perceived_best_actor_index] )
	return

if (ai_perceived_ID[i_perceived_best_actor_index] == -1)
	return

if (raptor_type != C_ID_Tyranosaure && ! ai_perceived_accessible[i_perceived_best_actor_index])
	return

if (ai_perceived_hiding_place_index[i_perceived_best_actor_index] != -1)
	return

if (ai_perceived_status[i_perceived_best_actor_index] & (Ci_PERCEIVED_ALREADY_GRABBED | Ci_PERCEIVED_IS_DEAD))
	return

if (ai_perceived_ID[i_perceived_best_actor_index] == C_ID_Joueur)
{
	if ( ! TIME_Elapsed(f_time_last_jackattack, 0.2) )
		return
}
//else if (IsThis_ID_Humain(ai_perceived_ID[i_perceived_best_actor_index]) && @"univ" Humains_PafTimer[ai_perceived_ID[i_perceived_best_actor_index]])
//	return

to_target = ao_perceived_actor[i_perceived_best_actor_index]

tf_gali_attack_dist = 1.5

switch(ai_perceived_ID[i_perceived_best_actor_index])
{
	case C_ID_Joueur :
		tf_gali_attack_dist = 2.0
		to_target_ref_bone = o_camera
		break

	case C_ID_Scolo :
		to_target_ref_bone = @to_target ANI_CanalObjectGet(5)
		break

	case C_ID_BatCharognard : 
		to_target_ref_bone = @to_target ANI_CanalObjectGet(Anim_Canal_Torse)
		break

	case C_ID_Raptor :
		if (MATH_VecDotProduct(OBJ_SightGet(), @to_target OBJ_SightGet()) > 0.0)
			to_target_ref_bone = @to_target ANI_CanalObjectGet(Anim_Canal_Bassin)
		else
			to_target_ref_bone = @to_target ANI_CanalObjectGet(Anim_Canal_Tete)
		break

	default:
		to_target_ref_bone = @to_target ANI_CanalObjectGet(Anim_Canal_Tete)
}

if ( ! to_target_ref_bone )
	to_target_ref_bone = to_target

ti_flag_ok = faux

switch(raptor_type)
{
	case C_ID_Tyranosaure :

		tv_temp = @to_target OBJ_PosGet()
		tv_temp -= OBJ_PosGet()
		
		if (tv_temp.z > -3.0 && tv_temp.z < 11.0)
		{	
//			tv_temp = @to_target_ref_bone OBJ_PosGet()
//			tv_temp -= v_look_head_pos
			tv_temp.z = 0.0	
		
			tf_dist = MATH_VecDotProduct(tv_temp, tv_temp)
	
			if (@ao_perceived_actor[i_perceived_best_actor_index] OBJ_HierarchyGet())
			{
				if (ai_perceived_ID[i_perceived_best_actor_index] == C_ID_Joueur)
				{
					if (i_flag_head_search)
						tf_test_dist = 36.0
					else
						tf_test_dist = 64.0
				}
				else
					tf_test_dist = 49.0
			}
			else if (tf_dist < 100.0)
			{
				tv_sight = OBJ_SightGet()
				OBJ_BankingGeneralSet(@to_target OBJ_PosGet() - OBJ_PosGet(), OBJ_BankingGet())

				// On teste les différentes attaque
				tv_me_to_target = @to_target OBJ_PosGet() - OBJ_PosGet()
				tf_me_to_target_dist = MATH_VecNorm(tv_me_to_target)
				tv_me_to_target /= tf_me_to_target_dist
				
				for (ti_i = 0; ti_i < 3; ti_i++)
				{
					tv_pos = MATH_VecLocalToGlobal(av_gao_bite_quat[ti_i])
					tf_norm = MATH_VecNorm(tv_pos)
					DBG_RenderVector(OBJ_PosGet(), tv_pos, color_jaune)
					tv_pos /= tf_norm
					DBG_RenderCone(OBJ_PosGet(), tv_pos * tf_norm, 20.0 * Cf_PiBy180, 0x80000080)
					
					if (tf_me_to_target_dist < tf_norm && MATH_VecDotProduct(tv_pos, tv_me_to_target) > Cf_Cos20)
					{
						i_mord_action_index = ai_bite_action_num[ti_i]
						ti_flag_ok = vrai
						break
					}
				}
				
				OBJ_BankingGeneralSet(tv_sight, OBJ_BankingGet())

				if (ai_perceived_ID[i_perceived_best_actor_index] == C_ID_Joueur && i_flag_head_search && ti_i != 2)
				{
					i_mord_action_index = -1
					ti_flag_ok = faux
					tf_test_dist = 36.0
				}
				else
				{
					tf_test_dist = 16.0
				}
			}

			if ( ! ti_flag_ok && tf_dist < tf_test_dist)
			{
				tf_dist = MATH_FloatSqrt(tf_dist)
				tv_temp /= tf_dist	
				
				if (MATH_VecDotProduct(tv_temp, v_look_virtual_sight) > Cf_Cos70)
					ti_flag_ok = vrai
			}
		}
		
		break

	case C_ID_Raptor :

//			DBG_RenderCone(v_look_head_pos, v_look_virtual_sight * 2.0, MATH_ACos(Cf_Cos20), 0xC00000FF)
//			tf_dist = MATH_LIB_ZoneInCone(v_look_head_pos, v_look_virtual_sight, Cf_Cos20, 2.0, @to_target COL_ZonePosGet(C_zde_corps), @to_target COL_ZoneSizeGet(C_zde_corps), faux, tf_dot_product)
//			if (tf_dist)
//				ti_flag_ok = vrai

		tf_sqr_dist = 3.0 * OBJ_ZoomGet()
		tf_sqr_dist *= tf_sqr_dist
	
		tv_temp = @to_target OBJ_PosGet()
		tv_temp -= OBJ_PosGet()

		if (MATH_VecDotProduct(OBJ_SightGet(), tv_temp) > 0.0 && tv_temp.z > -2.0 && tv_temp.z < 3.5)
		{	
			tv_temp = @to_target_ref_bone OBJ_PosGet()
			tv_temp -= v_look_head_pos
			tv_temp.z = 0.0	
		
			tf_dist = MATH_VecDotProduct(tv_temp, tv_temp)
	
			if (tf_dist < 2.0)
			{
				ti_flag_ok = vrai
			}
			else if (tf_dist < tf_sqr_dist)
			{
				tf_dist = MATH_FloatSqrt(tf_dist)
				tv_temp /= tf_dist	
				
				if (MATH_VecDotProduct(tv_temp, v_look_virtual_sight) > Cf_Cos60)
					ti_flag_ok = vrai
			}
		}
		
		break

	case C_ID_Galiminus :

		tv_temp = @to_target OBJ_PosGet()
		tv_temp -= OBJ_PosGet()
		
		if (MATH_AbsFloat(tv_temp.z) < 1.0)
		{	
			tv_temp = @to_target_ref_bone OBJ_PosGet()
			tv_temp -= v_look_head_pos
			tv_temp.z = 0.0
		
			tf_dist = MATH_VecDotProduct(tv_temp, tv_temp)
	
			tf_sqr_dist = tf_gali_attack_dist * OBJ_ZoomGet()
			tf_sqr_dist *= tf_sqr_dist

			if (tf_dist < 1.0)
			{
				ti_flag_ok = vrai
			}
			else if (tf_dist < tf_sqr_dist)
			{
				tf_dist = MATH_FloatSqrt(tf_dist)
				tv_temp /= tf_dist	
				
				if (MATH_VecDotProduct(tv_temp, v_look_virtual_sight) > Cf_Cos60)
					ti_flag_ok = vrai
			}
		}
		
		break
}

// MUR ENTRE MOI ET MA CIBLE ?
if (ti_flag_ok)
	PNJ_Raptor_Target_Reachable()
	
// On peut mordre
if (ti_flag_ok && i_flag_bite_no_wall)
	macro_change_etat("PNJ_Raptor_ETAT_MORD")

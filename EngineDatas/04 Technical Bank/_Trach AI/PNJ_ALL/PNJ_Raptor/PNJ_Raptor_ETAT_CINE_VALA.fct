#include "PNJ_Raptor_defines.var"

int		ti_i
int		ti_index
int		ti_flag_exit
int		ti_flag_a_table

object	to_obj

// SORTIE ETAT ===================================================================
if (i_sort_etat)
{
	i_sort_etat = faux

	i_flag_cine_vala_orient = faux
	o_cine_vala_wp = nobody

	return
}

// INITIALISATION ETAT ==============================================================
if (i_etat_courant != ETAT_CINE_VALA)
{
	i_dernier_etat = i_etat_courant
	i_etat_courant = ETAT_CINE_VALA

#ifndef _FINAL_
	if (@get_global DEBUG_SCRIPT)
	{
		DBG_TraceString("######## ")
		DBG_TraceObject(OBJ_Me())
		DBG_TraceString(" passe en CINE VALA\n")
	}
#endif				

	if (fct_last_etat)
	{
		i_sort_etat = vrai
		AI_Execute(fct_last_etat)
	}

	fct_last_etat = AI_TrackCurGet()
	fct_main_etat = AI_TrackCurGet()

//	PNJ_Raptor_Uncollide_Humans()

	f_time_start_etat = 0.0
}
else
{
	f_time_start_etat += TIME_GetDt()
}

// ANALYSE =======================================================================================================
AI_Execute("PNJ_Raptor_exec_check_msg")

AI_Execute("PNJ_Raptor_exec_check_vision")

AI_Execute("PNJ_Raptor_exec_check_sound")

AI_Execute("PNJ_Raptor_exec_check_collision")

AI_Execute("PNJ_Raptor_exec_check_paf")
if (i_flag_paf_fly)
	macro_change_etat("PNJ_Raptor_ETAT_PAF_FLY")
if (i_flag_paf_fall)
	macro_change_etat("PNJ_Raptor_ETAT_PAF_FALL")
if (i_flag_paf_repousse || i_flag_paf_slide || !f_lifecur)
	macro_change_etat("PNJ_Raptor_ETAT_PAF_SLIDE")

if (i_flag_force_fuite)
	macro_change_etat("PNJ_Raptor_ETAT_VALA")

AI_Execute("PNJ_Raptor_exec_check_shoot")

AI_Execute("PNJ_Raptor_exec_check_client")

AI_Execute("PNJ_Raptor_exec_update_best_interest")

AI_Execute("PNJ_Raptor_exec_check_requin")

// COMPORTEMENT =================================================================================================
to_obj = am_CineStack[0].msg_gao3 
if ( ! to_obj || (i_perceived_best_actor_index != -1 && to_obj == ao_perceived_actor[i_perceived_best_actor_index]) )
{
	i_flag_look = faux		
	i_flag_look_best_interet = vrai
}
else
{
	f_look_force_duration = Cf_EVENT_Duree_1Trame
	v_look_force_pos = @to_obj OBJ_PosGet()
}

i_flag_cine_vala_orient = am_CineStack[0].msg_int3
o_cine_vala_wp = am_CineStack[0].msg_gao2
if (ARR_ObjSearch(&ao_net_wp[0], i_net_wp_nb, o_cine_vala_wp) != -1)
{
	i_flag_in_my_net_cine_wp = vrai
}
else
{
	i_flag_in_my_net_cine_wp = faux
	i_cine_vala_dest_territory = GST_EVENT_Territory_Get(@o_cine_vala_wp OBJ_PosGet())
}

i_way_computation_mode = Ci_WAY_MODE_CINE_VALA
AI_Execute("PNJ_Raptor_exec_way_find_dest_pos")

if (i_flag_cine_vala_orient && ( ! o_next_wp || o_next_wp == o_cine_vala_wp ) )
	v_way_destpos = PNJ_Raptor_Circumvent_Wp_If_Bad_Sight(OBJ_PosGet(), @o_cine_vala_wp OBJ_PosGet(),  @o_cine_vala_wp OBJ_SightGet(), @o_cine_vala_wp OBJ_HorizonGet(), 2.0)

AI_Execute("PNJ_Raptor_exec_way_move")

switch(am_CineStack[0].msg_int1)
{
	case 2 : 
		i_flag_run = vrai
		break
		
	default:
		i_flag_run = faux
}

AI_Execute("PNJ_Raptor_exec_select_action")

switch(ACT_ActionGet())
{
	case Action_Normal_Attente :
	case Action_Blesse_Attente :
	case Action_Renifle_Attente :

		if ( ! i_way_wp_nb && i_way_status & Ci_WAY_STATUS_BEZIER_NO_MOVE)
			i_cine_close = vrai
			
		break
}

 

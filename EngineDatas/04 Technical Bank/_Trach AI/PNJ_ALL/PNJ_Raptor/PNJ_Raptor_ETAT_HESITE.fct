#include "PNJ_Raptor_defines.var"

int			ti_flag_ok

float		tf_norm
float		tf_dot_product

vector	tv_me_to_target
vector	tv_new_sight
vector	tv_new_banking


// SORTIE ETAT ===================================================================
if (i_sort_etat)
{
	i_sort_etat = faux

	i_flag_reinit_etat = faux
//	MSG_GlobalSetInvalid(mid_new_interet)

	return
}

// INITIALISATION ETAT ==============================================================
if (i_etat_courant != ETAT_HESITE || i_flag_reinit_etat)
{
	i_dernier_etat = i_etat_courant
	i_etat_courant = ETAT_HESITE

	if (i_perceived_best_actor_index != -1 && (ai_perceived_accessible[i_perceived_best_actor_index] && ai_perceived_status[i_perceived_best_actor_index] & Ci_PERCEIVED_IS_DEAD) )
	{
		// Cas particulier, on fera le cri en init du mode dévore
		i_flag_change_target = faux
		macro_change_etat("PNJ_Raptor_ETAT_DEVORE")
	}

	if (fct_last_etat && !i_flag_reinit_etat)
	{
		i_sort_etat = vrai
		AI_Execute(fct_last_etat)
	}

	i_flag_reinit_etat = faux
	i_flag_change_target = faux

	if (EVENT_InteretSeenTimeGet(mid_best_interet) == TIME_Get())
		i_flag_force_seen_time_update = vrai
	else
		i_flag_force_seen_time_update = faux

	fct_last_etat = AI_TrackCurGet()

	if (raptor_type == C_ID_Tyranosaure)
		f_delay_before_shout = MATH_RandFloat(0.5, 0.8)
	else
		f_delay_before_shout = MATH_RandFloat(0.0, 0.5)

	if (EVENT_InteretTargetGet(mid_best_interet) == o_main_actor)
		i_SND_roar_index = Ci_SND_Choose_Jack
	else
		i_SND_roar_index = Ci_SND_Choose_Another

	f_time_start_etat = 0.0
}
else
{
	f_time_start_etat += TIME_GetDt()
}

// ANALYSE ==================== ===================================================================================
AI_Execute("PNJ_Raptor_exec_check_msg")

AI_Execute("PNJ_Raptor_exec_check_vision")

AI_Execute("PNJ_Raptor_exec_check_sound")

AI_Execute("PNJ_Raptor_exec_check_collision")

AI_Execute("PNJ_Raptor_exec_check_paf")
if (i_flag_paf_fly)
	macro_change_etat("PNJ_Raptor_ETAT_PAF_FLY")
if (i_flag_paf_fall)
	macro_change_etat("PNJ_Raptor_ETAT_PAF_FALL")
if (i_flag_paf_repousse || i_flag_paf_slide || !f_lifecur)
	macro_change_etat("PNJ_Raptor_ETAT_PAF_SLIDE")

if (i_flag_force_fuite)
	macro_change_etat("PNJ_Raptor_ETAT_VALA")
if (! i_flag_in_my_territory)
	macro_change_etat("PNJ_Raptor_ETAT_VALA")

AI_Execute("PNJ_Raptor_exec_check_shoot")

AI_Execute("PNJ_Raptor_exec_check_client")

AI_Execute("PNJ_Raptor_exec_update_best_interest")

AI_Execute("PNJ_Raptor_exec_check_requin")

if (f_force_requin_mode_duration)
	macro_change_etat("PNJ_Raptor_ETAT_FIGHT")

if (MSG_GlobalIsValid(mid_best_interet) && i_flag_force_seen_time_update )
{
	DBG_RenderVector(OBJ_PosGet(), Cv_VerticalVector * 1000.0, color_rouge)
	EVENT_InteretSeenTimeSet(mid_best_interet, TIME_Get())
}

if (i_flag_change_target)
{
	i_flag_reinit_etat = vrai
}
else
{
	ti_flag_ok = faux

//	if (raptor_type == C_ID_Galiminus)
//		ti_flag_ok = vrai
//	else
	if (i_frame_nb < 10)
		ti_flag_ok = vrai
	else if ( raptor_type != C_ID_Tyranosaure && (i_flag_paf_visual || i_flag_paf_repousse))
		ti_flag_ok = vrai
	else if (!i_flag_cri && ( ! f_delay_before_shout && f_time_start_etat) )
		ti_flag_ok = vrai
	
	if (ti_flag_ok)
	{
		if (i_perceived_best_actor_index != -1 && ai_perceived_status[i_perceived_best_actor_index] & Ci_PERCEIVED_ALREADY_CHECKED)
		{
			if (raptor_type == C_ID_Raptor)
				macro_change_etat("PNJ_Raptor_ETAT_RODE")
			else
				macro_change_etat("PNJ_Raptor_ETAT_VALA")
		}
		else
			macro_change_etat(fct_main_etat)
	}
}

// COMPORTEMENT =================================================================================================
i_flag_look = vrai
i_flag_look_best_interet = vrai

if (raptor_type == C_ID_Tyranosaure)
{
	ti_flag_ok = faux

	if (PNJ_Raptor_In_Front_Of_Me(o_main_actor))
		ti_flag_ok = vrai
	else if (i_perceived_best_actor_index != -1 && PNJ_Raptor_In_Front_Of_Me(ao_perceived_actor[i_perceived_best_actor_index]))
		ti_flag_ok = vrai

	if (ti_flag_ok)
	{
//		if (MATH_VecDotProduct(DYN_SpeedGetVector(), OBJ_SightGet()) > 0.1)
//		{
//			// DERAPAGE
//			i_flag_force_cri_haut = vrai
//			f_delay_before_shout += TIME_GetDt()
//		}
//		else
		if (i_flag_dont_use_grid || f_time_start_etat > 3.0)
		{
			i_flag_force_cri_haut = vrai
		}
		else
		{
			tv_me_to_target = av_perceived_position[i_perceived_best_actor_index]
			tv_me_to_target -= OBJ_PosGet()
			tv_me_to_target.z = 0.0
			
			tf_norm = MATH_VecNorm(tv_me_to_target)
			
			if (MATH_FloatNullToler(tf_norm, 0.01))
			{
				tv_new_sight = OBJ_SightGet()
			}
			else
			{
				tv_me_to_target /= tf_norm
				tv_new_sight = MATH_VecBlendRotate(OBJ_SightGet(), tv_me_to_target, 4.0 * TIME_GetDt())
			}
	
			if (GRID_LIB_IsReachableFrom(OBJ_PosGet(), OBJ_PosGet() - (tv_new_sight * 6.0), 0b0, 0.0, faux, 0))
			{
				tv_new_banking = MATH_VecBlendRotate(OBJ_BankingGet(), v_virtual_banking, 60. * TIME_GetDt())
				OBJ_BankingGeneralSet(tv_new_sight, tv_new_banking)
	
				PNJ_Raptor_ActionSet(Action_Recul)
				i_flag_dont_change_action = vrai
			}
			else
			{
				i_flag_force_cri_haut = vrai
				DBG_RenderVector(@get_global v_grid_lib_last_blocked_pos, Cv_VerticalVector * 100.0, color_rouge)
			}
		}
	}
}
else
{
	AI_Execute("PNJ_Raptor_exec_test_bite")
}

//AI_Execute("PNJ_Raptor_exec_select_action")

if ( f_delay_before_shout )
{
	f_delay_before_shout -= TIME_GetDt()
	if ( f_delay_before_shout <= 0.0  && !i_flag_cri && (raptor_type != C_ID_Raptor || TIME_Elapsed(@get_global f_time_last_raptor_roar, 0.5)))
	{
		f_delay_before_shout = 0.0

		if (raptor_type == C_ID_Tyranosaure)
		{
			i_flag_cri = vrai
		}
		else
		{
			switch(i_dernier_etat)
			{
				case ETAT_HESITE :
				case ETAT_PAF_SLIDE :
				case ETAT_PAF_FALL :
				case ETAT_PAF_FLY :
				case ETAT_A_TERRE :
				case ETAT_FIGHT :
				case ETAT_GRAB :
				case ETAT_LANCE :
				case ETAT_MORD :
					if (i_perceived_best_actor_index == i_perceived_main_actor_index && f_on_screen_pourcent == -1.0)
						i_flag_cri = vrai
					break
	
				default:
					i_flag_cri = vrai
			}
		}
	}
}

AI_Execute("PNJ_Raptor_exec_select_action")


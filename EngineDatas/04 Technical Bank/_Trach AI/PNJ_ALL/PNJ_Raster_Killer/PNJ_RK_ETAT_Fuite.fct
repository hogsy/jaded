#include "PNJ_RK_defines.var"

int			ti_i
int			ti_ind
int			ti_index			
int			ti_link_num

float		tf_dist
float		tf_dot_product
float		tf_best_dot

vector	tv_new_sight

object	to_jump_wp
object	to_camera
object	to_next_wp
object	to_best_next_wp


if (i_flag_sortie_etat)
{
	i_flag_sortie_etat = faux
	return
}

if (i_etat_courant != ETAT_Fuite)
{
	// INTIALISATION
	i_dernier_etat = i_etat_courant
	i_etat_courant = ETAT_Fuite
	
	if (fct_last_etat)
	{
		i_flag_sortie_etat = vrai
		AI_Execute(fct_last_etat)
	}

	fct_last_etat = AI_TrackCurGet()
	
	n_net = @o_start_wp WAY_NetOfObj()

	v_start_pos = @o_start_wp OBJ_PosGet()

	@o_start_wp OBJ_BankingGeneralSet(@o_start_wp OBJ_PosGet() - @o_main_actor OBJ_PosGet(), Cv_VerticalVector)
	o_next_wp = @o_start_wp WAY_NetNextWP(n_net, o_start_wp, NetNextWP_Mode_axe_de_visee, 0)

	OBJ_PosSet(v_start_pos)
	OBJ_BankingGeneralSet(@o_next_wp OBJ_PosGet() - v_start_pos, Cv_VerticalVector)

	f_anim_speed_coef = MATH_RandFloat(1.0, 1.5)

	ACT_ActionSet(Ci_Action_Fuite)

	OBJ_FlagInvisibleSet(faux)
	
	f_off_screen_duration = 0.0

	ti_ind = MATH_RandInt(0, 5)
	if (ti_ind >= 2)
	{
		// Femme
		OBJ_ZoomSet(MATH_RandFloat(0.9,0.95))
		i_homme = faux

		if ( ! MATH_RandInt(0, 10) )
		{
			SPEECH_RequestPost
			(
				OBJ_Me(),
				OBJ_Me(), 
				SPEECH_CteTxg_SpeNewYork, 
				GeneNY_C_women_panic, 
				1.0, 
				SPEECH_Cte_PriorityDefault, 
				5,
				0,
				0,
				0
			)
		}
	}
	else
	{
		// Hommen
		OBJ_ZoomSet(MATH_RandFloat(0.95,1.05))
		i_homme = vrai

		if ( ! MATH_RandInt(0, 10) )
		{
			SPEECH_RequestPost
			(
				OBJ_Me(),
				OBJ_Me(), 
				SPEECH_CteTxg_SpeNewYork, 
				GeneNY_C_men_panic, 
				1.0, 
				SPEECH_Cte_PriorityDefault, 
				5,
				0,
				0,
				0
			)
		}
	}

	ANI_ShapeSelect(Anim_Canal_Bassin, ti_ind+1)
	ANI_ShapeSelect(Anim_Canal_Ventre, ti_ind+1)
	ANI_ShapeSelect(Anim_Canal_Tete, ti_ind+1)

	f_time_start_etat = 0.0
}
else
{
	f_time_start_etat += TIME_GetDt()
}

// ANALYSE =========================================================================

// COMPORTEMENT ===================================================================
if (MATH_VecDotProduct(v_start_pos - @o_next_wp OBJ_PosGet(), OBJ_PosGet() - @o_next_wp OBJ_PosGet()) < 0.0 || OBJ_SqrDistHorz(o_next_wp) < 4.0)
{
	v_start_pos = @o_next_wp OBJ_PosGet()
	@o_next_wp OBJ_BankingGeneralSet(@o_next_wp OBJ_PosGet() - @o_main_actor OBJ_PosGet(), Cv_VerticalVector)

	tf_best_dot = -1.0
	to_best_next_wp = nobody
	ti_link_num = 	WAY_GetNumLinks(n_net, o_next_wp)
	for (ti_i = 0; ti_i < ti_link_num; ti_i++)
	{
		to_next_wp = WAY_NetNextWP(n_net, o_next_wp, 6, ti_i)

		if (to_next_wp != o_start_wp)
		{
			tv_new_sight = @to_next_wp OBJ_PosGet() - @o_next_wp OBJ_PosGet()
			MATH_VecSetHorzNormalize(tv_new_sight)
			
			tf_dot_product = MATH_VecDotProduct(tv_new_sight, @o_next_wp OBJ_SightGet())
			if (tf_dot_product > 0.0)
				tf_dot_product = MATH_RandFloat(0.0, 1.0)
	
			if (tf_dot_product > tf_best_dot)
			{
				tf_best_dot = tf_dot_product
				to_best_next_wp = to_next_wp
			}
		}

	}

	o_start_wp	= o_next_wp
	o_next_wp = to_best_next_wp

	if ( ! to_best_next_wp )
		macro_change_etat("PNJ_RK_ETAT_Attente")
}

tv_new_sight = @o_next_wp OBJ_PosGet()
//DBG_RenderVector(OBJ_PosGet() + Cv_VerticalVector, tv_new_sight - OBJ_PosGet(), color_vert)

if (OBJ_LodVisGet() <= 0.0)
{
	f_off_screen_duration += TIME_GetDt()

	if (f_off_screen_duration	 > 5.0)
		macro_change_etat("PNJ_RK_ETAT_Attente")
}
else
{
	f_off_screen_duration = 0.0

	@get_global WAY_LIB_TEST_GRID = vrai
	if (WAY_LIB_Test_Occluder(OBJ_PosGet(), OBJ_SightGet(), 0.5, tv_new_sight, -1, tv_new_sight, to_jump_wp, & @"PNJ_Pacifique/PNJ_Car_Manager" o_car_manager ao_car[0], & @"PNJ_Pacifique/PNJ_Car_Manager" o_car_manager af_car_size[0], @"PNJ_Pacifique/PNJ_Car_Manager" o_car_manager i_car_nb, C_Occl_Type_All))
	{
		DBG_RenderVector(OBJ_PosGet() + Cv_VerticalVector, tv_new_sight - OBJ_PosGet(), color_rouge)
	}
	@get_global WAY_LIB_TEST_GRID = faux
}

tv_new_sight -= OBJ_PosGet()
tv_new_sight.z = 0.0

tv_new_sight = MATH_VecBlendRotate(OBJ_SightGet(), tv_new_sight, 6.0 * TIME_GetDt())
OBJ_BankingGeneralSet(tv_new_sight, Cv_VerticalVector)

ACT_LIB_ActionFrequencyMultiply(f_anim_speed_coef)

PNJ_RS_Soft_Col()
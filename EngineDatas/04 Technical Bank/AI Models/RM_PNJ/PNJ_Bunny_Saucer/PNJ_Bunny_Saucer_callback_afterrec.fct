 #include "PNJ_Bunny_Saucer_defines.var"

int			ti_i

float		tf_coef
float		tf_norm

vector	tv_pos
vector	tv_col_pos
vector	tv_traction

switch(i_etat_courant)
{
	case ETAT_Ride :
	case ETAT_Balade : 
	case ETAT_Interception : 
	case ETAT_Largage :
	case ETAT_In_The_Sky :
	case ETAT_Crash :
		break
		
	default:
		OBJ_BankingGeneralSet(v_real_sight, v_real_banking)
}

if (o_bascule && i_etat_courant == ETAT_Buggy)
{
//	DBG_RenderBox(o_bascule, cvector(-1.0, -2.0, -0.1), cvector(1.0, 2.0, 0.1), color_blanc)

	tv_pos = @o_bascule OBJ_PosGet()
	tv_pos -= OBJ_PosGet()
	tf_coef = MATH_VecDotProduct(tv_pos, @o_bascule OBJ_BankingGet())
	tv_pos = OBJ_PosGet()
	tv_pos += MATH_FloatLimit(tf_coef, 0.0, 4.0) * @o_bascule OBJ_BankingGet()
	@o_bascule OBJ_PosSet(tv_pos)

	if (o_Rided_Actor)
	{
		if ( o_last_Rided_Actor)
			@o_bascule RM_Bascule_Add(o_Rided_Actor, Cv_NullVector, 1.0)
		else
			@o_bascule RM_Bascule_Add(o_Rided_Actor, cvector(0.0, 0.0, -200.0), 1.0)
			
		@o_Rided_Actor OBJ_PosSet(Proc_PNJ_Bunny_Saucer_RidedPosGet())
		@o_Rided_Actor OBJ_BankingGeneralSet(Proc_PNJ_Bunny_Saucer_RidedSightGet(), Proc_PNJ_Bunny_Saucer_RidedBankingGet())
	}

	o_last_Rided_Actor = o_Rided_Actor

	@o_bascule RM_Bascule_Add_Force_At_Pos(@o_bascule OBJ_PosGet(), cvector(0.0, 0.0, -200.0))

	for (ti_i = 0; ti_i < 4; ti_i++)
	{
		switch(ti_i)
		{
			case 0 :
				tv_pos = @o_bascule OBJ_PosGet()
				tv_pos += @o_bascule OBJ_SightGet() * 2.0
				tv_pos -= @o_bascule OBJ_HorizonGet() * 1.0
				break
			
			case 1 :
				tv_pos = @o_bascule OBJ_PosGet()
				tv_pos += @o_bascule OBJ_SightGet() * 2.0
				tv_pos += @o_bascule OBJ_HorizonGet() * 1.0
				break
				
			case 2 :
				tv_pos = @o_bascule OBJ_PosGet()
				tv_pos -= @o_bascule OBJ_SightGet() * 2.0
				tv_pos -= @o_bascule OBJ_HorizonGet() * 1.0
				break
				
			case 3 :
				tv_pos = @o_bascule OBJ_PosGet()
				tv_pos -= @o_bascule OBJ_SightGet() * 2.0
				tv_pos += @o_bascule OBJ_HorizonGet() * 1.0
				break
		}			
	
		tv_pos += @o_bascule OBJ_BankingGet()
	
		if (@o_bascule COL_RayObject_Dist(tv_pos, -@o_bascule OBJ_BankingGet(), 4.0, all, OBJ_C_IdentityFlag_Anims | OBJ_C_IdentityFlag_AI, Ci_Filter_IdentityFlag, COL_C_Ray_on_ColMap_NoCrossable))
		{
			tv_col_pos	= COL_RayObject_PosGet()

			if (o_Rided_Actor && Proc_JOY_Boost_Pressed())
				tv_col_pos += @o_bascule OBJ_BankingGet() * MATH_RandFloat(0.0, 0.2)
		
			tv_traction = tv_pos
			tv_traction -= tv_col_pos
			tf_norm = MATH_VecDotProduct(tv_traction, @o_bascule OBJ_BankingGet())
			tv_traction = @o_bascule OBJ_BankingGet()
			tv_traction *= 1.0 - (MATH_FloatMin(tf_norm, 3.0) / 3.0)
			tv_traction *= 200.0
			
			tv_pos = MATH_VecDotProduct(-DYN_SpeedGetVector() * 10.0, OBJ_HorizonGet()) * OBJ_HorizonGet()
			if (o_Rided_Actor == o_main_actor)
			{
				if (glob_joynorm_get || Proc_JOY_Boost_Pressed())
					tv_pos += MATH_VecDotProduct(DYN_SpeedGetVector(), OBJ_SightGet()) * OBJ_SightGet()
				else
					tv_pos -= MATH_VecDotProduct(DYN_SpeedGetVector(), OBJ_SightGet()) * OBJ_SightGet()
			}

			tv_pos -= MATH_VecDotProduct(tv_pos, @o_bascule OBJ_BankingGet()) * @o_bascule OBJ_BankingGet()
			tv_traction += tv_pos

			@o_bascule RM_Bascule_Add_Force_At_Pos(tv_col_pos, tv_traction)

			tv_pos = tv_col_pos
			tv_pos += @o_bascule OBJ_BankingGet() * 0.5
			if (MATH_Modulo(ti_i, 2))
			{
				@ao_wheel[ti_i] OBJ_PosSet(tv_pos)
				@ao_wheel[ti_i] OBJ_BankingGeneralSet(OBJ_SightGet(), OBJ_HorizonGet())
				DBG_RenderCylinder(tv_pos, OBJ_HorizonGet(), 0.5, color_blanc)
			}
			else
			{
				@ao_wheel[ti_i] OBJ_PosSet(tv_pos)
				@ao_wheel[ti_i] OBJ_BankingGeneralSet(OBJ_SightGet(), -OBJ_HorizonGet())
				DBG_RenderCylinder(tv_pos, -OBJ_HorizonGet(), 0.5, color_blanc)
			}
		}
		else
		{
			tv_pos -= @o_bascule OBJ_BankingGet() * 2.5
			if (MATH_Modulo(ti_i, 2))
			{
				DBG_RenderCylinder(tv_pos, OBJ_HorizonGet(), 0.5, color_blanc)
				@ao_wheel[ti_i] OBJ_PosSet(tv_pos)
				@ao_wheel[ti_i] OBJ_BankingGeneralSet(OBJ_SightGet(), OBJ_HorizonGet())
			}
			else
			{
				DBG_RenderCylinder(tv_pos, -OBJ_HorizonGet(), 0.5, color_blanc)
				@ao_wheel[ti_i] OBJ_PosSet(tv_pos)
				@ao_wheel[ti_i] OBJ_BankingGeneralSet(OBJ_SightGet(), -OBJ_HorizonGet())
			}
		}
	}
}

PNJ_Bunny_Saucer_Penching()
PNJ_Bunny_Saucer_GFX_Smoke()
PNJ_Bunny_Saucer_GFX_Burn()
PNJ_Bunny_Saucer_GFX_Halo()
PNJ_Bunny_Saucer_Spark()
PNJ_Bunny_Saucer_Shoot()

if (o_porte_G && o_porte_D)
{
	tf_coef = f_door_coef
	tf_coef *= tf_coef

	tv_pos = @o_bascule OBJ_PosGet()
	tv_pos -= @o_bascule OBJ_SightGet() * 0.4
	tv_pos -= @o_bascule OBJ_HorizonGet() * MATH_FloatBlend(1.1, 3.6, tf_coef)
	tv_pos -= @o_bascule OBJ_BankingGet() * MATH_FloatBlend(3.0, 1.0, tf_coef * 2.0)
	@o_porte_G OBJ_PosSet(tv_pos)
	@o_porte_G OBJ_BankingGeneralSet(@o_bascule OBJ_SightGet(), @o_bascule OBJ_BankingGet())
	
	tv_pos = @o_bascule OBJ_PosGet()
	tv_pos -= @o_bascule OBJ_SightGet() * 0.4
	tv_pos += @o_bascule OBJ_HorizonGet() * MATH_FloatBlend(1.1, 3.6, tf_coef)
	tv_pos -= @o_bascule OBJ_BankingGet() * MATH_FloatBlend(3.0, 1.0, tf_coef * 2.0)
	@o_porte_D OBJ_PosSet(tv_pos)
	@o_porte_D OBJ_BankingGeneralSet(-@o_bascule OBJ_SightGet(), @o_bascule OBJ_BankingGet())
}


v_last_pos = OBJ_PosGet()
#include "PNJ_Lapin_defines.var"

object	to_mount
float		tf_dist


// SORTIE ETAT ===================================================================
if( i_sort_etat )
{
	i_sort_etat = faux
	return
}

// INITIALISATION ETAT ==============================================================
if( i_etat_courant != ETAT_Lapin_GET_MOUNT )
{
	i_dernier_etat = i_etat_courant
	i_etat_courant = ETAT_Lapin_GET_MOUNT
	if( fct_last_etat )
	{
		i_sort_etat = vrai
		AI_Execute(fct_last_etat)
	}
	Lapin_DBG_Trace_String_et_Gao(DBG_Trace_Etat, "GET MOUNT", o_RideMount_Nearest)
	fct_previous_etat = fct_last_etat
	fct_last_etat = AI_TrackCurGet()
	f_time_start_etat = 0.0
	SND_RequestPlay(Ci_SND_ITEM_RunTowards_Mount)
	i_path_way_force_recompute_flag = vrai
}
else
{
	f_time_start_etat += TIME_GetDt()
}

// STIMULI ===========================================================================
o_Grabbed_Actor = LNK_ServeurGet(Ci_LNK_GRAB_EAGLE, mid_Grabbed_LNK_ID, vrai, nofunc, nofunc)
if( o_Grabbed_Actor )
	macro_change_etat("Lapin_ETAT_GRABBED")

o_Rided_Actor = LNK_ServeurGet(Ci_LNK_RIDE_ON_NMI, mid_Rided_LNK_ID, vrai, nofunc, nofunc)
if( o_Rided_Actor )
	macro_change_etat("Lapin_ETAT_RIDED")

AI_Execute("Lapin_exec_check_vision")

MACRO_CHECK_PAFS

if( Lapin_ROPE_Gogo_Mode() )
	macro_change_etat("Lapin_ETAT_GOGO")

MACRO_CHECK_CHUTE

if( i_ventilo_flag )
	macro_change_etat("Lapin_ETAT_ASPIRE")

if( o_piege )
	macro_change_etat("Lapin_ETAT_PIEGE")

if( i_Beat_flag_danse )
	macro_change_etat("Lapin_ETAT_DANSE")

if( i_Beat_flag_alert )
	macro_change_etat("Lapin_ETAT_BEAT_ALERT")

AI_Execute("Lapin_exec_update_best_interest")

// FIND A MOUNT
f_RideMount_check_delai = 0.0
AI_Execute("Lapin_exec_check_mount")
if( ! o_RideMount_Nearest )
	MACRO_GO_IDLE(0)		// plus de monture dispo !!!

AI_Execute("Lapin_exec_fight_melee")

// anti_mashing ?
AI_Execute("Lapin_exec_check_anti_mashing")
if( i_anti_mashing_flag )
	MACRO_GO_IDLE(0)

AI_Execute("Lapin_exec_check_grappin")

AI_Execute("Lapin_exec_check_shoot")


tf_dist = OBJ_SqrDist(o_RideMount_Nearest)
if( tf_dist < 25.0 )
{
	i_flag_jump = vrai
	i_flag_jump_precis = vrai
	i_flag_jump_to_ride_a_mount = vrai
	v_jump_dest_pos = Proc_PNJ_RidedPosGet(o_RideMount_Nearest)
	macro_change_etat("Lapin_ETAT_JUMP")
}

// COMPORTEMENT ===================================================================
v_way_destpos = Proc_PNJ_RidedPosGet(o_RideMount_Nearest)
i_target_territory_ID = Lapin_PROC_POS_GET_TERRITORY_ID(v_way_destpos)

// Regard ----------------------------------------------------------
i_flag_look = vrai
v_look_pos = v_way_destpos

AI_Execute("Lapin_exec_way_find")
AI_Execute("Lapin_exec_way_move")
AI_Execute("Lapin_exec_way_orientation")
AI_Execute("Lapin_exec_way_select_action")



OBJ_CapaSet(Obj_Capa_Fight, none)

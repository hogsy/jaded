#include "PNJ_Lapin_defines.var"

object	to_lapinou
object	to_rope_gao
int			ti_type
int			ti_i
int			ti_flag_tension
vector	tv_pos_A
vector	tv_pos_B


// SORTIE ETAT ===================================================================
if (i_sort_etat)
{
	if( i_GFX_Zzzz != -1 )
	{
		GFX_Del(i_GFX_Zzzz)
		i_GFX_Zzzz = -1
	}
	f_GFX_Couronne_etoiles_duree = 0.0
	i_sort_etat = faux
	return
}

// INITIALISATION ETAT ==============================================================
if (i_etat_courant != ETAT_Lapin_MORT) 
{
	i_dernier_etat = i_etat_courant
	i_etat_courant = ETAT_Lapin_MORT
	if (fct_last_etat)
	{
		i_sort_etat = vrai
		AI_Execute(fct_last_etat)
	}
	Lapin_DBG_Trace_String(DBG_Trace_Etat, "MORT")
	fct_previous_etat = fct_last_etat
	fct_last_etat = AI_TrackCurGet()
	fct_main_etat = AI_TrackCurGet()		// MAIN ETAT !!!!
	f_time_start_etat = 0.0
	f_time_sous_mode = 0.0
	
	Lapin_ACTION_Set(Action_Mort)
	
	if( ! Mort_SeReleve && Mort_Destruction )
	{
		Lapin_OBBOX_Init(0.75)
		Lapin_TARGETTING(faux)
		
		if( @get_rayman PROC_RM_Current_HotSpot_Get() == OBJ_Me() )
			@get_global i_grappin_hotspot_nmi_died = vrai		// cancel grappin
	}
	else
		f_GFX_Couronne_etoiles_duree = 2.0
	
	Lapin_Del_Interest()
}
else
{
	f_time_start_etat += TIME_GetDt()
	f_time_sous_mode += TIME_GetDt()
}

// ANALYSE ===================================================================================================
o_Grabbed_Actor = LNK_ServeurGet(Ci_LNK_GRAB_EAGLE, mid_Grabbed_LNK_ID, vrai, nofunc, nofunc)
if( o_Grabbed_Actor )
	macro_change_etat("Lapin_ETAT_GRABBED")

if( Mort_SeReleve )		// compter avant de se relever
{
	o_Rided_Actor = LNK_ServeurGet(Ci_LNK_RIDE_ON_NMI, mid_Rided_LNK_ID, vrai, nofunc, nofunc)
	if( o_Rided_Actor )
		macro_change_etat("Lapin_ETAT_RIDED")
}

AI_Execute("Lapin_exec_check_paf")
if( i_flag_paf )
	macro_change_etat("Lapin_ETAT_PAF")

if( Lapin_ROPE_Gogo_Mode() )
	macro_change_etat("Lapin_ETAT_GOGO")

// COMPORTEMENT ===================================================================================================
DYN_FlagsSet(DYN_C_OptimizeColDisable, none)		// tomber si on active un piège sous mes pieds

// orientation
to_rope_gao = Lapin_ROPE_Get_Gao(tv_pos_A, tv_pos_B, ti_flag_tension)
if( to_rope_gao && ti_flag_tension )
	v_force_sight = OBJ_PosGet() - @to_rope_gao OBJ_PosGet()
else
	v_force_sight = OBJ_SightGet()
AI_Execute("Lapin_exec_way_orientation")

// anti mashing ???
if( Mort_SeReleve && Config_Lapin == Config_Lapin_Terminator )
{
	AI_Execute("Lapin_exec_check_anti_mashing")
	if( i_anti_mashing_flag )
	{
		goto RESPAWN
	}
}

// GFX Zzzz
if( Mort_SeReleve && f_Mort_duree < (Mort_SeReleve_Duree - 2.0) && ! f_GFX_Couronne_etoiles_duree )
{
	ACT_ActionSet(Action_Attente_Dodo)
	@o_canal_tete OBJ_MorphProgSet(1, 2.0)
	PROC_SFX_ZZZ(i_GFX_Zzzz, @o_canal_tete OBJ_PosGet(), 1.0 * OBJ_ZoomGet())
}
else
{
	if( i_GFX_Zzzz != -1 )
	{
		GFX_Del(i_GFX_Zzzz)
		i_GFX_Zzzz = -1
	}
}

if( f_Mort_duree < MATH_FloatMin(Mort_SeReleve_Duree - 0.25, 5.0) )
	return

// DUPLICATION ?
if( Mort_Duplication && ! i_Mort_Duplication_done )
{
	i_Mort_Duplication_done = vrai
	switch( Type )
	{
		case Lapin_Type_Petit :
			ti_type = -1
			break
		case Lapin_Type_Moyen :
			ti_type = Lapin_Type_Petit
			break
		case Lapin_Type_Geant :
			ti_type = Lapin_Type_Moyen
			break
	}
	if( ti_type != -1 )
	{
		for( ti_i = 0; ti_i < 2; ti_i++ )
		{
			to_lapinou = OBJ_Duplicate(OBJ_PosGet() + cvector(MATH_RandFloat(-2.0,2.0),MATH_RandFloat(-2.0,2.0), 0.2))
			@get_PNJ_Lapin_path to_lapinou Type = ti_type
			@to_lapinou OBJ_FlagInvisibleSet(vrai)
		}
	}
}

if( Mort_SeReleve )
{
	if( f_Mort_duree < Mort_SeReleve_Duree )
		return
}
else
{
	if( f_Mort_duree < 5.0 )
		return
}


// MORT POUR DE VRAI OU POUR DE FAUX ?
if( Mort_SeReleve )
{
	RESPAWN:
	if( ACT_ActionGet() == Action_Paf_fort_face_get_up )
	{
		if( ACT_ActionFinished() )
		{
			Lapin_MORT_PourDeFaux()
			MACRO_GO_IDLE(0)
		}
	}
	else
	{
		Lapin_ACTION_Set(Action_Paf_fort_face_get_up)
		if( i_anti_mashing_flag )
			Lapin_ACTION_RandomFreqSet(2.0, 2.5)
	}
}
else if( Mort_Destruction )
{
	if( Config_Lapin == Config_Lapin_Terminator )
	{
		// TERMINATOR => EXPLOSION
		if( ! i_Mort_Destruction_SND_flag )
		{
			i_Mort_Destruction_SND_flag = vrai
			f_time_sous_mode = 0.0
			SND_RequestPlay(Ci_SND_TERMINATOR_Explose)
			PROC_SFX_EXPLOSION_LAPIN(@o_canal_tete OBJ_PosGet(), 3.0)
			i_invisible_flag = vrai
			i_Mort_Destruction_flag = vrai
			i_flag_zde_fight_enable = vrai
		}
		else if( f_time_sous_mode > 1.5 )
		{
			MACRO_LAPIN_DESTROY
		}
	}
	else
	{
		// LAPIN => S'ENTERRE
		if( ACT_ActionGet() == Action_Paf_fort_face_get_up )
		{
			if( ACT_ActionFinished() )
			{
				i_taupe_destruction_0hp = vrai
				macro_change_etat("Lapin_ETAT_TAUPE")
			}
		}
		else
			Lapin_ACTION_Set(Action_Paf_fort_face_get_up)
	}
}

#include "PNJ_Lapin_defines.var"

int			ti_frame
object	to_target
object	to_gao
vector	tv_grav


// SORTIE ETAT ===================================================================
if (i_sort_etat)
{
	if( ! i_Rided_attack_flag )
		f_melee_delai = Atk_Poing_Delai + MATH_RandFloat(1.0, 2.0)
	i_Rided_attack_flag = faux
	i_Counter_attack_flag = faux
	i_Grappin_Control_attack_flag = faux
	i_anti_mashing_attack_flag = faux
	i_Atk_Activate_Explosive_Box_flag = faux
	i_flag_jump_to_attack = faux
	i_flag_jump_in_back = faux
	Lapin_EXPRESSION(Cf_Expr_Mouth_Closed)
	i_sort_etat = faux
	return
}

// INITIALISATION ETAT ==============================================================
if (i_etat_courant != ETAT_Lapin_MELEE) 
{
	i_dernier_etat = i_etat_courant
	i_etat_courant = ETAT_Lapin_MELEE
	if (fct_last_etat)
	{
		i_sort_etat = vrai
		AI_Execute(fct_last_etat)
	}
	Lapin_DBG_Trace_String(DBG_Trace_Etat, "MELEE")
	fct_previous_etat = fct_last_etat
	fct_last_etat = AI_TrackCurGet()
	f_time_start_etat = 0.0
	Lapin_EXPRESSION(Cf_Expr_Mouth_Middle)
	
	if( i_flag_jump_to_attack )
	{
		Lapin_ACTION_Set(Action_Fight_Melee_CoupDroit)
		ANI_CurrentFrameSet(0,7)
		tv_grav = cvector(0,0,-1)
	}
	else
	{
		tv_grav = Cv_DYN_Gravity
//		Lapin_ACTION_Set(Action_Fight_Annonce)
		if( Lapin_ITEM_Has_Gourdin() )
			Lapin_ACTION_Set(Action_Fight_Gourdin)
		else
			Lapin_ACTION_Set(Action_Fight_Melee_CoupDroit)
		
//		if( i_melee_force_action )
//			Lapin_ACTION_Set(i_melee_force_action)
//		else if( Lapin_ACTION_Family_Get() == Action_Family_Nage )
//			Lapin_ACTION_Set(Action_Nage_attack)
//		else if( Lapin_ITEM_Has_Gourdin() )
//			Lapin_ACTION_Set(Action_Fight_Gourdin)
//		else
//		{
////			if( i_melee_last_action == Action_Fight_Melee_CoupDroit )
////				Lapin_ACTION_Set(Action_Fight_Melee_CoupRevers)
////			else
//				Lapin_ACTION_Set(Action_Fight_Melee_CoupDroit)
//			i_melee_last_action = ACT_ActionGet()
//		}
////		Lapin_ACTION_RandomFreqSet(0.8, 1.2)
	}
	
	if( ACT_ActionGet() == Action_Fight_Counter_Attack )
		SND_RequestPlay(Ci_SND_FIGHT_Counter_Attack)
	else
		SND_RequestPlay(Ci_SND_FIGHT_Melee_Try_to_Hit)
	i_frappe_target_nb = 0	// new hit
	i_melee_force_action = 0
	
	if( OBJ_FlagsIdentityTest(OBJ_C_IdentityFlag_Dyna) )
		DYN_GravitySet(tv_grav)
}
else
{
	f_time_start_etat += TIME_GetDt()
}


// ANALYSE ===================================================================================================
o_Grabbed_Actor = LNK_ServeurGet(Ci_LNK_GRAB_EAGLE, mid_Grabbed_LNK_ID, vrai, nofunc, nofunc)
if( o_Grabbed_Actor )
	macro_change_etat("Lapin_ETAT_GRABBED")

if( ! i_Rided_attack_flag )
{
	o_Rided_Actor = LNK_ServeurGet(Ci_LNK_RIDE_ON_NMI, mid_Rided_LNK_ID, vrai, nofunc, nofunc)
	if( o_Rided_Actor )
		macro_change_etat("Lapin_ETAT_RIDED")
}

MACRO_CHECK_PAFS

if( Lapin_ROPE_Gogo_Mode() )
	macro_change_etat("Lapin_ETAT_GOGO")

if( ! i_flag_jump_to_attack )
{
	MACRO_CHECK_CHUTE
}

if( i_ventilo_flag )
	macro_change_etat("Lapin_ETAT_ASPIRE")

if( o_piege )
	macro_change_etat("Lapin_ETAT_PIEGE")

ti_frame = ANI_CurrentFrameGet(0)
switch( ACT_ActionGet() )
{
	case Action_Fight_Annonce :
		if( ACT_ActionFinished() )
		{
			if( Lapin_ITEM_Has_Gourdin() )
				Lapin_ACTION_Set(Action_Fight_Gourdin)
			else
				Lapin_ACTION_Set(Action_Fight_Melee_CoupDroit)
		}
		break
	case Action_Fight_Gourdin :
		if( ti_frame >= 3 && ti_frame <= 10 )
			i_flag_zde_fight_enable = vrai
		break
	case Action_Fight_Melee_CoupDroit :
		if( ti_frame >= 7 && ti_frame <= 12 )
			i_flag_zde_fight_enable = vrai
		break
	default:
		DBG_Error("???")
		break
}

//if( i_flag_zde_fight_enable )
//	Str_DisplayTextOnce("\cFF00\\h0.1\HIT", MACRO_2D)

// RIDED ======================================================================================================
if( o_Rided_Actor )
{
	if( ACT_ActionFinished() )
		MACRO_GO_IDLE(0)
	return
}

// ANALYSE ===================================================================================================

AI_Execute("Lapin_exec_check_vision")

if( ! o_Commander_cur )
{
	if ( ! MSG_GlobalIsValid(mid_best_interet) || i_perceived_best_actor_index == -1)
		MACRO_GO_IDLE(0)
}

AI_Execute("Lapin_exec_update_best_interest")

AI_Execute("Lapin_exec_check_grappin")

AI_Execute("Lapin_exec_check_shoot")

Lapin_INTERET_Attack()

ai_perceived_seen[i_perceived_best_actor_index] = vrai

// COMPORTEMENT ==============================================================================================

to_target = EVENT_InteretTargetGet(mid_best_interet)

if( ACT_ActionFinished() )
{
	// FUITE / ATTAQUE ? ------------------------------------
	switch( Fuite_Apres_Attaque )
	{
		case Ci_Fuite_Apres_Attaque_Toujours :
		case Ci_Fuite_Apres_Attaque_Poing :
			Lapin_FUITE(to_target)
			break
	}
	if( i_Grappin_Control_attack_flag )
		i_Grappin_Control_flag = vrai		// revenir en grappin control à la fin de l'attaque et uniquement pour cette exit de mode
	MACRO_GO_IDLE(0)
}

if( i_Atk_Activate_Explosive_Box_flag )
	to_gao = Lapin_Get_Target(to_target)		// frapper la bombox !!!
else
	to_gao = to_target

v_way_destpos = @to_gao OBJ_PosGet()
i_target_territory_ID = Lapin_PROC_POS_GET_TERRITORY_ID(v_way_destpos)

v_force_sight = v_way_destpos - OBJ_PosGet()
AI_Execute("Lapin_exec_way_orientation")

// Regard ----------------------------------------------------------
//i_flag_look = vrai
//v_look_pos = v_way_destpos


OBJ_CapaSet(Obj_Capa_Fight, none)

#include "PNJ_Lapin_defines.var"

object	to_target
vector	tv_temp
float		tf_norm


// SORTIE ETAT ===================================================================
if( i_sort_etat )
{
	i_Counter_flag = faux
	i_Counter_DodgeOnly_flag = faux
	f_Counter_last_time = TIME_Get()
	Lapin_EXPRESSION(Cf_Expr_Mouth_Closed)
	i_sort_etat = faux
	return
}

// INITIALISATION ETAT ==============================================================
if( i_etat_courant != ETAT_Lapin_COUNTER )
{
	i_dernier_etat = i_etat_courant
	i_etat_courant = ETAT_Lapin_COUNTER
	if( fct_last_etat )
	{
		i_sort_etat = vrai
		AI_Execute(fct_last_etat)
	}
	Lapin_DBG_Trace_String(DBG_Trace_Etat, "COUNTER")
	fct_previous_etat = fct_last_etat
	fct_last_etat = AI_TrackCurGet()
	f_time_start_etat = 0.0
		
	Lapin_ACTION_Set(Action_Fight_Counter_Dodge)
//	SND_RequestPlay(Ci_SND_FIGHT_Counter_Dodge)
	
	// POSITION DE CONTRE ATTAQUE ?
	to_target = EVENT_InteretTargetGet(mid_best_interet)
	o_Counter_actor = to_target
	v_Counter_start_pos = OBJ_PosGet()
	v_Counter_start_nmi_pos = @to_target OBJ_PosGet()
	
	if( i_Counter_DodgeOnly_flag )
		f_Counter_angle = 1.0
	else
		f_Counter_angle = 0.5	//MATH_RandFloat(0.5,1.0)
	f_Counter_coef = 1.0	// compute dest pos
	Lapin_COUNTER_PositionCompute()
	v_Counter_dest_pos = v_Counter_next_pos
	f_Counter_coef = 0.0	// début mvt
	Lapin_EXPRESSION(Cf_Expr_Mouth_Middle)
}
else
{
	f_time_start_etat += TIME_GetDt()
}

// STIMULI ===========================================================================
o_Grabbed_Actor = LNK_ServeurGet(Ci_LNK_GRAB_EAGLE, mid_Grabbed_LNK_ID, vrai, nofunc, nofunc)
if( o_Grabbed_Actor )
	macro_change_etat("Lapin_ETAT_GRABBED")

o_Rided_Actor = LNK_ServeurGet(Ci_LNK_RIDE_ON_NMI, mid_Rided_LNK_ID, vrai, nofunc, nofunc)
if( o_Rided_Actor )
	macro_change_etat("Lapin_ETAT_RIDED")

AI_Execute("Lapin_exec_check_vision")

MACRO_CHECK_PAFS

if( Lapin_ROPE_Gogo_Mode() )
	macro_change_etat("Lapin_ETAT_GOGO")

MACRO_CHECK_CHUTE

if( i_ventilo_flag )
	macro_change_etat("Lapin_ETAT_ASPIRE")

if( o_piege )
	macro_change_etat("Lapin_ETAT_PIEGE")

AI_Execute("Lapin_exec_update_best_interest")

if( ! MSG_GlobalIsValid(mid_best_interet) )
	MACRO_GO_IDLE(0)

// COMPORTEMENT ===================================================================
f_Counter_coef += MATH_FloatMin(1.0 - f_Counter_coef, 1 * TIME_GetDt())
Lapin_COUNTER_PositionCompute()
tv_temp = OBJ_PosGet() - v_Counter_dest_pos
DBG_RenderVector(OBJ_PosGet(), v_Counter_dest_pos - OBJ_PosGet(), color_bleu)
tf_norm = MATH_VecNorm(tv_temp)
if( f_time_start_etat && ( tf_norm < 0.5 || ACT_ActionFinished()) )
{
	if( i_Counter_DodgeOnly_flag )
	{
		// roulade to dodge !
		MACRO_GO_IDLE(0)
	}
	else
	{
		// roulade to attack !
		i_melee_force_action = Action_Fight_Counter_Attack
		i_Counter_attack_flag = vrai	
		if( Fuite_Apres_ContreAtk )
			Lapin_FUITE(o_Counter_actor)
		macro_change_etat("Lapin_ETAT_MELEE")
	}
}
else
{
	v_way_destpos = v_Counter_next_pos
	tv_temp = v_way_destpos - OBJ_PosGet()
	MATH_VecSetNormalize(tv_temp)
	i_target_territory_ID = Lapin_PROC_POS_GET_TERRITORY_ID(v_way_destpos)
	
	// Regard ----------------------------------------------------------
	i_flag_look = vrai
	v_look_pos = v_way_destpos
	
	v_traction_joy = tv_temp * 7.5
	v_force_sight = tv_temp
	f_orient_speed = 10.0
	AI_Execute("Lapin_exec_way_orientation")
}

OBJ_CapaSet(Obj_Capa_Fight, none)

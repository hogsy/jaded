#include "PNJ_Lapin_defines.var"

int				ti_i
int				ti_k
int				ti_flag_look_at_main_actor
int				ti_flag_dead_zone
int				ti_trame_num

float			tf_coef
float			tf_norm
float			tf_dt
float			tf_dist
float			tf_angle
float			tf_sign
float			tf_dot_product
float			tf_dist_min
float			tf_A
float			tf_B
float			tf_composante_Z

vector		tv_dest_pos
vector		tv_axis
vector		tv_bone_to_dest
vector		tv_temp

object		to_target
object		to_bone
object		to_tete

#define Cf_speed				12.0

if (i_frame_nb < 10)
	tf_dt = 1000.0
else
	tf_dt = TIME_GetDt()

v_look_pivot_pos = @ao_head_bones[0] OBJ_PosGet()

// LOOK BEST INTERET
switch(i_etat_courant)
{
	case ETAT_Lapin_FIGHT :
	case ETAT_Lapin_MELEE :
	case ETAT_Lapin_DODGE :
	case ETAT_Lapin_PROTECT :
	case ETAT_Lapin_GRAPPIN_CONTROL :
	case ETAT_Lapin_COUNTER :
	case ETAT_Lapin_BULLRUSH :
	case ETAT_Lapin_USE_POWERMOUNT :
		i_flag_look_best_interet = vrai
		break
	case ETAT_Lapin_USE_ITEM :
		switch( i_item_ID )
		{
			case RM_WEAPON_TYPE_GUN :
			case RM_WEAPON_TYPE_GRENADE :
				i_flag_look_best_interet = vrai
				break
		}
		break
	case ETAT_Lapin_JUMP :
		if( ! i_flag_jump_taupe && ! i_flag_jump_to_att_wp_anim && ! i_flag_jump_to_ride_a_mount && ! i_flag_jump_to_danse )
			i_flag_look_best_interet = vrai
		break
}

// LOOK INTERDIT
if( o_Rided_Actor )
{
	i_flag_look = faux
	i_flag_look_best_interet = faux
	if( MATH_FloatNullEpsilon(glob_joynorm_get) )
		v_look_pos = OBJ_PosGet()
	else
		v_look_pos = OBJ_PosGet() + (5.0 * glob_joyvector_get)
}
else if( o_Grabbed_Actor || i_is_in_water )
{
	i_flag_look = faux
	i_flag_look_best_interet = faux
}
switch( i_etat_courant )
{
	case ETAT_Lapin_GOGO :
	case ETAT_Lapin_TAUPE :
	case ETAT_Lapin_PIEGE :
	case ETAT_Lapin_BEAT_DANSE :
	case ETAT_Lapin_BEAT_ALERT :
		i_flag_look = faux
		i_flag_look_best_interet = faux
		break
	case ETAT_Lapin_USE_ITEM :
		switch( i_item_ID )
		{
			case RM_WEAPON_TYPE_CLE :
			case RM_WEAPON_TYPE_CAROTTE :
				i_flag_look = faux
				i_flag_look_best_interet = faux
				break
		}
		break
}

if (i_flag_look_best_interet && i_perceived_best_actor_index != -1)
{
	i_flag_look = vrai

	to_target = ao_perceived_actor[i_perceived_best_actor_index]

	to_bone = @to_target ANI_CanalObjectGet(Anim_Canal_Tete)
	if (to_bone)
		v_look_pos = @to_bone OBJ_PosGet()
	else
		v_look_pos = @to_target OBJ_PosGet()
}


// OPTIM =========================================
if( i_LOD_courant == Ci_Lapin_LOD_Invisible )
	i_flag_look = faux
tf_coef = OBJ_LodVisGet()
if (tf_coef < 0.05)
	i_flag_look = faux
if ( ! tf_coef )
	f_look_blend_coef = 0.0
// OPTIM =========================================


if (i_flag_look)
	f_look_blend_coef += MATH_FloatMin(1.0 - f_look_blend_coef, 4.0 * tf_dt)
else
	f_look_blend_coef -= MATH_FloatMin(f_look_blend_coef, 4.0 * tf_dt)

if (f_look_blend_coef)
{
	if (i_flag_look)
	{	
		if (tf_dt < 1.0)
		{
			tv_temp = v_look_pos - v_last_look_pos
			if (MATH_VecDotProduct(tv_temp, tv_temp) > 4.0)
				f_look_angle_blend_speed = 0.0
		}
		
		tv_temp = v_look_pos - v_look_pivot_pos
		tf_composante_Z = MATH_VecDotProduct(tv_temp, OBJ_BankingGet())
		tf_composante_Z *= MATH_FloatMax(MATH_VecDotProduct(- @ao_head_bones[1] OBJ_SightGet(), MATH_VecNormalize(tv_temp)), 0.0)
		tv_temp -=  tf_composante_Z * OBJ_BankingGet()
		
		//DBG_RenderVector(v_look_pivot_pos, tv_temp, color_jaune)
		
		tf_sign = MATH_FloatSign(MATH_VecDotProduct(tv_temp, OBJ_HorizonGet()))
		if (MATH_FloatSign(MATH_VecDotProduct(tv_temp, OBJ_SightGet())) < 0.0 && tf_sign != MATH_FloatSign(MATH_VecDotProduct(v_look_axis, OBJ_HorizonGet())))
			tv_temp = MATH_VecCrossProduct(OBJ_BankingGet(), v_look_axis) * tf_sign
		
		//DBG_RenderVector(v_look_pivot_pos, tv_temp, color_jaune)
		
		v_look_axis = MATH_VecBlendRotate(v_look_axis, tv_temp, f_look_angle_blend_speed * tf_dt)
		v_look_axis = MATH_LIB_VectorInPrisme(v_look_axis, OBJ_SightGet(), OBJ_BankingGet(), 2.6)
	}
	else
	{
		v_look_axis = MATH_VecBlendRotate(OBJ_SightGet() * f_look_axis_length, v_look_axis, f_look_blend_coef)
	}
	
	v_bezier_head_look_pos = v_look_pivot_pos
	v_bezier_head_look_pos += v_look_axis
//	DBG_RenderVector(v_look_pivot_pos, v_look_axis, color_jaune)
	AI_Execute("Lapin_exec_neck_IK")
}
else
{
	v_look_axis = OBJ_SightGet() * f_look_axis_length

	v_look_pos = v_look_pivot_pos
	v_look_pos += v_look_axis

	v_bezier_head_look_pos = v_look_pivot_pos
	v_bezier_head_look_pos += v_look_axis
}


v_last_look_pos = v_look_pos
v_look_head_pos = MATH_VecGlobalToLocal(@ao_head_bones[0] OBJ_PosGet() - OBJ_PosGet())
v_look_virtual_sight = @ao_head_bones[0] OBJ_SightGet()		// spécial lapin
v_look_banking = MATH_VecGlobalToLocal(v_look_virtual_sight)


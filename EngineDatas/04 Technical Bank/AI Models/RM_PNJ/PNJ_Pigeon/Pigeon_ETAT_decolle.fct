#include "PNJ_Pigeon_defines.var"

vector				tv_pos
vector				tv_travel_sight
vector				tv_v1
vector				tv_v2

float 					tf_cos


// SORTIE ETAT 	===================================================================
if( i_sort_etat )
{
	i_sort_etat = faux 
	return
}

// INIT ETAT 	=====================================================================
if( i_etat_courant != ETAT_PIGEON_DECOLLE)
{
	i_etat_courant = ETAT_PIGEON_DECOLLE
	if( fct_last_etat )
	{
		i_sort_etat = vrai
		AI_Execute(fct_last_etat)
	}
	fct_last_etat = AI_TrackCurGet()
	
	DYN_GravitySet(Cv_NullVector)
	v_travel_start = OBJ_PosGet()
	v_travel_start_sight = OBJ_SightGet()
	
	if (i_bored_by_someone)
	{
		if (MATH_VecSquareNorm(v_boot_camp - OBJ_PosGet()) > (2.0 * f_boot_camp_size * f_boot_camp_size))
			v_travel_end = v_boot_camp
		else			
			v_travel_end = v_travel_start + (MATH_RandFloat(5.0, 7.0) * OBJ_SightGet())
		v_travel_end.x += MATH_RandFloat(-2.0, 2.0)
		v_travel_end.y += MATH_RandFloat(-2.0, 2.0)
		v_travel_end.z += MATH_RandFloat(1.0, 2.0)
		v_travel_end_sight = -v_travel_start_sight
	}
	else
	{
		o_travel_wp_from = WAY_WPNearestOfOBJ(n_travel_network, all, none, none)
		v_travel_end = @o_travel_wp_from OBJ_PosGet()
		v_travel_end_sight = @o_travel_wp_from OBJ_SightGet()
	}
	
	f_travel_coef = 0.0
	f_travel_length = MATH_VecNorm(v_travel_end - v_travel_start)
	f_battement = 0.0
	i_battement_sens = 0

	f_time_start_etat = 0.0
} 
else
{
	f_time_start_etat += TIME_GetDt()
}

// STIMULI ========================================================================
AI_Execute("Pigeon_exec_DetectPaf")

f_flying_speed = MATH_FloatBlend(f_flying_speed, f_flying_speed_normal, 0.5 * TIME_GetDt())

o_rided_actor = LNK_ServeurGet(Ci_LNK_RIDE_ON_NMI, mid_Rided_LNK_ID, vrai, nofunc, nofunc)

// COMPORTEMENT ================================================================
//MATH_LIB_Bezier_Display(30, v_travel_start, v_travel_start + (v_travel_start_sight * 2.0), v_travel_end + (v_travel_end_sight * 3.0), v_travel_end, color_blanc)
DBG_RenderVector(v_travel_start, v_travel_start_sight, color_bleu)
DBG_RenderVector(v_travel_end, v_travel_end_sight, color_bleu)
tv_pos = MATH_LIB_Bezier_Pos_Get(f_travel_coef, v_travel_start, v_travel_start + (v_travel_start_sight * 2.0), v_travel_end + (v_travel_end_sight * 3.0), v_travel_end, tv_travel_sight)

// Battement d'ailes
if (f_battement >= 1.0)
	i_battement_sens = 1
else if (f_battement <= -1.0)
	i_battement_sens = 0

tv_v1 = OBJ_PosGet() - tv_pos
tv_v1.z = 0.0
if (MATH_VecNullEpsilon(tv_v1))
	tf_cos = 1.5
else
{
	tf_cos = (0.3 - MATH_VecNorm(tv_v1)) * 5.0
	tf_cos = MATH_FloatLimit(tf_cos, -1.5, 1.5)
}

if (!i_battement_sens)
	f_battement += tf_cos * 10.0 * TIME_GetDt() / (1.0 - f_battement)
else
	f_battement -= tf_cos * 10.0 * TIME_GetDt()
f_battement = MATH_FloatLimit(f_battement, -1.0, 1.0)
tv_pos.z += f_battement / 12.0

OBJ_PosSet(tv_pos)

tv_travel_sight = MATH_VecBlend(OBJ_SightGet(), tv_travel_sight, 2.0 * TIME_GetDt())
tf_cos = MATH_VecDotProduct(OBJ_BankingGet(), Cv_VerticalVector)
tv_v1 = MATH_VecBlend(OBJ_BankingGet(), Cv_VerticalVector, TIME_GetDt())
if (tf_cos < Cf_Cos20)
{
	if (f_battement > 0.0)
		tv_v1 = MATH_VecBlend(tv_v1, tv_travel_sight, f_battement / 20.0)
	OBJ_BankingGeneralSet(tv_travel_sight, tv_v1)
}
else
{
	if (f_battement > 0.0)
		tv_travel_sight = MATH_VecBlend(tv_travel_sight, -Cv_VerticalVector, f_battement / 20.0)
	OBJ_SightGeneralSet(tv_travel_sight, tv_v1)
}

f_factor = MATH_FloatLimit(f_factor + (TIME_GetDt() / 4.0), 0.0, 1.0)
f_travel_coef += (TIME_GetDt() / f_travel_length) * f_flying_speed * f_factor

tv_v1 = v_travel_end - OBJ_PosGet()
//tv_v2 = @o_travel_wp_to OBJ_PosGet() - tv_pos
if (MATH_VecNullToler(tv_v1, 0.01))
//if (f_travel_coef >= 1.0)
{
	if (i_bored_by_someone || (o_rided_actor != nobody))
	{
		i_bored_by_someone = 0
		AI_TrackCurChangeNow("Pigeon_ETAT_atterit")
	}
	else
		AI_TrackCurChangeNow("Pigeon_ETAT_vole")
}

DBG_RenderVector(OBJ_PosGet(), v_travel_end - OBJ_PosGet(), color_bleu)
DBG_RenderVector(v_travel_start, OBJ_PosGet() - v_travel_start, color_bleu)

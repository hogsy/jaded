#include "ROOT/EngineDatas/04 Technical Bank/AI Models/Global Library/menu_constant.var"
#include "Interface_MenuInGame_defs.var"

int			ti_Line
int			ti_Texte
int			ti_Col
int			ti_Count
float		tf_Height, tf_Val
vector	tv_Pos
int			ti_Save

if (INTMIG_EnterPage_Start)
	IntMIG_InitDemoMenuEntry()

if (!mo_Texte)
	return
if (!IntMIG_SetPageParams())
	return

IntMIG_ResetTexte( mi_PageString, 30 )

if ( INTMIG_EnterPage_Start )
	mf_Scroll = 0

ti_Count = @"univ" MENUDEMO_NbMap

if ( mf_Scroll )
{
	mf_Scroll *= 0.9
	if ( MATH_AbsFloat( mf_Scroll ) < 0.001)
		mf_Scroll = 0
}

tf_Height = 0.11
tv_Pos = cvector( 0.9, 0.5, 0 )
ti_Texte = 0

for ( ti_Line = 0; ti_Line < ti_Count ; ti_Line++ )
{
	tv_Pos.y = (ti_Line - mi_Cur) * 0.07
	tv_Pos.y += mf_Scroll
	

	if ( MATH_AbsFloat( tv_Pos.y ) > 0.3) continue
	if ( MATH_AbsFloat( tv_Pos.y ) > 0.05) 
	{
		tf_Val = (0.3 - MATH_AbsFloat( tv_Pos.y )) * 4 
		ti_Col = COLOR_Blend( 0, mi_ColorBlue, tf_Val * tf_Val )
	}
	else	
	{
		tf_Val = (0.05 - MATH_AbsFloat( tv_Pos.y )) * 20
		ti_Col = COLOR_Blend( mi_ColorBlue, mi_ColorWhite, tf_Val )

	}
		
	tv_Pos.y += 0.43

	IntMIG_DisplayDemoMenuEntry( ti_Texte, ti_Line, tv_Pos, ti_Col, 0 )
	ti_Texte++
}


@mo_Texte OBJ_TextSet( mi_PageString + ti_Texte, "\p16\c Options" )
IntMIG_CreateText_End( mi_PageString + ti_Texte, cvector( 0.9, 0.87, 0), 0.0, 6, -1, mi_ColorLine, 0, 0)
@mo_Texte OBJ_TextSet( mi_PageString + ti_Texte +1, "\p16\= Quit Demo" )
IntMIG_CreateText_End( mi_PageString + ti_Texte + 1, cvector( 0.9, 0.95, 0), 0.0, 6, -1, mi_ColorLine, 0, 0)

IntMIG_RestorePageParams()

if ( mi_Exit || mi_ChangePage || mi_Cheat)
	return

if (mi_CheatCode != 0)
{
	if (mi_CheatCode == CC_MenuDemo1)	{	@"univ"  MENU_NZ = 1 @"univ" MENUDEMO_QuitTimer = 0 IntMIG_InitDemoMenuEntry() }
	if (mi_CheatCode == CC_MenuDemo2)	{	@"univ"  MENU_NZ = 2 @"univ" MENUDEMO_QuitTimer = 0 IntMIG_InitDemoMenuEntry() }
	if (mi_CheatCode == CC_MenuDemo3)	{	@"univ"  MENU_NZ = 3 @"univ" MENUDEMO_QuitTimer = 0 IntMIG_InitDemoMenuEntry() }
	if (mi_CheatCode == CC_MenuDemo4)	{	@"univ"  MENU_NZ = 4 @"univ" MENUDEMO_QuitTimer = 0 IntMIG_InitDemoMenuEntry() }
	if (mi_CheatCode == CC_MenuDeBase)	{ 	@"univ" MENUDEMO_QuitTimer = 0 OBJ_CapaSet( OBJ_Capa_8, 0 )  }
}
	
ti_Save = mi_Cur
IntMIG_UpdateCur_Vertical( ti_Count, 0 )
if (ti_Save != mi_Cur )
{
	mf_Scroll = 0.07 * (mi_Cur - ti_Save)
	IntMIG_PlaySound( INTMIG_SOUND_BIGSELECTION )
}
	
if ( joy_valide )
{
	IntMIG_PlaySound( INTMIG_SOUND_BIGVALIDE )
	@"univ" PROG_i_MapReplay = faux
	INTMIG_LoadingPage( @"univ" MENUDEMO_Map[ mi_Cur ], @"univ" MENUDEMO_ES[ mi_Cur ] )
}

if (IO_ButtonJustPressed( joy_button_Select ) )
{
	IntMIG_PlaySound( INTMIG_SOUND_MENUOUT )
	IO_DemoSeti( 6 )
}
	
if (IO_ButtonJustPressed( joy_button_C ) )
{
	IntMIG_PlaySound( INTMIG_SOUND_MENUIN )
	INTMIG_ChangePage( "IntMIG_Page_OptStart", 0 )
}	



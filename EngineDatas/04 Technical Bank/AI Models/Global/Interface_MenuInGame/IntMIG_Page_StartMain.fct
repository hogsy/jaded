#include "Interface_MenuInGame_defs.var"

int			ti_Count
int			ti_SaveCur

#define PageStartMain_LineDiff			0.08
#define PageStartMain_PosLine( i ) cvector( 0.8, 0.4 + (PageStartMain_LineDiff * i) , 0) 
#define PageStartMain_PosSouligne( i ) cvector( 0.8, 0.43 + (PageStartMain_LineDiff * i) , 0) 

if (!mo_Texte)
	return
if (!IntMIG_SetPageParams())
	return
	
IntMIG_Debug_SlotTimeDisplay( @"univ" MENU_ProfileIndex, cvector( 0.2, 0.5, 0)  )

if ( @"univ" Cheat_i_Flags & (CHEAT_MenuCheatFinal | CHEAT_UnlockAllMaps ) )
{
	if ( (@"univ" PROG_i_Map == INTMIG_FIRSTMAPID ) && (@"univ" PROG_i_ES == -1 ) )
		@"univ" PROG_i_ES = INTMIG_FIRSTMAPES
}
@"univ" PROG_i_FirstPlay = 0
if ( (@"univ" PROG_i_Map == INTMIG_FIRSTMAPID ) && (@"univ" PROG_i_ES == -1 ) )
	@"univ" PROG_i_FirstPlay = 1

ti_Count = 2
if ( !@"univ" PROG_i_FirstPlay)
	ti_Count++
if ( @"univ" Cheat_i_Flags & CHEAT_MenuCheatFinal )
	ti_Count++

if (INTMIG_EnterPage_Start || mi_Page_Refresh)
{
	mi_Page_Refresh = 0
	IntMIG_CheckCur( ti_Count )
	IntMIG_ResetTexte( mi_PageString, 30 )
	IntMIG_Frame_Start()
	IntMIG_SetText_Page( mi_PageString, INTMIG_TXG, INTMIG_TEXT_StartMenu, PageStartMain_PosLine(0) , 0.0, 6, -1, INTMIG_Color_NotSel, 0, 1 )
	IntMIG_Frame_Add( mi_PageString )
	IntMIG_SetText_Page( mi_PageString + 1, INTMIG_TXG, INTMIG_TEXT_StartMenu + 1, PageStartMain_PosLine(1) , 0.0, 6, -1, INTMIG_Color_NotSel , 0, 1 )
	IntMIG_Frame_Add( mi_PageString + 1 )
	IntMIG_SetText_Page( mi_PageString + 2, INTMIG_TXG, INTMIG_TEXT_StartMenu + 2, PageStartMain_PosLine(2) , 0.0, 6, -1, INTMIG_Color_NotSel, 0, 1 )
	IntMIG_Frame_Add( mi_PageString + 2 )
	if ( @"univ" PROG_i_FirstPlay)
		@mo_Texte OBJ_TextColorSet( mi_PageString + 2, 0, -1, 0xFF202020 )
	if ( @"univ" Cheat_i_Flags & CHEAT_MenuCheatFinal )
	{
		IntMIG_SetText_Page( mi_PageString + 3, INTMIG_TXG, INTMIG_TEXT_StartMenu + 3, PageStartMain_PosLine(3),  0.0, 6, -1, INTMIG_Color_NotSel, 0, 1 )
		IntMIG_Frame_Add( mi_PageString + 3 )
	}
		
	IntMIG_Frame_End( mi_PageString + 7, 0xFFFFFFFF, 0.05, 0.03, 32 )
			
	IntMIG_SetText_Page( mi_PageString + 4, INTMIG_TXG, INTMIG_TEXT_Xselect, INTMIG_PosButton_BottomRight,  0.0, 6, -1, INTMIG_Color_Sel, 0, 1 )
	IntMIG_SetText_Page( mi_PageString + 5, INTMIG_TXG, INTMIG_TEXT_Tquit, INTMIG_PosButton_BottomLeft,  0.0, 4, -1, INTMIG_Color_Sel, 0, 1 )
	IntMIG_SetText_Page( mi_PageString + 6, INTMIG_TXG, INTMIG_TEXT_Souligne, PageStartMain_PosSouligne( mi_Cur ),  0.0, 6, -1, INTMIG_Color_Sel, 0, 1 )

	@mo_Texte OBJ_TextColorSet( mi_PageString + mi_Cur, 0, -1, INTMIG_Color_Sel )
	IntMIG_Text_FadeInMulti( mi_PageString, 8, 0.5, 0.0 )
}
else if (INTMIG_LeavePage_Start)
{
	IntMIG_Text_FadeOutMulti( mi_PageString, 8, 0.5 )
}

IntMIG_RestorePageParams()

if ( mi_Exit || mi_ChangePage || mi_Cheat )
	return
	
ti_SaveCur = mi_Cur
IntMIG_UpdateCur_Vertical( ti_Count, 1 )
if (mi_Cur != ti_SaveCur )
{
	IntMIG_PlaySound( INTMIG_SOUND_SELECTION )
	IntMIG_Text_ChangeColor( mi_PageString + ti_SaveCur, INTMIG_Color_Sel, INTMIG_Color_NotSel, 0.25, 0.0 )
	IntMIG_Text_ChangeColor( mi_PageString + mi_Cur, INTMIG_Color_NotSel, INTMIG_Color_Sel, 0.25, 0.0 )
	@mo_Texte OBJ_TextPivotSet( mi_PageString + 6, PageStartMain_PosSouligne( mi_Cur ) )
}
	
if ( joy_cancel )
{
	IntMIG_PlaySound( INTMIG_SOUND_CANCEL )
	if (@"univ" MENU_ProfileIndex != -1)
		INTMIG_YesNoPage( INTMIG_YESNO_QUITPROFILE )
	else
		INTMIG_YesNoPage( INTMIG_YESNO_QUITPLAYINGWITHOUTSAVE )
	return
}
	
if ( joy_valide )
{
	IntMIG_PlaySound( INTMIG_SOUND_VALIDE )
	switch ( mi_Cur )
	{
		case 0: 
			{
				if (@"univ" MENU_ProfileIndex == -1)
					@"univ" MENU_PlayWithoutSaving = 1
				else
					@"univ" MENU_PlayWithoutSaving = 0
				if (@"univ" PROG_i_FirstPlay)
				{
					//DBG_TraceString( "Save Buffer : Save2" ) DBG_TraceEOL()
					SAVE_Request( INO_e_SavRq_SaveSaveBuffer, 2)
					INTMIG_LoadingPage( @"univ" PROG_i_Map, @"univ" PROG_i_ES )
				}
				else
					INTMIG_ChangePage( "IntMIG_Page_Map", @"univ" PROG_i_MapCur )
				break
			}
		case 1: IntMIG_StoreOptions() INTMIG_ChangePage( "IntMIG_Page_OptStart" , 0 ) break
		case 2: 
			if ( (mi_XTRA_New >= 0) && (mi_XTRA_New < mi_XTRA_Nb) )
			{
				mi_XTraCur = mi_XTRA_New
				mi_XTRA_New = -1
			}
			INTMIG_ChangePage( "IntMIG_Page_Xtra" , mi_XTraCur ) 
			break
		case 3: mi_OnlineCode = 0 INTMIG_ChangePage( "IntMIG_Page_CheatFinal", 0) break
	}
}

#include "GS_Constant.var"

//-----------------------------------------------------------------------------------------------
// tmp var
//-----------------------------------------------------------------------------------------------
int ti_mode, ti_submode
int ti


//-----------------------------------------------------------------------------------------------
// render mode
//-----------------------------------------------------------------------------------------------
if(@"univ" SND_gi_RenderMode != mi_RenderMode)
{
	mi_RenderMode = @"univ" SND_gi_RenderMode
	SND_Seti(SND_Cte_SetRenderMode, mi_RenderMode)
	mi_RenderMode = SND_Geti(SND_Cte_SetRenderMode)
	@"univ" SND_gi_RenderMode = mi_RenderMode 
}


//-----------------------------------------------------------------------------------------------
// game mode
//-----------------------------------------------------------------------------------------------
ti_mode = (@"univ" SND_gi_GameMode) & SND_Cte_ModeMask
ti_submode = (@"univ" SND_gi_GameMode) & SND_Cte_SubModeMask

if(ti_mode != mi_GameMode)
{
	if( ti_mode == SND_Cte_ModeLoading )
	{
		// **** start loading ****
		SND_GotoModeLoading()
		mi_PrevGameMode = mi_GameMode
		mi_GameMode = SND_Cte_ModeLoading 
	}
	else if (ti_mode == SND_Cte_ModeDying )
	{
		// **** dying ****
		SND_GotoModeDying()
		mi_PrevGameMode = mi_GameMode
		mi_GameMode = SND_Cte_ModeDying 
	}
	else if ((mi_GameMode == SND_Cte_ModeDying ) && (ti_mode == SND_Cte_ModePlaying ))
	{
		// **** survival ****
		SND_GotoModeSurvival()
		mi_PrevGameMode = mi_GameMode
		mi_GameMode = SND_Cte_ModePlaying 
	}
	else if( mi_GameMode == SND_Cte_ModeLoading)
	{
		// **** end loading ****
		SND_GotoModeEndLoading()
		mi_PrevGameMode = mi_GameMode
		mi_GameMode = SND_Cte_ModePlaying 
	}
	else if( ((mi_GameMode == SND_Cte_ModePlaying ) || (mi_GameMode == SND_Cte_ModeDying )) && (ti_mode == SND_Cte_ModeMenu) )
	{
		// **** go to menu ****
		SND_GotoModeEnterMenu()
		mi_PrevGameMode = mi_GameMode
		mi_GameMode = SND_Cte_ModeMenu 
	}
	else if( (mi_GameMode == SND_Cte_ModeMenu ) && ((ti_mode == SND_Cte_ModePlaying )||(ti_mode == SND_Cte_ModeDying)) )
	{
		// **** go back to game ****
		SND_GotoModeExitMenu()
		if(mi_PrevGameMode == SND_Cte_ModeDying)
		{
			SND_GotoModeDying()
			mi_PrevGameMode = mi_GameMode
			mi_GameMode = SND_Cte_ModeDying 
		}
		else
		{
			mi_PrevGameMode = mi_GameMode
			mi_GameMode = SND_Cte_ModePlaying 
		}
	}
	else 
	{
		SND_M_Assert(0, "[GST_SOUND] bad Game Mode value")
	}	
	
	 
}

if( (mi_GameMode == SND_Cte_ModePlaying) && (ti_submode != mi_GameSubMode) )
{
	// ******************** A ***********************	
	if( (ti_submode  & SND_Cte_SubModeA) && !(mi_GameSubMode & SND_Cte_SubModeA) )
	{
		// **** on A ****
		for(ti=SND_Cte_GrpSpecialFX; ti<SND_Cte_MasterGrp; ti++)
		{
			if (ti == SND_Cte_GrpA) continue
			SND_M_FadeGroup(ti, 0.2, 0.5) // fade out : vol=0.2, time=0.5sec
		}
		mi_GameSubMode |= SND_Cte_SubModeA		
	}
	else if( (mi_GameSubMode  & SND_Cte_SubModeA) && !(ti_submode & SND_Cte_SubModeA) )
	{
		// **** off A ****
		for(ti=SND_Cte_GrpSpecialFX; ti<SND_Cte_MasterGrp; ti++)
		{
			if (ti == SND_Cte_GrpA) continue
			SND_M_FadeGroup(ti, 1.0, 0.5) // fade in : vol=1.0, time=0.5sec
		}				
		mi_GameSubMode &= ~SND_Cte_SubModeA		
	}	

	// ******************** B ***********************	
	if( (ti_submode  & SND_Cte_SubModeB) && !(mi_GameSubMode & SND_Cte_SubModeB) )
	{
		// **** on  ****
		for(ti=SND_Cte_GrpSpecialFX; ti<SND_Cte_MasterGrp; ti++)
		{
			if (ti == SND_Cte_GrpB) continue
			SND_M_FadeGroup(ti, 0.2, 0.5) // fade out : vol=0.2, time=0.5sec
		}
		mi_GameSubMode |= SND_Cte_SubModeB
	}
	else if( (mi_GameSubMode  & SND_Cte_SubModeB) && !(ti_submode & SND_Cte_SubModeB) )
	{
		// **** off  ****
		for(ti=SND_Cte_GrpSpecialFX; ti<SND_Cte_MasterGrp; ti++)
		{
			if (ti == SND_Cte_GrpB) continue
			SND_M_FadeGroup(ti, 1.0, 0.5) // fade in : vol=1.0, time=0.5sec
		}				
		mi_GameSubMode &= ~SND_Cte_SubModeB
	}	
}

@"univ" SND_gi_GameMode = mi_GameMode | mi_GameSubMode

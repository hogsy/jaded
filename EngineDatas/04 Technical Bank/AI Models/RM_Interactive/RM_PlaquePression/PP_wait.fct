int			ti_i
int			ti_cpt
int			ti_col_nb
object	to_col
object	tao_col[20]
vector	tv_offset
vector	tv_pos

ti_cpt = 0
ti_col_nb = COL_BV_PivotListGet(&tao_col[0], OBJ_C_IdentityFlag_AI, none, Ci_Filter_IdentityFlag)
for( ti_i = 0; ti_i < ti_col_nb; ti_i ++ )
{
	to_col = tao_col[ti_i]
	if( to_col == AI_MainActorGet(0) )
	{
		if( @to_col PROC_RM_MontureGet() )
			continue		// ne pas téléporter rayman s'il est sur une monture
		if( ! Enable_Rayman )
			continue
	}
	else if( @to_col AI_IsModel(get_RM_Box_path) )
	{
		if( ! Enable_Box )
			continue
	}
	else if( @to_col AI_IsModel(get_PNJ_Lapin_path) )
	{
		if( ! Enable_Lapin )
			continue
	}
	else if( EstUneMontureMajestueuse(to_col) )
	{
		if( ! Enable_Monture )
			continue
	}
	else
		continue
	// test Z pour valider (sauf les caisses dont le pivot est haut)
	if( ! @to_col AI_IsModel(get_RM_Box_path) )
	{
		tv_offset = @to_col OBJ_PosGet() - OBJ_PosGet()
		if( tv_offset.z > 1.5 )
			continue		// le gao n'est pas au sol
	}
	ti_cpt++
}

if( ti_cpt || f_timer_toujours_active ) // || IO_KeyPressed(65) )
{
	// s'enfonce
	if( ti_cpt )
		f_timer_toujours_active = f_duree_toujours_active		// timer
	
	// SND =========================
	if (ti_cpt_old != -1 && !ti_cpt_old)
		SND_RequestPlay(1)
	// SND =========================

	f_profondeur_courante += MATH_FloatMin(f_profondeur - f_profondeur_courante, f_vitesse_activation* TIME_GetDt())
	if( f_profondeur_courante > 0.5 * f_profondeur )
		OBJ_CapaSet(Obj_Capa_Switch, none)
	else
		OBJ_CapaSet(none, Obj_Capa_Switch)
}
else
{
	// remonte

	// SND =========================
	if (ti_cpt_old != -1 && ti_cpt_old)
		SND_RequestPlay(2)
	// SND =========================

	f_profondeur_courante -= MATH_FloatMin(f_profondeur_courante, f_vitesse_retour * TIME_GetDt())
	OBJ_CapaSet(none, Obj_Capa_Switch)
}

ti_cpt_old = ti_cpt

if( i_sens )		// remonte
	tv_offset = f_profondeur_courante * Cv_VerticalVector
else		// s'enfonce
	tv_offset = - f_profondeur_courante * Cv_VerticalVector

if (o_father)
{
	tv_pos = @o_father OBJ_PosGet()
	tv_pos += @o_father MATH_VecLocalToGlobal(v_init_pos + tv_offset)
	OBJ_PosSet(tv_pos)
}
else
{
	OBJ_PosSet(OBJ_PosInitGet() + tv_offset)
}

f_timer_toujours_active -= MATH_FloatMin(f_timer_toujours_active, TIME_GetDt())

//Str_DisplayFloatOnce(f_profondeur_courante, cvector(0.5,0.8,0))
//if( OBJ_CapaTest(Obj_Capa_Switch) )
//	Str_DisplayTextOnce("Obj_Capa_Switch", cvector(0.5,0.85,0))

//----------------------------------------------------------------------------------------------------------------------------------------
// INCLUDE
//----------------------------------------------------------------------------------------------------------------------------------------
#include "TXT_Constant.var"


//----------------------------------------------------------------------------------------------------------------------------------------
// VAR
//----------------------------------------------------------------------------------------------------------------------------------------
int ti_index, i, ti_previous, ti_listid, ti_flag

//----------------------------------------------------------------------------------------------------------------------------------------
// CODE
//----------------------------------------------------------------------------------------------------------------------------------------

// ---- wait activation request ----
//-------------------------------------------
if( i_TriggerState == 0 ) 
{
	return
}

// Ok je valide le CP (ok ok a chaque trame c'est pas top)
if (i_SF_NePlusSeRejouer)
	Super_SpecialFlag_set(i_SF_AlreadyCP)


if( b_TestTriggerWhenRetrying )
	ti_flag = SPEECH_Cte_FlagsTestTriggerWhenRetrying
else
	ti_flag = 0

// ---- goto the next line ----
//------------------------------------
switch(i_Mode)
{
	case 1 : // sequencial => linked list
	//----------------------------
		ti_listid = MATH_RandInt(10,2147483648) 
		ti_listid &= 0xFFFFFFF0
		ti_listid |= i_text_nb
		
		ti_index = 0
		for(i=0; i<TXT_Cte_ReferenceSize; i++)
		{
			if( ai_played_text[i] ) continue
			
			ai_played_text[i] = 1
			SPEECH_RequestPost
				(
				OBJ_Me(), 
				nobody, 
				TEXT_FileGet(at_Text[i]), 
				TEXT_EntryGet(at_Text[i]), 
				af_RequestTimeOut[i], 
				SPEECH_Cte_PriorityDefault, 
				0,
				ti_listid,
				ti_index,
				ti_flag
				)
				
			if(AI_TriggerIsValid(at_CallBack[i]))
			{
				call_trigger(at_CallBack[i])
			}
				
			ti_index++
		}

		if(! i_WaitActivationEnd )
		{
			AI_TrackStop(0)
		}
		AI_TrackCurChangeNow("TXT_InactiveLoop")
		break
		
	case 2 : // random => choose one text randomly, and stop
	//-------------------------
		// get randomly a new index (but once never played)
		i_current_index  = MATH_RandInt(0,TXT_Cte_ReferenceSize)
		while( ai_played_text[i_current_index] )
		{
			i_current_index++
			if(i_current_index >= TXT_Cte_ReferenceSize)	i_current_index = 0
		}

		ai_played_text[i_current_index] = 1
					
		SPEECH_RequestPost
			(
			OBJ_Me(), 
			nobody, 
			TEXT_FileGet(at_Text[i_current_index]), 
			TEXT_EntryGet(at_Text[i_current_index]), 
			af_RequestTimeOut[i_current_index], 
			SPEECH_Cte_PriorityDefault, 
			0,
			0,
			0,
			ti_flag
			)
			
		if(AI_TriggerIsValid(at_CallBack[i_current_index]))
		{
			call_trigger(at_CallBack[i_current_index])
		}
		
		if(! i_WaitActivationEnd )
		{
			AI_TrackStop(0)
		}
		AI_TrackCurChangeNow("TXT_InactiveLoop")
		break
}





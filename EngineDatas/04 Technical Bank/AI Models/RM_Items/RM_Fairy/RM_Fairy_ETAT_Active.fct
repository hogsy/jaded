#include "RM_Fairy_defines.var"

Include_UltraProcedure_Header

float		tf_norm

vector	tv_pos
vector	tv_pivot

if (i_flag_sortie_etat)
{
	i_flag_sortie_etat = faux
	return
}

if (i_etat_courant != ETAT_Active)
{
	// INTIALISATION
	i_dernier_etat = i_etat_courant
	i_etat_courant = ETAT_Active
	
	if (fct_last_etat)
	{
		i_flag_sortie_etat = vrai
		AI_Execute(fct_last_etat)
	}
	
	fct_last_etat = "RM_Fairy_ETAT_Active"
	
	OBJ_FlagInvisibleSet(faux)

	f_blend_speed = 0.0
	f_time_start_etat = 0.0
}
else
{
	f_time_start_etat += TIME_GetDt()
}

// ANALYSE ===============================================================
RM_Fairy_Fill_Info()

if ( ! o_info && ! i_mode_look )
	macro_change_etat("RM_Fairy_ETAT_Attente")

tv_pivot = @o_main_actor OBJ_PosGet()
tv_pivot += @o_main_actor OBJ_BankingGet() * 0.5


f_blend_speed += MATH_FloatMin(12.0 - f_blend_speed, 12.0 * TIME_GetDt())

if (o_info)
	tv_pos = v_info_pos
else
	tv_pos = tv_pivot + (@get_Kamera OBJ_SightGet() * 3.0)

if (@o_main_actor COL_CollideType(COL_C_Ground))
{
	v_ground_pos = @o_main_actor COL_CollidedPointGet(COL_C_Ground)
	v_ground_normal = @o_main_actor COL_NormalGet(COL_C_Ground)
}

if (MATH_VecSquareNorm(v_ground_pos - @o_main_actor OBJ_PosGet()) < 9.0)
	tv_pos -= MATH_FloatMin(MATH_VecDotProduct(tv_pos - v_ground_pos, v_ground_normal) - 0.5, 0.0) * v_ground_normal

if (@o_main_actor COL_CollideType(COL_C_Wall))
{
	v_wall_pos = @o_main_actor COL_CollidedPointGet(COL_C_Wall)
	v_wall_normal = @o_main_actor COL_NormalGet(COL_C_Wall)
}

if (MATH_VecSquareNorm(v_wall_pos - @o_main_actor OBJ_PosGet()) < 9.0)
	tv_pos -= MATH_FloatMin(MATH_VecDotProduct(tv_pos - v_wall_pos, v_wall_normal) - 0.5, 0.0) * v_wall_normal

tv_pos = tv_pivot + MATH_VecBlendRotate(OBJ_PosGet() - tv_pivot, tv_pos - tv_pivot, f_blend_speed * TIME_GetDt())
OBJ_PosSet(tv_pos)
OBJ_BankingGeneralSet(tv_pivot - OBJ_PosGet(), Cv_VerticalVector)

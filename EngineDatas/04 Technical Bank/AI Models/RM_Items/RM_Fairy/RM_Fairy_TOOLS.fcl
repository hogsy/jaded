procedure_local void RM_Fairy_Fill_Info()
{
	int				ti_i
	int				ti_canal
	int				ti_type

	float			tf_best_dist	
	float			tf_dist
	float			tf_size
	float			tf_pourcent
	float			tf_extrusion

	object		to_last_info
	object		to_obj
	object		to_canal
	
	vector		tv_cam_to_obj
	vector		tv_pos
	vector		tv_2D_center_pos

	text			t_text

	if (i_flag_check_info)
		return
		
	i_flag_check_info = vrai

	to_last_info = o_info
	o_info = nobody
	i_info_index = -1
	tf_best_dist = Cf_Infinit

	for (ti_i = 0; ti_i < @get_list_manager i_fairy_info_nb; ti_i++)
	{
		to_obj = @get_list_manager ao_fairy_info[ti_i]
		if (@to_obj OBJ_FlagsControlGet() & (OBJ_C_ControlFlag_ForceInactive | OBJ_C_ControlFlag_SectoInactive))
			continue
	
		ti_type = @get_list_manager ai_fairy_info_type[ti_i]
		switch(ti_type)
		{
			case 0 :
				// FLOATING INFO
				if ( ! i_mode_look )
					continue
				break
				
			case 1 :
				// SNAP POS WHEN MODE LOOK
				if ( ! i_mode_look )
					continue
				if ( ! @o_main_actor COL_Pivot_BVCollide(to_obj) )
					continue
				break

			case 2 :
				// SNAP POS
				if ( ! @o_main_actor COL_Pivot_BVCollide(to_obj) )
					continue
				break
				
			case 3 :
				// DIRECTION
				i_info_index = ti_i
				ti_i = 100000
				continue
				break
		}

		ti_canal = @get_list_manager ai_fairy_info_canal[ti_i]
		tf_size = @get_list_manager af_fairy_info_size[ti_i]
	
		if (ti_canal != -1)
			to_canal = @to_obj ANI_CanalObjectGet(ti_canal)
		else
			to_canal = to_obj
			
		tv_pos = @to_canal OBJ_PosGet()
		tv_pos += @to_canal MATH_VecGlobalToLocal(@get_list_manager av_fairy_info_offset[ti_i])

		tv_cam_to_obj = tv_pos - @get_Kamera OBJ_PosGet()
		tf_dist = MATH_VecSquareNorm(tv_cam_to_obj)
		if (tf_dist > tf_best_dist)
			continue
			
//		procedure float VIEW_LIB_Pourcent_On_Screen(vector tv_pos, float tf_size, float tf_X_size, float tf_Y_size, byref vector tv_2D_center_pos, int ti_draw_debug_info)		
		tf_pourcent = VIEW_LIB_Pourcent_On_Screen(tv_pos, tf_size, 1.0, 1.0, tv_2D_center_pos, vrai)
		
		if (tf_pourcent > 0.5)
		{
			// C'est à l'écran	
			tf_extrusion	 = @get_list_manager af_fairy_info_extrusion[ti_i]
			if (tf_extrusion >= 0.0)
			{
				// Onw va faire un lancer de rayon
				tv_cam_to_obj /= MATH_FloatSqrt(tf_dist)
				tv_pos -= tv_cam_to_obj * tf_extrusion

				if ( ! COL_LIB_Can_See_Actor(to_obj, @get_Kamera OBJ_PosGet(), tv_pos, all, OBJ_C_IdentityFlag_Anims, Ci_Filter_IdentityFlag, COL_C_Ray_on_ColMap_NoCrossable) )
					continue
			}
		
			i_info_index = ti_i
			tf_best_dist = tf_dist
		}
	}

	if (i_info_index != -1)
	{
		o_info = @get_list_manager ao_fairy_info[i_info_index]
		ti_canal = @get_list_manager ai_fairy_info_canal[i_info_index]
	
		if (ti_canal != -1)
			to_canal = @o_info ANI_CanalObjectGet(ti_canal)
		else
			to_canal = o_info
			
		v_info_pos = @to_canal OBJ_PosGet()
		v_info_pos += @to_canal MATH_VecGlobalToLocal(@get_list_manager av_fairy_info_offset[i_info_index])
		
		switch(@get_list_manager ai_fairy_info_type[i_info_index])
		{
			case 0 :

				v_info_pos -= @get_Kamera OBJ_PosGet()
				MATH_VecSetNormalize(v_info_pos)
				v_info_pos *= 4.0
				v_info_pos += @get_Kamera OBJ_PosGet()
				
				break
				
			case 1 :
				break

			case 3 :
				v_info_pos -= @o_main_actor OBJ_PosGet()
				MATH_VecSetNormalize(v_info_pos)
				v_info_pos *= 2.0
				v_info_pos += @o_main_actor OBJ_PosGet()
				break

		}	

		if ( ! to_last_info )
		{
			t_text = @get_list_manager txt_fairy_info[i_info_index][0]
			SPEECH_RequestPost(OBJ_Me(), OBJ_Me(), TEXT_FileGet(t_text), TEXT_EntryGet(t_text), SPEECH_Cte_TimeOutDefault + 0.5, SPEECH_Cte_PriorityDefault, SPEECH_Cte_RandomSize, 0, 0, 0)
		}
	}
	
	if ( to_last_info != o_info )
		f_blend_speed = 0.0

}
